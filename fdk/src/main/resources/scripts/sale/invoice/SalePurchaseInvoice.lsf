MODULE SalePurchaseInvoice;

REQUIRE SaleInvoice, PurchaseInvoice;

PRIORITY Sale;

//----------------------------------------------------------------------------//

// Создание накладной на основе накладной //

FORM invoiceInvoices 'Накладные'
    OBJECTS s = LegalEntity FIXED PANEL
    PROPERTIES (s) READONLY nameLegalEntity
    OBJECTS c = LegalEntity FIXED PANEL
    PROPERTIES (c) READONLY nameLegalEntity

    OBJECTS ss = Stock FIXED PANEL
    PROPERTIES (ss) READONLY nameStock
    OBJECTS cs = Stock FIXED PANEL
    PROPERTIES (cs) READONLY nameStock

    OBJECTS t=DATE FIXED PANEL
    PROPERTIES(t) OBJVALUE

    OBJECTS o = Invoice
    PROPERTIES (o) READONLY isPostedInvoice FORCE GRID, numberInvoice, seriesInvoice, dateInvoice, timeInvoice, nameCurrencyInvoice,
                            countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                            noteInvoice, objectClassName
    FILTERS supplierInvoice(o) == s,
            customerInvoice(o) == c,
            supplierStockInvoice(o) == ss,
            customerStockInvoice(o) == cs,
            isPostedInvoice(o)

    FILTERGROUP filters1
        FILTER 'Только этой даты' 'F11' dateInvoice(o) == t DEFAULT

    OBJECTS d = InvoiceDetail

    PROPERTIES (d) READONLY indexInvoiceDetail, idBarcodeSkuInvoiceDetail, nameSkuInvoiceDetail, shortNameUOMSkuInvoiceDetail
    PROPERTIES (d) READONLY SHOWIF showPackInvoice(o) idBarcodePackInvoiceDetail, shortNameUOMPackInvoiceDetail, amountPackInvoiceDetail, packQuantityInvoiceDetail
    PROPERTIES (d) READONLY quantityInvoiceDetail, priceInvoiceDetail, sumInvoiceDetail, numberVATInvoiceDetail, valueVATInvoiceDetail,
                            VATSumInvoiceDetail, invoiceSumInvoiceDetail, nameSupplierStockInvoiceDetail

    FILTERS invoiceInvoiceDetail(d) == o
;

DESIGN invoiceInvoices FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        NEW documentContainer BEFORE functions.box {

            childConstraints = TO THE BOTTOM;
            NEW headerBox1 {
                childConstraints = TO THE RIGHTBOTTOM;
                NEW headerBox11 {
                    childConstraints = TO THE RIGHTBOTTOM;
                    title = 'Поставщик';
                    ADD s.box;
                    ADD ss.box;
                }
                NEW headerBox12 {
                    childConstraints = TO THE RIGHTBOTTOM;
                    title = 'Покупатель';
                    ADD c.box;
                    ADD cs.box;
                }
            }
            NEW headerBox2 {
                childConstraints = TO THE RIGHTBOTTOM;
                ADD t.box;
            }
            ADD o.box;
            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW printTab {
                    title = 'Печатные формы';
                    NEW printContainer {
                        title = 'Печать';
                        childConstraints = TO THE BOTTOM;
                        fillVertical = 1.0;
                    }
                }
            }
        }
    }
}


fillInvoiceUserInvoice 'Заполнить на основе накладной' =  ACTION (userInvoice) {
    FORM invoiceInvoices OBJECTS s = Purchase.supplierUserInvoice(userInvoice), c = Purchase.customerUserInvoice(userInvoice),
                                   ss = Purchase.supplierStockUserInvoice(userInvoice), cs = Purchase.customerStockUserInvoice(userInvoice),
                                   t = Purchase.dateUserInvoice(userInvoice) MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL saleInvoice = Invoice();
        SET saleInvoice() <- chosenObject('o');

        FOR invoiceInvoiceDetail(invoiceDetail) == saleInvoice() ADDOBJ d = Purchase.UserInvoiceDetail DO {
            SET Purchase.userInvoiceUserInvoiceDetail(d) <- userInvoice;
            SET Purchase.skuUserInvoiceDetail(d) <- skuInvoiceDetail(invoiceDetail);
            SET Purchase.batchUserInvoiceDetail(d) <- batchInvoiceDetail(invoiceDetail);
            SET Purchase.barcodePackUserInvoiceDetail(d) <- barcodePackInvoiceDetail(invoiceDetail);
            SET Purchase.amountPackUserInvoiceDetail(d) <- amountPackInvoiceDetail(invoiceDetail);
            SET Purchase.packQuantityUserInvoiceDetail(d) <- packQuantityInvoiceDetail(invoiceDetail);
            SET Purchase.quantityUserInvoiceDetail (d) <- quantityInvoiceDetail(invoiceDetail);
            SET Purchase.VATUserInvoiceDetail(d) <- VATInvoiceDetail(invoiceDetail);
            SET Purchase.valueVATUserInvoiceDetail(d) <- valueVATInvoiceDetail(invoiceDetail);
            SET Purchase.priceUserInvoiceDetail(d) <- priceInvoiceDetail(invoiceDetail);
            SET Purchase.invoicePriceUserInvoiceDetail(d) <- invoicePriceInvoiceDetail(invoiceDetail);
        }
    }
} IN Purchase.invoiceGroup;

EXTEND FORM Purchase.userInvoice
    PROPERTIES(i) fillInvoiceUserInvoice
;
EXTEND DESIGN Purchase.userInvoice {
    headerCreateDetail {
        NEW headerCreateSale {
            title = 'Накладная (продажа)';
            ADD PROPERTY(fillInvoiceUserInvoice);
        }
    }
}

//----------------------------------------------------------------------------//

GROUP invoiceGroup 'Информация о накладной' : base;

createPurchaseInvoiceInvoice 'Создать накладную (закупка)' = ABSTRACT BOOLEAN (Invoice) PERSISTENT;
createPurchaseInvoiceUserInvoice 'Создать накладную (закупка)' = DATA BOOLEAN (UserInvoice) PERSISTENT;
createPurchaseInvoiceInvoice(invoice) += createPurchaseInvoiceUserInvoice(invoice);

createPurchaseInvoiceInvoiceDetail 'Создать накладную (закупка)' (invoiceDetail) = createPurchaseInvoiceInvoice(invoiceInvoiceDetail(invoiceDetail))PERSISTENT;
createPurchaseInvoiceUserInvoiceDetail 'Создать накладную (закупка)' (userInvoiceDetail) = createPurchaseInvoiceUserInvoice(userInvoiceUserInvoiceDetail(userInvoiceDetail));
backgroundPurchaseInvoice 'Цвет' (invoice) = RGB(255, 255, 190) IF invoice IS Invoice;

isCompanyCustomerUserInvoice (userInvoice) =  isCompanyLegalEntity(customerUserInvoice(userInvoice));
EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundPurchaseInvoice(i) SHOWIF isCompanyCustomerUserInvoice(i) createPurchaseInvoiceUserInvoice
;
EXTEND DESIGN userInvoice {
    headerCreateDocuments {
        NEW headerCreatePurchase {
            title = 'Накладная (закупка)';
            ADD PROPERTY(createPurchaseInvoiceUserInvoice);
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY BACKGROUND backgroundPurchaseInvoice(i) createPurchaseInvoiceInvoice
;
//--  Связь поставки с поставкой

invoiceDetailInvoiceDetail = ABSTRACT InvoiceDetail (Purchase.InvoiceDetail) PERSISTENT;
invoiceDetailUserInvoiceDetail = DATA InvoiceDetail (Purchase.UserInvoiceDetail);
invoiceDetailInvoiceDetail(invoiceDetail) += invoiceDetailUserInvoiceDetail(invoiceDetail);

CONSTRAINT Purchase.supplierInvoiceDetail(detail) != supplierInvoiceDetail(invoiceDetailUserInvoiceDetail(detail)) OR
           Purchase.supplierStockInvoiceDetail(detail) != supplierStockInvoiceDetail(invoiceDetailUserInvoiceDetail(detail)) OR
           Purchase.customerInvoiceDetail(detail) != customerInvoiceDetail(invoiceDetailUserInvoiceDetail(detail)) OR
           Purchase.customerStockInvoiceDetail(detail) != customerStockInvoiceDetail(invoiceDetailUserInvoiceDetail(detail)) OR
           Purchase.skuInvoiceDetail(detail) != skuInvoiceDetail(invoiceDetailUserInvoiceDetail(detail))
    CHECKED BY invoiceDetailUserInvoiceDetail
        MESSAGE 'Поставщик, покупатель, склад поставщика и склад покупателя в накладной и накладной на осове накладной должны соответствовать друг другу';


descriptionIndexInvoiceDetailInvoiceDetail 'Строка накладной (продажа)' (detail) = descriptionIndexInvoiceDetail(invoiceDetailInvoiceDetail(detail));
descriptionIndexInvoiceDetailUserInvoiceDetail 'Строка накладной (продажа)' (detail) = descriptionIndexInvoiceDetail(invoiceDetailUserInvoiceDetail(detail));

quantityInvoiceDetailInvoiceInvoice (sale, purchase) = GROUP SUM Purchase.quantityInvoiceDetail(detail) BY invoiceInvoiceDetail(invoiceDetailInvoiceDetail(detail)), Purchase.invoiceInvoiceDetail(detail);

saleInvoicesInvoice 'Поставки (продажа)' (purchase) = GROUP CONCAT toString255(descriptionInvoice(sale)) IF quantityInvoiceDetailInvoiceInvoice(sale, purchase) , ', '
                                                        BY purchase
                                                        ORDER sale IN invoiceGroup MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;

relationPurchaseInvoice 'Связь' (purchase) = GROUP SUM quantityInvoiceDetailInvoiceInvoice (sale, purchase) BY purchase PERSISTENT;

invoicedInvoiceDetail 'Кол-во (выписано)' (invoiceDetail) = GROUP SUM Purchase.quantityInvoiceDetail(detail) IF Purchase.isPostedInvoiceDetail(detail)
                                                                   BY invoiceDetailInvoiceDetail(detail) PERSISTENT;

toInvoicedInvoiceDetail 'Не выписано' (invoiceDetail) = quantityInvoiceDetail (invoiceDetail) (-) invoicedInvoiceDetail(invoiceDetail);

CLASS PurchaseInvoice 'Накладная на основе накладной': Purchase.Invoice;
CLASS PurchaseInvoicePosted 'Проведенная накладная на основе накладной' : PurchaseInvoice, PostedObject;
CLASS PurchaseInvoiceDetail 'Строка накладной на основе накладной' : Purchase.InvoiceDetail;

@defineDocumentTables(purchaseInvoice);

@defineDocumentAggregation(invoice, purchaseInvoice, createPurchaseInvoiceInvoice);
Purchase.invoiceInvoiceDetail(detail) += purchaseInvoicePurchaseInvoiceDetail(detail);

@defineDocumentDetailIndex(purchaseInvoice);

Purchase.dateInvoice(invoice) += datePurchaseInvoice(invoice);
Purchase.timeInvoice(invoice) += timePurchaseInvoice(invoice);

@defineDocumentAggregationStockPrefix(invoice, purchaseInvoice, supplierStock, 'Склад поставщика', , );
Purchase.supplierStockInvoice(invoice) += supplierStockPurchaseInvoice(invoice);
Purchase.dataSupplierStockInvoiceDetail(invoiceDetail) += dataSupplierStockInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
@defineDocumentAggregationStockPrefix(invoice, purchaseInvoice, customerStock, 'Склад покупателя', , );
Purchase.customerStockInvoice(invoice) += customerStockPurchaseInvoice(invoice);
Purchase.dataCustomerStockInvoiceDetail(invoiceDetail) += dataCustomerStockInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));

@defineDocumentAggregationStockPrefix(invoice, purchaseInvoice, supplier, 'Поставщик', , );
Purchase.supplierInvoice(invoice) += supplierPurchaseInvoice(invoice);
@defineDocumentAggregationStockPrefix(invoice, purchaseInvoice, customer, 'Покупатель', , );
Purchase.customerInvoice(invoice) += customerPurchaseInvoice(invoice);

@defineDocumentAggregationPosted(invoice, purchaseInvoice);
Purchase.isPostedInvoice(invoice) += isPostedPurchaseInvoice(invoice);

Purchase.numberInvoice(invoice) += numberInvoice(invoicePurchaseInvoice(invoice));
Purchase.seriesInvoice(invoice) += seriesInvoice(invoicePurchaseInvoice(invoice));
seriesNumberPurchaseInvoice 'Серия/номер документа' (purchaseInvoice) = seriesNumberInvoice(invoicePurchaseInvoice(purchaseInvoice));

noteInvoicePurchaseInvoice 'Примечание' (purchaseInvoice) = noteInvoice(invoicePurchaseInvoice(purchaseInvoice));
Purchase.noteInvoice(invoice) += noteInvoicePurchaseInvoice(invoice);

currencyPurchaseInvoice  (purchaseInvoice) = currencyInvoice(invoicePurchaseInvoice(purchaseInvoice));
Purchase.currencyInvoice (invoice) += currencyPurchaseInvoice(invoice);

agreementPurchaseInvoice  (purchaseInvoice) = agreementInvoice(invoicePurchaseInvoice(purchaseInvoice));
Purchase.agreementInvoice (invoice) += agreementPurchaseInvoice(invoice);

priceListTypePurchaseInvoice  (purchaseInvoice) = priceListTypeInvoice(invoicePurchaseInvoice(purchaseInvoice));
Purchase.priceListTypeInvoice (invoice) += priceListTypePurchaseInvoice(invoice);

@defineDocumentDescription(purchaseInvoice, PurchaseInvoiceDetail, seriesNumberPurchaseInvoice, 'Поставка на основе поставки ');
Purchase.descriptionInvoice (invoice) += descriptionPurchaseInvoice(invoice);

Purchase.contractSkuInvoice (invoice) += contractSkuInvoice(invoicePurchaseInvoice(invoice));
Purchase.isCommissionInvoice (invoice) += isCommissionInvoice(invoicePurchaseInvoice(invoice));

@defineDocumentAggregationDetailSku(invoice, purchaseInvoice, sku);
Purchase.skuInvoiceDetail(invoiceDetail) +=  skuPurchaseInvoiceDetail(invoiceDetail);

@defineDocumentAggregationDetailBatch(invoice, purchaseInvoice);
Purchase.batchInvoiceDetail(invoiceDetail) += batchPurchaseInvoiceDetail(invoiceDetail);

Purchase.quantityInvoiceDetail(invoiceDetail) += quantityInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.priceListTypeInvoiceDetail(invoiceDetail) += priceListTypeInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));

Purchase.priceInvoiceDetail(invoiceDetail) += priceInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.sumInvoiceDetail(invoiceDetail) += sumInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.VATInvoiceDetail(invoiceDetail) += VATInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.valueVATInvoiceDetail(invoiceDetail) += valueVATInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.invoicePriceInvoiceDetail(invoiceDetail) += invoicePriceInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.VATSumInvoiceDetail(invoiceDetail) += VATSumInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.invoiceSumInvoiceDetail(invoiceDetail) += invoiceSumInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));

Purchase.editInvoice(invoice) += ACTION EXEC editInvoice(invoicePurchaseInvoice(invoice));

Purchase.showPackInvoice(invoice) +=  showPackInvoice(invoicePurchaseInvoice(invoice));
Purchase.barcodePackInvoiceDetail(invoiceDetail) += barcodePackInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.amountPackInvoiceDetail(invoiceDetail) +=  amountPackInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));
Purchase.packQuantityInvoiceDetail(invoiceDetail) +=  packQuantityInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(invoiceDetail));

invoiceDetailInvoiceDetail(invoiceDetail) += invoiceDetailPurchaseInvoiceDetail(invoiceDetail);

//------------------------------ Создание аггрегированных объектов через операции -----------------------------//

// -- Операция
@defineOperationProperty(createPurchaseInvoice, 'Накладную (закупка)', createContainer);
@deriveDocumentOperationProperty(UserInvoice, createPurchaseInvoice);

//-- Действие

overFillInvoiceUserInvoiceInvoice = ABSTRACT ACTION (Purchase.UserInvoice, Invoice);
overFillInvoiceUserInvoiceDetailInvoiceDetail = ABSTRACT ACTION (Purchase.UserInvoiceDetail, InvoiceDetail);

moveUserInvoiceInvoice 'Накладная (закупка)' =  ACTION (invoice) NEWSESSION{

    FOR ADDOBJ i = Purchase.UserInvoice DO {

        SET Purchase.agreementUserInvoice(i) <- agreementInvoice(invoice);
        SET Purchase.priceListTypeUserInvoice(i) <- priceListTypeInvoice(invoice);
        SET Purchase.supplierUserInvoice(i) <- supplierInvoice(invoice);
        SET Purchase.customerUserInvoice(i) <- customerInvoice(invoice);
        SET Purchase.supplierStockUserInvoice(i) <- supplierStockInvoice(invoice);
        SET Purchase.customerStockUserInvoice(i) <- customerStockInvoice(invoice);
        SET numberObject(i) <- numberInvoice(invoice);
        SET seriesObject(i) <- seriesInvoice(invoice);
        SET Purchase.noteUserInvoice(i) <- noteInvoice(invoice);
        SET Purchase.currencyUserInvoice(i) <- currencyInvoice(invoice);
        SET Purchase.contractSkuUserInvoice(i) <- contractSkuInvoice(invoice);
        SET Purchase.isCommissionUserInvoice(i) <- isCommissionInvoice(invoice);
        SET Purchase.showPackUserInvoice(i) <- showPackInvoice(invoice);
        SET Purchase.operationUserInvoice(i) <- operationInvoice(invoice);
        EXEC overFillInvoiceUserInvoiceInvoice(i, invoice);

        FOR invoiceInvoiceDetail(detail)==invoice ADDOBJ d = Purchase.UserInvoiceDetail DO {

            SET Purchase.userInvoiceUserInvoiceDetail(d) <- i;

            SET Purchase.priceListTypeUserInvoiceDetail(d) <- priceListTypeInvoiceDetail(detail);
            SET Purchase.dataSupplierStockUserInvoiceDetail(d) <- dataSupplierStockInvoiceDetail(detail);
            SET Purchase.dataCustomerStockUserInvoiceDetail(d) <- dataCustomerStockInvoiceDetail(detail);

            SET Purchase.skuUserInvoiceDetail(d) <- skuInvoiceDetail(detail);
            SET Purchase.quantityUserInvoiceDetail (d) <- quantityInvoiceDetail(detail);
            SET Purchase.VATUserInvoiceDetail (d) <- VATInvoiceDetail(detail);
            SET Purchase.valueVATUserInvoiceDetail (d) <- valueVATInvoiceDetail(detail);
            SET Purchase.priceUserInvoiceDetail (d) <- priceInvoiceDetail(detail);
            SET Purchase.invoicePriceUserInvoiceDetail (d) <- invoicePriceInvoiceDetail(detail);

            SET Purchase.barcodePackUserInvoiceDetail (d) <- barcodePackInvoiceDetail(detail);
            SET Purchase.amountPackUserInvoiceDetail (d) <- amountPackInvoiceDetail(detail);
            SET Purchase.packQuantityUserInvoiceDetail (d) <- packQuantityInvoiceDetail(detail);

            SET Purchase.batchUserInvoiceDetail(d) <- batchInvoiceDetail(detail);
            EXEC overFillInvoiceUserInvoiceDetailInvoiceDetail(d,detail);
        }

    FORM Purchase.userInvoice OBJECTS i = i MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

EXTEND FORM invoices
    PROPERTIES(i) moveUserInvoiceInvoice
;
EXTEND DESIGN invoices {
    createdContainer{
        ADD PROPERTY(moveUserInvoiceInvoice);
    }
}

