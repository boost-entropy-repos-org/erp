MODULE Company;

REQUIRE LegalEntity, Bank;

NAMESPACE LegalEntity;

CLASS Company 'Компания' : LegalEntity, TaxUnitGroup;
TABLE company (Company);

// ---------------------------------------- Формы для ввода компаний ------------------------------------- //

FORM company 'Компания'
    OBJECTS co=Company FIXED PANEL
    PROPERTIES(co) nameLegalEntity, nameCountryLegalEntity, nameLanguageLegalEntity, nameCurrencyLegalEntity,
                   nameOwnershipLegalEntity, shortNameOwnershipLegalEntity, fullNameLegalEntity,
                   addressLegalEntity ON CHANGE dialogAddressLegalEntity(co),
                   postAddressLegalEntity ON CHANGE dialogPostAddressLegalEntity(co),
                   managerLegalEntity ON CHANGE dialogManagerLegalEntity(co),
                   accountantLegalEntity ON CHANGE dialogAccountantLegalEntity(co),
                   phoneLegalEntity ON CHANGE dialogPhoneLegalEntity(co),
                   emailLegalEntity, siteLegalEntity

    OBJECTS a=Bank.Account
    PROPERTIES(a)  numberAccount, nameBankAccount, addressBankAccount, departmentBankAccount, CBUBankAccount, MFOBankAccount, noteAccount, ADDOBJ, DELETESESSION
    FILTERS Bank.legalEntityAccount(a) == co

    EDIT Company OBJECT co
;

DESIGN company FROM DEFAULT {
    main{
       preferredSize = (1024, 768);
       co.box {
            childConstraints = TO THE RIGHT;

            NEW column1 {
                childConstraints = TO THE BOTTOM;

                NEW row11 {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY(nameLegalEntity(co));
                    ADD PROPERTY(nameCountryLegalEntity);
                    ADD PROPERTY(nameCurrencyLegalEntity);
                    ADD PROPERTY(nameLanguageLegalEntity);
                }
                ADD co.lawGroup;
            }

            NEW column2 {
                childConstraints = TO THE BOTTOM;

                ADD co.contactGroup;
                ADD co.managementGroup;
            }
       }

       NEW extendContainer BEFORE functions.box {
            type = TABBED;
            ADD a.box;
       }
   }
}

FORM companies 'Компании'

    OBJECTS co=Company
    PROPERTIES(co) READONLY nameLegalEntity, fullNameLegalEntity, shortNameOwnershipLegalEntity,
                            numberAccountLegalEntity, addressLegalEntity, phoneLegalEntity
    PROPERTIES(co) ADDFORM, EDITFORM, DELETE
;
