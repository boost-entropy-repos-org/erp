MODULE Paas;

REQUIRE System, Authentication, Contact;

CLASS Project 'Проект';
projectName 'Имя' = DATA ISTRING[100] (Project);
projectDescription 'Описание' = DATA STRING[100] (Project);
projectOwner 'Владелец' = DATA CustomUser (Project);
projectOwnerName 'Имя владельца' (project) = nameContact(projectOwner(project));
projectOwnerLogin 'Логин владельца' (project) = loginCustomUser(projectOwner(project));

CLASS Module 'Модуль';
moduleName 'Имя модуля' = DATA ISTRING[100] (Module);
moduleInProject 'Модуль в проекте' = DATA BOOLEAN (Project, Module);
moduleSource 'Исходный код модуля' = DATA TEXT (Module);

CLASS Database 'База данных';
databaseName 'Имя базы' = DATA ISTRING[100] (Database);

CLASS Status 'Статус конфигурации' {
    init 'Стартует',
    started 'Работает',
    stopping 'Останавливается',
    stopped 'Остановлен'
}
FORM statuses
    OBJECTS s = Status
    PROPERTIES(s) staticCaption
    DIALOG Status OBJECT s
;

CLASS Configuration 'Конфигурация';
configurationName 'Имя' = DATA ISTRING[100] (Configuration);
configurationProject 'Проект' = DATA Project (Configuration);
configurationDatabase 'База данных' = DATA Database (Configuration);
configurationDatabaseName 'Имя базы данных' (configuration) = databaseName(configurationDatabase(configuration));
configurationPort 'Порт для запуска' = DATA INTEGER (Configuration);
configurationExportName 'Имя для экспорта через RMI' = DATA STRING[40] (Configuration);
configurationStatus 'Статус' = DATA Status (Configuration);
configurationStatusName 'Статус' (configuration) = staticCaption(configurationStatus(configuration));

databaseConfiguration 'Конфигурация' (database) = GROUP AGGR configuration BY configurationDatabase(configuration);

configurationDelete 'Удалить' = ACTION (c) {
    IF configurationStatus(c) == Status.stopped THEN {
        DELETE c;
    }
} IMAGE 'delete.png';

configurationStartAction = ACTION CUSTOM 'paas.properties.StartConfigurationActionProperty';
configurationStart 'Запустить' = ACTION (c) {
    IF configurationStatus(c) == Status.stopped THEN {
        configurationStartAction(c);
    }
} IMAGE 'start.png';

configurationStopAction = ACTION CUSTOM 'paas.properties.StopConfigurationActionProperty';
configurationStop 'Остановить' = ACTION (c) {
    IF configurationStatus(c) == Status.started THEN {
        configurationStopAction(c);
    }
} IMAGE 'stop.png';

refreshStatuses 'Обновить' = ACTION CUSTOM 'paas.properties.RefreshStatusActionProperty' IMAGE 'refresh.png';

FORM selectModulesForm 'Выбор модулей'
    OBJECTS p=Project FIXED PANEL, m=Module FIXED GRID
    PROPERTIES(p) projectName, projectDescription, projectOwnerName
    PROPERTIES(m) moduleName
    PROPERTIES(p, m) moduleInProject
    FILTERGROUP filters1
        FILTER 'Невыбранные объекты' 'F9' p IS Project AND m IS Module AND NOT moduleInProject(p, m) DEFAULT
        FILTER 'Выбранные объекты' 'F10' moduleInProject(p, m)
;

selectProjectModules 'Выбрать модули' (project) = ACTION FORM selectModulesForm OBJECTS p = project MODAL;

FORM modulesForm 'Модули'
    OBJECTS m=Module
    PROPERTIES(m) moduleName
    PROPERTIES(m) FORCE PANEL moduleSource, ADDOBJ, DELETESESSION
;

DESIGN modulesForm FROM DEFAULT {
    m.grid {
        fillVertical = 0.5;
    }

    m.panel {
        fillVertical = 1.5;
    }

    PROPERTY(moduleSource) {
        panelLabelAbove = TRUE;
        fillHorizontal = 1;
        fillVertical = 1;
    }
}


FORM projectsForm 'Проекты'
    OBJECTS p=Project FIXED PANEL
    PROPERTIES(p) projectName READONLY, projectOwnerName, ADDOBJ, DELETESESSION

    OBJECTS m=Module
    PROPERTIES(m) moduleName, moduleSource FORCE PANEL, ADDOBJ, DELETESESSION
    PROPERTIES(p) TODRAW m FORCE PANEL selectProjectModules

    OBJECTS c=Configuration
    PROPERTIES(c) configurationName, configurationDatabaseName, configurationPort, configurationExportName, configurationStatusName, configurationStart, configurationStop, ADDOBJ, configurationDelete
    PROPERTIES(p) TODRAW c FORCE PANEL refreshStatuses

    FILTERS configurationProject(c) == p;
;

DESIGN projectsForm FROM DEFAULT {
    m.grid {
        fillVertical = 0.5;
    }

    m.panel {
        fillVertical = 1.5;
    }

    PROPERTY(moduleSource) {
        panelLabelAbove = TRUE;
        fillHorizontal = 1;
        fillVertical = 1;
    }
}

NAVIGATOR {
    ADD modulesForm;
    ADD projectsForm;
}




