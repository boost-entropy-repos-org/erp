MODULE PurchaseDeclarationDetailBy;

REQUIRE PurchaseDeclarationDetail, LegalEntityBy, Item;

NAMESPACE Purchase;

UNPLegalEntityDeclaration 'Полное имя импортера' (declaration) = UNPLegalEntity(legalEntityDeclaration(declaration));

customsCodeUOM 'Код в декларации' = DATA VARSTRING[3] (UOM);
EXTEND FORM UOM
    PROPERTIES(u) customsCodeUOM
;
EXTEND FORM UOMs
    PROPERTIES(u) READONLY customsCodeUOM
;
customsCodeUOMDeclarationDetail 'Код ед. изм. в декларации' (d) = customsCodeUOM(UOMDeclarationDetail(d));

typePaymentCustomsDocument 'Вид платежа' = DATA VARSTRING[4] (CustomsDocument);
refDocCustomsDocument 'Ссылка на документ' = DATA VARSTRING[6] (CustomsDocument);
descriptionCustomsDocument 'Описание' = DATA VARSTRING[60] (CustomsDocument);

markinDeclarationDetail = ABSTRACT VARSTRING[1000] (DeclarationDetail);
extraComponentsQuantityDeclarationDetail 'Кол-во с учетом комплектов' = ABSTRACT NUMERIC[14,3] (DeclarationDetail);

isVATCustomsExceptionCustomsDocument 'По льготам' = DATA BOOLEAN (CustomsDocument); 

inDeclarationDetailBrand = GROUP SUM 1 IF inDeclarationDetailUserInvoiceDetail(d, i) BY d, brandItem(skuUserInvoiceDetail(i));
nameBrandDeclarationDetail 'Бренды' =
    GROUP CONCAT nameBrand(b) IF inDeclarationDetailBrand(d, b), ','
              BY d ORDER b MINCHARWIDTH 20 PREFCHARWIDTH 40 MAXCHARWIDTH 60;

inDeclarationDetailManufacturer = GROUP SUM 1 IF inDeclarationDetailUserInvoiceDetail(d, i) BY d, manufacturerItem(skuUserInvoiceDetail(i));
nameManufacturerDeclarationDetail 'Производители' =
    GROUP CONCAT nameManufacturer(m) IF inDeclarationDetailManufacturer(d, m), ','
              BY d ORDER m MINCHARWIDTH 20 PREFCHARWIDTH 40 MAXCHARWIDTH 60;

EXTEND FORM declaration
    PROPERTIES(cd) BEFORE deleted descriptionCustomsDocument, refDocCustomsDocument, typePaymentCustomsDocument, isVATCustomsExceptionCustomsDocument
;

isVATCustomsExceptionCustomsGroup 'Льготный НДС' = DATA BOOLEAN (CustomsGroup);
EXTEND FORM customsGroup
    PROPERTIES(cg) isVATCustomsExceptionCustomsGroup  
;
EXTEND DESIGN customsGroup {
    dateContainer {
        ADD PROPERTY(isVATCustomsExceptionCustomsGroup(cg));
    }
}

EXTEND FORM customsGroups
    PROPERTIES(cg) READONLYIF isReadonly() BEFORE deleteg isVATCustomsExceptionCustomsGroup 
;

isVATCustomsExceptionDeclarationDetail(d) = VATCustomsExceptionDeclarationDetail(d) OR isVATCustomsExceptionCustomsGroup(customsGroupDeclarationDetail(d)); 
