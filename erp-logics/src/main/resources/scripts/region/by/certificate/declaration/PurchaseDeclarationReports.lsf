MODULE PurchaseDeclarationReports;

REQUIRE PurchaseDeclarationDetailBy;

NAMESPACE Purchase;

// -------------------------------------------- Сбивка по декларации ------------------------------------- //

left6 = FORMULA STRING[6] 'left($1, 6)';  // ISO

quantity 'Кол-во' (declaration, code)= GROUP SUM quantity(DeclarationDetail detail) 
    BY declaration(detail), left6(codeCustomsGroup(detail));
    
quantity 'Кол-во' (code, detail)= GROUP SUM quantity(DeclarationDetail detail) 
    BY left6(codeCustomsGroup(detail)), detail;    
    
nameCustoms'Наименование' (declaration, code)= GROUP MIN nameCustoms(DeclarationDetail detail) 
    BY declaration(detail), left6(codeCustomsGroup(detail));    
        
sum 'Стоимость' (declaration, code)= GROUP SUM sum(DeclarationDetail detail) 
    BY declaration(detail), left6(codeCustomsGroup(detail));
sumNetWeight 'Вес нетто, кг' (declaration, code)= GROUP SUM sumNetWeight(DeclarationDetail detail) 
    BY declaration(detail), left6(codeCustomsGroup(detail));
sumGrossWeight 'Вес брутто, кг' (declaration, code)= GROUP SUM sumGrossWeight(DeclarationDetail detail) 
    BY declaration(detail), left6(codeCustomsGroup(detail));
        
            
FORM declarationReport 'Сбивка по декларации'

    OBJECTS d = Declaration PANEL
    PROPERTIES(d) number, series, date, time, sumDeclarationDetail,
                  sumNetWeightDeclarationDetail, sumGrossWeightDeclarationDetail 
                  
    OBJECTS s=STRING[6]
    PROPERTIES objS = VALUE(s), nameCustoms(d,s), sum(d,s), sumNetWeight(d,s),
               sumGrossWeight(d,s)
    ORDER BY objS
    FILTERS quantity(d,s)
    
    OBJECTS dd=DeclarationDetail

    PROPERTIES(dd) number, invoices READONLY, nameCustoms, shortNameUOM,
                   nameCountry, sumNetWeight, sumGrossWeight,
                   codeCustomsGroup, nameVATCustomsException BACKGROUND backgroundVATCustomsException(dd),
                   quantity, price, sum, deliverySum, chargeSum, homeSum,
                   percentDuty, weightDuty, dutySum,
                   numberVAT, percentVAT, VATSum
                 

    FILTERS declaration(dd) == d,
            quantity(s,dd)

;
printReport 'Сбивка по декларации' (Declaration declaration) = { PRINT declarationReport OBJECTS d = declaration; }   IMAGE 'print.png' IN print;

EXTEND FORM declarations
    PROPERTIES (d) printReport TOOLBAR
;
