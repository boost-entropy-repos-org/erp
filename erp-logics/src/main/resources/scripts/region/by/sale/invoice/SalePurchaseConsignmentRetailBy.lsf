MODULE SalePurchaseConsignmentRetailBy;

REQUIRE PricingSalePurchase, ConsignmentBy, PricingSale;

NAMESPACE Sale;


useRetailPriceOperation 'Использовать розничные цены в ТТН (отгрузка на магазин)' = DATA BOOLEAN (Operation);
useVATOperation 'Печатать НДС в ТТН (отгрузка на магазин)' = DATA BOOLEAN (Operation);

EXTEND FORM operation
    PROPERTIES(o) useRetailPriceOperation
    PROPERTIES(o) SHOWIF useRetailPriceOperation(o) useVATOperation
;
DESIGN operation {
    purchaseContainer {
        ADD PROPERTY(useRetailPriceOperation(o)) AFTER PROPERTY(namePurchasePricingOperationOperation(o));
        ADD PROPERTY(useVATOperation(o)) AFTER PROPERTY(useRetailPriceOperation(o));
    }
}
@defineDocumentInterfaceProperty (invoice, useRetailPrice, 'Использовать розн. цены в ТТН');
@deriveDocumentOperationProperty(UserInvoice, useRetailPrice);
@defineDocumentInterfaceProperty (invoice, useVAT, 'Печатать НДС в ТТН');
@deriveDocumentOperationProperty(UserInvoice, useVAT);

EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundPurchaseRetailInvoice(i) useRetailPriceUserInvoice
    PROPERTIES(i) SHOWIF useRetailPriceUserInvoice(i) BACKGROUND backgroundPurchaseRetailInvoice(i)
                  useVATUserInvoice                 
;
DESIGN userInvoice {
    headerCreateSalePricing  {
        ADD PROPERTY(useRetailPriceUserInvoice(i));
        ADD PROPERTY(useVATUserInvoice(i));
    }
}
         
overPriceConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d)
    THEN 
        IF createPurchasePricingInvoice(invoiceInvoiceDetail(d)) 
            THEN
                IF useVATInvoiceDetail(d) THEN NUMERIC[14,2](round0((purchaseRetailPriceInvoiceDetail(d) (-) purchaseRetailVATSumInvoiceDetail(d)/quantityConsignmentDetail(d)))) 
                ELSE purchaseRetailPriceInvoiceDetail(d)
            ELSE
                IF useVATInvoiceDetail(d) THEN NUMERIC[14,2](round0((retailPriceInvoiceDetail(d) (-) retailVATSumInvoiceDetail(d)/quantityConsignmentDetail(d)))) 
                ELSE retailPriceInvoiceDetail(d);
                         
overSumConsignmentDetail(d) += WHEN  useRetailPriceInvoiceDetail(d)
    THEN 
        IF createPurchasePricingInvoice(invoiceInvoiceDetail(d)) 
            THEN
                IF useVATInvoiceDetail(d) THEN NUMERIC[16,2](round0(overPriceConsignmentDetail(d)) * purchasePricingQuantityInvoiceDetail(d))
                ELSE purchaseRetailSumInvoiceDetail(d)
            ELSE
                IF useVATInvoiceDetail(d) THEN NUMERIC[16,2](round0(overPriceConsignmentDetail(d)) * pricingQuantityInvoiceDetail(d))
                ELSE retailSumInvoiceDetail(d);    
                      
overVatConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
    THEN 
        IF createPurchasePricingInvoice(invoiceInvoiceDetail(d)) 
            THEN
                IF useVATInvoiceDetail(d) THEN valuePurchaseRetailVATInvoiceDetail(d)
                ELSE  0.0 IF d IS InvoiceDetail
            ELSE
                IF useVATInvoiceDetail(d) THEN valueRetailVATInvoiceDetail(d)
                ELSE  0.0 IF d IS InvoiceDetail;
                
overSumVATConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
    THEN 
        IF createPurchasePricingInvoice(invoiceInvoiceDetail(d)) 
            THEN
                IF useVATInvoiceDetail(d) THEN purchaseRetailSumInvoiceDetail(d) (-) overSumConsignmentDetail(d)  
                ELSE 0.0 IF d IS InvoiceDetail
            ELSE
                IF useVATInvoiceDetail(d) THEN retailSumInvoiceDetail(d) (-) overSumConsignmentDetail(d)  
                ELSE 0.0 IF d IS InvoiceDetail;
        
overSumInvoiceConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
        THEN 
            IF createPurchasePricingInvoice(invoiceInvoiceDetail(d)) 
                THEN purchaseRetailSumInvoiceDetail(d)
                ELSE retailSumInvoiceDetail(d);

