MODULE OperationConsignmentBy;

REQUIRE Operation, ConsignmentBy;

NAMESPACE Operation;

META defineOperationConsignment(dumb)

    DESIGN operation {
        tabContainer {
            NEW consignment {
                caption = 'Оформление накладных';
            }
        }    
    }
    
    @defineOperationProperty(shipmentBase, 'Основание отпуска (ТТН)', VARSTRING[100], consignment);

    consignmentType 'Тип накладной' = DATA ConsignmentType (Operation);
    nameConsignmentType 'Тип накладной' (Operation o) = staticCaption(consignmentType(o));
    
    ownerTrackType 'Владелец автомобиля по умолчанию' = DATA OwnerTrackType (Operation);
    nameOwnerTrackType 'Владелец автомобиля по умолчанию' (Operation o) = staticCaption(ownerTrackType(o));
    
    EXTEND FORM operation
        PROPERTIES nameConsignmentType(o), nameOwnerTrackType (o);
    ;
    
    DESIGN operation {
        consignment {
            MOVE PROPERTY(nameConsignmentType(o));
            MOVE PROPERTY(nameOwnerTrackType(o));
        }
    }

    @defineOperationProperty(notUseDriverTruck, 'Не использовать справочники для выбора водителей и машин', consignment);
    @defineOperationProperty(notUseIssuanceAllowed, 'Не использовать справочник сотрудников "Отпуск разрешил"', consignment);
    @defineOperationProperty(notUseIssuanceExecuted, 'Не использовать справочник сотрудников "Отпуск произвел"', consignment);
    @defineOperationProperty(useGoodsAccepted, 'Использовать справочник сотрудников "Принял грузополучатель"', consignment);
    @defineOperationProperty(notPrintGr, 'Не писать прописью граммы в ТТН', consignment);
    @defineOperationProperty(banOpenedPrint, 'Запрещено печатать непроведенные документы', consignment); 
    
END