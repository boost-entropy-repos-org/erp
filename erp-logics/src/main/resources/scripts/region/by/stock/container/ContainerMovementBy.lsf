MODULE ContainerMovementBy;

REQUIRE ContainerMovement, StockDocumentBy;

NAMESPACE ContainerMovement;

FORM printContainerMovementBy 'Отчет по таре'

    OBJECTS params = (dFrom = DATE, dTo = DATE, dep = DepartmentStore) PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)
    PROPERTIES(dep) SELECTOR name, nameStore, nameLegalEntity

    OBJECTS bt=Batch
    PROPERTIES() balanceInBatches, balanceOutBatches, sumMoveOutBatches, balanceMoveInBatches,sumMoveInBatches, balanceMoveOutBatches 
    PROPERTIES READONLY accountSumContainerStockDateFrom(), accountSumContainerStockDateTo()
    PROPERTIES(bt) READONLY idBarcodeSku, nameSku, nameSupplier, lastPrice
    PROPERTIES(bt) READONLY balanceIn, sumIn, balanceOut, sumOut, balanceMoveIn, sumMoveIn, balanceMoveOut, sumMoveOut
    
    FILTERS inContainerMovement(bt)
    
    ORDER BY nameSku(bt)
;

printContainerMovementBy 'Распечатать' (DATE dateFrom, DATE dateTo, DepartmentStore departmentStore) =
    ACTION PRINT printContainerMovementBy OBJECTS dFrom = dateFrom, dTo = dateTo, dep = departmentStore   IMAGE 'print.png' IN print;

EXTEND FORM containerMovement

    PROPERTIES printContainerMovementBy(dFrom, dTo, dep) TODRAW params 
;

DESIGN containerMovement{

    print {
        MOVE PROPERTY(printContainerMovementBy(dFrom,dTo,dep));
    }
}
//-- 
FORM printStockDocumentContainerBy 'Товарный отчет с тарой'
    OBJECTS params = (dtFrom = DATE, dtTo = DATE) PANEL,
            ds = Stock PANEL
    PROPERTIES objFrom = OBJVALUE(dtFrom), objTo = OBJVALUE(dtTo)
    PROPERTIES(ds) SELECTOR fullName, address, nameLegalEntity, fullNameLegalEntity, nameCustomUserResponsiblePerson, namePositionResponsiblePerson, nameCustomUserBooker,
                            namePositionBooker, nameCustomUserController, namePositionController, UNP, 
                            shortNamePositionResponsiblies, positionShortNameResponsiblePerson, positionShortNameController,
                            positionShortNameBooker, namePositionResponsiblies

    PROPERTIES sumAccountBDocumentLedger(ds, dtFrom), sumItemAccountBDocumentLedger(ds, dtFrom), sumContainerAccountBDocumentLedger(ds, dtFrom),
               sumItemAccountADocumentLedger(ds, dtTo), sumContainerAccountADocumentLedger(ds, dtTo), sumAccountADocumentLedger(ds, dtTo)
    PROPERTIES(ds, dtFrom, dtTo) countIncStockDocumentInterval, countOutStockDocumentInterval,
               sumItemIncStockDocumentInterval, sumContainerIncStockDocumentInterval,
               subtotalItemIncStockDocumentInterval, subtotalContainerIncStockDocumentInterval,
               sumItemOutStockDocumentInterval, sumContainerOutStockDocumentInterval, countStockDocumentInterval

    OBJECTS il = IncStockDocumentLedger
    PROPERTIES(il) SELECTOR order, iDateTime = dateTime, iObjName = objectClassName, iDescription = description,
                   sumItem, sumContainer, sum, nameLegalEntity,
                   date, seriesNumber, nameLegalEntityStock,
                   overNameStockDocumentLedger
    FILTERS active(il),
            stock(il) == ds,
            date(il) >= dtFrom,
            date(il) <= dtTo
    ORDER BY date(il), order(il), seriesNumber(il) 

    OBJECTS ol = OutStockDocumentLedger
    PROPERTIES(ol) SELECTOR order, oDateTime = dateTime, oObjName = objectClassName, oDescription = description,
                   sumItem, sumContainer, sum, nameLegalEntity,
                   date, seriesNumber, nameLegalEntityStock,
                   overNameStockDocumentLedger
    FILTERS active(ol),
            stock(ol) == ds,
            date(ol) >= dtFrom,
            date(ol) <= dtTo
    ORDER BY date(ol),order(ol), seriesNumber(ol) 
    
    OBJECTS e=Employee 
    PROPERTIES (e) positionShortName, positionName
    FILTERS isResponsiblePerson(ds,e)
    PROPERTIES orderResponsiblePerson(ds,e)
    ORDER BY orderResponsiblePerson(ds,e)
    
    OBJECTS bt=(sk=Sku, p = NUMERIC[16,4])
    PROPERTIES() balanceInBatches, balanceOutBatches, sumMoveOutBatches, balanceMoveInBatches,sumMoveInBatches, balanceMoveOutBatches 

    PROPERTIES(sk,p) balanceInPrice, sumInPrice, balanceOutPrice, sumOutPrice, sumMoveOutPrice, balanceMoveInPrice,sumMoveInPrice, balanceMoveOutPrice 
    PROPERTIES READONLY accountSumContainerStockDateFrom(), accountSumContainerStockDateTo()
    PROPERTIES(sk) READONLY idBarcode, name
    PROPERTIES price = OBJVALUE (p)
    
    FILTERS isContainer(sk),
            balanceInPrice (sk,p) OR balanceOutPrice(sk,p) OR balanceMoveInPrice(sk,p) OR balanceMoveOutPrice(sk,p),
            sk IS Sku AND ds IS Stock
    
    ORDER BY name(sk)    
    
    OBJECTS e1=Employee 
    PROPERTIES (e1) positionShortName, positionName
    FILTERS isResponsiblePerson(ds,e1)    
    PROPERTIES orderResponsiblePerson(ds,e1)
    ORDER BY orderResponsiblePerson(ds,e1)
;

printDocumentContainerBy 'С тарой' (DATE dateFrom, DATE dateTo, Stock stock) = ACTION {    

    includeContainerMovementBatchFromTo (stock, dateFrom, dateTo);
    PRINT printStockDocumentContainerBy OBJECTS dtFrom = dateFrom, dtTo = dateTo, ds = stock ;
    
}  IMAGE 'print.png' IN print;
    
EXTEND FORM sumStockDocumentLedger

    PROPERTIES printDocumentContainerBy(dtFrom, dtTo, ds) TODRAW params 
;
DESIGN sumStockDocumentLedger{
    print {
        MOVE PROPERTY(printDocumentContainerBy(dtFrom,dtTo,ds));
    }
}