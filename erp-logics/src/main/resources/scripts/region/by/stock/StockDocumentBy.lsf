MODULE StockDocumentBy;

REQUIRE StockDocument, StockBy;

NAMESPACE StockDocument;

positionShortNameResponsiblePerson 'Материально ответственное лицо' (Stock stock) = positionShortName(responsiblePerson(stock));
positionShortNameController 'Материально ответственное лицо' (Stock stock) = positionShortName(controller(stock));
positionShortNameBooker 'Материально ответственное лицо' (Stock stock) = positionShortName(booker(stock));

overNameStockDocumentLedger(Object l) = OVERRIDE objectClassName(l), type(l), nameOperation(l);

FORM printStockDocumentBy 'Товарный отчет'
    OBJECTS params = (dtFrom = DATE, dtTo = DATE) PANEL,
            ds = Stock PANEL
    PROPERTIES objFrom = VALUE(dtFrom), objTo = VALUE(dtTo)
    PROPERTIES(ds) SELECTOR fullName, address, nameLegalEntity, fullNameLegalEntity, nameCustomUserResponsiblePerson, namePositionResponsiblePerson, nameCustomUserBooker,
                            namePositionBooker, nameCustomUserController, namePositionController, UNP, 
                            shortNamePositionResponsiblies, positionShortNameResponsiblePerson, positionShortNameController,
                            positionShortNameBooker

    PROPERTIES sumAccountBDocumentLedger(ds, dtFrom), sumItemAccountBDocumentLedger(ds, dtFrom), sumContainerAccountBDocumentLedger(ds, dtFrom),
               sumItemAccountADocumentLedger(ds, dtTo), sumContainerAccountADocumentLedger(ds, dtTo), sumAccountADocumentLedger(ds, dtTo)
    PROPERTIES(ds, dtFrom, dtTo) countIncStockDocumentInterval, countOutStockDocumentInterval,
               sumItemIncStockDocumentInterval, sumContainerIncStockDocumentInterval,
               subtotalItemIncStockDocumentInterval, subtotalContainerIncStockDocumentInterval,
               sumItemOutStockDocumentInterval, sumContainerOutStockDocumentInterval, countStockDocumentInterval

    OBJECTS il = IncStockDocumentLedger
    PROPERTIES(il) SELECTOR iDateTime = dateTime, iObjName = objectClassName, iDescription = description,
                   sumItem, sumContainer, sum, nameLegalEntity,
                   date, seriesNumber, nameLegalEntityStock,
                   overNameStockDocumentLedger
    FILTERS active(il),
            stock(il) == ds,
            date(il) >= dtFrom,
            date(il) <= dtTo
    ORDER BY date(il), seriesNumber(il) 

    OBJECTS ol = OutStockDocumentLedger
    PROPERTIES(ol) SELECTOR oDateTime = dateTime, oObjName = objectClassName, oDescription = description,
                   sumItem, sumContainer, sum, nameLegalEntity,
                   date, seriesNumber, nameLegalEntityStock,
                   overNameStockDocumentLedger
    FILTERS active(ol),
            stock(ol) == ds,
            date(ol) >= dtFrom,
            date(ol) <= dtTo
    ORDER BY date(ol), seriesNumber(ol) 
    
    OBJECTS e=Employee 
    PROPERTIES  positionShortName(e), orderResponsiblePerson(ds,e)
    FILTERS isResponsiblePerson(ds,e)
    ORDER BY orderResponsiblePerson(ds,e)
;

printDocumentBy 'Распечатать' (DATE dateFrom, DATE dateTo, Stock stock) =
    ACTION PRINT printStockDocumentBy OBJECTS dtFrom = dateFrom, dtTo = dateTo, ds = stock   IMAGE 'print.png' IN print;

EXTEND FORM sumStockDocumentLedger

    PROPERTIES printDocumentBy(dtFrom, dtTo, ds) TODRAW params 
;
DESIGN sumStockDocumentLedger{
    print {
        MOVE PROPERTY(printDocumentBy(dtFrom,dtTo,ds));
    }
}

// -------------------------------------- Суммы по операциям за интервал --------------------------------------------- //

FORM printStockDocumentOperationBy 'Товарный отчет'
    OBJECTS params = (dtFrom = DATE, dtTo = DATE) PANEL,
            ds = Stock PANEL
    PROPERTIES objFrom = VALUE(dtFrom), objTo = VALUE(dtTo)
    PROPERTIES(ds) SELECTOR fullName, nameLegalEntity, nameCustomUserResponsiblePerson, namePositionResponsiblePerson, nameCustomUserBooker,
                            namePositionBooker, nameCustomUserController, namePositionController, UNP

    PROPERTIES sumAccountBDocumentLedger(ds, dtFrom), sumItemAccountBDocumentLedger(ds, dtFrom), sumContainerAccountBDocumentLedger(ds, dtFrom),
               sumItemAccountADocumentLedger(ds, dtTo), sumContainerAccountADocumentLedger(ds, dtTo), sumAccountADocumentLedger(ds, dtTo)
    PROPERTIES(ds, dtFrom, dtTo) countIncStockDocumentInterval, countOutStockDocumentInterval,
               sumItemIncStockDocumentInterval, sumContainerIncStockDocumentInterval,
               subtotalItemIncStockDocumentInterval, subtotalContainerIncStockDocumentInterval,
               sumItemOutStockDocumentInterval, sumContainerOutStockDocumentInterval
//-- Приход                 
    OBJECTS o1 = Operation           
    PROPERTIES(o1) name, id
    PROPERTIES(ds,dtFrom,dtTo,o1) countIncStockDocumentInterval, sumItemIncStockDocumentInterval,
               sumContainerIncStockDocumentInterval            
    
    FILTERS countIncStockDocumentInterval(ds,dtFrom,dtTo,o1)
    
    OBJECTS il = IncStockDocumentLedger
    PROPERTIES(il) dateTime, objectClassName, description,
                   sumItem, sumContainer, sum
    FILTERS isPosted(il),
            stock(il) == ds,
            date(il) >= dtFrom,
            date(il) <= dtTo,
            operation(il) == o1              
    ORDER BY dateTime(il)
    
    
    PROPERTIES(ds,dtFrom,dtTo) countNotOperationIncStockDocumentInterval, sumItemNotOperationIncStockDocumentInterval,
               sumContainerNotOperationIncStockDocumentInterval  

    OBJECTS il1 = IncStockDocumentLedger
    PROPERTIES(il1) dateTime, objectClassName, description,
                   sumItem, sumContainer, sum
    FILTERS isPosted(il1) AND NOT operation(il1),
            stock(il1) == ds,
            date(il1) >= dtFrom,
            date(il1) <= dtTo
    ORDER BY dateTime(il1)                       
//-- Расход    
    OBJECTS o2 = Operation           
    PROPERTIES(o2) name, id
    PROPERTIES(ds,dtFrom,dtTo,o2) countOutStockDocumentInterval, sumItemOutStockDocumentInterval,
               sumContainerOutStockDocumentInterval                       
    FILTERS countOutStockDocumentInterval(ds,dtFrom,dtTo,o2)
        
    OBJECTS ol = OutStockDocumentLedger
    PROPERTIES(ol) dateTime, objectClassName, description,
                   sumItem, sumContainer, sum
    FILTERS isPosted(ol),
            stock(ol) == ds,
            date(ol) >= dtFrom,
            date(ol) <= dtTo,
            operation(ol) == o2
    ORDER BY dateTime(ol)
    

    PROPERTIES(ds,dtFrom,dtTo) countNotOperationOutStockDocumentInterval, sumItemNotOperationOutStockDocumentInterval,
               sumContainerNotOperationOutStockDocumentInterval     

    OBJECTS ol2 = OutStockDocumentLedger
    PROPERTIES(ol2) dateTime, objectClassName, description,
                   sumItem, sumContainer, sum
    FILTERS isPosted(ol2) AND NOT operation(ol2),
            stock(ol2) == ds,
            date(ol2) >= dtFrom,
            date(ol2) <= dtTo
            
    ORDER BY dateTime(ol2)
;

printDocumentOperationBy 'По операциям' (DATE dateFrom, DATE dateTo, Stock stock) =
    ACTION PRINT printStockDocumentOperationBy OBJECTS dtFrom = dateFrom, dtTo = dateTo, ds = stock   IMAGE 'print.png' IN print;

printDocumentOperationByXlsx 'По операциям (xlsx)' (DATE dateFrom, DATE dateTo, Stock stock) =
    ACTION PRINT printStockDocumentOperationBy OBJECTS dtFrom = dateFrom, dtTo = dateTo, ds = stock XLSX IMAGE 'print.png' IN print;

EXTEND FORM sumStockDocumentLedger

    PROPERTIES printDocumentOperationBy(dtFrom, dtTo, ds), printDocumentOperationByXlsx(dtFrom, dtTo, ds)  TODRAW params 
;

DESIGN sumStockDocumentLedger{
    print {
        MOVE PROPERTY(printDocumentOperationBy(dtFrom,dtTo,ds));
        MOVE PROPERTY(printDocumentOperationByXlsx(dtFrom,dtTo,ds));
    }
}