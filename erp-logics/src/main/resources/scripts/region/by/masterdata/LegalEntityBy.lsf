MODULE LegalEntityBy;

REQUIRE LegalEntity;

NAMESPACE LegalEntity;

UNP 'УНП' = DATA VARSTRING[9] (LegalEntity) IN doc FIXEDCHARWIDTH 9;
OKPO 'ОКПО' = DATA VARSTRING[20] (LegalEntity) IN doc MINCHARWIDTH 15 PREFCHARWIDTH 15;
OKYLP 'ОКЮЛП' = DATA VARSTRING[20] (LegalEntity) IN doc MINCHARWIDTH 15 PREFCHARWIDTH 15;

legalEntityUNP (unp) = GROUP MAX LegalEntity legalEntity BY UNP(legalEntity);

META defineDocumentUNPLegalEntity(object, contact, caption)
    UNP###contact 'УНП'###caption (###object object) = UNP(contact(object)) IN documentPrm;
    OKPO###contact 'ОКПО'###caption (###object object) = OKPO(contact(object)) IN documentPrm;
    OKYLP###contact 'ОКЮЛП'###caption (###object object) = OKYLP(contact(object)) IN documentPrm;
END



EXTEND FORM legalEntity

    PROPERTIES(l) SHOWIF toShowUNP(l) UNP  
    PROPERTIES(l) SHOWIF toShow(l) OKPO, OKYLP
;

DESIGN legalEntity{

    column2 {
        MOVE l.doc {
            columns = 3;
        }
    }
}

EXTEND FORM legalEntities
    PROPERTIES(l) READONLY UNP BEFORE shortNameOwnership(l)
;

EXTEND FORM supplierLegalEntities
    PROPERTIES(l) READONLY UNP BEFORE shortNameOwnership(l)
;

// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultOwnerships () += ACTION  {

    EXEC loadDefaultOwnership ('Закрытое акционерное общество', 'ЗАО', '112');
    EXEC loadDefaultOwnership ('Совместное закрытое акционерное общество', 'СЗАО', '112');
    EXEC loadDefaultOwnership ('Индивидуальный предприниматель', 'ИП', '112');
    EXEC loadDefaultOwnership ('Открытое акционерное общество', 'ОАО', '112');
    EXEC loadDefaultOwnership ('Открытое акционерное общество торговое', 'ОАОТ', '112');
    EXEC loadDefaultOwnership ('Общество с дополнительной ответственностью', 'ОДО', '112');
    EXEC loadDefaultOwnership ('Общество с ограниченной ответственностью', 'ООО', '112');
    EXEC loadDefaultOwnership ('Республиканское дочернее унитарное предприятие', 'РДУП', '112');
    EXEC loadDefaultOwnership ('Республиканское унитарное предприятие', 'РУП', '112');
    EXEC loadDefaultOwnership ('Коммунальное унитарное предприятие', 'КУП', '112');
    EXEC loadDefaultOwnership ('Совместное общество с ограниченной ответственностью', 'СООО', '112');
    EXEC loadDefaultOwnership ('Совместное предприятие', 'СП', '112');
    EXEC loadDefaultOwnership ('Сельскохозяйственный производственный кооператив', 'СПК', '112');
    EXEC loadDefaultOwnership ('Торговое частное унитарное предприятие', 'ТЧУП', '112');
    EXEC loadDefaultOwnership ('Унитарное предприятие', 'УП', '112');
    EXEC loadDefaultOwnership ('Частное торговое унитарное предприятие', 'ЧТУП', '112');
    EXEC loadDefaultOwnership ('Частное унитарное предприятие', 'ЧУП', '112');
    EXEC loadDefaultOwnership ('Частное унитарное торговое предприятие', 'ЧУТП', '112');

    EXEC overLoadDefaultOwnerships ();
};

loadDefaultLegalEntityGroups () += ACTION  {

    EXEC loadDefaultLegalEntityGroup ('Все', 'Все');
    EXEC loadDefaultRecLegalEntityGroup ('Поставщики', 'Поставщики', 'Все');
    EXEC loadDefaultRecLegalEntityGroup ('Покупатели', 'Покупатели', 'Все');
    EXEC loadDefaultRecLegalEntityGroup ('Компании', 'Компании', 'Все');

}

uniqueUNPSupplier 'Запретить повтор УНП для поставщиков' = DATA BOOLEAN ();
CONSTRAINT uniqueUNPSupplier() AND CHANGED (UNP(LegalEntity l)) AND [= GROUP SUM 1 IF isSupplier(LegalEntity l) BY UNP(l)](UNP(l)) > 1 MESSAGE 'Запрещен повтор УНП для поставщиков';

uniqueUNPCustomer 'Запретить повтор УНП для покупателей' = DATA BOOLEAN ();
CONSTRAINT uniqueUNPCustomer() AND CHANGED (UNP(LegalEntity l)) AND [= GROUP SUM 1 IF isCustomer(LegalEntity l) BY UNP(l)](UNP(l)) > 1 MESSAGE 'Запрещен повтор УНП для покупателей';

uniqueUNP 'Запретить повтор УНП' = DATA BOOLEAN ();
CONSTRAINT uniqueUNP() AND CHANGED (UNP(LegalEntity l)) AND [= GROUP SUM 1 BY UNP(LegalEntity l)](UNP(l)) > 1 MESSAGE 'Запрещен повтор УНП';

EXTEND FORM options
    PROPERTIES() uniqueUNPSupplier, uniqueUNPCustomer, uniqueUNP
;
DESIGN options {
    commons {
        MOVE PROPERTY(uniqueUNPSupplier());
        MOVE PROPERTY(uniqueUNPCustomer());
        MOVE PROPERTY(uniqueUNP());
    }
}




