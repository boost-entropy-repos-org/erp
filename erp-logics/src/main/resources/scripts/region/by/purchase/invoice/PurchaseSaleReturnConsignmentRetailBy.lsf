MODULE PurchaseSaleReturnConsignmentRetailBy;

REQUIRE PricingPurchaseReturn, PurchaseSaleReturnInvoice, ConsignmentBy;

NAMESPACE PurchaseReturn;

useRetailPriceOperation 'Использовать розничные цены в ТТН (возврат из магазина)' = DATA BOOLEAN (Purchase.Operation);
useVATOperation 'Печатать НДС в ТТН (возврат из магазина)' = DATA BOOLEAN (Purchase.Operation);
useEmptyVATOperation 'Печатать пустой НДС в ТТН (возврат из магазина)' = DATA BOOLEAN (Purchase.Operation);

EXTEND FORM Purchase.operation
    PROPERTIES(o) useRetailPriceOperation
    PROPERTIES(o) SHOWIF useRetailPriceOperation(o) useVATOperation
;
DESIGN Purchase.operation {
    saleReturnContainer {
        MOVE PROPERTY(useRetailPriceOperation(o)) AFTER PROPERTY(nameSaleReturnOperationOperation(o));
        MOVE PROPERTY(useVATOperation(o)) AFTER PROPERTY(useRetailPriceOperation(o));
    }
}
@defineDocumentInterfaceProperty (invoice, useRetailPrice, 'Использовать розн. цены в ТТН');
@deriveDocumentOperationProperty(UserInvoice, useRetailPrice);
@defineDocumentInterfaceProperty (invoice, useVAT, 'Печатать НДС в ТТН');
@deriveDocumentOperationProperty(UserInvoice, useVAT);

@defineDocumentInterfaceProperty (invoice, useEmptyVAT, 'Печатать пустой НДС в ТТН');
@deriveDocumentOperationProperty(UserInvoice, useEmptyVAT);

EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundRetailInvoice(i) useRetailPriceUserInvoice
    PROPERTIES(i) SHOWIF useRetailPriceUserInvoice(i) BACKGROUND backgroundRetailInvoice(i)
                  useVATUserInvoice, useEmptyVATUserInvoice                 
;
DESIGN userInvoice {
    headerCreatePricing  {
        MOVE PROPERTY(useRetailPriceUserInvoice(i));
        MOVE PROPERTY(useVATUserInvoice(i));
        MOVE PROPERTY(useEmptyVATUserInvoice(i));
    }
}

overPriceConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
    THEN    
        IF useVATInvoiceDetail(d) THEN NUMERIC[14,2](round0((retailPriceInvoiceDetail(d) (-) retailVATSumInvoiceDetail(d)/quantityConsignmentDetail(d)))) 
        ELSE retailPriceInvoiceDetail(d);         
overSumConsignmentDetail(d) += WHEN  useRetailPriceInvoiceDetail(d)
    THEN 
        IF useVATInvoiceDetail(d) THEN NUMERIC[16,2](round0(overPriceConsignmentDetail(d))*pricingQuantityInvoiceDetail(d))
        ELSE retailSumInvoiceDetail(d);    
          
useEmptyVATConsignmentDetail (d) += WHEN useRetailPriceInvoiceDetail(d) THEN  useEmptyVATInvoiceDetail(d);            
            
overVatConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
    THEN 
        IF useVATInvoiceDetail(d) THEN valueRetailVATInvoiceDetail(d)
        ELSE  0.0 IF d IS InvoiceDetail;
overSumVATConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
    THEN 
        IF useVATInvoiceDetail(d) THEN retailSumInvoiceDetail(d) (-) overSumConsignmentDetail(d)  
        ELSE 0.0 IF d IS InvoiceDetail; 
overSumInvoiceConsignmentDetail(d) += WHEN useRetailPriceInvoiceDetail(d) 
    THEN retailSumInvoiceDetail(d);
