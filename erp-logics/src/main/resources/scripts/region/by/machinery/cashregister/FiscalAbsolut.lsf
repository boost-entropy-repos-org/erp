MODULE FiscalAbsolut;

REQUIRE System,
        POS;


needDLL() = GROUP SUM 1 IF computer(CashRegister c) == currentComputer() AND sidModel(groupMachinery(c)) == 'Absolut';
onClientStarted() += {
	IF needDLL() THEN {
	    IF is64Java(currentComputer()) THEN
	        loadLibrary('lib/win64/absolut.dll');
	    ELSE
	        loadLibrary('lib/win32/absolut.dll');
	}
};

fiscalAbsolutPrint 'Напечатать фискальный чек' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutPrintReceiptActionProperty' (Receipt);
postPrint(Receipt receipt) += { IF sidCashRegisterModel(receipt) == 'Absolut' THEN fiscalAbsolutPrint(receipt); }

fiscalAbsolutReceiptTitle 'Заголовок чека Absolut' = ABSTRACT TEXT ();
fiscalAbsolutTop 'Дополнительный текст перед чеком Absolut' = ABSTRACT TEXT (Receipt);
fiscalAbsolutBottom 'Дополнительный текст после чека Absolut' = ABSTRACT TEXT (Receipt);

fiscalAbsolutPrintCopyReceipt 'Копия чека' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutPrintCopyReceiptActionProperty' ();
fiscalPrintCopyReceipt() += { IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutPrintCopyReceipt(); }

fiscalAbsolutXReport 'X-отчёт' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutXReportActionProperty' ();
fiscalXReport() += { IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutXReport(); }

fiscalAbsolutZReport 'Z-отчёт' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutZReportActionProperty' ();
fiscalZReport() += { IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutZReport(); }

fiscalAbsolutReportTop 'Заголовок отчёта Absolut' = ABSTRACT TEXT ();

fiscalAbsolutService 'Перемещение денег' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutServiceInOutActionProperty' (CashOperation);
fiscalService(CashOperation cashOperation) += { IF sidCashRegisterModel(cashOperation) == 'Absolut' THEN fiscalAbsolutService(cashOperation); }

fiscalAbsolutDisplayText 'Вывести текст на дисплей' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutDisplayTextActionProperty' (ReceiptDetail);
fiscalDisplayText (ReceiptDetail receiptDetail) += { IF sidCashRegisterModel(receiptDetail) == 'Absolut' THEN fiscalAbsolutDisplayText(receiptDetail); }

fiscalAbsolutCancel 'Отменить чек' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutCancelReceiptActionProperty' (Receipt);
fiscalCancel(Receipt receipt) += { IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutCancel(receipt); }

printZeroReceipt 'Нулевой чек при открытии z-отчёта (Absolut)' = DATA BOOLEAN ();
fiscalAbsolutOpenZReport 'Z-отчёт' = ACTION CUSTOM 'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutOpenZReportActionProperty' ();
fiscalOpenZReport() += { IF sidModelCurrentCashRegister() == 'Absolut' AND printZeroReceipt() THEN fiscalAbsolutOpenZReport(); }

saveCommentOnFiscalTapeAbsolut 'Сохранять комментарий для контрольной ленты' = DATA BOOLEAN ();
groupPaymentsByVAT 'Разбивать оплату по ставкам НДС' = DATA BOOLEAN ();
fiscalAbsolutPrefixCode128 'Префикс для Code128' = DATA VARSTRING[10] ();
sumPaymentAbsolut 'Суммовой чек' = DATA BOOLEAN ();
maxLinesAbsolut 'Макс. кол-во строк наименования' = DATA INTEGER ();
printSumWithDiscountAbsolut 'Печатать сумму со скидкой' = DATA BOOLEAN ();
EXTEND FORM options PROPERTIES() saveCommentOnFiscalTapeAbsolut, groupPaymentsByVAT, printZeroReceipt,
                                 fiscalAbsolutPrefixCode128, sumPaymentAbsolut, maxLinesAbsolut, printSumWithDiscountAbsolut;
DESIGN options {
    machinery {
        NEW absolut {
            caption = 'Absolut';
            MOVE PROPERTY(saveCommentOnFiscalTapeAbsolut());
            MOVE PROPERTY(groupPaymentsByVAT());
            MOVE PROPERTY(printZeroReceipt());
            MOVE PROPERTY(fiscalAbsolutPrefixCode128());
            MOVE PROPERTY(sumPaymentAbsolut());
            NEW notSum {
                caption = 'Не суммовой чек';
                MOVE PROPERTY(maxLinesAbsolut());
                MOVE PROPERTY(printSumWithDiscountAbsolut());
            }
        }
    }
}

overChangeBarcodeSale(VARSTRING[30] string, Receipt receipt) += {
    IF startsWith(string, fiscalAbsolutPrefixCode128()) == 1 AND receipt IS Receipt AND string IS VARSTRING[30] THEN {
        FOR VARSTRING[30](Receipt r AS Receipt) == substrFrom(string, 5) DO {
            DIALOG searchSkuReturnReceipt OBJECTS st = departmentStore(r), c = cashRegister(r), s = r INPUT, r INPUT rd DO {
                createReturn(receipt, s, rd);
            }
            consumedChangeBarcodeSaleReceipt() <- TRUE;
        }
    }
};
