MODULE TripInvoiceConsignmentBy;

REQUIRE Shipment, Invoice, TripInvoiceBy, InvoiceConsignmentBy;

truckConsignment(invoice) += truckTrip(tripInvoice(invoice));
driverConsignment(invoice) += driverTrip(tripInvoice(invoice));
waybillConsignment(invoice) += seriesNumberTrip(tripInvoice(invoice));

META setTripPropTrip(prop)
    setTrip###prop##Trip = ACTION (invoice) {
        ASSIGN prop##Invoice(invoice) <- prop##Trip(tripInvoice(invoice));
    }
    WHEN CHANGED(prop##Trip(tripInvoice(invoice))) DO EXEC setTrip###prop##Trip(invoice);
END

issuanceAllowedTrip = DATA Employee(Trip);
nameIssuanceAllowedTrip 'Отпуск разрешил' (trip) = nameContact(issuanceAllowedTrip(trip));
@setTripPropTrip(issuanceAllowed);

issuanceExecutedTrip = DATA Employee(Trip);
nameIssuanceExecutedTrip 'Отпуск произвел' (trip) = nameContact(issuanceExecutedTrip(trip));
@setTripPropTrip(issuanceExecuted);

dataForwarderTrip = DATA Employee(Trip);
nameForwarderTrip 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (t)= nameContact(OVERRIDE driverTrip(t), dataForwarderTrip(t));

overForwarderTrip= OVERRIDE  driverTrip(t), dataForwarderTrip(t);

setTripForwarderTrip = ACTION (invoice) {
    ASSIGN dataForwarderInvoice(invoice) <- overForwarderTrip(tripInvoice(invoice));
}
WHEN CHANGED(overForwarderTrip(tripInvoice(invoice))) DO EXEC setTripForwarderTrip(invoice);

loadingExecuterTrip = DATA LegalEntity(Trip);
nameLoadingExecuterTrip 'Исполнитель погрузки' (trip) = nameLegalEntity(loadingExecuterTrip(trip));
@setTripPropTrip(loadingExecuter);

unloadingExecuterTrip = DATA LegalEntity(Trip);
nameUnloadingExecuterTrip 'Исполнитель разгрузки' (trip) = nameLegalEntity(unloadingExecuterTrip(trip));
@setTripPropTrip(unloadingExecuter);

wayOfLoadingTrip = DATA WayOfLoading(Trip);
nameWayOfLoadingTrip 'Способ погрузки' (trip) = nameWayOfLoading(wayOfLoadingTrip(trip));
@setTripPropTrip(wayOfLoading);

wayOfUnloadingTrip = DATA WayOfLoading(Trip);
nameWayOfUnloadingTrip 'Способ разгрузки' (trip) = nameWayOfLoading(wayOfUnloadingTrip(trip));
@setTripPropTrip(wayOfUnloading);

codeLoadingTrip 'Код ПРР' = DATA STRING[3] (Trip);
@setTripPropTrip(codeLoading);

currencyTrip 'Валюта (ИД)' = DATA Currency(Trip);
shortNameCurrencyTrip 'Валюта' (trip) = shortNameCurrency(currencyTrip(trip));
documentNameCurrencyTrip 'Валюта в накладной сокр.' (trip) = documentNameCurrency(currencyTrip(trip));
@setTripPropTrip(currency);

//Время прибытия                              
timeOfArrivalTrip 'Время прибытия' = DATA TIME (Trip);
setTripDateTimeTrip = ACTION (invoice) {
    ASSIGN arrivalTimeInvoice(invoice) <- dateTimeToDateTime(dateInvoice(invoice), timeOfArrivalTrip(tripInvoice(invoice)));
}
WHEN CHANGED(dateTimeTrip(tripInvoice(invoice))) DO EXEC setTripDateTimeTrip(invoice);

downtimeTrip 'Время простоя (мин.)' = DATA  INTEGER (Trip);
setTripDowntimeTrip = ACTION (invoice) {
    ASSIGN downtimeInvoice(invoice) <- downtimeTrip(tripInvoice(invoice));
}
WHEN CHANGED(downtimeTrip(tripInvoice(invoice))) DO EXEC setTripDowntimeTrip(invoice);

overCopyTrip(t, trip) += ACTION(t, trip) {
    ASSIGN issuanceAllowedTrip(t) <- issuanceAllowedTrip(trip);         
    ASSIGN issuanceExecutedTrip(t) <- issuanceExecutedTrip(trip);        
    ASSIGN dataForwarderTrip(t) <- dataForwarderTrip(trip);        
    ASSIGN loadingExecuterTrip(t) <- loadingExecuterTrip(trip);        
    ASSIGN unloadingExecuterTrip(t) <- unloadingExecuterTrip(trip);
    ASSIGN wayOfLoadingTrip(t) <- wayOfLoadingTrip(trip);
    ASSIGN wayOfUnloadingTrip(t) <- wayOfUnloadingTrip(trip);
    ASSIGN codeLoadingTrip(t) <- codeLoadingTrip(trip);
    ASSIGN currencyTrip(t) <- currencyTrip(trip);
    ASSIGN timeOfArrivalTrip(t) <- timeOfArrivalTrip(trip);
    ASSIGN downtimeTrip(t) <- downtimeTrip(trip);
}

printHorizontalConsignmentTrip 'Печать ТТН-1 (гор.)' (trip) = ACTION (trip) {
    FOR inTripInvoice(trip, invoice) DO {
        EXEC printConsignmentHorizontalA(invoice);
        EXEC printConsignmentHorizontalB(invoice);
        EXEC printConsignmentAttach(invoice);
    };
} TOOLBAR;

printVerticalConsignmentTrip 'Печать ТТН-1 (верт.)' (trip) = ACTION (trip) {
    FOR inTripInvoice(trip, invoice) DO {
        EXEC printConsignmentVerticalA(invoice);
        EXEC printConsignmentVerticalB(invoice);
        EXEC printConsignmentAttach(invoice);
    };
} TOOLBAR;

EXTEND FORM trip
    PROPERTIES (t) nameIssuanceAllowedTrip, nameIssuanceExecutedTrip, nameForwarderTrip, nameLoadingExecuterTrip, 
                   nameWayOfLoadingTrip, nameUnloadingExecuterTrip, nameWayOfUnloadingTrip, codeLoadingTrip, documentNameCurrencyTrip,
                   timeOfArrivalTrip, downtimeTrip
    PROPERTIES printHorizontalConsignmentTrip(t) TODRAW i
    PROPERTIES printVerticalConsignmentTrip(t) TODRAW i

;

EXTEND DESIGN trip {

    pane {
        NEW consignment AFTER invoices {
            caption = 'Атрибуты накладных';
            NEW issuanceContainer {
                caption = 'Отпуск';
                ADD PROPERTY(nameIssuanceAllowedTrip(t));
                ADD PROPERTY(nameIssuanceExecutedTrip(t));
                ADD PROPERTY(nameForwarderTrip(t));
            }
            NEW loadingContainer {
                caption = 'ППР';
                ADD PROPERTY(nameLoadingExecuterTrip(t));
                ADD PROPERTY(nameWayOfLoadingTrip(t));
                ADD PROPERTY(nameUnloadingExecuterTrip(t));
                ADD PROPERTY(nameWayOfUnloadingTrip(t));
                ADD PROPERTY(codeLoadingTrip(t));
                ADD PROPERTY(timeOfArrivalTrip(t));
                ADD PROPERTY(downtimeTrip(t));
            }
            NEW dopContainer {
                caption = 'Дополнительно';
                ADD PROPERTY(documentNameCurrencyTrip(t));
            }

        }
    }
}
