MODULE  BOMHeatTreatmentBy;

REQUIRE BOMBy, BOMHeatTreatment;

NAMESPACE BOM;

// ----------------- Акт контрольной проработки-------------------- //

actNettoNetWeightProduct 'Вес нетто изделия для акта г' (p) = actQuantityBOM(BOMProduct(p))* (OVERRIDE 1.0 IF p IS Product, netWeightSku(skuProduct(p)))*1000;
nameSkusBOMDate 'Компоненты (все)' = GROUP CONCAT nameSku(sku) IF recBruttoQuantityBOMSkuDate(BOM, sku, date), ', ' 
    BY BOM, date
    ORDER nameSku(sku);
    
netWeightProductBOM 'Вес' (BOM) = GROUP SUM netWeightSku(skuProduct(p))*1000 BY BOMProduct(p);    
    
//-- тепловая обработка    
countHeatTechnologiesComponent (c)= GROUP SUM 1 IF inTechnologyComponent(t, c) AND heatTechnology(t) BY c;    
    
minHeatTechnologyComponent (c) = GROUP MIN t IF inTechnologyComponent(t,c) AND heatTechnology(t) BY c;
maxHeatTechnologyComponent (c) = GROUP MAX t IF inTechnologyComponent(t,c) AND heatTechnology(t) BY c;
//equalsMaxMinHeatTechnologyComponent (c) = minHeatTechnologyComponent (c) == maxHeatTechnologyComponent (c);    
     
wastageHeatComponentDate (c,date) = [= (1-((100-X)*(100-Y)*(100-Z)/1000000))*100](
    (IF countHeatTechnologiesComponent(c) THEN (OVERRIDE 0 IF c IS Component, wastageTechnologyMaterialDate(minHeatTechnologyComponent(c),materialComponent(c), date)) ELSE 0 IF c IS Component),
    (IF countHeatTechnologiesComponent(c) > 1 THEN (OVERRIDE 0 IF c IS Component, wastageTechnologyMaterialDate(maxHeatTechnologyComponent(c),materialComponent(c), date)) ELSE 0 IF c IS Component),
    (IF heatWastageComponent(c) THEN (OVERRIDE 0 IF c IS Component, wastageComponent(c)) ELSE 0 IF c IS Component) 
);
//-- холодная обработка
countColdTechnologiesComponent (c)= GROUP SUM 1 IF inTechnologyComponent(t, c) AND NOT heatTechnology(t) BY c;    
    
minColdTechnologyComponent (c) = GROUP MIN t IF inTechnologyComponent(t,c) AND NOT heatTechnology(t) BY c;
maxColdTechnologyComponent (c) = GROUP MAX t IF inTechnologyComponent(t,c) AND NOT heatTechnology(t) BY c;
//equalsMaxMinColdTechnologyComponent (c) = minColdTechnologyComponent (c) == maxColdTechnologyComponent (c);    
     
wastageColdComponentDate (c,date) = [= (1-((100-X)*(100-Y)*(100-Z)/1000000))*100](
    (IF countColdTechnologiesComponent(c) THEN (OVERRIDE 0 IF c IS Component, wastageTechnologyMaterialDate(minHeatTechnologyComponent(c),materialComponent(c), date)) ELSE 0 IF c IS Component),
    (IF countColdTechnologiesComponent(c) > 1 THEN (OVERRIDE 0 IF c IS Component, wastageTechnologyMaterialDate(maxHeatTechnologyComponent(c),materialComponent(c), date)) ELSE 0 IF c IS Component),
    (IF NOT heatWastageComponent(c) THEN (OVERRIDE 0 IF c IS Component, wastageComponent(c)) ELSE 0 IF c IS Component) 
);    

actBruttoNetWeightComponentDate 'Выход полуфабриката брутто, г' (c,dt)=overBruttoQuantityComponentDate(c,dt)*actQuantityBOM(BOMComponent(c))*1000/quantityProductBOM(BOMComponent(c));
actNettoNetWeightComponent 'Выход полуфабриката нетто, г' (c)=nettoQuantityComponent(c)*actQuantityBOM(BOMComponent(c))*1000*(OVERRIDE 1.0 IF c IS Component, overNetWeightComponent(c))/quantityProductBOM(BOMComponent(c));
actBruttoNetWeightComponentSkuDate 'Вес товара для компонента, если он полуфабрикат' (component, sku, date)= recBruttoQuantityBOMSkuDate(BOMProduct(materialComponent(component)), sku, date) *
    bruttoQuantityComponentDate(component, date)/
    (quantityProduct(materialComponent(component)) IF quantityProduct(materialComponent(component))!=0) *
    (OVERRIDE 1.0 IF sku IS Sku, netWeightItem(sku))*
    actQuantityBOM(BOMComponent(component))*1000/
    (quantityProductBOM(BOMComponent(component)) IF quantityProductBOM(BOMComponent(component))!=0); 

actBruttoNetWeightSkusComponentDate 'Вес компонента, если он полуфабрикат по товарам' (component, date)= GROUP SUM actBruttoNetWeightComponentSkuDate(component, sku, date) BY component, date;

actNBruttoNetWeightComponentSkuDate 'Вес товара для компонента, если он полуфабрикат' (component, sku, date)= recNettoQuantityBOMSku(BOMProduct(materialComponent(component)), sku) *
    bruttoQuantityComponentDate(component, date)/
    (quantityProduct(materialComponent(component)) IF quantityProduct(materialComponent(component))!=0) *
    (OVERRIDE 1.0 IF sku IS Sku, netWeightItem(sku))*
    actQuantityBOM(BOMComponent(component))*1000/
    (quantityProductBOM(BOMComponent(component)) IF quantityProductBOM(BOMComponent(component))!=0); 

actNBruttoNetWeightSkusComponentDate 'Вес компонента, если он полуфабрикат по товарам' (component, date)= GROUP SUM actNBruttoNetWeightComponentSkuDate(component, sku, date) BY component, date;

actBruttoNetWeightProductSkuDate 'Вес изделия по товарам, брутто г' (product, sku, date)= recBruttoQuantityBOMSkuDate(BOMProduct(product), sku, date) *
    actQuantityBOM(BOMProduct(product))*1000/
    (quantityProduct(product) IF (quantityProduct(product)!=0)) *
    (OVERRIDE 1.0 IF sku IS Sku, netWeightItem(sku)); 

actBruttoNetWeightSkusProductDate 'Вес брутто изделия по товарам, г' (product, date)= GROUP SUM actBruttoNetWeightProductSkuDate(product, sku, date) BY product, date;
actPercProductDate '%' (p,dt) = (1 - (actNettoNetWeightProduct(p)/ (actBruttoNetWeightSkusProductDate(p,dt) IF actBruttoNetWeightSkusProductDate(p,dt)!=0)))*100;

FORM actStudyOfCulinaryBOM 'Акт контрольной проработки'
    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES (dt) date = OBJVALUE , extractYearBOMsDate 

    OBJECTS b=BOM FIXED PANEL 
    PROPERTIES(b)  READONLY productsBOM, productsUOMBOM, nameBOM, numberBOM, seriesBOM, fromDateBOM, toDateBOM, componentsBOM, overNettoNetWeightProductBOM, overNettoNetWeightComponentBOM, pricePercentBOM, noteBOM
    PROPERTIES(b) nameOwnershipCompanyBOM, nameCompanyBOM, shortNameOwnershipCompanyBOM, recipesBOM, extractYearFromDateBOM,
                  actQuantityBOM, nameActStockBOM, actStudyOfCulinaryCommitteeBOM, commonNameEmployeeCommitteeBOM, netWeightProductBOM, dateBOM    
   
    PROPERTIES(b,dt) firstShortNameChiefBOMDate, namePositionChiefBOMDate, nameSkusBOMDate

    OBJECTS p = Product
    PROPERTIES(p) READONLY nameSkuProduct, shortNameUOMProduct 
    PROPERTIES READONLY actNettoNetWeightProduct(p), actBruttoNetWeightSkusProductDate(p,dt), actPercProductDate(p,dt)
                  
    FILTERS       BOMProduct(p) == b

    OBJECTS c = Component FIXED GRID
    FILTERS       BOMComponent(c) == b,
                  materialComponent(c) IS Product     
    PROPERTIES    READONLY  nameMaterialComponent(c), wastageHeatComponentDate(c,dt), wastageColdComponentDate(c,dt), actBruttoNetWeightComponentDate(c,dt), 
                  actNettoNetWeightComponent(c), actBruttoNetWeightSkusComponentDate(c,dt), actNBruttoNetWeightSkusComponentDate(c,dt)         

    OBJECTS sk = Sku
    PROPERTIES(sk) READONLY nameSku 

    PROPERTIES     READONLY actBruttoNetWeightComponentSkuDate(c,sk,dt)
    FILTERS        actBruttoNetWeightComponentSkuDate(c,sk,dt), actNBruttoNetWeightComponentSkuDate(c,sk,dt)
    
    OBJECTS cc = Component
    PROPERTIES(cc) READONLY nameMaterialComponent, shortNameUOMComponent, overNettoNetWeightComponent
    PROPERTIES    READONLY  wastageHeatComponentDate(cc,dt), wastageColdComponentDate(cc,dt), actBruttoNetWeightComponentDate(cc,dt), 
                  actNettoNetWeightComponent(cc)                                    
                  
    FILTERS       BOMComponent(cc) == b,
                  materialComponent(cc) IS Sku     
                  
    OBJECTS e=Employee
    PROPERTIES(e) READONLY nameContact, namePositionEmployee
    FILTERS       inCommitteeBOMEmployee(b,e)                  
              
;
printActStudyOfCulinaryBOM 'Акт контрольной проработки новый'  = ACTION (BOM) NEWSESSION {
    FORM dialogDate MODAL;
        IF formResult() == FormResult.ok THEN {
            FORM actStudyOfCulinaryBOM OBJECTS dt = chosenDate('d'), b = BOM PRINT;
        }
    apply(); 
} IMAGE 'print.png' IN print;

EXTEND FORM BOMs
    PROPERTIES(b) FORCE  PANEL printActStudyOfCulinaryBOM    
;