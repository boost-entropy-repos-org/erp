MODULE  BOMHeatTreatmentBy;

REQUIRE BOMBy, BOMHeatTreatment;

NAMESPACE BOM;

// ----------------- Акт контрольной проработки-------------------- //

actNettoNetWeight 'Вес нетто изделия для акта г' (Product p) = actQuantity(BOM(p))* (OVERRIDE 1.0 IF p IS Product, netWeight(sku(p)))*1000;
nameSkus 'Компоненты (все)' = GROUP CONCAT Stock.name(Sku sku) IF recBruttoQuantity(BOM BOM, sku, DATE date), ', ' 
    BY BOM, date
    ORDER Stock.name(sku);
    
netWeightProduct 'Вес' (BOM) = GROUP SUM netWeight(sku(Product p))*1000 BY BOM(p);    
    
//-- тепловая обработка    
countHeatTechnologies (c)= GROUP SUM 1 IF in(Technology t, Component c) AND heat(t) BY c;    
    
minHeatTechnology (c) = GROUP MIN Technology t IF in(t,Component c) AND heat(t) BY c;
maxHeatTechnology (c) = GROUP MAX Technology t IF in(t,Component c) AND heat(t) BY c;
//equalsMaxMinHeatTechnologyComponent (c) = minHeatTechnologyComponent (c) == maxHeatTechnologyComponent (c);    
     
wastageHeat (Component c,DATE date) = [= (1-((100-X)*(100-Y)*(100-Z)/1000000))*100](
    (IF countHeatTechnologies(c) THEN (OVERRIDE 0 IF c IS Component, wastage(minHeatTechnology(c),material(c), date)) ELSE 0 IF c IS Component),
    (IF countHeatTechnologies(c) > 1 THEN (OVERRIDE 0 IF c IS Component, wastage(maxHeatTechnology(c),material(c), date)) ELSE 0 IF c IS Component),
    (IF heatWastage(c) THEN (OVERRIDE 0 IF c IS Component, wastage(c)) ELSE 0 IF c IS Component) 
);
//-- холодная обработка
countColdTechnologies (c)= GROUP SUM 1 IF in(Technology t, Component c) AND NOT heat(t) BY c;    
    
minColdTechnology (c) = GROUP MIN Technology t IF in(t,Component c) AND NOT heat(t) BY c;
maxColdTechnology (c) = GROUP MAX Technology t IF in(t,Component c) AND NOT heat(t) BY c;
//equalsMaxMinColdTechnologyComponent (c) = minColdTechnologyComponent (c) == maxColdTechnologyComponent (c);    
     
wastageCold (Component c,DATE date) = [= (1-((100-X)*(100-Y)*(100-Z)/1000000))*100](
    (IF countColdTechnologies(c) THEN (OVERRIDE 0 IF c IS Component, wastage(minHeatTechnology(c),material(c), date)) ELSE 0 IF c IS Component),
    (IF countColdTechnologies(c) > 1 THEN (OVERRIDE 0 IF c IS Component, wastage(maxHeatTechnology(c),material(c), date)) ELSE 0 IF c IS Component),
    (IF NOT heatWastage(c) THEN (OVERRIDE 0 IF c IS Component, wastage(c)) ELSE 0 IF c IS Component) 
);    

actBruttoNetWeight 'Выход полуфабриката брутто, г' (Component c,DATE dt)=overBruttoQuantity(c,dt)*actQuantity(BOM(c))*1000/quantityProduct(BOM(c));
actNettoNetWeight 'Выход полуфабриката нетто, г' (Component c)=nettoQuantity(c)*actQuantity(BOM(c))*1000*(OVERRIDE 1.0 IF c IS Component, overNetWeight(c))/quantityProduct(BOM(c));
actBruttoNetWeight 'Вес товара для компонента, если он полуфабрикат' (Component component, Sku sku, DATE date)= recBruttoQuantity(BOM(material(component)), sku, date) *
    bruttoQuantity(component, date)/
    (quantity(material(component)) IF quantity(material(component))!=0) *
    (OVERRIDE 1.0 IF sku IS Sku, netWeight[Item](sku))*
    actQuantity(BOM(component))*1000/
    (quantityProduct(BOM(component)) IF quantityProduct(BOM(component))!=0); 

actBruttoNetWeightSkus 'Вес компонента, если он полуфабрикат по товарам' (component, date)= GROUP SUM actBruttoNetWeight(Component component, Sku sku, DATE date) BY component, date;

actNBruttoNetWeight 'Вес товара для компонента, если он полуфабрикат' (Component component, Sku sku, DATE date)= recNettoQuantity(BOM(material(component)), sku) *
    bruttoQuantity(component, date)/
    (quantity(material(component)) IF quantity(material(component))!=0) *
    (OVERRIDE 1.0 IF sku IS Sku, netWeight[Item](sku))*
    actQuantity(BOM(component))*1000/
    (quantityProduct(BOM(component)) IF quantityProduct(BOM(component))!=0); 

actNBruttoNetWeightSkus 'Вес компонента, если он полуфабрикат по товарам' (component, date)= GROUP SUM actNBruttoNetWeight(Component component, Sku sku, DATE date) BY component, date;

actBruttoNetWeight 'Вес изделия по товарам, брутто г' (Product product, Sku sku, DATE date)= recBruttoQuantity(BOM(product), sku, date) *
    actQuantity(BOM(product))*1000/
    (quantity(product) IF (quantity(product)!=0)) *
    (OVERRIDE 1.0 IF sku IS Sku, netWeight[Item](sku)); 

actBruttoNetWeightSkus 'Вес брутто изделия по товарам, г' (product, date)= GROUP SUM actBruttoNetWeight(Product product, Sku sku, DATE date) BY product, date;
actPerc '%' (Product p,DATE dt) = (1 - (actNettoNetWeight(p)/ (actBruttoNetWeightSkus(p,dt) IF actBruttoNetWeightSkus(p,dt)!=0)))*100;

FORM actStudyOfCulinaryBOM 'Акт контрольной проработки'
    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES (dt) date = OBJVALUE , extractYearBOMs 

    OBJECTS b=BOM FIXED PANEL 
    PROPERTIES(b)  READONLY products, productsUOM, name, number, series, fromDate, toDate, components, overNettoNetWeightProduct, overNettoNetWeightComponent, pricePercent, note
    PROPERTIES(b) nameOwnershipCompany, nameCompany, shortNameOwnershipCompany, recipes, extractYearFromDate,
                  actQuantity, nameActStock, actStudyOfCulinaryCommittee, commonNameEmployeeCommittee, netWeightProduct, date    
   
    PROPERTIES(b,dt) firstShortNameChief, namePositionChief, nameSkus

    OBJECTS p = Product
    PROPERTIES(p) READONLY nameSku, shortNameUOM 
    PROPERTIES READONLY actNettoNetWeight(p), actBruttoNetWeightSkus(p,dt), actPerc(p,dt)
                  
    FILTERS       BOM(p) == b

    OBJECTS c = Component FIXED GRID
    FILTERS       BOM(c) == b,
                  material(c) IS Product     
    PROPERTIES    READONLY  nameMaterial(c), wastageHeat(c,dt), wastageCold(c,dt), actBruttoNetWeight(c,dt), 
                  actNettoNetWeight(c), actBruttoNetWeightSkus(c,dt), actNBruttoNetWeightSkus(c,dt)         

    OBJECTS sk = Sku
    PROPERTIES(sk) READONLY Stock.name 

    PROPERTIES     READONLY actBruttoNetWeight(c,sk,dt)
    FILTERS        actBruttoNetWeight(c,sk,dt), actNBruttoNetWeight(c,sk,dt)
    
    OBJECTS cc = Component
    PROPERTIES(cc) READONLY nameMaterial, shortNameUOM, overNettoNetWeight
    PROPERTIES    READONLY  wastageHeat(cc,dt), wastageCold(cc,dt), actBruttoNetWeight(cc,dt), 
                  actNettoNetWeight(cc)                                    
                  
    FILTERS       BOM(cc) == b,
                  material(cc) IS Sku     
                  
    OBJECTS e=Employee
    PROPERTIES(e) READONLY name[Contact], namePosition
    FILTERS       inCommittee(b,e)                  
              
;
printActStudyOfCulinaryNew 'Акт контрольной проработки новый'(BOM BOM)  = ACTION {
    DIALOG dialogDate OBJECTS d INPUT DO
        PRINT actStudyOfCulinaryBOM OBJECTS dt = d, b = BOM ;
} IMAGE 'print.png' IN print;

EXTEND FORM BOMs
    PROPERTIES(b) PANEL printActStudyOfCulinaryNew   
;