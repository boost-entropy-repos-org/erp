MODULE PriceLimitAlcohol;

REQUIRE PriceList, ItemFood;


CLASS AlcoholLimit 'Минимальные цены на алкоголь';
CLASS AlcoholLimitDetail 'Строка минимальных цен на алкоголь';

@defineDocument(alcoholLimit);
@deriveDocumentHeaderTimePrefix(AlcoholLimit, );

@defineDocumentPosted(alcoholLimit);

volume 'Объем, л' = DATA NUMERIC[9,3] (AlcoholLimitDetail) NONULL;
alcoholLimitDetail = GROUP AGGR AlcoholLimitDetail d BY volume(d);

value 'Содержание алкоголя, %' = DATA NUMERIC[8,3] (Alcohol);

EXTEND FORM attributesItem
    PROPERTIES (alcohol) READONLY value;
    
EXTEND FORM alcohols
    PROPERTIES (t) READONLY value;
    
EXTEND FORM alcohol 
    PROPERTIES (t) value;

basePrice 'Цена за 1 л' = DATA NUMERIC[16,5](AlcoholLimit);
basePrice 'Цена за 1 л'(AlcoholLimitDetail d) = basePrice(alcoholLimit(d));

baseAlcohol = DATA Alcohol(AlcoholLimit) NONULL;
nameBaseAlcohol 'Эталонное содержание алкоголя' (AlcoholLimit h) = name(baseAlcohol(h));
baseAlcohol (AlcoholLimitDetail d) = baseAlcohol(alcoholLimit(d));

roundCondition  = DATA RoundCondition(AlcoholLimit);
nameRoundCondition 'Условия округления'(AlcoholLimit h) = name(roundCondition(h));
roundCondition (AlcoholLimitDetail h) = roundCondition(alcoholLimit(h));

minPrice 'Минимальная цена'(AlcoholLimitDetail d, Alcohol a) = round(volume(d) * basePrice(d) / value(baseAlcohol(d)) * value(a), roundCondition(d));

FORM alcoholLimit 'Минимальные цены на алкоголь'

    OBJECTS r = AlcoholLimit PANEL
    PROPERTIES (r) isPosted, date, time, basePrice, nameBaseAlcohol, nameRoundCondition, note
                                                              
    PROPERTIES (r) READONLY countAlcoholLimitDetail       

    OBJECTS a = Alcohol GRID
    PROPERTIES (a) value
    
    OBJECTS d = AlcoholLimitDetail
    PROPERTIES (d) volume, basePrice
    PROPERTIES (d, a) minPrice COLUMNS (a) HEADER name(a)
    ORDER BY value(a)
    FILTERS value(a)
                
    PROPERTIES (d) NEW, deleteid=DELETE GRID

    PROPERTIES(r) TODRAW d deleteAlcoholLimitDetail
    
    FILTERS alcoholLimit(d) == r
    
    EVENTS
        ON OK prePost(r)

    EDIT AlcoholLimit OBJECT r
;

DESIGN alcoholLimit {
    BOX {
        preferredSize = (1024, 768);
        NEW header.box {
            type = CONTAINERH;

            NEW headerParams {
                fill = 1;
                type = CONTAINERV;
                MOVE GROUP(r,documentHeader) {
                    type = CONTAINERV;
                    NEW first {
                        type = CONTAINERH;
                        MOVE PROPERTY (isPosted(r));
                        MOVE PROPERTY(date(r));
                        MOVE PROPERTY(time(r));                 
                    }
                    NEW second {
                        type = CONTAINERH;
                        MOVE PROPERTY (basePrice(r));
                        MOVE PROPERTY(nameBaseAlcohol(r));
                        MOVE PROPERTY(nameRoundCondition(r));                 
                    }
                                                                                                                      
                }
                MOVE GROUP(r,documentPrm);
            }
            MOVE GROUP(r,documentSum) {
                columns = 1;
            }
        }
        NEW specification.box {
            fill = 1;
            MOVE BOX(d) {
                caption = 'Спецификация';
            }
            PROPERTY (value(a)){
                hide = TRUE;
            }
        }
        PROPERTY(formOk()) {
            caption = 'Провести';
        }
        MOVE TOOLBARBOX;

    }
}

copy 'Копировать' (AlcoholLimit alco)= {
    NEWSESSION {
        NEW a = AlcoholLimit {
            basePrice(a) <- basePrice(alco);
            baseAlcohol(a) <- baseAlcohol(alco);
            roundCondition(a) <- roundCondition(alco);
            FOR alco == alcoholLimit(AlcoholLimitDetail alcoDet) NEW d = AlcoholLimitDetail DO {
                alcoholLimit(d) <- a;
                volume(d) <- volume(alcoDet);
            }
            
            SHOW alcoholLimit OBJECTS r = a MANAGESESSION DOCKED NOCANCEL;
        }
    }
} EDITKEY 'F5' TOOLBAR;

FORM alcoholLimits 'Минимальные цены на алкоголь'

    OBJECTS r = AlcoholLimit 
    PROPERTIES (r) READONLY isPosted, date, time,basePrice, nameBaseAlcohol, nameRoundCondition, note        
    PROPERTIES (r) READONLY countAlcoholLimitDetail
    
    PROPERTIES (r) NEWSESSION NEW, EDIT, copy, deleter=DELETE   
    PROPERTIES (r) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer,
                    postedNameUser, postedTime, postedHostnameComputer 
    
    OBJECTS a = Alcohol GRID
    PROPERTIES (a) value
    
    OBJECTS d = AlcoholLimitDetail
    PROPERTIES (d) READONLY volume, basePrice
    PROPERTIES (d, a) READONLY minPrice COLUMNS (a) HEADER name(a)
    ORDER BY value(a)
    FILTERS value(a)

    FILTERS alcoholLimit(d) == r
                
    LIST AlcoholLimit OBJECT r
;
DESIGN alcoholLimits {
    BOX {
        preferredSize = (1024, 768);

        NEW documentContainer BEFORE TOOLBARBOX {
            fill = 1;

            type = SPLITV;
            MOVE BOX(r);    

            NEW documentDetail {
                fill = 1;
                type = TABBED;
                PROPERTY (value(a)){ hide = TRUE; }
                MOVE BOX(d) {
                    fill = 1;
                    caption = 'Спецификация';
                }
                NEW documentHistory {
                    caption = 'История';
                    type = CONTAINERV;

                    MOVE GROUP(r,created);
                    MOVE GROUP(r,posted);
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';
                        type = CONTAINERV;
                    }
                }
                NEW actionContainer {
                    caption = 'Действия';
                    type = CONTAINERH;
                    NEW createdContainer {
                        caption = 'Создание на основе';
                    }
                }
            }
        }
    }
}

NAVIGATOR {
    priceListDocuments{
        ADD alcoholLimits;
    }
}

minAlcoholLimitPrice = GROUP LAST minPrice(AlcoholLimitDetail ad, Alcohol a) 
            BY Item s, DATETIME dt
            ORDER dateTime(ad), ad
            WHERE isPosted(ad) AND dateTime(ad) <= dt AND volume(s) == volume(ad) AND alcohol(s) == a;