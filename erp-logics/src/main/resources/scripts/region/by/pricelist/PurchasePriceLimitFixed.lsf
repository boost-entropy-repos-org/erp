MODULE PurchasePriceLimitFixed;

REQUIRE PricingPurchase, PriceList;

NAMESPACE Purchase;

fixedLimitPriceListType = DATA DataPriceListType();
nameFixedLimitPriceListType 'Вид цены для ограничений по максимальным ценам' = name(fixedLimitPriceListType());

minLimitPriceListType = DATA DataPriceListType();
nameMinLimitPriceListType 'Вид цены для ограничений по минимальным ценам' = name(minLimitPriceListType());

EXTEND FORM options
    PROPERTIES() nameFixedLimitPriceListType, nameMinLimitPriceListType 
;

DESIGN options {
    pricings {
        MOVE PROPERTY(nameFixedLimitPriceListType());
        MOVE PROPERTY(nameMinLimitPriceListType());
    }
}

fixedLimitPrice(UserInvoiceDetail d) = prevPriceA(fixedLimitPriceListType(), sku(d), customerStock(d), pricingDateTime(d));
WHEN SESSION FORMS userInvoice GOAFTER retailPrice[UserInvoiceDetail] 
            SETCHANGED(retailPrice(UserInvoiceDetail d))
            AND fixedLimitPrice(d) < retailPrice(d) DO {
    retailPrice(d) <- fixedLimitPrice(d);
    retailMarkup(d) <- calcRetailMarkup(d);
}

minLimitPrice(UserInvoiceDetail d) = prevPriceA(minLimitPriceListType(), sku(d), customerStock(d), pricingDateTime(d));
WHEN SESSION FORMS userInvoice GOAFTER retailPrice[UserInvoiceDetail] 
            SETCHANGED(retailPrice(UserInvoiceDetail d))
            AND minLimitPrice(d) > retailPrice(d) DO {
    retailPrice(d) <- minLimitPrice(d);
    retailMarkup(d) <- calcRetailMarkup(d);
}
