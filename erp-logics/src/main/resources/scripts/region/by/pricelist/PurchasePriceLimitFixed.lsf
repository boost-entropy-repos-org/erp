MODULE PurchasePriceLimitFixed;

REQUIRE PricingPurchase, PriceList;

NAMESPACE Purchase;

fixedLimitPriceListType = DATA DataPriceListType();
nameFixedLimitPriceListType 'Вид цены для ограничений по максимальным ценам' = nameDataPriceListType(fixedLimitPriceListType());

minLimitPriceListType = DATA DataPriceListType();
nameMinLimitPriceListType 'Вид цены для ограничений по минимальным ценам' = nameDataPriceListType(minLimitPriceListType());

EXTEND FORM options
    PROPERTIES() nameFixedLimitPriceListType, nameMinLimitPriceListType 
;

DESIGN options {
    commons {
        MOVE PROPERTY(nameFixedLimitPriceListType());
    }
}

fixedLimitPriceUserInvoiceDetail(d) = priceALedgerPriceListTypeSkuStockDateTime(fixedLimitPriceListType(), skuUserInvoiceDetail(d), customerStockUserInvoiceDetail(d), dateTimeUserInvoiceDetail(d));
WHEN SESSION FORMS userInvoice GOAFTER retailPriceUserInvoiceDetail 
            CHANGED(retailPriceUserInvoiceDetail(d))
            AND fixedLimitPriceUserInvoiceDetail(d) < retailPriceUserInvoiceDetail(d) DO {
    retailPriceUserInvoiceDetail(d) <- fixedLimitPriceUserInvoiceDetail(d);

    retailMarkupUserInvoiceDetail(d) <- [= round2(min(((X/Z*100/(100+Y))-1)*100,99999))](
                                                       retailPriceUserInvoiceDetail(d),
                                                       pricingPriceUserInvoiceDetail(d),
                                                       valueRetailVATUserInvoiceDetail(d));
}

minLimitPriceUserInvoiceDetail(d) = priceALedgerPriceListTypeSkuStockDateTime(minLimitPriceListType(), skuUserInvoiceDetail(d), customerStockUserInvoiceDetail(d), dateTimeUserInvoiceDetail(d));
WHEN SESSION FORMS userInvoice GOAFTER retailPriceUserInvoiceDetail 
            CHANGED(retailPriceUserInvoiceDetail(d))
            AND minLimitPriceUserInvoiceDetail(d) > retailPriceUserInvoiceDetail(d) DO {
    retailPriceUserInvoiceDetail(d) <- minLimitPriceUserInvoiceDetail(d);

    retailMarkupUserInvoiceDetail(d) <- [= round2(min(((X/Z*100/(100+Y))-1)*100,99999))](
                                                       retailPriceUserInvoiceDetail(d),
                                                       pricingPriceUserInvoiceDetail(d),
                                                       valueRetailVATUserInvoiceDetail(d));
}
