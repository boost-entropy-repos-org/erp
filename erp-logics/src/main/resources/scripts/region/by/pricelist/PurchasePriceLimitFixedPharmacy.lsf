MODULE PurchasePriceLimitFixedPharmacy;

REQUIRE PricingPurchase, PriceList, PricingPurchasePriceDivider;

NAMESPACE Purchase;

maxLimitPriceListType = DATA DataPriceListType();
nameFixedLimitPriceListType 'Вид цены для ограничений по максимальным ценам' = name(maxLimitPriceListType());

minLimitPriceListType = DATA DataPriceListType();
nameMinLimitPriceListType 'Вид цены для ограничений по минимальным ценам' = name(minLimitPriceListType());

EXTEND FORM options
    PROPERTIES() nameFixedLimitPriceListType, nameMinLimitPriceListType 
;

DESIGN options {
    pricings {
        MOVE PROPERTY(nameFixedLimitPriceListType());
        MOVE PROPERTY(nameMinLimitPriceListType());
    }
}

maxLimitPrice 'Максимальная цена' (UserInvoiceDetail d) = prevPriceA(maxLimitPriceListType(), sku(d), customerStock(d), pricingDateTime(d));
WHEN LOCAL FORMS userInvoice GOAFTER retailPrice[UserInvoiceDetail] 
            SETCHANGED(retailPrice(UserInvoiceDetail d))
            AND maxLimitPrice(d) < retailPrice(d) DO {
    retailPrice(d) <- maxLimitPrice(d);        
    retailPrice(d) <- CASE WHEN round2(retailPrice(d)/priceDivider(d))*priceDivider(d) < retailPrice(d) AND 
                                round2(retailPrice(d)/priceDivider(d)/(1(+)valueRetailVAT(d)/100.0))*priceDivider(d) > price(d) 
                           THEN round2(retailPrice(d)/priceDivider(d))*priceDivider(d)
                           WHEN floor(retailPrice(d)/priceDivider(d)*100/(1(+)valueRetailVAT(d)/100.0))*priceDivider(d)/100 <= price(d)                             
                           THEN ceil(retailPrice(d)/priceDivider(d)*100)*priceDivider(d)/100
                           ELSE floor(retailPrice(d)/priceDivider(d)*100)*priceDivider(d)/100 WHERE round2(retailPrice(d)/priceDivider(d))*priceDivider(d) != retailPrice(d);            
    retailMarkup(d) <- calcRetailMarkup(d);
}

minLimitPrice 'Минимальная цена' (UserInvoiceDetail d) = prevPriceA(minLimitPriceListType(), sku(d), customerStock(d), pricingDateTime(d));
WHEN LOCAL FORMS userInvoice GOAFTER retailPrice[UserInvoiceDetail] 
            SETCHANGED(retailPrice(UserInvoiceDetail d))
            AND minLimitPrice(d) > retailPrice(d) DO {
    retailPrice(d) <- minLimitPrice(d);                
    retailPrice(d) <- CASE WHEN round2(retailPrice(d)/priceDivider(d))*priceDivider(d) < retailPrice(d) AND 
                                round2(retailPrice(d)/priceDivider(d)/(1(+)valueRetailVAT(d)/100.0))*priceDivider(d) > price(d) 
                           THEN round2(retailPrice(d)/priceDivider(d))*priceDivider(d)
                           WHEN floor(retailPrice(d)/priceDivider(d)*100/(1(+)valueRetailVAT(d)/100.0))*priceDivider(d)/100 <= price(d)                             
                           THEN ceil(retailPrice(d)/priceDivider(d)*100)*priceDivider(d)/100
                           ELSE floor(retailPrice(d)/priceDivider(d)*100)*priceDivider(d)/100 WHERE round2(retailPrice(d)/priceDivider(d))*priceDivider(d) != retailPrice(d);          
    retailMarkup(d) <- calcRetailMarkup(d);
}

EXTEND FORM userInvoice
    PROPERTIES(pd) SHOWIF createPricing[Invoice](i) minLimitPrice, maxLimitPrice
;