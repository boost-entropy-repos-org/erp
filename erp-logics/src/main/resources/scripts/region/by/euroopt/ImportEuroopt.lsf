MODULE ImportEuroopt;

REQUIRE System, Item, ItemDescription, ItemNutrition, ImportUserPriceList, PurchasePack;

CLASS EurooptItemGroup 'Группа товаров';
TABLE eurooptItemGroup (EurooptItemGroup);

title 'Заголовок' = DATA VARSTRING[100] (EurooptItemGroup);
url 'URL' = DATA VARSTRING[255] (EurooptItemGroup) INDEXED;
in 'Вкл.' = DATA LOCAL BOOLEAN (EurooptItemGroup);
eurooptItemGroup = GROUP BY url(EurooptItemGroup e) AGGR e; 

CLASS EurooptItemList 'Список товаров';
TABLE eurooptItemList (EurooptItemList);

eurooptItemGroup = DATA EurooptItemGroup(EurooptItemList);
url 'URL' = DATA VARSTRING[255] (EurooptItemList) INDEXED;
dataIn 'Вкл.' = DATA LOCAL BOOLEAN (EurooptItemList);
in 'Вкл.'(EurooptItemList e) = OVERRIDE dataIn(e), in(eurooptItemGroup(e));
eurooptItemList = GROUP BY url(EurooptItemList e) AGGR e; 

CLASS EurooptItem 'Товар';
TABLE eurooptItem (EurooptItem);

eurooptItemList = DATA EurooptItemList(EurooptItem);
idBarcode 'Штрихкод' (EurooptItem e) = DATA VARSTRING[15](EurooptItem);
noBarcode 'Без штрихкода' (EurooptItem e) = DATA BOOLEAN(EurooptItem);
sku(EurooptItem e) = skuBarcode(idBarcode(e));
name 'Наименование' (EurooptItem e) = name(sku(e));
url 'URL' = DATA VARSTRING[255] (EurooptItem) INDEXED;
dataIn 'Вкл.' = DATA LOCAL BOOLEAN (EurooptItem);
in 'Вкл.'(EurooptItem e) = OVERRIDE dataIn(e), in(eurooptItemList(e));
eurooptItem = GROUP BY url(EurooptItem e) AGGR e;

useTor 'Использовать Tor' = DATA BOOLEAN();
synchronizeItemsAction 'Синхронизировать списки товаров' = CUSTOM 'lsfusion.erp.region.by.euroopt.SynchronizeItemsEurooptActionProperty' ();
importItemsInfoAction 'Импортировать информацию о товарах' = CUSTOM 'lsfusion.erp.region.by.euroopt.ImportItemsInfoEurooptActionProperty' ();
onlyBarcode 'Импортировать только штрихкоды' = DATA BOOLEAN();
importImageAndPriceAction 'Импортировать изображения и цены товаров' = CUSTOM 'lsfusion.erp.region.by.euroopt.ImportImageAndPriceEurooptActionProperty' ();
importImages 'Импортировать изображения' = DATA BOOLEAN();
importPrices 'Импортировать цены' = DATA BOOLEAN();

idItemGroup 'Код группы товаров' (Barcode barcode) = id(skuGroup(sku(barcode)));

FORM exportEuroopt 'Экспорт товаров'
    OBJECTS i = EurooptItem
    PROPERTIES READONLY titleItemGroup 'Заголовок' = title(eurooptItemGroup(eurooptItemList(i))), 
                        urlItemGroup 'URL группы' = url(eurooptItemGroup(eurooptItemList(i))),
                        urlItemList 'URL списка' = url(eurooptItemList(i)),
                        url = url(i),
                        noBarcode 'Без штрихкода' = 1 IF noBarcode(i),
                        idBarcode = idBarcode(i)
    ORDER urlItemGroup, urlItemList, url
;

exportEuroopt 'Экспорт товаров (xls)' () = {
    PRINT exportEuroopt XLS;
}

importEuroopt 'Импорт товаров (xls)' () = {
    LOCAL NESTED titleItemGroup = VARSTRING[100] (INTEGER);
    LOCAL NESTED urlItemGroup = VARSTRING[255] (INTEGER);
    LOCAL NESTED urlItemList = VARSTRING[255] (INTEGER);
    LOCAL NESTED url = VARSTRING[255] (INTEGER);
    LOCAL NESTED noBarcode = VARSTRING[1] (INTEGER);
    LOCAL NESTED idBarcode = VARSTRING[15] (INTEGER);
    
    INPUT f = EXCELFILE DO { 
        DELETE EurooptItem e WHERE e IS EurooptItem;
        DELETE EurooptItemList e WHERE e IS EurooptItemList;
        DELETE EurooptItemGroup e WHERE e IS EurooptItemGroup;
        apply();
           
        IMPORT XLS TO titleItemGroup=B, urlItemGroup=C, urlItemList=D, url=E, noBarcode=F, idBarcode=G
                      FROM f AS EXCELFILE;
                      
        FOR INTEGER i == [=GROUP BY lower(urlItemGroup(INTEGER i)) MIN i IF i >= 2](VARSTRING[255] url) AND NOT eurooptItemGroup(url) NEW eig = EurooptItemGroup DO {
            url(eig) <- urlItemGroup(i);
            title(eig) <- titleItemGroup(i);
        }
        apply();
        
        FOR INTEGER i == [=GROUP BY lower(urlItemList(INTEGER i)) MIN i IF i >= 2](VARSTRING[255] url) AND NOT eurooptItemList(url) NEW eil = EurooptItemList DO {
            url(eil) <- urlItemList(i);
            eurooptItemGroup(eil) <- eurooptItemGroup(urlItemGroup(i));
        }
        apply();
        
        FOR INTEGER i == [=GROUP BY lower(url(INTEGER i)) MIN i IF i >= 2](VARSTRING[255] url) AND NOT eurooptItem(url) NEW ei = EurooptItem DO {
            url(ei) <- url(i);
            eurooptItemList(ei) <- eurooptItemList(urlItemList(i));
            noBarcode(ei) <- TRUE IF noBarcode(i) == '1';
            idBarcode(ei) <- idBarcode(i);
        }
        apply();
    }
}

EXTEND FORM integrationData
    PROPERTIES() synchronizeItemsAction, onlyBarcode, importItemsInfoAction, importImages, importPrices, importImageAndPriceAction, useTor,
                 exportEuroopt, importEuroopt
    OBJECTS eig = EurooptItemGroup, eil = EurooptItemList, ei = EurooptItem
    PROPERTIES(eig) READONLY in CHANGEABLE, title, url
    PROPERTIES(eil) in, url READONLY
    PROPERTIES(ei) READONLY in CHANGEABLE, url, noBarcode, idBarcode, name
    FILTERS eurooptItemGroup(eil) == eig, eurooptItemList(ei) == eil          
;
DESIGN integrationData {
    pane {
        NEW euroopt {
            caption = 'Евроопт';
            MOVE PROPERTY(useTor());          
            MOVE PROPERTY(synchronizeItemsAction());
            NEW items {
                caption = 'Импорт товаров';
                MOVE PROPERTY(onlyBarcode());      
                MOVE PROPERTY(importItemsInfoAction());                             
            }
            NEW prices {
                caption = 'Импорт изображений и цен';
                MOVE PROPERTY(importImages());
                MOVE PROPERTY(importPrices());
                MOVE PROPERTY(importImageAndPriceAction());
            }
            NEW xls {
                MOVE PROPERTY(exportEuroopt());
                MOVE PROPERTY(importEuroopt());
            }
            MOVE BOX(eig);
            MOVE BOX(eil);
            MOVE BOX(ei);
        }
    }
}