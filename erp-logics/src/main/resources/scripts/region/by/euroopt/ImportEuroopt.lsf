MODULE ImportEuroopt;

REQUIRE System, Item, ItemDescription, ItemNutrition, ImportUserPriceList, PurchasePack;

CLASS EurooptItemGroup 'Группа товаров';
TABLE eurooptItemGroup (EurooptItemGroup);

title 'Заголовок' = DATA VARSTRING[100] (EurooptItemGroup);
url 'URL' = DATA VARSTRING[255] (EurooptItemGroup);
in 'Вкл.' = DATA LOCAL BOOLEAN (EurooptItemGroup);
eurooptItemGroup = GROUP AGGR EurooptItemGroup e BY url(e); 

CLASS EurooptItemList 'Список товаров';
TABLE eurooptItemList (EurooptItemList);

eurooptItemGroup = DATA EurooptItemGroup(EurooptItemList);
url 'URL' = DATA VARSTRING[255] (EurooptItemList);
dataIn 'Вкл.' = DATA LOCAL BOOLEAN (EurooptItemList);
in 'Вкл.'(EurooptItemList e) = OVERRIDE dataIn(e), in(eurooptItemGroup(e));
eurooptItemList = GROUP AGGR EurooptItemList e BY url(e); 

CLASS EurooptItem 'Товар';
TABLE eurooptItem (EurooptItem);

eurooptItemList = DATA EurooptItemList(EurooptItem);
idBarcode 'Штрихкод' (EurooptItem e) = DATA VARSTRING[15](EurooptItem);
name (EurooptItem e) = name(skuBarcode(idBarcode(e)));
url 'URL' = DATA VARSTRING[255] (EurooptItem);
dataIn 'Вкл.' = DATA LOCAL BOOLEAN (EurooptItem);
in 'Вкл.'(EurooptItem e) = OVERRIDE dataIn(e), in(eurooptItemList(e));
eurooptItem = GROUP AGGR EurooptItem e BY url(e);

useTor 'Использовать Tor' = DATA BOOLEAN();
synchronizeItemsAction 'Синхронизировать списки товаров' = CUSTOM 'lsfusion.erp.region.by.euroopt.SynchronizeItemsEurooptActionProperty' ();
importItemsInfoAction 'Импортировать информацию о товарах' = CUSTOM 'lsfusion.erp.region.by.euroopt.ImportItemsInfoEurooptActionProperty' ();
onlyBarcode 'Импортировать только штрихкоды' = DATA BOOLEAN();
importImageAndPriceAction 'Импортировать изображения и цены товаров' = CUSTOM 'lsfusion.erp.region.by.euroopt.ImportImageAndPriceEurooptActionProperty' ();
importImages 'Импортировать изображения' = DATA BOOLEAN();
importPrices 'Импортировать цены' = DATA BOOLEAN();

idItemGroup 'Код группы товаров' (Barcode barcode) = id(skuGroup(sku(barcode)));

EXTEND FORM integrationData
    PROPERTIES() synchronizeItemsAction, onlyBarcode, importItemsInfoAction, importImages, importPrices, importImageAndPriceAction, useTor
    OBJECTS eig = EurooptItemGroup, eil = EurooptItemList, ei = EurooptItem
    PROPERTIES(eig) READONLY in CHANGEABLE, title, url
    PROPERTIES(eil) in, url READONLY
    PROPERTIES(ei) READONLY in CHANGEABLE, url, idBarcode, name
    FILTERS eurooptItemGroup(eil) == eig, eurooptItemList(ei) == eil          
;
DESIGN integrationData {
    pane {
        NEW euroopt {
            caption = 'Евроопт';
            MOVE PROPERTY(useTor());          
            MOVE PROPERTY(synchronizeItemsAction());
            NEW items {
                caption = 'Импорт товаров';
                MOVE PROPERTY(onlyBarcode());      
                MOVE PROPERTY(importItemsInfoAction());                             
            }
            NEW prices {
                caption = 'Импорт изображений и цен';
                MOVE PROPERTY(importImages());
                MOVE PROPERTY(importPrices());
                MOVE PROPERTY(importImageAndPriceAction());
            }
            MOVE BOX(eig);
            MOVE BOX(eil);
            MOVE BOX(ei);
        }
    }
}