MODULE WarePriceList;

REQUIRE PriceListType, Ware, WareItem;

NAMESPACE PriceList;

CLASS WarePriceListType 'Цена cо стеклопосудой' : PriceListType;

nameWarePriceListType 'Наименование' = DATA VARISTRING[50](WarePriceListType);
namePriceListType(type) += nameWarePriceListType(type) IF type IS WarePriceListType;

basePriceListTypeWarePriceListType(type) = DATA BasePriceListType (WarePriceListType);
nameBasePriceListTypeWarePriceListType 'Базовый вид цены' (type) = nameBasePriceListType(basePriceListTypeWarePriceListType(type));

ledgerPriceListTypePriceListType(type) += basePriceListTypeWarePriceListType(type) AS LedgerPriceListType;

includeVATPriceListType(type) += type IS WarePriceListType;

@defineDocumentHeaderCurrency(warePriceListType);
currencyPriceListType(warePriceListType) += currencyWarePriceListType(warePriceListType);

wareSku (sku) = ABSTRACT Ware (Sku);
wareSku (sku) += wareItem(sku);

// для товаров
priceBWarePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) =
    priceBBasePriceListTypeSkuStockDateTime(basePriceListTypeWarePriceListType(type), sku, stock, dateTime) (+)
    (warePriceDate(wareSku(sku), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);
priceAWarePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) =
    priceABasePriceListTypeSkuStockDateTime(basePriceListTypeWarePriceListType(type), sku, stock, dateTime) (+)
    (warePriceDate(wareSku(sku), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);

priceBPriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceBWarePriceListTypeSkuStockDateTime(type, sku, stock, dateTime);
priceAPriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceAWarePriceListTypeSkuStockDateTime(type, sku, stock, dateTime);

// для партий

priceBWarePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) =
    priceBBasePriceListTypeBatchStockDateTime(basePriceListTypeWarePriceListType(type), batch, stock, dateTime) (+)
    (warePriceDate(wareSku(skuBatch(batch)), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);
priceAWarePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) =
    priceABasePriceListTypeBatchStockDateTime(basePriceListTypeWarePriceListType(type), batch, stock, dateTime) (+)
    (warePriceDate(wareSku(skuBatch(batch)), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);

priceBPriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceBWarePriceListTypeBatchStockDateTime(type, batch, stock, dateTime);
priceAPriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceAWarePriceListTypeBatchStockDateTime(type, batch, stock, dateTime);

FORM warePriceListType 'Цена cо стеклопосудой'
    OBJECTS w = WarePriceListType FIXED PANEL
    PROPERTIES(w) nameWarePriceListType, includeVATPriceListType, nameCurrencyWarePriceListType, nameBasePriceListTypeWarePriceListType

    EDIT WarePriceListType OBJECT w
;

DESIGN warePriceListType {
    w.box{
        MOVE PROPERTY(nameWarePriceListType(w));
        MOVE PROPERTY(includeVATPriceListType(w));
        MOVE PROPERTY(nameCurrencyWarePriceListType(w));
        MOVE PROPERTY(nameBasePriceListTypeWarePriceListType(w));
    }
}

addWarePriceListType 'Добавить цену cо стеклопосудой' = ACTION ADDFORM WarePriceListType;
editWarePriceListType 'Редактировать' = ACTION EDITFORM WarePriceListType;
editPriceListType(priceListType) += ACTION editWarePriceListType(priceListType);

EXTEND FORM priceListTypes
    PROPERTIES() addWarePriceListType TODRAW pt FORCE PANEL
;