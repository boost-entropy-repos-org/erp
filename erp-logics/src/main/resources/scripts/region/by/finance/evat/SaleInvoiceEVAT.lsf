MODULE SaleInvoiceEvat;

REQUIRE SaleInvoice, SaleReturnInvoice, EVAT, CustomsGroup;

NAMESPACE Sale;

@defineOperationProperty(eVAT, 'Создавать ЭСЧФ', createContainer);

skipCreateEVAT = ABSTRACT BOOLEAN (Invoice);
skipCreateEVAT = ABSTRACT BOOLEAN (SaleReturn.Invoice);

createEVATSaleInvoice 'Накладные (продажа)' (DATE dFrom, DATE dTo) = ACTION {
    FOR eVAT(operation(Invoice i)) AND NOT skipCreateEVAT(i) AND date(i) >= dFrom AND date(i) <= dTo AND quantityInvoiceDetail(i) ADDOBJ e = EVAT DO {
        status(e) <- EVATStatus.original;
        
        date(e) <- date(i);
        
        numberContract (e) <- number(contractSku(i));
        dateContract (e) <- dateFrom(contractSku(i));
        codeDocType (e) <- 603;
        blankCodeDoc (e) <- OVERRIDE '402861', blankCodeDoc();
        seriesDoc(e) <- series(i);
        numberDoc(e) <- number(i);
        unpSender(e) <- UNP(supplier(i));
        
        //Поставщик
        supplier(e) <- supplier(i);
        branchCodeSupplier(e) <- branchCode(supplierStock(i));
        
        //Получатель
        customer(e) <- customer(i);
        branchCodeCustomer(e) <- branchCode(customerStock(i));
        
        noCustomer(e) <- NOT VATSumInvoiceDetail(i);
        
        FOR invoice(InvoiceDetail id) == i AND quantity(id) > 0 ADDOBJ ed = EVATDetail DO {
            evat(ed) <- e;

            name(ed) <- nameSku(id);
            code(ed) <- OVERRIDE '0000000000', codeCustomsGroup(defaultCountry(), sku(id));
            shortNameUOM(ed) <- shortNameUOM(sku(id));
            quantity(ed) <- quantity(id); 
            price(ed) <- price(id);
            sum(ed) <- sum(id);
            exciseSum(ed) <- 0;
            vatRate(ed) <- valueVAT(id);
            vatSum(ed) <- VATSum(id);
            sumWithVAT(ed) <- invoiceSum(id);
            
            IF valueVAT(id) == 0 THEN
                descriptionType(ed) <- DescriptionType.vatExcemption
            ELSE
                descriptionType(ed) <- DescriptionType.deductionInFull;
        } 
    } 
    APPLY {};

    FOR eVAT(operation(SaleReturn.Invoice i)) AND NOT skipCreateEVAT(i) AND date(i) >= dFrom AND date(i) <= dTo ADDOBJ e = EVAT DO {
        status(e) <- EVATStatus.additionalNoRef;
        
        date(e) <- date(i);
        
        numberContract (e) <- number(contractSku(i));
        dateContract (e) <- dateFrom(contractSku(i));
        codeDocType (e) <- 603;
        blankCodeDoc (e) <- '402861';
        seriesDoc(e) <- series(i);
        numberDoc(e) <- number(i);
        unpSender(e) <- UNP(supplier(i));
        
        //Поставщик
        supplier(e) <- supplier(i);
        branchCodeSupplier(e) <- branchCode(supplierStock(i));
        
        //Получатель
        customer(e) <- customer(i);
        branchCodeCustomer(e) <- branchCode(customerStock(i));
        
        noCustomer(e) <- NOT VATSumInvoiceDetail(i);

        FOR invoice(SaleReturn.InvoiceDetail id) == i AND quantity(id) > 0 ADDOBJ ed = EVATDetail DO {
            evat(ed) <- e;

            name(ed) <- nameSku(id);
            code(ed) <- OVERRIDE '0000000000', codeCustomsGroup(defaultCountry(), sku(id));
            shortNameUOM(ed) <- shortNameUOM(sku(id));
            quantity(ed) <- -quantity(id); 
            price(ed) <- price(id);
            sum(ed) <- -sum(id);
            exciseSum(ed) <- 0;
            vatRate(ed) <- valueVAT(id);
            vatSum(ed) <- -VATSum(id);
            sumWithVAT(ed) <- -invoiceSum(id);
            
            IF valueVAT(id) == 0 THEN
                descriptionType(ed) <- DescriptionType.vatExcemption
            ELSE
                descriptionType(ed) <- DescriptionType.deductionInFull;
        } 
    }
    APPLY {}
} CONFIRM;

EXTEND FORM evats
    PROPERTIES createEVATSaleInvoice(dFrom, dTo) TOOLBAR TODRAW e
;

DESIGN evats {
    generate {
        MOVE PROPERTY(createEVATSaleInvoice(dFrom, dTo));
    }
}