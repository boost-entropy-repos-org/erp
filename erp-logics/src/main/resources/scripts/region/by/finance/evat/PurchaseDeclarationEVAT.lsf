MODULE PurchaseDeclarationEVAT;

REQUIRE PurchaseDeclarationDetail, EVAT;

NAMESPACE Purchase;

createEVATDeclaration 'Таможенные декларации' (LegalEntity l, DATE dFrom, DATE dTo) = ACTION {
    FOR date(Declaration d) >= dFrom AND date(d) <= dTo AND isPosted(d) ADDOBJ e = EVAT DO {
        status(e) <- EVATStatus.original;
        
        date(e) <- date(d);
        
        numberContract (e) <- number([= GROUP MIN contractSku(InvoiceDetail d) IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        dateContract (e) <- dateFrom([= GROUP MIN contractSku(InvoiceDetail d) IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        
        legalEntityStatusSupplier(e) <- LegalEntityStatus.foreignOrganization;
        legalEntityStatusCustomer(e) <- LegalEntityStatus.buyerObjects;
        codeDocType (e) <- 609;
        dateDoc(e) <- date([= GROUP MIN InvoiceDetail d IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        seriesDoc(e) <- OVERRIDE 'ДК', series([= GROUP MIN InvoiceDetail d IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        numberDoc(e) <- number([= GROUP MIN InvoiceDetail d IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        unpSender(e) <- UNP(l);
        
        //Поставщик
        supplier(e) <- [= GROUP MIN supplier(InvoiceDetail d) IF currency(d) == currency(declaration(d)) BY declaration(d)](d);
//        branchCodeSupplier(e) <- branchCode(supplierStock(i));
        
        declarationSupplier(e) <- OVERRIDE 'не задан', number(d);
                
        //Получатель
        customer(e) <- l;
        declarationCustomer(e) <- number(d);
//        branchCodeCustomer(e) <- branchCode(customerStock(i));
        
        declarationCustomer(e) <- OVERRIDE 'не задан', number(d);
        
        //Грузоотправитель
        unpConsignor (e) <- OVERRIDE UNP(supplier(e)), UNPForeign(supplier(e)); 
        IF  isEEU(country(supplier(e))) THEN {
            consignor(e) <- VARISTRING[200](OVERRIDE name(supplier(e)), fullName(supplier(e)));
            countryCodeConsignor (e) <- sid(country(supplier(e)));
            
            shippingAddressConsignor(e) <- address([= GROUP MIN supplierStock(InvoiceDetail d) IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        }
        
        //Грузополучатель
        IF  isEEU(country(supplier(e))) THEN {
            consignee(e) <- VARISTRING[200](OVERRIDE name(customer(e)), fullName(customer(e)));
            countryCodeConsignee (e) <- sid(country(customer(e)));
            unpConsignee (e) <- UNP(customer(e)); 
            shippingAddressConsignee(e) <- address([= GROUP MIN customerStock(InvoiceDetail d) IF currency(d) == currency(declaration(d)) BY declaration(d)](d));
        }
                
        FOR declaration(DeclarationDetail id) == d ADDOBJ ed = EVATDetail DO {
            evat(ed) <- e;

            name(ed) <- nameCustoms(id);
            code(ed) <- codeCustomsGroup(id);
            quantity(ed) <- quantity(id); 
            price(ed) <- [= GROUP SUM homeSum(InvoiceDetail d) IF in(DeclarationDetail dd, d) BY dd](id) / quantity(id);
            sum(ed) <- [= GROUP SUM homeSum(InvoiceDetail d) IF in(DeclarationDetail dd, d) BY dd](id); //round2(sum(id) * rateExchange(declaration(id))); //homeSum(id) (+) dutySum(id);
            exciseSum(ed) <- 0;
            vatRate(ed) <- percentVAT(id);
            vatSum(ed) <- 0; // VATSum(id);
            sumWithVAT(ed) <- 0; // homeSum(id) (+) VATSum(id);
            
            descriptionType(ed) <- DescriptionType.importVAT;
        }
        FOR NUMERIC[16,5] vatSum == [= GROUP SUM adjustVATSum(DeclarationDetail id) BY declaration(id), percentVAT(id)](d, NUMERIC[10,5] vat) ADDOBJ ed = EVATDetail DO {
            evat(ed) <- e;
            name(ed) <- 'Сумма НДС, уплаченная при ввозе';
            code(ed) <- [= GROUP MAX codeCustomsGroup(DeclarationDetail id) BY declaration(id), percentVAT(id)](d, vat);
            sum(ed) <- 0;
            exciseSum(ed) <- 0;
            vatRate(ed) <- vat;
            vatSum(ed) <- vatSum;
            sumWithVAT(ed) <- 0;
            
            descriptionType(ed) <- DescriptionType.importVAT;
        }
    } 
    APPLY {};
} CONFIRM;

EXTEND FORM evats
    PROPERTIES createEVATDeclaration(l, dFrom, dTo) TOOLBAR TODRAW e
;

DESIGN evats {
    generate {
        MOVE PROPERTY(createEVATDeclaration(l, dFrom, dTo));
    }
}