MODULE PaymentDocumentBy;

REQUIRE System,
        LegalEntityBy,
        Stock,
        Utils,
        Bank,
        Numerator;
        
NAMESPACE Payment;

CLASS ABSTRACT PaymentDocument 'Платежный документ';

datePaymentDocument 'Дата' (paymentDocument) = ABSTRACT DATE (PaymentDocument);
numberPaymentDocument 'Номер' (paymentDocument) = ABSTRACT STRING[20] (PaymentDocument);

numberContractPaymentDocument 'Номер договора' (paymentDocument) = ABSTRACT STRING[20] (PaymentDocument);
dateContractPaymentDocument 'Дата договора' (paymentDocument) = ABSTRACT DATE (PaymentDocument);

notAcceptancePaymentDocument 'Без акцепта' =  ABSTRACT BOOLEAN (PaymentDocument);

currencyPaymentDocument =  ABSTRACT Currency (PaymentDocument);
nameCurrencyPaymentDocument 'Валюта' (document) = nameCurrency(currencyPaymentDocument(document));
sidCurrencyPaymentDocument 'Код валюты' (document) = sidCurrency(currencyPaymentDocument(document));
shortNameCurrencyPaymentDocument 'Валюта сокр.' (document) = shortNameCurrency(currencyPaymentDocument(document));

sumPaymentDocument 'Сумма' = ABSTRACT NUMERIC[16,2] (PaymentDocument);

// ---------------------------------- Плательщик ---------------------------- //

accountPayerPaymentDocument= ABSTRACT Bank.Account(PaymentDocument);
numberAccountPayerPaymentDocument 'р/сч плательщика' (document)= Bank.numberAccount(accountPayerPaymentDocument(document));
nameBankAccountPayerPaymentDocument 'Наименование банка плательщика' (document) = nameBankAccount(accountPayerPaymentDocument(document)) IN account;
addressBankAccountPayerPaymentDocument 'Адрес банка плательщика' (document) = addressBankAccount(accountPayerPaymentDocument(document)) IN account;
MFOBankAccountPayerPaymentDocument 'Код МФО банка плательщика' (document) = MFOBankAccount(accountPayerPaymentDocument(document)) IN account;
departmentBankAccountPayerPaymentDocument 'Отдел банка плательщика' (document) = departmentBankAccount(accountPayerPaymentDocument(document)) IN account;
CBUBankAccountPayerPaymentDocument 'ЦБУ банка плательщика' (document) = CBUBankAccount(accountPayerPaymentDocument(document)) IN account;
fullNameBankAccountPayerPaymentDocument= VARSTRING[255](CONCAT ', ', nameBankAccountPayerPaymentDocument(document),
                                                                     addressBankAccountPayerPaymentDocument(document),
                                                                     departmentBankAccountPayerPaymentDocument(document),
                                                                     CBUBankAccountPayerPaymentDocument(document));

payerPaymentDocument (document) = Bank.legalEntityAccount (accountPayerPaymentDocument(document));
UNPPayerPaymentDocument 'УНП плательщика' (document) = UNPLegalEntity(payerPaymentDocument(document));
addressPayerPaymentDocument 'Юридический адрес плательщика' (document) =
    addressLegalEntityDate(payerPaymentDocument(document), datePaymentDocument(document)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
namePayerPaymentDocument 'Полное наименование плательщика' (document) = nameLegalEntity(payerPaymentDocument(document)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

// ---------------------------------- Бенифициар ---------------------------- //

accountBeneficiariesPaymentDocument= ABSTRACT Bank.Account(PaymentDocument);
numberAccountBeneficiariesPaymentDocument 'р/сч бенефициара' (document)= Bank.numberAccount(accountBeneficiariesPaymentDocument(document));
nameBankAccountBeneficiariesPaymentDocument 'Наименование банка бенефициара' (document) = nameBankAccount(accountBeneficiariesPaymentDocument(document)) IN account;
addressBankAccountBeneficiariesPaymentDocument 'Адрес банка бенефициара' (document) = addressBankAccount(accountBeneficiariesPaymentDocument(document)) IN account;
MFOBankAccountBeneficiariesPaymentDocument 'Код МФО банка бенефициара' (document) = MFOBankAccount(accountBeneficiariesPaymentDocument(document)) IN account;
departmentBankAccountBeneficiariesPaymentDocument 'Отдел банка бенефициара' (document) = departmentBankAccount(accountBeneficiariesPaymentDocument(document)) IN account;
CBUBankAccountBeneficiariesPaymentDocument 'ЦБУ банка бенефициара' (document) = CBUBankAccount(accountBeneficiariesPaymentDocument(document)) IN account;

fullNameBankAccountBeneficiariesPaymentDocument= VARSTRING[255](CONCAT ', ', nameBankAccountBeneficiariesPaymentDocument(document),
                                                                          addressBankAccountBeneficiariesPaymentDocument(document),
                                                                          departmentBankAccountBeneficiariesPaymentDocument(document),
                                                                          CBUBankAccountBeneficiariesPaymentDocument(document));

beneficiariesPaymentDocument (document)= Bank.legalEntityAccount (accountBeneficiariesPaymentDocument(document));
UNPBeneficiariesPaymentDocument 'УНП бенефициара' (document) = UNPLegalEntity(beneficiariesPaymentDocument(document));
addressBeneficiariesPaymentDocument 'Юридический адрес бенефициара' (document) =
    addressLegalEntityDate(beneficiariesPaymentDocument(document), datePaymentDocument(document)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
nameBeneficiariesPaymentDocument 'Наименование бенефициара' (document) = nameLegalEntity(beneficiariesPaymentDocument(document)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

// ---------------------------------- Корреспондент ---------------------------- //
accountCorrespondentPaymentDocument= ABSTRACT Bank.Account(PaymentDocument);
numberAccountCorrespondentPaymentDocument 'р/сч корреспондента' (document)= Bank.numberAccount(accountCorrespondentPaymentDocument(document));
nameBankAccountCorrespondentPaymentDocument 'Наименование банка корреспондента' (document) = nameBankAccount(accountCorrespondentPaymentDocument(document)) IN account;
addressBankAccountCorrespondentPaymentDocument 'Адрес банка корреспондента' (document) = addressBankAccount(accountCorrespondentPaymentDocument(document)) IN account;
MFOBankAccountCorrespondentPaymentDocument 'Код МФО банка корреспондента' (document) = MFOBankAccount(accountCorrespondentPaymentDocument(document)) IN account;
departmentBankAccountCorrespondentPaymentDocument 'Отдел банка корреспондента' (document) = departmentBankAccount(accountCorrespondentPaymentDocument(document)) IN account;
CBUBankAccountCorrespondentPaymentDocument 'ЦБУ банка корреспондента' (document) = CBUBankAccount(accountCorrespondentPaymentDocument(document)) IN account;
fullNameBankAccountCorrespondentPaymentDocument= VARSTRING[255](CONCAT ', ', nameBankAccountCorrespondentPaymentDocument(document),
                                                                          addressBankAccountCorrespondentPaymentDocument(document),
                                                                          departmentBankAccountCorrespondentPaymentDocument(document),
                                                                          CBUBankAccountCorrespondentPaymentDocument(document));

correspondentPaymentDocument (document)= Bank.legalEntityAccount (accountCorrespondentPaymentDocument(document));
UNPCorrespondentPaymentDocument 'УНП корреспондента' (document) = UNPLegalEntity(correspondentPaymentDocument(document));
addressCorrespondentPaymentDocument 'Юридический адрес корреспондента' (document) =
    addressLegalEntityDate(correspondentPaymentDocument(document), datePaymentDocument(document)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
nameCorrespondentPaymentDocument 'Наименование корреспондента' (document) = nameLegalEntity(correspondentPaymentDocument(document)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

textPaymentDocument 'Назначение платежа' = ABSTRACT TEXT(PaymentDocument);

// ------------------------------------- Очередь --------------------------------- //

CLASS FilePayment 'Очередь';
TABLE filePayment (FilePayment);

nameFilePayment 'Наименование' = DATA VARSTRING[20](FilePayment) IN base;

FORM filePayment 'Очередь'
    OBJECTS t=FilePayment FIXED PANEL
    PROPERTIES(t) nameFilePayment
    EDIT FilePayment OBJECT t
;

FORM filePayments 'Очередь'
    OBJECTS t=FilePayment
    PROPERTIES(t) nameFilePayment READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameFilePayment(t)
    DIALOG FilePayment OBJECT t
;
DESIGN filePayments FROM DEFAULT { main{ preferredSize = (600, 400); } }

filePaymentPaymentDocument =  ABSTRACT FilePayment (PaymentDocument);
nameFilePaymentPaymentDocument 'Очередь' (document) = nameFilePayment(filePaymentPaymentDocument(document));

// ---------------------------- Формы для платежек --------------------------------- //

META definePaymentDocumentForm(form, caption)
    FORM form###document caption
        OBJECTS c=PaymentDocument FIXED PANEL

        PROPERTIES (c) datePaymentDocument, numberPaymentDocument, notAcceptancePaymentDocument, nameCurrencyPaymentDocument,
                       sidCurrencyPaymentDocument, shortNameCurrencyPaymentDocument, sumPaymentDocument, numberContractPaymentDocument, dateContractPaymentDocument,
                       numberAccountPayerPaymentDocument, nameBankAccountPayerPaymentDocument, addressBankAccountPayerPaymentDocument,
                       MFOBankAccountPayerPaymentDocument, departmentBankAccountPayerPaymentDocument, CBUBankAccountPayerPaymentDocument,
                       UNPPayerPaymentDocument, addressPayerPaymentDocument, namePayerPaymentDocument,
                       numberAccountBeneficiariesPaymentDocument, nameBankAccountBeneficiariesPaymentDocument, addressBankAccountBeneficiariesPaymentDocument,
                       MFOBankAccountBeneficiariesPaymentDocument, departmentBankAccountBeneficiariesPaymentDocument, CBUBankAccountBeneficiariesPaymentDocument,
                       UNPBeneficiariesPaymentDocument, addressBeneficiariesPaymentDocument, nameBeneficiariesPaymentDocument,
                       numberAccountCorrespondentPaymentDocument, nameBankAccountCorrespondentPaymentDocument, addressBankAccountCorrespondentPaymentDocument,
                       MFOBankAccountCorrespondentPaymentDocument, departmentBankAccountCorrespondentPaymentDocument, CBUBankAccountCorrespondentPaymentDocument,
                       UNPCorrespondentPaymentDocument, addressCorrespondentPaymentDocument, nameCorrespondentPaymentDocument,
                       textPaymentDocument, nameFilePaymentPaymentDocument,
                       fullNameBankAccountPayerPaymentDocument, fullNameBankAccountBeneficiariesPaymentDocument, fullNameBankAccountCorrespondentPaymentDocument

    ;
    print###form###document caption (paymentDocument) = ACTION FORM form###document OBJECTS c = paymentDocument PRINT IMAGE 'print.png' IN print;
END

@definePaymentDocumentForm(paymentRequest , 'Платежное требование');
@definePaymentDocumentForm(paymentOrder, 'Платежное поручение');

// ---------------------------- Формы для платежек (список на дату)--------------------------------- //

reportFilePrintPaymentDocumentD () = 'Payment_paymentRequestDocument_c.jrxml';

META definePaymentDocumentFormDate(form, caption)
    FORM form caption
        OBJECTS d=DATE REPORTFILE reportFilePrintPaymentDocumentD() FIXED PANEL
        PROPERTIES(d)  OBJVALUE

        OBJECTS c=PaymentDocument
        PROPERTIES (c) datePaymentDocument, numberPaymentDocument, notAcceptancePaymentDocument, nameCurrencyPaymentDocument,
                       sidCurrencyPaymentDocument, shortNameCurrencyPaymentDocument, sumPaymentDocument, numberContractPaymentDocument, dateContractPaymentDocument,
                       numberAccountPayerPaymentDocument, nameBankAccountPayerPaymentDocument, addressBankAccountPayerPaymentDocument,
                       MFOBankAccountPayerPaymentDocument, departmentBankAccountPayerPaymentDocument, CBUBankAccountPayerPaymentDocument,
                       UNPPayerPaymentDocument, addressPayerPaymentDocument, namePayerPaymentDocument,
                       numberAccountBeneficiariesPaymentDocument, nameBankAccountBeneficiariesPaymentDocument, addressBankAccountBeneficiariesPaymentDocument,
                       MFOBankAccountBeneficiariesPaymentDocument, departmentBankAccountBeneficiariesPaymentDocument, CBUBankAccountBeneficiariesPaymentDocument,
                       UNPBeneficiariesPaymentDocument, addressBeneficiariesPaymentDocument, nameBeneficiariesPaymentDocument,
                       numberAccountCorrespondentPaymentDocument, nameBankAccountCorrespondentPaymentDocument, addressBankAccountCorrespondentPaymentDocument,
                       MFOBankAccountCorrespondentPaymentDocument, departmentBankAccountCorrespondentPaymentDocument, CBUBankAccountCorrespondentPaymentDocument,
                       UNPCorrespondentPaymentDocument, addressCorrespondentPaymentDocument, nameCorrespondentPaymentDocument,
                       textPaymentDocument, nameFilePaymentPaymentDocument,
                       fullNameBankAccountPayerPaymentDocument, fullNameBankAccountBeneficiariesPaymentDocument, fullNameBankAccountCorrespondentPaymentDocument
        FILTERS  datePaymentDocument(c)==d

    ;
    print###form caption (date) = ACTION FORM form OBJECTS d = date PRINT IMAGE 'print.png' IN print;
END

@definePaymentDocumentFormDate(paymentRequestDocumentDate , 'Список П/Т за день');

// ----------------- Implement Платежки --------------- //


META definePaymentDocument (object)
    @definePaymentDocumentInner (object, ###object);
END

META definePaymentDocumentInner (object, class)

    text###object 'Назначение платежа' = DATA TEXT(class);

    notAcceptance###object 'Без акцепта' =  DATA BOOLEAN (class);

    filePayment###object =  DATA FilePayment (class);
    nameFilePayment###object 'Очередь' (object) = nameFilePayment(filePayment###object(object)) IN documentPrm;
END

META implementPaymentDocument (concrete)
    @implementPaymentDocumentInner (concrete, ###concrete);
END
META implementPaymentDocumentInner (concrete, class)
    EXTEND CLASS class : PaymentDocument;

    datePaymentDocument (document) += date###concrete(document);
    numberPaymentDocument (document) += seriesNumber###concrete(document);
    numberContractPaymentDocument (document) += seriesNumberContract###concrete(document);
    dateContractPaymentDocument (document) += dateContract###concrete(document);

    accountPayerPaymentDocument (document) += accountFrom###concrete(document);
    accountBeneficiariesPaymentDocument (document) += accountTo###concrete(document);

    notAcceptancePaymentDocument (document) += notAcceptance###concrete(document);
    currencyPaymentDocument (document) += currencyContract(contract###concrete(document));
    sumPaymentDocument (document) += sum###concrete(document);
    textPaymentDocument (document) += text###concrete(document);
    filePaymentPaymentDocument (document) += filePayment###concrete(document);

END
META extendFormPaymentDocument (form, concrete, object, container)
    EXTEND FORM  form
        PROPERTIES(concrete) notAcceptance###object, nameFilePayment###object, text###object;

    EXTEND DESIGN  form {
        paramContainer {
            ADD PROPERTY(notAcceptance###object(concrete));
            ADD PROPERTY(nameFilePayment###object(concrete));
        }
        ADD PROPERTY(text###object(concrete)) AFTER paramContainer {
            panelLabelAbove = TRUE;
            alignment = STRETCH;
        }
    }
END