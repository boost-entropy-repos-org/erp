MODULE ConsignmentBy;

REQUIRE System,
        LegalEntityBy,
        Stock,
        Sku,
        Employee,
        Utils,
        Transport,
        Numerator; // вот эту зависимость нужно будет убрать

NAMESPACE Consignment;

CLASS WayOfLoading 'Способ ПРР';
TABLE wayOfLoading (WayOfLoading);

nameWayOfLoading 'Наименование' = DATA VARISTRING[50](WayOfLoading) IN base;

FORM wayOfLoading
    OBJECTS w = WayOfLoading
    PROPERTIES(w) nameWayOfLoading, DELETESESSION
    PROPERTIES(w) ADDFORM, EDITFORM
    DIALOG WayOfLoading OBJECT w
;

GROUP sumConsignmentGroup 'Суммы' : base;

CLASS ABSTRACT Consignment 'Накладная';
CLASS ABSTRACT ConsignmentDetail 'Строка накладной';

dateConsignment 'Дата' (consignment) = ABSTRACT DATE (Consignment);

// ---------------------------------- Юридические лица ---------------------- //

countRowsSimpleVertical 'Макс. кол-во строк в бланке (ТН-2 вер.)' = DATA INTEGER ();
countRowsSimpleHorizontal 'Макс. кол-во строк в бланке (ТН-2 гор.)' = DATA INTEGER ();
countRowsVerticalA 'Макс. кол-во строк в бланке (ТТН-1 вер.А)' = DATA INTEGER ();
countRowsVerticalAB 'Макс. кол-во строк в бланке (ТТН-1 вер.А/Б)' = DATA INTEGER ();
countRowsHorizontal 'Макс. кол-во строк в бланке (ТТН-1 гор.А)' = DATA INTEGER ();

EXTEND FORM options
    PROPERTIES() countRowsSimpleVertical, countRowsSimpleHorizontal, countRowsVerticalA, countRowsVerticalAB, countRowsHorizontal

;

EXTEND DESIGN options {
    commons {
        NEW consignment  {
            type = CONTAINERV;
            caption = 'Накладная';
            ADD PROPERTY(countRowsSimpleVertical);
            ADD PROPERTY(countRowsSimpleHorizontal);
            ADD PROPERTY(countRowsVerticalA);
            ADD PROPERTY(countRowsVerticalAB);
            ADD PROPERTY(countRowsHorizontal);
        }

    }
}

// ---------------------------------- Юридические лица ---------------------- //
supplierConsignment = ABSTRACT LegalEntity (Consignment);
UNPSupplierConsignment 'УНП отправителя' (consignment) = UNPLegalEntity(supplierConsignment(consignment));
addressSupplierConsignment 'Юр. адрес отправителя' (consignment) =
    addressLegalEntityDate(supplierConsignment(consignment), dateConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
fullNameSupplierConsignment 'Наим-ие отправителя' (consignment) = fullNameLegalEntity(supplierConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

customerConsignment = ABSTRACT LegalEntity (Consignment);
UNPCustomerConsignment 'УНП получателя' (consignment) = UNPLegalEntity(customerConsignment(consignment));
addressCustomerConsignment 'Юр. адрес получателя' (consignment) =
    addressLegalEntityDate(customerConsignment(consignment), dateConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
fullNameCustomerConsignment 'Наим-ие получателя' (consignment) = fullNameLegalEntity(customerConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

payerConsignment = ABSTRACT LegalEntity (Consignment);
UNPPayerConsignment 'УНП заказчика перевозки' (consignment) = UNPLegalEntity(payerConsignment(consignment));
addressPayerConsignment 'Юр. адрес заказчика перевозки' (consignment) =
    addressLegalEntityDate(payerConsignment(consignment), dateConsignment(consignment));
fullNamePayerConsignment 'Наим-ие для накладных заказчика перевозки' (consignment) =
    fullNameLegalEntity(payerConsignment(consignment)) ;

// ------------------------------------- Атрибуты --------------------------------- //

GROUP carConsignmentGroup 'Автомобиль' : base;

truckConsignment = ABSTRACT Truck(Consignment);
dataNameTruckConsignment = ABSTRACT VARSTRING[70] (Consignment);
nameTruckConsignment 'Автомобиль' (consignment) = OVERRIDE nameTruck(truckConsignment(consignment)),
                                                           dataNameTruckConsignment(consignment) IN carConsignmentGroup;

dataOwnerConsignment = ABSTRACT VARSTRING[100] (Consignment);
ownerTruckConsignment 'Владелец автомобиля' (consignment) = OVERRIDE ownerTruck(truckConsignment(consignment)),
                                                                     dataOwnerConsignment(consignment) IN carConsignmentGroup;

dataTrailerConsignment = ABSTRACT VARSTRING[100] (Consignment);
trailerConsignment 'Прицеп' (consignment) = OVERRIDE trailerTruck(truckConsignment(consignment)),
                                                     dataTrailerConsignment(consignment) IN carConsignmentGroup;

driverConsignment = ABSTRACT Driver(Consignment);
dataNameDriverConsignment 'Водитель' = ABSTRACT VARSTRING[40] (Consignment);
nameDriverConsignment 'Водитель' (consignment) = OVERRIDE nameContact(driverConsignment(consignment)),
                                                          dataNameDriverConsignment(consignment) IN carConsignmentGroup;

dataWaybillConsignment 'Путевой лист' (consignment) = ABSTRACT VARSTRING[20] (Consignment) IN carConsignmentGroup;

waybillConsignment 'Путевой лист' (consignment) = ABSTRACT STRING[20] (Consignment);

overWaybillConsignment 'Путевой лист' (consignment) = OVERRIDE waybillConsignment(consignment), dataWaybillConsignment(consignment) IN carConsignmentGroup;


// Склад погрузки
supplierStockConsignment (consignment) = ABSTRACT Stock (Consignment);
nameSupplierStockConsignment 'Склад погрузки' (consignment) = nameStock(supplierStockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

dataAddressSupplierStockConsignment 'Пункт погрузки' (consignment) = ABSTRACT VARSTRING[50] (Consignment);
addressSupplierStockConsignment 'Пункт погрузки' (consignment) = OVERRIDE addressStock(supplierStockConsignment(consignment)),
                                                                          dataAddressSupplierStockConsignment (consignment) IN carConsignmentGroup;

// Склад разгрузки
customerStockConsignment (consignment) = ABSTRACT Stock (Consignment);
nameCustomerStockConsignment 'Склад разгрузки' (consignment) = nameStock(customerStockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

dataAddressCustomerStockConsignment 'Пункт разгрузки' (consignment) = ABSTRACT VARSTRING[50] (Consignment) IN carConsignmentGroup;
addressCustomerStockConsignment 'Пункт разгрузки' (consignment) = OVERRIDE addressStock(customerStockConsignment(consignment)),
                                                                           dataAddressCustomerStockConsignment (consignment) IN carConsignmentGroup;

readdressingConsignment 'Переадресовка' (consignment) = ABSTRACT VARSTRING[50] (Consignment) IN carConsignmentGroup;

// ------------------------------------- Отпуск товара --------------------------------- //

GROUP issuanceConsignmentGroup 'Отпуск' : base;

shipmentBaseConsignment 'Основание отпуска' (consignment) = ABSTRACT VARSTRING[30] (Consignment) IN issuanceConsignmentGroup;

issuanceAllowedConsignment (consignment) = ABSTRACT Employee (Consignment);
nameIssuanceAllowedConsignment 'Отпуск разрешил' (consignment) = nameContact(issuanceAllowedConsignment(consignment)) IN issuanceConsignmentGroup;

issuanceExecutedConsignment (consignment) = ABSTRACT Employee(Consignment);
nameIssuanceExecutedConsignment 'Отпуск произвел' (consignment) = nameContact(issuanceExecutedConsignment(consignment)) IN issuanceConsignmentGroup;

forwarderConsignment 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (consignment) = ABSTRACT VARSTRING[40] (Consignment) IN issuanceConsignmentGroup;

warrantConsignment 'По доверенности (номер, дата)' (consignment) = ABSTRACT VARSTRING[30] (Consignment) IN issuanceConsignmentGroup;
warrantHolderConsignment 'По доверенности выданной (наименование орг-ии)' (consignment) = ABSTRACT VARSTRING[100] (Consignment) IN issuanceConsignmentGroup;

goodsAcceptedConsignment 'Принял грузополучатель' (consignment) = ABSTRACT VARSTRING[40] (Consignment) IN issuanceConsignmentGroup;

// ------------------------------------- Погрузочно-разгрузочные работы --------------------------------- //

GROUP loadingConsignmentGroup 'ПРР' : base;

loadingExecuterConsignment (consignment) = ABSTRACT Employee(Consignment);
nameLoadingExecuterConsignment 'Исполнитель ПРР' (consignment) = nameContact(loadingExecuterConsignment(consignment)) IN loadingConsignmentGroup;

wayOfLoadingConsignment (consignment) = ABSTRACT WayOfLoading(Consignment);
nameWayOfLoadingConsignment 'Способ ПРР' (consignment) = nameWayOfLoading(wayOfLoadingConsignment(consignment)) IN loadingConsignmentGroup;

codeLoadingConsignment 'Код ПРР' (consignment) = ABSTRACT STRING[3] (Consignment)IN loadingConsignmentGroup;

arrivalTimeConsignment 'Время прибытия' (consignment) = ABSTRACT DATETIME(Consignment) IN carConsignmentGroup;
departureTimeConsignment 'Время убытия' (consignment) = ABSTRACT DATETIME(Consignment) IN carConsignmentGroup;
downtimeConsignment 'Время простоя' (consignment) = ABSTRACT STRING[10] (Consignment) IN carConsignmentGroup;

raceQuantityConsignment 'Количество ездок' (consignment) = ABSTRACT INTEGER (Consignment) IN carConsignmentGroup;

noteConsignment 'Примечание' (consignment) = ABSTRACT VARSTRING[100] (Consignment) MINCHARWIDTH 30 PREFCHARWIDTH 80;

namePayerConsignment 'Заказчик перевозки ' (consignment) =
    [FORMULA VARSTRING[100] 'CAST($1 AS TEXT) ||  \' , \' || CAST($2 AS TEXT)']
    (nameLegalEntity(payerConsignment(consignment)), UNPLegalEntity(payerConsignment(consignment))) IN carConsignmentGroup;

skuConsignmentDetail (consignmentDetail) = ABSTRACT Sku (ConsignmentDetail);
nameSkuConsignmentDetail 'Наименование товара' (consignmentDetail) = nameSku(skuConsignmentDetail(consignmentDetail));

shortNameUOMConsignmentDetail 'Единица измерения' (consignmentDetail) = shortNameUOMSku(skuConsignmentDetail(consignmentDetail));
//shortNameUOMConsignmentDetail 'Единица измерения' (consignmentDetail) = ABSTRACT STRING[15] (ConsignmentDetail);

consignmentConsignmentDetail (consignmentDetail) = ABSTRACT Consignment (ConsignmentDetail);

quantityConsignmentDetail 'Количество' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (ConsignmentDetail);

priceConsignmentDetail 'Цена' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);

shipmentPriceConsignmentDetail 'Цена поставщика' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
purchaseRetailPriceConsignmentDetail 'Цена розничная' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);

retailMarkupConsignmentDetail 'Надбавка розн.' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (ConsignmentDetail);
wholesaleMarkupConsignmentDetail 'Надбавка опт.' (consignmentDetail) = ABSTRACT NUMERIC[16,3] (ConsignmentDetail);

sumConsignmentDetail 'Стоимость' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

vatConsignmentDetail 'НДС, %' (consignmentDetail) = ABSTRACT NUMERIC[10,5] (ConsignmentDetail);

sumVATConsignmentDetail 'Сумма НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

sumInvoiceConsignmentDetail 'Сумма с НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

sumShipmentConsignmentDetail 'Сумма учётная' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

sumShipmentContainerConsignment 'Сумма тары' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);
sumShipmentItemConsignment 'Сумма товаров' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);

sumRetailVATConsignmentDetail 'Сумма НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

sumRetailContainerConsignment 'Сумма тары' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);
sumRetailItemConsignment 'Сумма товаров' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);

sumWholesaleMarkupConsignment 'Надбавка' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);

sumRetailConsignmentDetail 'Сумма розничная' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

packQuantityConsignmentDetail 'Количество грузовых мест' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (ConsignmentDetail);

grossWeightConsignmentDetail 'Масса груза, кг.' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (ConsignmentDetail);
grossWeightTonConsignmentDetail 'Масса груза, т.' (consignmentDetail) = round6(grossWeightConsignmentDetail(consignmentDetail) / 1000);

noteConsignmentDetail 'Примечание' (consignmentDetail) = ABSTRACT VARSTRING[100] (ConsignmentDetail) MINCHARWIDTH 30 PREFCHARWIDTH 80;
extraDescriptionConsignmentDetail 'Доп. наименование' (consignmentDetail) = ABSTRACT VARSTRING[1500] (ConsignmentDetail) MINCHARWIDTH 30 PREFCHARWIDTH 80;

countConsignmentDetailConsignment 'Количество строк' (consignment) = GROUP SUM 1 IF consignmentDetail IS ConsignmentDetail
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

quantityConsignmentDetailConsignment 'Количество (всего)' (consignment) = GROUP SUM quantityConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

packQuantityConsignmentDetailConsignment 'Общее количество грузовых мест' (consignment) = GROUP SUM packQuantityConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

packQuantityConsignment 'Общее количество грузовых мест' = ABSTRACT NUMERIC[8,2] (Consignment);
overPackQuantityConsignmentDetailConsignment 'Общее количество грузовых мест' (consignment) = OVERRIDE packQuantityConsignmentDetailConsignment(consignment),
                                                                                                       packQuantityConsignment(consignment);

grossWeightTonConsignmentDetailConsignment 'Общая масса груза, т.' (consignment) = GROUP SUM grossWeightTonConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

grossWeightConsignmentDetailConsignment 'Общая масса груза, кг.'(consignment) = GROUP SUM grossWeightConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;


isCustomsFlowConsignment 'Расход с СВХ' (consignment) = ABSTRACT BOOLEAN (Consignment);

currencyConsignment 'Валюта (ИД)' (consignment) = ABSTRACT Currency (Consignment);
shortNameCurrencyConsignment 'Валюта' (consignment) = shortNameCurrency(currencyConsignment(consignment));
documentNameCurrencyConsignment 'Валюта в накладной сокр.' (consignment) = documentNameCurrency(currencyConsignment(consignment));
isCurrencyConsignment 'Выбрана валюта' (consignment) = TRUE IF currencyConsignment(consignment);

//// Посуда на возврате
isWareConsignment 'Возврат поставщику' (consignment) = ABSTRACT BOOLEAN (Consignment);
//wareSumConsignmentDetailConsignment 'Сумма посуды с НДС' (consignment) = ABSTRACT NUMERIC[14,2] (Consignment);
supplierSumConsignmentDetailConsignment 'Сумма поставщика без НДС' (consignment) = ABSTRACT NUMERIC[14,2] (Consignment);
//
//wareConsignmentDetail (consignmentDetail) = ABSTRACT Ware (ConsignmentDetail);
//nameWareConsignmentDetail 'Посуда' (consignmentDetail) = nameWare(wareConsignmentDetail(consignmentDetail));
//warePriceConsignmentDetail 'Цена посуды с НДС' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//wareSumConsignmentDetail 'Сумма посуды с НДС' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//valueWareRangeConsignmentDetail 'НДС посуды, %' (consignmentDetail) = ABSTRACT NUMERIC[6,2] (ConsignmentDetail);
//wareVATSumConsignmentDetail 'Сумма НДС по посуде' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//wareSupplierPriceConsignmentDetail 'Цена посуды без НДС' (consignmentDetail) =  ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//wareSupplierSumConsignmentDetail 'Сумма посуды без НДС' (consignmentDetail) =  ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//
//sumIaWConsignmentDetail 'Сумма с посудой без НДС' (consignmentDetail) = sumConsignmentDetail(consignmentDetail) (+) wareSupplierSumConsignmentDetail(consignmentDetail);
overSumIaWConsignmentDetail = ABSTRACT NUMERIC[16,2] (ConsignmentDetail) PERSISTENT;
sumIaWConsignmentDetail (detail) = OVERRIDE sumConsignmentDetail(detail), overSumIaWConsignmentDetail(detail) PERSISTENT;

sumConsignmentDetailConsignment 'Сумма с посудой без НДС (всего)' (consignment) = GROUP SUM sumIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;
//
//sumVATIaWConsignmentDetail 'Сумма НДС' (consignmentDetail) = sumVATConsignmentDetail(consignmentDetail) (+) wareVATSumConsignmentDetail(consignmentDetail);
overSumVATIaWConsignmentDetail = ABSTRACT NUMERIC[16,2] (ConsignmentDetail) PERSISTENT;
sumVATIaWConsignmentDetail (detail) = OVERRIDE sumVATConsignmentDetail(detail), overSumVATIaWConsignmentDetail(detail) PERSISTENT;

sumVATConsignmentDetailConsignment 'Сумма НДС (всего)' (consignment) = GROUP SUM sumVATIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;
//
//sumInvoiceIaWConsignmentDetail 'Сумма с НДС' (consignmentDetail) = sumInvoiceConsignmentDetail(consignmentDetail) (+) wareSumConsignmentDetail(consignmentDetail);
overSumInvoiceIaWConsignmentDetail = ABSTRACT NUMERIC[16,2] (ConsignmentDetail) PERSISTENT;
sumInvoiceIaWConsignmentDetail (detail) = OVERRIDE sumInvoiceConsignmentDetail(detail), overSumInvoiceIaWConsignmentDetail(detail) PERSISTENT;

sumInvoiceConsignmentDetailConsignment 'Сумма c НДС (всего)' (consignment) = GROUP SUM sumInvoiceIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumShipmentConsignmentDetailConsignment 'Сумма учётная' (consignment) = GROUP SUM sumShipmentConsignmentDetail(consignmentDetail)
                                                                               BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumRetailVATConsignmentDetailConsignment 'Сумма НДС' (consignment) = GROUP SUM sumRetailVATConsignmentDetail(consignmentDetail)
                                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumRetailConsignmentDetailConsignment 'Сумма c НДС (всего)' (consignment) = GROUP SUM sumRetailConsignmentDetail(consignmentDetail)
                                                                                   BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

// ---------------------------- Формы для накладных --------------------------------- //

META defineConsignmentFormSimple(form, caption)

    FORM form caption
        OBJECTS c=Consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, shipmentBaseConsignment, nameIssuanceAllowedConsignment, nameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       quantityConsignmentDetailConsignment, sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment,
                       sumInvoiceConsignmentDetailConsignment,
                       UNPCustomerConsignment, UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment,
                       fullNameSupplierConsignment, fullNameCustomerConsignment, numberObject, seriesObject, countConsignmentDetailConsignment,
                       isWareConsignment, isCustomsFlowConsignment, isCurrencyConsignment, shortNameCurrencyConsignment,
                       noteConsignment, documentNameCurrencyConsignment
        PROPERTIES()   TODRAW c countRowsSimpleVertical, countRowsSimpleHorizontal
        OBJECTS d=ConsignmentDetail

        PROPERTIES(d) nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      noteConsignmentDetail, extraDescriptionConsignmentDetail

        FILTERS consignmentConsignmentDetail(d) == c
    ;
    print###form caption (consignment) = ACTION FORM form OBJECTS c = consignment PRINT IMAGE 'print.png' IN printGroup;
END

@defineConsignmentFormSimple(consignmentSimpleVertical, 'ТН-2, вертикальная');
@defineConsignmentFormSimple(consignmentSimpleHorizontal, 'ТН-2, горизонтальная');
@defineConsignmentFormSimple(consignmentSimpleAttach, 'Приложение к ТН-2');

META defineConsignmentForm(form, caption)
    FORM form caption
        OBJECTS c=Consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, UNPCustomerConsignment, UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment, fullNameSupplierConsignment,
                       fullNameCustomerConsignment, nameTruckConsignment, ownerTruckConsignment, trailerConsignment, nameDriverConsignment, overWaybillConsignment,
                       fullNamePayerConsignment, UNPPayerConsignment, addressPayerConsignment, shipmentBaseConsignment, addressSupplierStockConsignment, addressCustomerStockConsignment,
                       readdressingConsignment, nameIssuanceAllowedConsignment, nameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       nameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                       arrivalTimeConsignment, departureTimeConsignment, downtimeConsignment, raceQuantityConsignment,
                       quantityConsignmentDetailConsignment, overPackQuantityConsignmentDetailConsignment, grossWeightTonConsignmentDetailConsignment, grossWeightConsignmentDetailConsignment,
                       sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                       countConsignmentDetailConsignment, supplierSumConsignmentDetailConsignment,
                       seriesObject, numberObject, isCustomsFlowConsignment, isCurrencyConsignment, shortNameCurrencyConsignment,
                       noteConsignment, documentNameCurrencyConsignment
        PROPERTIES()   TODRAW c countRowsVerticalA, countRowsVerticalAB, countRowsHorizontal
        OBJECTS d=ConsignmentDetail

        PROPERTIES(d) nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      packQuantityConsignmentDetail, grossWeightTonConsignmentDetail, noteConsignmentDetail, extraDescriptionConsignmentDetail


        FILTERS consignmentConsignmentDetail(d) == c
    ;
    print###form caption (consignment) = ACTION FORM form OBJECTS c = consignment PRINT IMAGE 'print.png' IN printGroup;
    print###form###Auto caption (consignment) = ACTION FORM form OBJECTS c = consignment PRINT AUTO IMAGE 'print.png' IN printGroup;
END

@defineConsignmentForm(consignmentVerticalA, 'ТТН-1, вертикальная (сторона А)');
@defineConsignmentForm(consignmentHorizontalA, 'ТТН-1, горизонтальная (сторона А)');
@defineConsignmentForm(consignmentVerticalB, 'ТТН-1, вертикальная (сторона Б)');
@defineConsignmentForm(consignmentHorizontalB, 'ТТН-1, горизонтальная (сторона Б)');
@defineConsignmentForm(consignmentAttach, 'Приложение к ТТН-1');
@defineConsignmentForm(consignmentVerticalAB, 'ТТН-1, вертикальная (сторона А/Б)');

FORM consignment 'Атрибуты накладной'
    OBJECTS c=Consignment FIXED PANEL

    PROPERTIES (c) READONLY objectClassName, nameSupplierStockConsignment, seriesObject, numberObject
    PROPERTIES (c) dateConsignment, UNPCustomerConsignment, UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment, fullNameSupplierConsignment,
                   fullNameCustomerConsignment, nameTruckConsignment, ownerTruckConsignment, trailerConsignment, nameDriverConsignment, overWaybillConsignment,
                   fullNamePayerConsignment, UNPPayerConsignment, addressPayerConsignment, shipmentBaseConsignment, addressSupplierStockConsignment, addressCustomerStockConsignment,
                   readdressingConsignment, nameIssuanceAllowedConsignment, nameIssuanceExecutedConsignment,
                   forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                   nameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                   arrivalTimeConsignment, departureTimeConsignment, downtimeConsignment, raceQuantityConsignment,
                   quantityConsignmentDetailConsignment, overPackQuantityConsignmentDetailConsignment, grossWeightTonConsignmentDetailConsignment,
                   sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                   countConsignmentDetailConsignment, noteConsignment, shortNameCurrencyConsignment//, SHOWIF isCustomsFlowConsignment(c)


    OBJECTS d=ConsignmentDetail

    PROPERTIES(d) nameSkuConsignmentDetail READONLY, shortNameUOMConsignmentDetail READONLY, quantityConsignmentDetail, priceConsignmentDetail,
                  sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                  packQuantityConsignmentDetail, grossWeightTonConsignmentDetail, noteConsignmentDetail READONLY, extraDescriptionConsignmentDetail READONLY

    FILTERS consignmentConsignmentDetail(d) == c
;

DESIGN consignment FROM DEFAULT {
    main {
        preferredSize = (1024, 768);

        NEW header {
            caption = 'Шапка документа';
            type = CONTAINERH;
            ADD PROPERTY (objectClassName) { preferredCharWidth = 15; }
            ADD PROPERTY(nameSupplierStockConsignment);
            ADD PROPERTY(numberObject);
            ADD PROPERTY(seriesObject);
            ADD PROPERTY(dateConsignment);
        }

        NEW supplier {
            caption = 'Грузоотправитель';
            type = CONTAINERH;
            ADD PROPERTY(fullNameSupplierConsignment);
            ADD PROPERTY(UNPSupplierConsignment);
            ADD PROPERTY(addressSupplierConsignment);
        }

        NEW customer {
            caption = 'Грузополучатель';
            type = CONTAINERH;
            ADD PROPERTY(fullNameCustomerConsignment);
            ADD PROPERTY(UNPCustomerConsignment);
            ADD PROPERTY(addressCustomerConsignment);
        }

        NEW payer {
            caption = 'Заказчик ';
            type = CONTAINERH;
            ADD PROPERTY(fullNamePayerConsignment);
            ADD PROPERTY(UNPPayerConsignment);
            ADD PROPERTY(addressPayerConsignment);
        }
        ADD c.carConsignmentGroup {
            columns = 4;
        }
        NEW misc {
            caption = 'Дополнительно';
            
            type = CONTAINERH;
            ADD PROPERTY (noteConsignment(c));
            ADD PROPERTY (shortNameCurrencyConsignment(c));
        }
        ADD c.issuanceConsignmentGroup {
            columns = 4;
        }
        ADD c.loadingConsignmentGroup {
            columns = 4;
        }
        ADD c.sumConsignmentGroup {
            columns = 4;
            ADD PROPERTY(countConsignmentDetailConsignment);
            ADD PROPERTY(quantityConsignmentDetailConsignment);
            ADD PROPERTY(overPackQuantityConsignmentDetailConsignment);
            ADD PROPERTY(grossWeightTonConsignmentDetailConsignment);
            ADD PROPERTY(sumConsignmentDetailConsignment);
            ADD PROPERTY(sumVATConsignmentDetailConsignment);
            ADD PROPERTY(sumInvoiceConsignmentDetailConsignment);
        }
    }
        
    ADD d.box;
    ADD functions.box;
}

editConsignment 'Заполнить атрибуты накладной' (consignment) = ACTION FORM consignment OBJECTS c = consignment DOCKEDMODAL IMAGE 'edit.png';

printDoubleSidedConsignmentsVertical 'Печать ТТН-1 (двусторонняя)' (consignment) = ACTION (consignment){
    IF countConsignmentDetailConsignment(consignment) <= countRowsVerticalAB () THEN {
        printConsignmentVerticalABAuto(consignment);
//        FORM blankPage; 
        printConsignmentVerticalABAuto(consignment);
//        FORM blankPage; 
        printConsignmentVerticalABAuto(consignment);
//        FORM blankPage; 
        printConsignmentVerticalABAuto(consignment);
//        FORM blankPage;                      
    } ELSE {
        IF  countConsignmentDetailConsignment(consignment) <= countRowsVerticalA () THEN {
                printConsignmentVerticalAAuto(consignment);
                printConsignmentVerticalBAuto(consignment);
                printConsignmentVerticalAAuto(consignment);
                printConsignmentVerticalBAuto(consignment);
                printConsignmentVerticalAAuto(consignment);
                printConsignmentVerticalBAuto(consignment);
                printConsignmentVerticalAAuto(consignment);
                printConsignmentVerticalBAuto(consignment);                                                
        } ELSE {
                printConsignmentVerticalABAuto(consignment);
                printConsignmentAttachAuto(consignment);
                printConsignmentVerticalABAuto(consignment);
                printConsignmentAttachAuto(consignment);  
                printConsignmentVerticalABAuto(consignment);
                printConsignmentAttachAuto(consignment);
                printConsignmentVerticalABAuto(consignment);
                printConsignmentAttachAuto(consignment);                                                        
        } 
    }        
}

FORM printConsignment 'Печать накладных'
    OBJECTS c=Consignment FIXED PANEL

    PROPERTIES (c)  printDoubleSidedConsignmentsVertical, printConsignmentSimpleVertical, printConsignmentSimpleHorizontal, printConsignmentSimpleAttach,
                    printConsignmentVerticalA, printConsignmentVerticalAB, printConsignmentHorizontalA, printConsignmentVerticalB,
                    printConsignmentHorizontalB, printConsignmentAttach, editConsignment
;

DESIGN printConsignment FROM DEFAULT {

    c.box {

        childConstraints = TO THE BOTTOM;

        NEW case55{
            childConstraints = TO THE BOTTOM;

            NEW contOne {
                caption = 'Накладная';
                ADD PROPERTY(editConsignment);
            }
            NEW tn{
                childConstraints = TO THE RIGHT;
                caption = 'ТН-2';
                ADD PROPERTY(printConsignmentSimpleVertical);
                ADD PROPERTY(printConsignmentSimpleHorizontal);
                ADD PROPERTY(printConsignmentSimpleAttach);
            }
        }
        NEW ttn1{
            childConstraints = TO THE RIGHT;
            caption = 'ТТН-1';
            NEW ttn1V {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(printDoubleSidedConsignmentsVertical);
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentVerticalAB);
            }
            NEW ttn1H {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentHorizontalB);
            }
            NEW ttn1A {
                ADD PROPERTY(printConsignmentAttach);
            }
        }

    }
}

printConsignment 'Печать' (consignment) = ACTION FORM printConsignment OBJECTS c = consignment MODAL IMAGE 'print.png';


// ------------------------------------- Метакод по объявлению и имплементации накладных ------------------ //

// ----------------- Объявление заголовка накладной --------------- //

META defineConsignmentHeader (object)
    @defineConsignmentHeaderInner (object, ###object);
END

META defineConsignmentHeaderInner (object, class)
    payer###object (object) = DATA LegalEntity (class) IN carConsignmentGroup;

    dataNameTruck###object 'Автомобиль' (object) = DATA VARSTRING[30] (class) IN carConsignmentGroup;
    dataOwner###object 'Владелец автомобиля' (object) = DATA VARSTRING[100] (class) IN carConsignmentGroup;
    dataTrailer###object 'Прицеп' (object) = DATA VARSTRING[50] (class) IN carConsignmentGroup;
    dataNameDriver###object 'Водитель' (object) = DATA VARSTRING[40] (class) IN carConsignmentGroup;
    waybill###object 'Путевой лист' (object) = DATA VARSTRING[20] (class) IN carConsignmentGroup;

    dataAddressSupplierStock###object 'Пункт погрузки' (object) = DATA VARSTRING[50] (class);
    dataAddressCustomerStock###object 'Пункт разгрузки' (object) = DATA VARSTRING[50] (class);

    readdressing###object 'Переадресовка' (object) = DATA VARSTRING[50] (class) IN carConsignmentGroup;

    shipmentBase###object 'Основание отпуска' (object) = DATA VARSTRING[30] (class) IN issuanceConsignmentGroup;

    issuanceAllowed###object (object) = DATA Employee(class);

    issuanceExecuted###object (object) = DATA Employee(class);

    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object) = DATA VARSTRING[40] (class) IN issuanceConsignmentGroup;

    warrant###object 'По доверенности (номер, дата)' (object) = DATA VARSTRING[30] (class) IN issuanceConsignmentGroup;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = DATA VARSTRING[100] (class) IN issuanceConsignmentGroup;

    goodsAccepted###object 'Принял грузополучатель' (object) = DATA VARSTRING[40] (class) IN issuanceConsignmentGroup;

    loadingExecuter###object (object) = DATA Employee(class);

    wayOfLoading###object (object) = DATA WayOfLoading(class);

    codeLoading###object 'Код ПРР' (object) = DATA STRING[3] (class) IN loadingConsignmentGroup;

    arrivalTime###object 'Время прибытия' (object) = DATA DATETIME(class) IN carConsignmentGroup;
    departureTime###object 'Время убытия' (object) = DATA DATETIME(class) IN carConsignmentGroup;
    downtime###object 'Время простоя' (object) = DATA STRING[10] (class) IN carConsignmentGroup;

    raceQuantity###object 'Количество ездок' (object) = DATA INTEGER (class) IN carConsignmentGroup;
END

META defineConsignmentAbstractHeader(object)
    @defineConsignmentAbstractHeaderInner(object, ###object);
END

META defineConsignmentAbstractHeaderInner(object, class)

    payer###object 'Заказчик перевозки (ИД)' (object) = ABSTRACT LegalEntity (class) IN carConsignmentGroup;

    dataNameTruck###object 'Автомобиль' (object) = ABSTRACT VARSTRING[30] (class) IN carConsignmentGroup;
    dataOwner###object 'Владелец автомобиля' (object) = ABSTRACT VARSTRING[100] (class) IN carConsignmentGroup;
    dataTrailer###object 'Прицеп' (object) = ABSTRACT VARSTRING[100] (class) IN carConsignmentGroup;
    dataNameDriver###object 'Водитель' (object) = ABSTRACT VARSTRING[40] (class) IN carConsignmentGroup;
    waybill###object 'Путевой лист' (object) = ABSTRACT VARSTRING[20] (class) IN carConsignmentGroup;

    dataAddressSupplierStock###object 'Пункт погрузки' (object) = ABSTRACT VARSTRING[50] (class);
    dataAddressCustomerStock###object 'Пункт разгрузки' (object) = ABSTRACT VARSTRING[50] (class);

    readdressing###object 'Переадресовка' (object) = ABSTRACT VARSTRING[50] (class) IN carConsignmentGroup;

    shipmentBase###object 'Основание отпуска' (object) = ABSTRACT VARSTRING[30] (class) IN issuanceConsignmentGroup;

    issuanceAllowed###object (object) = ABSTRACT Employee(class);

    issuanceExecuted###object (object) = ABSTRACT Employee(class);

    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object) = ABSTRACT VARSTRING[40] (class) IN issuanceConsignmentGroup;

    warrant###object 'По доверенности (номер, дата)' (object) = ABSTRACT VARSTRING[30] (class) IN issuanceConsignmentGroup;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = ABSTRACT VARSTRING[100] (class) IN issuanceConsignmentGroup;

    goodsAccepted###object 'Принял грузополучатель' (object) = ABSTRACT VARSTRING[40] (class) IN issuanceConsignmentGroup;

    loadingExecuter###object (object) = ABSTRACT Employee(class);

    wayOfLoading###object (object) = ABSTRACT WayOfLoading(class);

    codeLoading###object 'Код ПРР' (object) = ABSTRACT STRING[3] (class) IN loadingConsignmentGroup;

    arrivalTime###object 'Время прибытия' (object) = ABSTRACT DATETIME(class) IN carConsignmentGroup;
    departureTime###object 'Время убытия' (object) = ABSTRACT DATETIME(class) IN carConsignmentGroup;
    downtime###object 'Время простоя' (object) = ABSTRACT STRING[10] (class) IN carConsignmentGroup;

    raceQuantity###object 'Количество ездок' (object) = ABSTRACT INTEGER (class) IN carConsignmentGroup;
END

META defineConsignmentInterfaceHeader(object, stockProp)
    @defineConsignmentAbstractHeader (object);
    @defineConsignmentHeader (user###object);
END

META defineConsignmentInterfaceHeader(object)
    @defineConsignmentInterfaceHeader(object, stock);
END

// ----------------- Implement заголовка накладной --------------- //

META implementConsignmentHeader (concrete)
    EXTEND CLASS concrete : Consignment;

    dateConsignment (consignment) += date###concrete(consignment);

    payerConsignment (consignment) += payer###concrete (consignment);

    dataNameTruckConsignment (consignment) += dataNameTruck###concrete (consignment);
    dataOwnerConsignment (consignment) += dataOwner###concrete (consignment);
    dataTrailerConsignment (consignment) += dataTrailer###concrete (consignment);
    dataNameDriverConsignment (consignment) += dataNameDriver###concrete (consignment);
    dataWaybillConsignment (consignment) += waybill###concrete (consignment);

    dataAddressSupplierStockConsignment (consignment) += dataAddressSupplierStock###concrete (consignment);
    dataAddressCustomerStockConsignment (consignment) += dataAddressCustomerStock###concrete (consignment);

    readdressingConsignment (consignment) += readdressing###concrete (consignment);

    shipmentBaseConsignment (consignment) += shipmentBase###concrete (consignment);

    issuanceAllowedConsignment (consignment) += issuanceAllowed###concrete (consignment);

    issuanceExecutedConsignment (consignment) += issuanceExecuted###concrete (consignment);

    forwarderConsignment (consignment) += forwarder###concrete (consignment);

    warrantConsignment (consignment) += warrant###concrete (consignment);
    warrantHolderConsignment (consignment) += warrantHolder###concrete (consignment);

    goodsAcceptedConsignment (consignment) += goodsAccepted###concrete (consignment);

    loadingExecuterConsignment (consignment) += loadingExecuter###concrete (consignment);

    wayOfLoadingConsignment (consignment) += wayOfLoading###concrete (consignment);
    codeLoadingConsignment (consignment) += codeLoading###concrete (consignment);

    arrivalTimeConsignment (consignment) += arrivalTime###concrete (consignment);
    departureTimeConsignment (consignment) += departureTime###concrete (consignment);
    downtimeConsignment (consignment) += downtime###concrete (consignment);

    raceQuantityConsignment (consignment) += raceQuantity###concrete (consignment);
END

META implementConsignmentDocumentHeader (concrete, object, prefix)

    prefix###payer###concrete (concrete) += payer###object (concrete);

    prefix###dataNameTruck###concrete (concrete) += dataNameTruck###object (concrete);
    prefix###dataOwner###concrete (concrete) += dataOwner###object (concrete);
    prefix###dataTrailer###concrete (concrete) += dataTrailer###object (concrete);
    prefix###dataNameDriver###concrete (concrete) += dataNameDriver###object (concrete);
    prefix###waybill###concrete (concrete) += waybill###object (concrete);

    prefix###dataAddressSupplierStock###concrete (concrete) += dataAddressSupplierStock###object (concrete);
    prefix###dataAddressCustomerStock###concrete (concrete) += dataAddressCustomerStock###object (concrete);

    prefix###readdressing###concrete (concrete) += readdressing###object (concrete);

    prefix###shipmentBase###concrete (concrete) += shipmentBase###object (concrete);

    prefix###issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    prefix###issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    prefix###forwarder###concrete (concrete) += forwarder###object (concrete);

    prefix###warrant###concrete (concrete) += warrant###object (concrete);
    prefix###warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    prefix###goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    prefix###loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);

    prefix###wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);
    prefix###codeLoading###concrete (concrete) += codeLoading###object (concrete);

    prefix###arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    prefix###departureTime###concrete (concrete) += departureTime###object (concrete);
    prefix###downtime###concrete (concrete) += downtime###object (concrete);

    prefix###raceQuantity###concrete (concrete) += raceQuantity###object (concrete);

END

META implementConsignmentInterfaceHeader(object)
    @implementConsignmentDocumentHeader(object, user###object, );
    @implementConsignmentHeader (object);
END
//--

META implementConsignmentDocumentHeaderPrefix (concrete, object, NS)

    NS.payer###concrete (concrete) += payer###object (concrete);

    NS.dataNameTruck###concrete (concrete) += dataNameTruck###object (concrete);
    NS.dataOwner###concrete (concrete) += dataOwner###object (concrete);
    NS.dataTrailer###concrete (concrete) += dataTrailer###object (concrete);
    NS.dataNameDriver###concrete (concrete) += dataNameDriver###object (concrete);
    NS.waybill###concrete (concrete) += waybill###object (concrete);

    NS.dataAddressSupplierStock###concrete (concrete) += dataAddressSupplierStock###object (concrete);
    NS.dataAddressCustomerStock###concrete (concrete) += dataAddressCustomerStock###object (concrete);

    NS.readdressing###concrete (concrete) += readdressing###object (concrete);

    NS.shipmentBase###concrete (concrete) += shipmentBase###object (concrete);

    NS.issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    NS.issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    NS.forwarder###concrete (concrete) += forwarder###object (concrete);

    NS.warrant###concrete (concrete) += warrant###object (concrete);
    NS.warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    NS.goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    NS.loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);

    NS.wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);
    NS.codeLoading###concrete (concrete) += codeLoading###object (concrete);

    NS.arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    NS.departureTime###concrete (concrete) += departureTime###object (concrete);
    NS.downtime###concrete (concrete) += downtime###object (concrete);

    NS.raceQuantity###concrete (concrete) += raceQuantity###object (concrete);

END

// -------------------------- Implement строк накладной -------------------- //

META implementConsignmentDetail (concrete, skuProp)
    @implementConsignmentDetailInner (concrete, ###concrete, skuProp);
END

META implementConsignmentDetailInner (concrete, concreteClass, skuProp)
    EXTEND CLASS concreteClass##Detail : ConsignmentDetail;

    consignmentConsignmentDetail (consignmentDetail) += concrete###concrete##Detail (consignmentDetail);
    skuConsignmentDetail (consignmentDetail) += skuProp###concrete##Detail (consignmentDetail);
    quantityConsignmentDetail (consignmentDetail) += quantity###concrete##Detail (consignmentDetail);

    packQuantityConsignmentDetail (consignmentDetail) += packQuantity###concrete##Detail (consignmentDetail);
    grossWeightConsignmentDetail (consignmentDetail) += grossWeight###concrete##Detail (consignmentDetail);
END
META implementConsignmentDetail (concrete)
    @implementConsignmentDetail (concrete, sku);
END

// -------------------------- Implement всей накладной -------------------- //

META implementConsignment(concrete, skuProp)
    @implementConsignmentHeader(concrete);
    @implementConsignmentDetail(concrete, skuProp);
END
