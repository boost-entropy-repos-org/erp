MODULE ConsignmentBy;

REQUIRE System,
        LegalEntityBy,
        Stock,
        Sku,
        Employee,
        Utils,
        Transport,
        Numerator,
        StockContract,
        SkuLedger; // вот эту зависимость нужно будет убрать

NAMESPACE Consignment;

CLASS WayOfLoading 'Способ ПРР';
TABLE wayOfLoading (WayOfLoading);

nameWayOfLoading 'Наименование' = DATA VARISTRING[50](WayOfLoading) IN base;

FORM wayOfLoading 'Способ ПРР'
    OBJECTS w = WayOfLoading FIXED PANEL 
    PROPERTIES(w) nameWayOfLoading
    EDIT WayOfLoading OBJECT w
;

FORM dialogWayOfLoading 'Способ ПРР'
    OBJECTS w = WayOfLoading
    PROPERTIES(w) nameWayOfLoading READONLY, DELETESESSION
    PROPERTIES(w) ADDFORM, EDITFORM
    DIALOG WayOfLoading OBJECT w
;

GROUP sumConsignment 'Суммы' : base;

CLASS ABSTRACT Consignment 'Накладная';
TABLE consignment(Consignment);

CLASS ABSTRACT ConsignmentDetail 'Строка накладной';

dataDateConsignment 'Дата' (consignment) = ABSTRACT DATE (Consignment);
overDateConsignment 'Дата' (consignment) = ABSTRACT DATE (Consignment);
dateConsignment 'Дата' (consignment) = OVERRIDE dataDateConsignment(consignment), overDateConsignment(consignment);


numberConsignment 'Номер' = ABSTRACT STRING[18] (Consignment) IN numbered MINCHARWIDTH 7;
seriesConsignment 'Серия' = ABSTRACT STRING[2] (Consignment) IN numbered FIXEDCHARWIDTH 3; 

overSeriesNumberConsignment 'Серия/Номер' (consignment) = ABSTRACT STRING[21] (Consignment);

seriesNumberConsignment 'Серия/Номер' (o) = OVERRIDE 
    (CONCAT '', seriesConsignment(o), numberConsignment(o)),
    overSeriesNumberConsignment(o)  MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20;


// ------------- Типы накладной ------------------ //
CLASS ConsignmentType 'Тип накладной' {
    ttn1 'ТТН-1',
    tn2 'ТН-2'
}


FORM consignmentTypes 'Типы накладной'
    OBJECTS t = ConsignmentType
    PROPERTIES(t) READONLY staticCaption
    DIALOG ConsignmentType OBJECT t
;

DESIGN consignmentTypes {
    PROPERTY(staticCaption(t)) { caption = 'Тип накладной'; } 
}

TABLE consignmentType (ConsignmentType);

consignmentTypeConsignment 'Тип накладной' = ABSTRACT ConsignmentType (Consignment);
showTTN1Consignment (c) = consignmentTypeConsignment(c) == ConsignmentType.ttn1 OR NOT consignmentTypeConsignment(c);   
showTN2Consignment (c) = consignmentTypeConsignment(c) == ConsignmentType.tn2 OR NOT consignmentTypeConsignment(c);   

isTTN1Consignment (c) = consignmentTypeConsignment(c) == ConsignmentType.ttn1;   
isTN2Consignment (c) = consignmentTypeConsignment(c) == ConsignmentType.tn2;   

// ---------------------------------- Юридические лица ---------------------- //

countRowsSimpleVertical 'Макс. кол-во строк в бланке (ТН-2 вер.)' = DATA INTEGER ();
countRowsSimpleHorizontal 'Макс. кол-во строк в бланке (ТН-2 гор.)' = DATA INTEGER ();
countRowsVerticalA 'Макс. кол-во строк в бланке (ТТН-1 вер.А)' = DATA INTEGER ();
countRowsVerticalAB 'Макс. кол-во строк в бланке (ТТН-1 вер.А/Б)' = DATA INTEGER ();
countRowsHorizontal 'Макс. кол-во строк в бланке (ТТН-1 гор.А)' = DATA INTEGER ();

EXTEND FORM options
    PROPERTIES() countRowsSimpleVertical, countRowsSimpleHorizontal, countRowsVerticalA, countRowsVerticalAB, countRowsHorizontal

;

DESIGN options {
    pane {
        NEW consignment  {
            type = CONTAINERV;
            caption = 'Оформление ТТН';
            MOVE PROPERTY(countRowsSimpleVertical());
            MOVE PROPERTY(countRowsSimpleHorizontal());
            MOVE PROPERTY(countRowsVerticalA());
            MOVE PROPERTY(countRowsVerticalAB());
            MOVE PROPERTY(countRowsHorizontal());
        }

    }
}

// ---------------------------------- Юридические лица ---------------------- //
dataSupplierConsignment = ABSTRACT LegalEntity (Consignment);
overSupplierConsignment = ABSTRACT LegalEntity (Consignment);
supplierConsignment (consignment)= OVERRIDE dataSupplierConsignment(consignment), overSupplierConsignment(consignment);

UNPSupplierConsignment 'УНП отправителя' (consignment) = UNPLegalEntity(supplierConsignment(consignment));
overAddressSupplierConsignment = ABSTRACT VARISTRING[150] (Consignment);
addressSupplierConsignment 'Юр. адрес отправителя' (consignment) =
    OVERRIDE addressLegalEntityDate(supplierConsignment(consignment), dateConsignment(consignment)), overAddressSupplierConsignment(consignment) MINCHARWIDTH 30 PREFCHARWIDTH 50;
overFullNameSupplierConsignment = ABSTRACT VARISTRING[200] (Consignment);  
fullNameSupplierConsignment 'Наим-ие отправителя' (consignment) = 
    OVERRIDE fullNameLegalEntity(supplierConsignment(consignment)), overFullNameSupplierConsignment(consignment) MINCHARWIDTH 30 PREFCHARWIDTH 50;

dataCustomerConsignment = ABSTRACT LegalEntity (Consignment);
overCustomerConsignment = ABSTRACT LegalEntity (Consignment);
customerConsignment (consignment)= OVERRIDE dataCustomerConsignment(consignment), overCustomerConsignment(consignment);

UNPCustomerConsignment 'УНП получателя' (consignment) = UNPLegalEntity(customerConsignment(consignment));
overAddressCustomerConsignment = ABSTRACT VARISTRING[150] (Consignment);
addressCustomerConsignment 'Юр. адрес получателя' (consignment) =
    OVERRIDE addressLegalEntityDate(customerConsignment(consignment), dateConsignment(consignment)), overAddressCustomerConsignment(consignment) MINCHARWIDTH 30 PREFCHARWIDTH 50;
nameCustomerConsignment 'Наим-ие получателя' (consignment) = nameLegalEntity(customerConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
overFullNameCustomerConsignment = ABSTRACT VARISTRING[200] (Consignment);  
fullNameCustomerConsignment 'Наим-ие получателя' (consignment) = 
    OVERRIDE fullNameLegalEntity(customerConsignment(consignment)), overFullNameCustomerConsignment(consignment) MINCHARWIDTH 30 PREFCHARWIDTH 50;

payerConsignment = ABSTRACT LegalEntity (Consignment);
UNPPayerConsignment 'УНП заказчика перевозки' (consignment) = UNPLegalEntity(payerConsignment(consignment));
addressPayerConsignment 'Юр. адрес заказчика перевозки' (consignment) =
    addressLegalEntityDate(payerConsignment(consignment), dateConsignment(consignment));
fullNamePayerConsignment 'Наим-ие для накладных заказчика перевозки' (consignment) =
    fullNameLegalEntity(payerConsignment(consignment)) ;

// ------------------------------------- Атрибуты --------------------------------- //

GROUP carConsignment 'Автомобиль' : base;

defaultTruckConsignment = ABSTRACT Truck(Consignment);
dataTruckConsignment = ABSTRACT Truck(Consignment);
truckConsignment(consignment) = OVERRIDE defaultTruckConsignment(consignment), dataTruckConsignment(consignment);  

dataNameTruckConsignment = DATA VARSTRING[70](Consignment);
nameTruckConsignment 'Автомобиль' (consignment) = OVERRIDE nameTruck(truckConsignment(consignment)), dataNameTruckConsignment(consignment) IN carConsignment;

notUseDriverTruckConsignment = ABSTRACT BOOLEAN(Consignment);

changeTruckConsignment = ACTION (consignment){
    
    IF notUseDriverTruckConsignment(consignment) THEN {
        REQUEST VARSTRING[70] INPUT;
        dataNameTruckConsignment(consignment) <- requestedString();
    } ELSE {
        REQUEST OBJECT t FORM trucks DIALOG SHOWDROP;
        IF formResult() == FormResult.ok THEN {
            dataTruckConsignment(consignment) <- requestedObject();
        } ELSE IF formResult() == FormResult.drop THEN {
            dataTruckConsignment(consignment) <- NULL;
        }
    }
        
}

CLASS OwnerTrackType 'Владелец автомобиля' {
    supplier 'Грузоотравитель',
    customer 'Грузополучатель'
}

FORM ownerTrackTypes 'Типы владельца автомобиля'
    OBJECTS t = OwnerTrackType
    PROPERTIES(t) READONLY staticCaption
    DIALOG OwnerTrackType OBJECT t
;

DESIGN ownerTrackTypes {
    PROPERTY(staticCaption(t)) { caption = 'Владелец автомобиля'; } 
}

TABLE ownerTrackType (OwnerTrackType);

ownerTrackTypeConsignment 'Владелец автомобиля' = ABSTRACT OwnerTrackType (Consignment);

dataOwnerTruckConsignment 'Владелец автомобиля' = DATA VARISTRING[200] (Consignment);

ownerTruckConsignment 'Владелец автомобиля' (consignment) = OVERRIDE  (CASE 
    WHEN supplierConsignment(consignment) == legalEntityTruck(truckConsignment(consignment)) OR ownerTrackTypeConsignment(consignment) == OwnerTrackType.supplier THEN 'Грузоотравитель'
    WHEN customerConsignment(consignment) == legalEntityTruck(truckConsignment(consignment)) OR ownerTrackTypeConsignment(consignment) == OwnerTrackType.customer THEN 'Грузополучатель'
    ELSE 
    OVERRIDE nameLegalEntityTruck(truckConsignment(consignment)), ownerTruck(truckConsignment(consignment))), dataOwnerTruckConsignment(consignment)  IN carConsignment;

dataTrailerConsignment = ABSTRACT VARSTRING[100] (Consignment);
trailerConsignment 'Прицеп' (consignment) = OVERRIDE trailerTruck(truckConsignment(consignment)),
                                                     dataTrailerConsignment(consignment) IN carConsignment;

driverConsignment = ABSTRACT Driver(Consignment);
dataEmployeeConsignment = ABSTRACT Employee(Consignment);

dataNameDriverConsignment = DATA VARSTRING[30] (Consignment);
nameDriverConsignment 'Водитель' (consignment) = OVERRIDE shortNameContact(driverConsignment(consignment)),
                                                          shortNameContact(dataEmployeeConsignment(consignment)), dataNameDriverConsignment(consignment) IN carConsignment;
                                                          
changeDriverConsignment = ACTION (consignment){
    
    IF notUseDriverTruckConsignment(consignment) THEN {
        REQUEST VARSTRING[70] INPUT;
        dataNameDriverConsignment(consignment) <- requestedString();
    } ELSE {
        REQUEST OBJECT e FORM employees DIALOG SHOWDROP;
        IF formResult() == FormResult.ok THEN {
            dataEmployeeConsignment(consignment) <- requestedObject();
        } ELSE IF formResult() == FormResult.drop THEN {
            dataEmployeeConsignment(consignment) <- NULL;
        }
    }
        
}

dataWaybillConsignment 'Путевой лист' (consignment) = ABSTRACT VARSTRING[20] (Consignment) IN carConsignment;

waybillConsignment 'Путевой лист' (consignment) = ABSTRACT STRING[20] (Consignment);

overWaybillConsignment 'Путевой лист' (consignment) = OVERRIDE waybillConsignment(consignment), dataWaybillConsignment(consignment) IN carConsignment;


// Склад погрузки
supplierStockConsignment (consignment) = ABSTRACT Stock (Consignment);
nameSupplierStockConsignment 'Склад погрузки' (consignment) = nameStock(supplierStockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

dataAddressSupplierStockConsignment 'Пункт погрузки' (consignment) = ABSTRACT VARSTRING[60] (Consignment);
addressSupplierStockConsignment 'Пункт погрузки' (consignment) = OVERRIDE addressStock(supplierStockConsignment(consignment)),
                                                                          dataAddressSupplierStockConsignment (consignment) IN carConsignment;

// Склад разгрузки
customerStockConsignment (consignment) = ABSTRACT Stock (Consignment);
nameCustomerStockConsignment 'Склад разгрузки' (consignment) = nameStock(customerStockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

dataAddressCustomerStockConsignment 'Пункт разгрузки' (consignment) = ABSTRACT VARSTRING[50] (Consignment) IN carConsignment;
addressCustomerStockConsignment 'Пункт разгрузки' (consignment) = OVERRIDE addressStock(customerStockConsignment(consignment)),
                                                                           dataAddressCustomerStockConsignment (consignment) IN carConsignment;

readdressingConsignment 'Переадресовка' (consignment) = ABSTRACT VARSTRING[50] (Consignment) IN carConsignment;

// ------------------------------------- Отпуск товара --------------------------------- //

GROUP issuanceConsignment 'Отпуск' : base;

contractConsignment = ABSTRACT Contract (Consignment);
overDescriptionContract 'Описание' (contract) = 'дог. '+ descriptionContract(contract);

descriptionContractConsignment = overDescriptionContract(contractConsignment(consignment));

dataShipmentBaseConsignment 'Основание отпуска' (consignment) = ABSTRACT VARSTRING[100] (Consignment);
overShipmentBaseConsignment 'Основание отпуска' (consignment) = ABSTRACT VARSTRING[100] (Consignment);
shipmentBaseConsignment 'Основание отпуска' (consignment) = OVERRIDE  VARSTRING[100](descriptionContractConsignment(consignment)),
                                                                      dataShipmentBaseConsignment(consignment),
                                                                      overShipmentBaseConsignment(consignment) IN issuanceConsignment MINCHARWIDTH 20 PREFCHARWIDTH 20;

issuanceAllowedConsignment (consignment) = ABSTRACT Employee (Consignment);
nameIssuanceAllowedConsignment 'Отпуск разрешил' (consignment) = positionShortNameEmployee(issuanceAllowedConsignment(consignment)) IN issuanceConsignment MINCHARWIDTH 20 PREFCHARWIDTH 20;

issuanceExecutedConsignment (consignment) = ABSTRACT Employee(Consignment);
nameIssuanceExecutedConsignment 'Отпуск произвел' (consignment) = positionShortNameEmployee(issuanceExecutedConsignment(consignment)) IN issuanceConsignment MINCHARWIDTH 20 PREFCHARWIDTH 20;

//forwarderConsignment 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (consignment) = ABSTRACT VARSTRING[40] (Consignment) IN issuanceConsignment;
dataForwarderConsignment  = ABSTRACT Employee (Consignment);
forwarderConsignment 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (consignment) = positionShortNameEmployee(dataForwarderConsignment(consignment)) IN issuanceConsignment MINCHARWIDTH 30 PREFCHARWIDTH 30;

warrantConsignment 'По доверенности (номер, дата)' (consignment) = ABSTRACT VARSTRING[30] (Consignment) IN issuanceConsignment;
warrantHolderConsignment 'По доверенности выданной (наименование орг-ии)' (consignment) = ABSTRACT VARSTRING[200] (Consignment) IN issuanceConsignment;

goodsAcceptedConsignment 'Принял грузополучатель' (consignment) = ABSTRACT VARSTRING[40] (Consignment) IN issuanceConsignment;

// ------------------------------------- Погрузочно-разгрузочные работы --------------------------------- //

GROUP loadingConsignment 'ПРР' : base;

loadingExecuterConsignment (consignment) = ABSTRACT LegalEntity(Consignment);
nameLoadingExecuterConsignment 'Исполнитель погрузки' (consignment) = nameLegalEntity(loadingExecuterConsignment(consignment));
overNameLoadingExecuterConsignment 'Исполнитель погрузки' (consignment)= OVERRIDE 'Грузоотправитель' IF consignment IS Consignment, nameLoadingExecuterConsignment(consignment) IN loadingConsignment;

wayOfLoadingConsignment (consignment) = ABSTRACT WayOfLoading(Consignment);
nameWayOfLoadingConsignment 'Способ погрузки' (consignment) = nameWayOfLoading(wayOfLoadingConsignment(consignment)) IN loadingConsignment;

unloadingExecuterConsignment (consignment) = ABSTRACT LegalEntity(Consignment);
nameUnloadingExecuterConsignment 'Исполнитель разгрузки' (consignment) = nameLegalEntity(unloadingExecuterConsignment(consignment));
overNameUnloadingExecuterConsignment 'Исполнитель разгрузки' (consignment)= OVERRIDE 'Грузополучатель' IF consignment IS Consignment, nameUnloadingExecuterConsignment(consignment) IN loadingConsignment;

wayOfUnloadingConsignment (consignment) = ABSTRACT WayOfLoading(Consignment);
nameWayOfUnloadingConsignment 'Способ разгрузки' (consignment) = nameWayOfLoading(wayOfUnloadingConsignment(consignment)) IN loadingConsignment;

codeLoadingConsignment 'Код ПРР' (consignment) = ABSTRACT STRING[3] (Consignment)IN loadingConsignment;

notArrivalTimeConsignment 'Не печатать текущее время прибытия в накладной' () = DATA BOOLEAN ();
isArrivalTimeConsignment 'Печатать текущее время прибытия в накладной' = ABSTRACT BOOLEAN (Consignment);

dataArrivalTimeConsignment 'Время прибытия' (consignment) = ABSTRACT DATETIME(Consignment);
arrivalTimeConsignment 'Время прибытия' (consignment) = OVERRIDE currentDateTime() IF  isArrivalTimeConsignment(consignment),
                                                        currentDateTime() IF consignment IS Consignment AND NOT notArrivalTimeConsignment(),
                                                        dataArrivalTimeConsignment(consignment) IN carConsignment;

dataDowntime 'Время простоя (мин.)' = DATA  INTEGER ();

EXTEND FORM options
    PROPERTIES() dataDowntime, notArrivalTimeConsignment
;

DESIGN options {
    consignment  {
        MOVE PROPERTY(dataDowntime());
        MOVE PROPERTY(notArrivalTimeConsignment());
    }
}

dataDowntimeConsignment 'Время простоя' (consignment) = ABSTRACT INTEGER (Consignment);
downtimeConsignment 'Время простоя' (consignment) = OVERRIDE dataDowntime() IF isArrivalTimeConsignment(consignment), 
                                                             dataDowntime() IF consignment IS Consignment AND NOT notArrivalTimeConsignment(), 
                                                             dataDowntimeConsignment(consignment) IN carConsignment;

departureTimeConsignment 'Время убытия' (consignment) = sumDateTimeMinutes(arrivalTimeConsignment(consignment), LONG(downtimeConsignment(consignment))) IN carConsignment;                                                                

raceQuantityConsignment 'Количество ездок' (consignment) = ABSTRACT INTEGER (Consignment) IN carConsignment;

countPagesConsignment 'Кол-во листов в приложении' (consignment) = ABSTRACT INTEGER (Consignment);

dataNoteConsignment 'Примечание' (consignment) = ABSTRACT VARSTRING[500] (Consignment);
overNoteConsignment 'Примечание' (consignment) = DATA VARSTRING[500] (Consignment) ;
noteConsignment 'Примечание' (consignment) = OVERRIDE dataNoteConsignment(consignment), overNoteConsignment(consignment) MINCHARWIDTH 30 PREFCHARWIDTH 80;

namePayerConsignment 'Заказчик перевозки ' (consignment) =
    nameLegalEntity(payerConsignment(consignment)) + ' , ' + UNPLegalEntity(payerConsignment(consignment)) IN carConsignment;

indexConsignmentDetail 'Номер строки' = ABSTRACT INTEGER (ConsignmentDetail); 
skipConsignmentDetail = ABSTRACT BOOLEAN (ConsignmentDetail); 

batchConsignmentDetail = ABSTRACT Batch (ConsignmentDetail);
descriptionBatchConsignmentDetail 'Партия' (d) = descriptionBatch(batchConsignmentDetail(d)) MINCHARWIDTH 20 PREFCHARWIDTH 40;
nameBatchConsignmentDetail 'Партия' (d) = nameBatch(batchConsignmentDetail(d)) MINCHARWIDTH 20 PREFCHARWIDTH 40;

skuConsignmentDetail (d) = ABSTRACT Sku (ConsignmentDetail);
nameSkuConsignmentDetail 'Наименование товара' (d) = IF batchConsignmentDetail(d) THEN documentNameSkuBatch(batchConsignmentDetail(d)) ELSE nameSku(skuConsignmentDetail(d));
//nameSkuConsignmentDetail 'Наименование товара' (d) = nameSku(skuConsignmentDetail(d));

shortNameUOMConsignmentDetail 'Единица измерения' (d) = shortNameUOMSku(skuConsignmentDetail(d));
//shortNameUOMConsignmentDetail 'Единица измерения' (d) = ABSTRACT STRING[15] (ConsignmentDetail);

consignmentConsignmentDetail (d) = ABSTRACT Consignment (ConsignmentDetail);

//useRetailPriceConsignment 'Использовать розничные цены' = ABSTRACT BOOLEAN (Consignment);

quantityConsignmentDetail 'Количество' (d) = ABSTRACT NUMERIC[14,3] (ConsignmentDetail);

invoicePriceConsignmentDetail 'Цена с НДС' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
isInvoicePriceConsignmentDetail 'Печатать в качестве цены цену с НДС' (d) = ABSTRACT BOOLEAN (ConsignmentDetail);

dataPriceConsignmentDetail 'Цена' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
overPriceConsignmentDetail 'Цена' (d) = ABSTRACT CASE NUMERIC[14,2] (ConsignmentDetail);
priceConsignmentDetail 'Цена' (d) = IF NOT isInvoicePriceConsignmentDetail(d) 
                                                        THEN (OVERRIDE dataPriceConsignmentDetail(d), overPriceConsignmentDetail(d))
                                                        ELSE invoicePriceConsignmentDetail(d);

shipmentPriceConsignmentDetail 'Цена поставщика' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
purchaseRetailPriceConsignmentDetail 'Цена розничная' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);

retailMarkupConsignmentDetail 'Надбавка розн.' (d) = ABSTRACT NUMERIC[14,3] (ConsignmentDetail);
wholesaleMarkupConsignmentDetail 'Надбавка опт.' (d) = ABSTRACT NUMERIC[16,3] (ConsignmentDetail);


invoiceSumConsignmentDetail 'Сумма с НДС' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
dataSumConsignmentDetail 'Стоимость' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
overSumConsignmentDetail 'Стоимость' (d) = ABSTRACT CASE NUMERIC[16,2] (ConsignmentDetail);
sumConsignmentDetail 'Стоимость' (d) = IF NOT isInvoicePriceConsignmentDetail(d)  
                                                            THEN (OVERRIDE dataSumConsignmentDetail(d), overSumConsignmentDetail(d))
                                                            ELSE invoiceSumConsignmentDetail(d);

notVatConsignmentDetail 'Не надо проставлять ставку' (d) = ABSTRACT CASE BOOLEAN (ConsignmentDetail);  
useEmptyVATConsignmentDetail 'Печатать пустой строку с НДС' (d) = ABSTRACT CASE BOOLEAN (ConsignmentDetail);     
                                                                                                                       
useEmptyVATConsignmentDetailConsignment = GROUP SUM 1 IF useEmptyVATConsignmentDetail(d) BY consignmentConsignmentDetail(d);                                                                                                                       
                                                              
dataVatConsignmentDetail 'НДС, %' (d) = ABSTRACT NUMERIC[10,5] (ConsignmentDetail);
overVatConsignmentDetail 'НДС, %' (d) = ABSTRACT CASE NUMERIC[10,5] (ConsignmentDetail);
vatConsignmentDetail 'НДС, %' (d) = (OVERRIDE dataVatConsignmentDetail(d),
                                                             overVatConsignmentDetail(d)) IF NOT notVatConsignmentDetail(d);

dataSumVATConsignmentDetail 'Сумма НДС' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
overSumVATConsignmentDetail 'Сумма НДС' (d) = ABSTRACT CASE NUMERIC[16,2] (ConsignmentDetail);
sumVATConsignmentDetail 'Сумма НДС' (d) = OVERRIDE dataSumVATConsignmentDetail(d),
                                                                   overSumVATConsignmentDetail(d);

dataSumInvoiceConsignmentDetail 'Сумма с НДС' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
overSumInvoiceConsignmentDetail 'Сумма с НДС' (d) = ABSTRACT CASE NUMERIC[16,2] (ConsignmentDetail);
over2SumInvoiceConsignmentDetail 'Сумма с НДС' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
sumInvoiceConsignmentDetail 'Сумма с НДС' (d) = OVERRIDE dataSumInvoiceConsignmentDetail(d),
                                                                        overSumInvoiceConsignmentDetail(d),
                                                                        over2SumInvoiceConsignmentDetail(d);
                                                                         
sumShipmentConsignmentDetail 'Сумма учётная' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

sumShipmentContainerConsignment 'Сумма тары' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);
sumShipmentItemConsignment 'Сумма товаров' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);

sumRetailVATConsignmentDetail 'Сумма НДС' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

sumRetailContainerConsignment 'Сумма тары' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);
sumRetailItemConsignment 'Сумма товаров' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);

sumWholesaleMarkupConsignment 'Надбавка' (consignment) = ABSTRACT NUMERIC[16,2] (Consignment);

sumRetailConsignmentDetail 'Сумма розничная' (d) = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);

dataPackQuantityConsignmentDetail 'Количество грузовых мест' (d) = ABSTRACT NUMERIC[21,6] (ConsignmentDetail);
overPackQuantityConsignmentDetail 'Количество грузовых мест' (d) = ABSTRACT CASE NUMERIC[21,6] (ConsignmentDetail);
packQuantityConsignmentDetail 'Количество грузовых мест' (d) = OVERRIDE dataPackQuantityConsignmentDetail(d), 
                                                                                        overPackQuantityConsignmentDetail(d);

dataGrossWeightConsignmentDetail 'Масса груза, кг.' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
overGrossWeightConsignmentDetail 'Масса груза, кг.' (d) = ABSTRACT NUMERIC[16,5] (ConsignmentDetail);
grossWeightConsignmentDetail 'Масса груза, кг.' (d) = OVERRIDE dataGrossWeightConsignmentDetail(d),
                                                                               overGrossWeightConsignmentDetail(d);

grossWeightTonConsignmentDetail 'Масса груза, т.' (d) = round6(NUMERIC[17,6](grossWeightConsignmentDetail(d)) / 1000);

noteConsignmentDetail 'Примечание' (d) = ABSTRACT VARSTRING[150] (ConsignmentDetail) MINCHARWIDTH 30 PREFCHARWIDTH 80;
extraDescriptionConsignmentDetail 'Доп. наименование' (d) = ABSTRACT VARSTRING[1500] (ConsignmentDetail) MINCHARWIDTH 30 PREFCHARWIDTH 80;

countConsignmentDetailConsignment 'Количество строк' (consignment) = GROUP SUM 1 IF d IS ConsignmentDetail AND NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;

quantityConsignmentDetailConsignment 'Количество (всего)' (consignment) = GROUP SUM quantityConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;

packQuantityConsignmentDetailConsignment 'Общее количество грузовых мест' (consignment) = GROUP SUM packQuantityConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment PERSISTENT;

packQuantityConsignment 'Общее количество грузовых мест' = ABSTRACT CASE NUMERIC[8,2] (Consignment) PERSISTENT;
overPackQuantityConsignmentDetailConsignment 'Общее количество грузовых мест' (consignment) = OVERRIDE packQuantityConsignmentDetailConsignment(consignment),
                                                                                                       packQuantityConsignment(consignment);

grossWeightTonConsignmentDetailConsignment 'Общая масса груза, т.' (consignment) = GROUP SUM grossWeightTonConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;

grossWeightConsignmentDetailConsignment 'Общая масса груза, кг.'(consignment) = GROUP SUM grossWeightConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment PERSISTENT;

grossWeightConsignment 'Общая масса груза, кг' = ABSTRACT CASE NUMERIC[8,2] (Consignment) PERSISTENT;
overGrossWeightConsignmentDetailConsignment 'Общая масса груза, кг' = OVERRIDE grossWeightConsignmentDetailConsignment(consignment),
                                                                               grossWeightConsignment(consignment);

isCustomsFlowConsignment 'Расход с СВХ' (consignment) = ABSTRACT BOOLEAN (Consignment);

dataCurrencyConsignment 'Валюта (ИД)' (consignment) = ABSTRACT Currency (Consignment);
overCurrencyConsignment 'Валюта (ИД)' (consignment) = ABSTRACT CASE Currency (Consignment);
currencyConsignment 'Валюта (ИД)' (consignment) = OVERRIDE dataCurrencyConsignment(consignment), overCurrencyConsignment(consignment);
shortNameCurrencyConsignment 'Валюта' (consignment) = shortNameCurrency(currencyConsignment(consignment));
documentNameCurrencyConsignment 'Валюта (сокр.)' (consignment) = documentNameCurrency(currencyConsignment(consignment));
isCurrencyConsignment 'Выбрана валюта' (consignment) = TRUE IF currencyConsignment(consignment);

//// Посуда на возврате
isWareConsignment 'Возврат поставщику' (consignment) = ABSTRACT BOOLEAN (Consignment);
//wareSumConsignmentDetailConsignment 'Сумма посуды с НДС' (consignment) = ABSTRACT NUMERIC[14,2] (Consignment);
supplierSumConsignmentDetailConsignment 'Сумма поставщика без НДС' (consignment) = ABSTRACT NUMERIC[14,2] (Consignment);
//
//wareConsignmentDetail (d) = ABSTRACT Ware (ConsignmentDetail);
//nameWareConsignmentDetail 'Посуда' (d) = nameWare(wareConsignmentDetail(d));
//warePriceConsignmentDetail 'Цена посуды с НДС' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//wareSumConsignmentDetail 'Сумма посуды с НДС' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//valueWareRangeConsignmentDetail 'НДС посуды, %' (d) = ABSTRACT NUMERIC[6,2] (ConsignmentDetail);
//wareVATSumConsignmentDetail 'Сумма НДС по посуде' (d) = ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//wareSupplierPriceConsignmentDetail 'Цена посуды без НДС' (d) =  ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//wareSupplierSumConsignmentDetail 'Сумма посуды без НДС' (d) =  ABSTRACT NUMERIC[14,2] (ConsignmentDetail);
//
//sumIaWConsignmentDetail 'Сумма с посудой без НДС' (d) = sumConsignmentDetail(d) (+) wareSupplierSumConsignmentDetail(d);
overSumIaWConsignmentDetail = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
sumIaWConsignmentDetail (detail) = OVERRIDE sumConsignmentDetail(detail), overSumIaWConsignmentDetail(detail);

sumConsignmentDetailConsignment 'Сумма с посудой без НДС (всего)' (consignment) = GROUP SUM sumIaWConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;
//
//sumVATIaWConsignmentDetail 'Сумма НДС' (d) = sumVATConsignmentDetail(d) (+) wareVATSumConsignmentDetail(d);
overSumVATIaWConsignmentDetail = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
sumVATIaWConsignmentDetail (detail) = OVERRIDE sumVATConsignmentDetail(detail), overSumVATIaWConsignmentDetail(detail);

sumVATConsignmentDetailConsignment 'Сумма НДС (всего)' (consignment) = GROUP SUM sumVATIaWConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;
//
//sumInvoiceIaWConsignmentDetail 'Сумма с НДС' (d) = sumInvoiceConsignmentDetail(d) (+) wareSumConsignmentDetail(d);
overSumInvoiceIaWConsignmentDetail = ABSTRACT NUMERIC[16,2] (ConsignmentDetail);
sumInvoiceIaWConsignmentDetail (detail) = OVERRIDE sumInvoiceConsignmentDetail(detail), overSumInvoiceIaWConsignmentDetail(detail);

sumInvoiceConsignmentDetailConsignment 'Сумма c НДС (всего)' (consignment) = GROUP SUM sumInvoiceIaWConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;

sumShipmentConsignmentDetailConsignment 'Сумма учётная' (consignment) = GROUP SUM sumShipmentConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                                               BY consignmentConsignmentDetail(d) IN sumConsignment;

sumRetailVATConsignmentDetailConsignment 'Сумма НДС' (consignment) = GROUP SUM sumRetailVATConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                                            BY consignmentConsignmentDetail(d) IN sumConsignment;

sumRetailConsignmentDetailConsignment 'Сумма c НДС (всего)' (consignment) = GROUP SUM sumRetailConsignmentDetail(d) IF NOT skipConsignmentDetail(d)
                                                                                   BY consignmentConsignmentDetail(d) IN sumConsignment;

//----------проверка на заполнение атрибутов ---------

stopPrintConsignment = DATA LOCAL BOOLEAN ();

checkMandatoryAttributeConsignment = ABSTRACT ACTION LIST (Consignment);

META checkConsignmentAttribute(type, attrib, msg)

checkMandatoryAttributeConsignment(c) += ACTION (c) {
    IF is##type##Consignment(c) AND NOT attrib##Consignment(c) THEN {
        stopPrintConsignment() <- TRUE;
        MESSAGE NO WAIT msg;
    }
}
END

@checkConsignmentAttribute(TTN1, nameDriver, 'Для накладной должен быть заполнен водитель');
@checkConsignmentAttribute(TTN1, nameTruck, 'Для накладной должен быть заполнен автомобиль');
@checkConsignmentAttribute(TTN1, overWaybill, 'Для накладной должен быть заполнен путевой лист');

// ---------------------------- Формы для накладных --------------------------------- //

META defineConsignmentFormSimple(form, caption)

    FORM consignmentSimple###form caption
        OBJECTS c=Consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, shipmentBaseConsignment, nameIssuanceAllowedConsignment, nameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       quantityConsignmentDetailConsignment, sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment,
                       sumInvoiceConsignmentDetailConsignment,
                       UNPCustomerConsignment, UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment,
                       fullNameSupplierConsignment, fullNameCustomerConsignment, numberConsignment, seriesConsignment, seriesNumberConsignment, countConsignmentDetailConsignment,
                       isWareConsignment, isCustomsFlowConsignment, isCurrencyConsignment, shortNameCurrencyConsignment,
                       noteConsignment, documentNameCurrencyConsignment, countPagesConsignment, useEmptyVATConsignmentDetailConsignment
        PROPERTIES()   TODRAW c countRowsSimpleVertical, countRowsSimpleHorizontal
        OBJECTS d=ConsignmentDetail

        PROPERTIES(d) indexConsignmentDetail, nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, useEmptyVATConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      noteConsignmentDetail, extraDescriptionConsignmentDetail
        ORDER BY indexConsignmentDetail(d)
        FILTERS consignmentConsignmentDetail(d) == c,
                NOT skipConsignmentDetail(d)
    ;
    print###consignmentSimple###form caption (consignment) = ACTION FORM consignmentSimple###form OBJECTS c = consignment PRINT IMAGE 'print.png' IN print;
    print###consignmentSimple###form##Auto caption (consignment) = ACTION FORM consignmentSimple###form OBJECTS c = consignment PRINT AUTO IMAGE 'print.png' IN print;
END

@defineConsignmentFormSimple(vertical, 'ТН-2, вертикальная');
@defineConsignmentFormSimple(horizontal, 'ТН-2, горизонтальная');
@defineConsignmentFormSimple(attach, 'Приложение к ТН-2');


META defineConsignmentForm(form, caption)  // объявляем форму
    FORM form caption
    ;
END
META defineConsignment(form, prefix)  // добавляем объет на форму (по сторонам)
    EXTEND FORM form 
        OBJECTS prefix=Consignment FIXED PANEL
    ;
END
META defineConsignmentAll(form, prefix, report) // добавляем объет на форму (сквозная)
    EXTEND FORM form 
        OBJECTS prefix=Consignment FIXED PANEL
        REPORTFILES prefix report()
    ;
END

META defineExtendConsignmentForm(form, prefix) // добавляем свойства (по сторонам / сквозная)
    EXTEND FORM form 
        PROPERTIES (prefix) prefix##dateConsignment=dateConsignment, prefix##UNPCustomerConsignment=UNPCustomerConsignment, 
                       prefix##UNPSupplierConsignment=UNPSupplierConsignment, prefix##addressSupplierConsignment=addressSupplierConsignment, 
                       prefix##addressCustomerConsignment=addressCustomerConsignment, prefix##fullNameSupplierConsignment=fullNameSupplierConsignment,
                       prefix##fullNameCustomerConsignment=fullNameCustomerConsignment, prefix##nameTruckConsignment=nameTruckConsignment, 
                       prefix##ownerTruckConsignment=ownerTruckConsignment, prefix##trailerConsignment=trailerConsignment, 
                       prefix##nameDriverConsignment=nameDriverConsignment, prefix##overWaybillConsignment=overWaybillConsignment,
                       prefix##fullNamePayerConsignment=fullNamePayerConsignment, prefix##UNPPayerConsignment=UNPPayerConsignment, 
                       prefix##addressPayerConsignment=addressPayerConsignment, 
                       prefix##shipmentBaseConsignment=shipmentBaseConsignment, prefix##addressSupplierStockConsignment=addressSupplierStockConsignment, 
                       prefix##addressCustomerStockConsignment=addressCustomerStockConsignment,
                       prefix##readdressingConsignment=readdressingConsignment, prefix##nameIssuanceAllowedConsignment=nameIssuanceAllowedConsignment, 
                       prefix##nameIssuanceExecutedConsignment=nameIssuanceExecutedConsignment,
                       prefix##forwarderConsignment=forwarderConsignment, prefix##warrantConsignment=warrantConsignment, 
                       prefix##warrantHolderConsignment=warrantHolderConsignment, prefix##goodsAcceptedConsignment=goodsAcceptedConsignment,
                       prefix##overNameLoadingExecuterConsignment=overNameLoadingExecuterConsignment, prefix##nameWayOfLoadingConsignment=nameWayOfLoadingConsignment, 
                       prefix##overNameUnloadingExecuterConsignment=overNameUnloadingExecuterConsignment, prefix##nameWayOfUnloadingConsignment=nameWayOfUnloadingConsignment, 
                       prefix##codeLoadingConsignment=codeLoadingConsignment,
                       prefix##arrivalTimeConsignment=arrivalTimeConsignment, prefix##departureTimeConsignment=departureTimeConsignment, 
                       prefix##downtimeConsignment=downtimeConsignment, prefix##raceQuantityConsignment=raceQuantityConsignment,
                       prefix##quantityConsignmentDetailConsignment=quantityConsignmentDetailConsignment, 
                       prefix##overPackQuantityConsignmentDetailConsignment=overPackQuantityConsignmentDetailConsignment, 
                       prefix##grossWeightTonConsignmentDetailConsignment=grossWeightTonConsignmentDetailConsignment, 
                       prefix##grossWeightConsignmentDetailConsignment=grossWeightConsignmentDetailConsignment,
                       prefix##sumConsignmentDetailConsignment=sumConsignmentDetailConsignment, 
                       prefix##sumVATConsignmentDetailConsignment=sumVATConsignmentDetailConsignment, 
                       prefix##sumInvoiceConsignmentDetailConsignment=sumInvoiceConsignmentDetailConsignment,
                       prefix##countConsignmentDetailConsignment=countConsignmentDetailConsignment, 
                       prefix##supplierSumConsignmentDetailConsignment=supplierSumConsignmentDetailConsignment,
                       prefix##seriesConsignment=seriesConsignment, prefix##numberConsignment=numberConsignment, prefix##seriesNumberConsignment=seriesNumberConsignment, 
                       prefix##isCustomsFlowConsignment=isCustomsFlowConsignment, 
                       prefix##isCurrencyConsignment=isCurrencyConsignment, prefix##shortNameCurrencyConsignment=shortNameCurrencyConsignment,
                       prefix##noteConsignment=noteConsignment, prefix##documentNameCurrencyConsignment=documentNameCurrencyConsignment,    
                       prefix##countPagesConsignment=countPagesConsignment, prefix##useEmptyVATConsignmentDetailConsignment = useEmptyVATConsignmentDetailConsignment 
        PROPERTIES()   TODRAW prefix prefix##countRowsVerticalA=countRowsVerticalA, prefix##countRowsVerticalAB=countRowsVerticalAB, 
                       prefix##countRowsHorizontal=countRowsHorizontal
 
 
        OBJECTS prefix##d=ConsignmentDetail

        PROPERTIES(prefix##d) prefix##indexConsignmentDetail=indexConsignmentDetail, prefix##nameSkuConsignmentDetail=nameSkuConsignmentDetail, prefix##shortNameUOMConsignmentDetail=shortNameUOMConsignmentDetail, 
                      prefix##quantityConsignmentDetail=quantityConsignmentDetail, prefix##priceConsignmentDetail=priceConsignmentDetail,
                      prefix##sumConsignmentDetail=sumConsignmentDetail, prefix##vatConsignmentDetail=vatConsignmentDetail, prefix##useEmptyVATConsignmentDetail=useEmptyVATConsignmentDetail,
                      prefix##sumVATConsignmentDetail=sumVATConsignmentDetail, prefix##sumInvoiceConsignmentDetail=sumInvoiceConsignmentDetail,
                      prefix##packQuantityConsignmentDetail=packQuantityConsignmentDetail, prefix##grossWeightTonConsignmentDetail=grossWeightTonConsignmentDetail,
                      prefix##grossWeightConsignmentDetail=grossWeightConsignmentDetail,prefix##noteConsignmentDetail=noteConsignmentDetail, 
                      prefix##extraDescriptionConsignmentDetail=extraDescriptionConsignmentDetail
        ORDER BY prefix##indexConsignmentDetail
        FILTERS consignmentConsignmentDetail(prefix##d) == prefix,
                NOT skipConsignmentDetail(prefix##d)
    ;        
          
END 
META definePrintConsignmentForm(form, caption, prefix)  //печать накладной по сторонам
        printConsignment###form caption (consignment) = ACTION {
            stopPrintConsignment() <- NULL;
            checkMandatoryAttributeConsignment(consignment);
            IF NOT stopPrintConsignment() THEN FORM consignment###form OBJECTS prefix = consignment PRINT;
        } IMAGE 'print.png' IN print;
        printConsignment###form###Auto caption (consignment) = ACTION {
            stopPrintConsignment() <- NULL;
            checkMandatoryAttributeConsignment(consignment);
            IF NOT stopPrintConsignment() THEN FORM consignment###form OBJECTS prefix = consignment PRINT AUTO;
        } IMAGE 'print.png' IN print; 
END  
  
META defineExtendPrintConsignmentFormAll(form, caption, prefixA, prefixB)       //печать накладной сквозная
        printConsignment###form caption (consignment) = ACTION {
            stopPrintConsignment() <- NULL;
            checkMandatoryAttributeConsignment(consignment);
            IF NOT stopPrintConsignment() THEN FORM consignment###form OBJECTS prefixA = consignment, prefixB = consignment PRINT;            
        } IMAGE 'print.png' IN print;
        printConsignment###form###Auto caption (consignment) = ACTION {
            stopPrintConsignment() <- NULL;
            checkMandatoryAttributeConsignment(consignment);
            FORM consignment###form OBJECTS prefixA = consignment, prefixB = consignment PRINT AUTO;
        } IMAGE 'print.png' IN print; 
END   

//-------------------  Создание накладной по сторонам ----------------------//

@defineConsignmentForm(consignmentVerticalA, 'ТТН-1, вертикальная (сторона А)');
@defineConsignmentForm(consignmentHorizontalA, 'ТТН-1, горизонтальная (сторона А)');
@defineConsignmentForm(consignmentVerticalB, 'ТТН-1, вертикальная (сторона Б)');
@defineConsignmentForm(consignmentHorizontalB, 'ТТН-1, горизонтальная (сторона Б)');
@defineConsignmentForm(consignmentAttach, 'Приложение к ТТН-1');
@defineConsignmentForm(consignmentVerticalAB, 'ТТН-1, вертикальная (сторона А/Б)');

@defineConsignment(consignmentVerticalA, a);
@defineConsignment(consignmentHorizontalA, a);
@defineConsignment(consignmentVerticalB, b);
@defineConsignment(consignmentHorizontalB, b);
@defineConsignment(consignmentAttach, c);
@defineConsignment(consignmentVerticalAB, a);

@defineExtendConsignmentForm(consignmentVerticalA, a);
@defineExtendConsignmentForm(consignmentHorizontalA, a);
@defineExtendConsignmentForm(consignmentVerticalB, b);
@defineExtendConsignmentForm(consignmentHorizontalB, b);
@defineExtendConsignmentForm(consignmentAttach, c);
@defineExtendConsignmentForm(consignmentVerticalAB, a);

@definePrintConsignmentForm(verticalA, 'ТТН-1, вертикальная (сторона А)', a);
@definePrintConsignmentForm(horizontalA, 'ТТН-1, горизонтальная (сторона А)', a);
@definePrintConsignmentForm(verticalB, 'ТТН-1, вертикальная (сторона Б)', b);
@definePrintConsignmentForm(horizontalB, 'ТТН-1, горизонтальная (сторона Б)', b);
@definePrintConsignmentForm(attach, 'Приложение к ТТН-1', c);
printConsignmentAttachXLS 'Приложение к ТТН-1 (xls)' (consignment) = ACTION FORM consignmentAttach OBJECTS c = consignment PRINT XLSX IN print;

@definePrintConsignmentForm(verticalAB, 'ТТН-1, вертикальная (сторона А/Б)', a);

//-------------------  Создание сквозной накладной  ----------------------//

@defineConsignmentForm(consignmentVertical, 'ТТН-1, вертикальная');
reportFilePrintConsignmentVerticalA () = 'Consignment_consignmentVerticalA_a.jrxml';
@defineConsignmentAll(consignmentVertical, a, reportFilePrintConsignmentVerticalA);
reportFilePrintConsignmentVerticalB () = 'Consignment_consignmentVerticalB_b.jrxml';
@defineConsignmentAll(consignmentVertical, b, reportFilePrintConsignmentVerticalB);
@defineExtendConsignmentForm(consignmentVertical, a);
@defineExtendConsignmentForm(consignmentVertical, b);
@defineExtendPrintConsignmentFormAll(vertical, 'ТТН-1, вертикальная', a, b);


@defineConsignmentForm(consignmentHorizontal, 'ТТН-1, горизонтальная');
reportFilePrintConsignmentHorizontalA () = 'Consignment_consignmentHorizontalA_a.jrxml';
@defineConsignmentAll(consignmentHorizontal, a, reportFilePrintConsignmentHorizontalA);
reportFilePrintConsignmentHorizontalB () = 'Consignment_consignmentHorizontalB_b.jrxml';
@defineConsignmentAll(consignmentHorizontal, b, reportFilePrintConsignmentHorizontalB);
@defineExtendConsignmentForm(consignmentHorizontal, a);
@defineExtendConsignmentForm(consignmentHorizontal, b);
@defineExtendPrintConsignmentFormAll(horizontal, 'ТТН-1, горизонтальная', a, b);

hidePayerConsignmnet = ABSTRACT BOOLEAN (Consignment);
showPayerConsignment(consignment) = showTTN1Consignment(consignment) AND NOT hidePayerConsignmnet(consignment);

FORM consignment 'Атрибуты накладной'
    OBJECTS c=Consignment FIXED PANEL

    PROPERTIES(c) READONLY nameSupplierStockConsignment
    PROPERTIES(c)  numberConsignment, seriesConsignment, dateConsignment, UNPCustomerConsignment READONLY, UNPSupplierConsignment READONLY, 
                   addressSupplierConsignment READONLY, addressCustomerConsignment READONLY, fullNameSupplierConsignment READONLY, fullNameCustomerConsignment READONLY 
                   
    PROPERTIES(c)  shipmentBaseConsignment,
                   nameIssuanceAllowedConsignment, nameIssuanceExecutedConsignment

    PROPERTIES(c) SHOWIF showTTN1Consignment(c)
                   nameTruckConsignment ON CHANGE changeTruckConsignment(c), ownerTruckConsignment, trailerConsignment, nameDriverConsignment ON CHANGE changeDriverConsignment(c), overWaybillConsignment,
                   addressSupplierStockConsignment, addressCustomerStockConsignment, readdressingConsignment,
                   forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                   overNameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                   overNameUnloadingExecuterConsignment, nameWayOfUnloadingConsignment,
                   arrivalTimeConsignment, downtimeConsignment, departureTimeConsignment READONLY, raceQuantityConsignment
    PROPERTIES(c)  quantityConsignmentDetailConsignment, overPackQuantityConsignmentDetailConsignment, grossWeightConsignmentDetailConsignment,
                   sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                   countConsignmentDetailConsignment, noteConsignment, shortNameCurrencyConsignment, countPagesConsignment//, SHOWIF isCustomsFlowConsignment(c)
    PROPERTIES(c) SHOWIF showPayerConsignment(c) fullNamePayerConsignment, UNPPayerConsignment, addressPayerConsignment                 


    OBJECTS d=ConsignmentDetail

    PROPERTIES(d) indexConsignmentDetail, nameSkuConsignmentDetail READONLY, shortNameUOMConsignmentDetail READONLY, quantityConsignmentDetail, priceConsignmentDetail,
                  sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                  packQuantityConsignmentDetail, grossWeightConsignmentDetail, noteConsignmentDetail READONLY, extraDescriptionConsignmentDetail READONLY
    ORDER BY indexConsignmentDetail(d)  

    FILTERS consignmentConsignmentDetail(d) == c,
            NOT skipConsignmentDetail(d)
;

DESIGN consignment {
    main {
        preferredSize = (1024, 768);

        NEW header {
            caption = 'Шапка документа';
            type = CONTAINERH;
            MOVE PROPERTY(nameSupplierStockConsignment(c));
            MOVE PROPERTY(numberConsignment(c));
            MOVE PROPERTY(seriesConsignment(c));
            MOVE PROPERTY(dateConsignment(c));
        }

        NEW supplier {
            caption = 'Грузоотправитель';
            type = CONTAINERH;
            MOVE PROPERTY(fullNameSupplierConsignment(c));
            MOVE PROPERTY(UNPSupplierConsignment(c));
            MOVE PROPERTY(addressSupplierConsignment(c));
        }

        NEW customer {
            caption = 'Грузополучатель';
            type = CONTAINERH;
            MOVE PROPERTY(fullNameCustomerConsignment(c));
            MOVE PROPERTY(UNPCustomerConsignment(c));
            MOVE PROPERTY(addressCustomerConsignment(c));
        }

        NEW payer {
            caption = 'Заказчик ';
            type = CONTAINERH;
            MOVE PROPERTY(fullNamePayerConsignment(c));
            MOVE PROPERTY(UNPPayerConsignment(c));
            MOVE PROPERTY(addressPayerConsignment(c));
        }
        MOVE c.carConsignment {
            columns = 4;
        }
        NEW misc {
            caption = 'Дополнительно';
            
            type = CONTAINERH;
            MOVE PROPERTY (noteConsignment(c));
            MOVE PROPERTY (shortNameCurrencyConsignment(c));
        }
        MOVE c.issuanceConsignment {
            columns = 3;
        }
        MOVE c.loadingConsignment {
            columns = 4;
        }
        NEW sum {
            type = CONTAINERH;
            MOVE c.sumConsignment {
                columns = 4;
                MOVE PROPERTY(countConsignmentDetailConsignment(c));
                MOVE PROPERTY(quantityConsignmentDetailConsignment(c));
                MOVE PROPERTY(overPackQuantityConsignmentDetailConsignment(c));
                MOVE PROPERTY(grossWeightConsignmentDetailConsignment(c));
                MOVE PROPERTY(sumConsignmentDetailConsignment(c));
                MOVE PROPERTY(sumVATConsignmentDetailConsignment(c));
                MOVE PROPERTY(sumInvoiceConsignmentDetailConsignment(c));
            }
            NEW columnReight{
                caption = 'Приложение';
                MOVE PROPERTY(countPagesConsignment(c));
            }
        }
    }
        
    MOVE d.box;
    MOVE functions.box;
}

editAttributesConsignment 'Заполнить атрибуты накладной' (consignment) = ACTION (consignment) NEWSESSION {
    FORM consignment OBJECTS c = consignment MANAGESESSION DOCKEDMODAL; 
} IMAGE 'edit.png';

FORM printConsignment 'Печать накладных'
    OBJECTS c=Consignment FIXED PANEL

    PROPERTIES (c)  printConsignmentSimpleVertical, printConsignmentSimpleHorizontal, printConsignmentSimpleAttach,
                    printConsignmentVerticalA, printConsignmentVerticalAB, printConsignmentHorizontalA, printConsignmentVerticalB,
                    printConsignmentHorizontalB, printConsignmentAttach, editAttributesConsignment,
                    printConsignmentVertical, printConsignmentHorizontal, printConsignmentAttachXLS
;

DESIGN printConsignment {

    c.box {

        type = CONTAINERV;

        NEW case55{
            type = CONTAINERV;

            NEW contOne {
                caption = 'Накладная';
                MOVE PROPERTY(editAttributesConsignment(c));
            }
            NEW tn{
                type = CONTAINERH;
                caption = 'ТН-2';
                MOVE PROPERTY(printConsignmentSimpleVertical(c));
                MOVE PROPERTY(printConsignmentSimpleHorizontal(c));
                MOVE PROPERTY(printConsignmentSimpleAttach(c));
            }
        }
        NEW ttn1{
            type = CONTAINERH;
            caption = 'ТТН-1';
            NEW ttn1V {
                type = CONTAINERV;
                MOVE PROPERTY(printConsignmentVerticalA(c));
                MOVE PROPERTY(printConsignmentVerticalB(c));
                MOVE PROPERTY(printConsignmentVerticalAB(c));
                MOVE PROPERTY(printConsignmentVertical(c));
                MOVE PROPERTY(printConsignmentHorizontal(c));
            }
            NEW ttn1H {
                type = CONTAINERV;
                MOVE PROPERTY(printConsignmentHorizontalA(c));
                MOVE PROPERTY(printConsignmentHorizontalB(c));
            }
            NEW ttn1A {
                MOVE PROPERTY(printConsignmentAttach(c));
            }
        }
        NEW export {
            type = CONTAINERH;
            caption = 'Экспорт';  
            MOVE PROPERTY(printConsignmentAttachXLS(c));
        }
    }
}

printConsignment 'Печать' (consignment) = ACTION FORM printConsignment OBJECTS c = consignment MODAL IMAGE 'print.png';


// ------------------------------------- Метакод по объявлению и имплементации накладных ------------------ //

// ----------------- Объявление заголовка накладной --------------- //

META defineConsignmentHeader (object)
    @defineConsignmentHeaderInner (object, ###object);
END

META defineConsignmentHeaderInner (object, class)
    payer###object (object) = DATA LegalEntity (class) IN carConsignment;

    dataTruck###object (object) = DATA Truck (class);
    nameDataTruck###object 'Автомобиль' (object) = nameTruck(dataTruck###object(object)) IN carConsignment;
    dataTrailer###object 'Прицеп' (object) = DATA VARSTRING[50] (class) IN carConsignment;
    dataEmployee###object (object) = DATA Employee (class);
    nameDataEmployee###object 'Водитель' (object) = nameContact(dataEmployee###object(object)) IN carConsignment;
    waybill###object 'Путевой лист' (object) = DATA VARSTRING[20] (class) IN carConsignment;

    dataAddressSupplierStock###object 'Пункт погрузки' (object) = DATA VARSTRING[50] (class);
    dataAddressCustomerStock###object 'Пункт разгрузки' (object) = DATA VARSTRING[50] (class);

    readdressing###object 'Переадресовка' (object) = DATA VARSTRING[50] (class) IN carConsignment;

    overShipmentBase###object 'Основание отпуска' (object) = DATA VARSTRING[100] (class) IN issuanceConsignment;

    issuanceAllowed###object (object) = DATA Employee(class);

    issuanceExecuted###object (object) = DATA Employee(class);

    dataForwarder###object (object) = DATA Employee (class) IN issuanceConsignment;
    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object)= nameContact(dataForwarder###object(object));
    
    warrant###object 'По доверенности (номер, дата)' (object) = DATA VARSTRING[30] (class) IN issuanceConsignment;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = DATA VARSTRING[200] (class) IN issuanceConsignment;

    goodsAccepted###object 'Принял грузополучатель' (object) = DATA VARSTRING[40] (class) IN issuanceConsignment;

    loadingExecuter###object (object) = DATA LegalEntity(class);
    wayOfLoading###object (object) = DATA WayOfLoading(class);

    unloadingExecuter###object (object) = DATA LegalEntity(class);
    wayOfUnloading###object (object) = DATA WayOfLoading(class);

    codeLoading###object 'Код ПРР' (object) = DATA STRING[3] (class) IN loadingConsignment;

    arrivalTime###object 'Время прибытия' (object) = DATA DATETIME(class) IN carConsignment;
    downtime###object 'Время простоя' (object) = DATA INTEGER (class) IN carConsignment;
    raceQuantity###object 'Количество ездок' (object) = DATA INTEGER (class) IN carConsignment;
    raceQuantity###object(o) <- 1 WHEN SET(o IS class);

    countPages###object 'Кол-во страниц в приложении' (object) = DATA INTEGER (class);
END

META defineConsignmentAbstractHeader(object)
    @defineConsignmentAbstractHeaderInner(object, ###object);
END

META defineConsignmentAbstractHeaderInner(object, class)

    payer###object 'Заказчик перевозки (ИД)' (object) = ABSTRACT LegalEntity (class) IN carConsignment;

    dataTruck###object 'Автомобиль' (object) = ABSTRACT Truck (class);
    nameDataTruck###object 'Автомобиль' (object) = nameTruck(dataTruck###object(object)) IN carConsignment;
    
    dataTrailer###object 'Прицеп' (object) = ABSTRACT VARSTRING[100] (class) IN carConsignment;
    dataEmployee###object (object) = ABSTRACT Employee (class);
    nameDataEmployee###object 'Водитель' (object) = nameContact(dataEmployee###object(object)) IN carConsignment;
    waybill###object 'Путевой лист' (object) = ABSTRACT VARSTRING[20] (class) IN carConsignment;

    dataAddressSupplierStock###object 'Пункт погрузки' (object) = ABSTRACT VARSTRING[50] (class);
    dataAddressCustomerStock###object 'Пункт разгрузки' (object) = ABSTRACT VARSTRING[50] (class);

    readdressing###object 'Переадресовка' (object) = ABSTRACT VARSTRING[50] (class) IN carConsignment;

    overShipmentBase###object 'Основание отпуска' (object) = ABSTRACT VARSTRING[100] (class) IN issuanceConsignment;

    issuanceAllowed###object (object) = ABSTRACT Employee(class);

    issuanceExecuted###object (object) = ABSTRACT Employee(class);

    dataForwarder###object (object) = ABSTRACT Employee (class) IN issuanceConsignment;
    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object)= nameContact(dataForwarder###object(object));

    warrant###object 'По доверенности (номер, дата)' (object) = ABSTRACT VARSTRING[30] (class) IN issuanceConsignment;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = ABSTRACT VARSTRING[200] (class) IN issuanceConsignment;

    goodsAccepted###object 'Принял грузополучатель' (object) = ABSTRACT VARSTRING[40] (class) IN issuanceConsignment;

    loadingExecuter###object (object) = ABSTRACT LegalEntity(class);
    wayOfLoading###object (object) = ABSTRACT WayOfLoading(class);

    unloadingExecuter###object (object) = ABSTRACT LegalEntity(class);
    wayOfUnloading###object (object) = ABSTRACT WayOfLoading(class);

    codeLoading###object 'Код ПРР' (object) = ABSTRACT STRING[3] (class) IN loadingConsignment;

    defaultArrivalTime###object = ABSTRACT DATETIME(class);
    arrivalTime###object 'Время прибытия' (object) = ABSTRACT DATETIME(class) IN carConsignment;

    defaultDowntime###object (object) = ABSTRACT INTEGER (class) IN carConsignment;
    downtime###object 'Время простоя' (object) = ABSTRACT INTEGER (class) IN carConsignment;
         
    raceQuantity###object 'Количество ездок' (object) = ABSTRACT INTEGER (class) IN carConsignment;

    countPages###object 'Кол-во листов в приложении' (object) = ABSTRACT INTEGER (class); 
END

META defineConsignmentInterfaceHeader(object, stockProp)
    @defineConsignmentAbstractHeader (object);
    @defineConsignmentHeader (user###object);
END

META defineConsignmentInterfaceHeader(object)
    @defineConsignmentInterfaceHeader(object, stock);
END

// ----------------- Implement заголовка накладной --------------- //

META implementConsignmentHeader (concrete)
    EXTEND CLASS concrete : Consignment;

    dataDateConsignment (consignment) += date###concrete(consignment);
    numberConsignment (consignment) += number###concrete(consignment);
    seriesConsignment (consignment) += series###concrete(consignment);

    payerConsignment (consignment) += payer###concrete (consignment);

    dataTruckConsignment (consignment) += dataTruck###concrete (consignment);
    dataTrailerConsignment (consignment) += dataTrailer###concrete (consignment);
    dataEmployeeConsignment (consignment) += dataEmployee###concrete (consignment);
    dataWaybillConsignment (consignment) += waybill###concrete (consignment);

    dataAddressSupplierStockConsignment (consignment) += dataAddressSupplierStock###concrete (consignment);
    dataAddressCustomerStockConsignment (consignment) += dataAddressCustomerStock###concrete (consignment);

    readdressingConsignment (consignment) += readdressing###concrete (consignment);
    
    countPagesConsignment (consignment) += countPages###concrete (consignment);
    
    contractConsignment(consignment) += contractSku###concrete (consignment);
    overShipmentBaseConsignment (consignment) += overShipmentBase###concrete (consignment);

    issuanceAllowedConsignment (consignment) += issuanceAllowed###concrete (consignment);

    issuanceExecutedConsignment (consignment) += issuanceExecuted###concrete (consignment);

    dataForwarderConsignment (consignment) += dataForwarder###concrete (consignment);

    warrantConsignment (consignment) += warrant###concrete (consignment);
    warrantHolderConsignment (consignment) += warrantHolder###concrete (consignment);

    goodsAcceptedConsignment (consignment) += goodsAccepted###concrete (consignment);

    loadingExecuterConsignment (consignment) += loadingExecuter###concrete (consignment);
    wayOfLoadingConsignment (consignment) += wayOfLoading###concrete (consignment);

    unloadingExecuterConsignment (consignment) += unloadingExecuter###concrete (consignment);
    wayOfUnloadingConsignment (consignment) += wayOfUnloading###concrete (consignment);

    codeLoadingConsignment (consignment) += codeLoading###concrete (consignment);

    dataArrivalTimeConsignment (consignment) += OVERRIDE defaultArrivalTime###concrete(consignment), arrivalTime###concrete(consignment);
    dataDowntimeConsignment (consignment) += OVERRIDE defaultDowntime###concrete(consignment), downtime###concrete(consignment);   
     
    raceQuantityConsignment (consignment) += raceQuantity###concrete (consignment);
END
                                                                
META implementConsignmentHeaderData (concrete) // без OVERRIDE для dataArrivalTimeConsignment, dataDowntimeConsignment
    EXTEND CLASS concrete : Consignment;

    dataDateConsignment (consignment) += date###concrete(consignment);
    numberConsignment (consignment) += number###concrete(consignment);
    seriesConsignment (consignment) += series###concrete(consignment);

    payerConsignment (consignment) += payer###concrete (consignment);

    dataTruckConsignment (consignment) += dataTruck###concrete (consignment);
    dataTrailerConsignment (consignment) += dataTrailer###concrete (consignment);
    dataEmployeeConsignment (consignment) += dataEmployee###concrete (consignment);
    dataWaybillConsignment (consignment) += waybill###concrete (consignment);

    dataAddressSupplierStockConsignment (consignment) += dataAddressSupplierStock###concrete (consignment);
    dataAddressCustomerStockConsignment (consignment) += dataAddressCustomerStock###concrete (consignment);

    readdressingConsignment (consignment) += readdressing###concrete (consignment);
    
    countPagesConsignment (consignment) += countPages###concrete (consignment);

    overShipmentBaseConsignment (consignment) += overShipmentBase###concrete (consignment);

    issuanceAllowedConsignment (consignment) += issuanceAllowed###concrete (consignment);

    issuanceExecutedConsignment (consignment) += issuanceExecuted###concrete (consignment);

    dataForwarderConsignment (consignment) += dataForwarder###concrete (consignment);

    warrantConsignment (consignment) += warrant###concrete (consignment);
    warrantHolderConsignment (consignment) += warrantHolder###concrete (consignment);

    goodsAcceptedConsignment (consignment) += goodsAccepted###concrete (consignment);

    loadingExecuterConsignment (consignment) += loadingExecuter###concrete (consignment);
    wayOfLoadingConsignment (consignment) += wayOfLoading###concrete (consignment);

    unloadingExecuterConsignment (consignment) += unloadingExecuter###concrete (consignment);
    wayOfUnloadingConsignment (consignment) += wayOfUnloading###concrete (consignment);

    codeLoadingConsignment (consignment) += codeLoading###concrete (consignment);

    dataArrivalTimeConsignment (consignment) += arrivalTime###concrete(consignment);
    dataDowntimeConsignment (consignment) += downtime###concrete(consignment);   
     
    raceQuantityConsignment (consignment) += raceQuantity###concrete (consignment);
END

META implementConsignmentDocumentHeader (concrete, object, prefix)

    prefix###payer###concrete (concrete) += payer###object (concrete);
                                                                                  
    prefix###dataTruck###concrete (concrete) += dataTruck###object (concrete);
    prefix###dataTrailer###concrete (concrete) += dataTrailer###object (concrete);
    prefix###dataEmployee###concrete (concrete) += dataEmployee###object (concrete);
    prefix###waybill###concrete (concrete) += waybill###object (concrete);

    prefix###dataAddressSupplierStock###concrete (concrete) += dataAddressSupplierStock###object (concrete);
    prefix###dataAddressCustomerStock###concrete (concrete) += dataAddressCustomerStock###object (concrete);

    prefix###readdressing###concrete (concrete) += readdressing###object (concrete);

    prefix###countPages###concrete (concrete) += countPages###object (concrete);

    prefix###overShipmentBase###concrete (concrete) += overShipmentBase###object (concrete);

    prefix###issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    prefix###issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    prefix###dataForwarder###concrete (concrete) += dataForwarder###object (concrete);

    prefix###warrant###concrete (concrete) += warrant###object (concrete);
    prefix###warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    prefix###goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    prefix###loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);
    prefix###wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);

    prefix###unloadingExecuter###concrete (concrete) += unloadingExecuter###object (concrete);
    prefix###wayOfUnloading###concrete (concrete) += wayOfUnloading###object (concrete);

    prefix###codeLoading###concrete (concrete) += codeLoading###object (concrete);

    prefix###arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    prefix###downtime###concrete (concrete) += downtime###object (concrete);
        
    prefix###raceQuantity###concrete (concrete) += raceQuantity###object (concrete);

END

META implementConsignmentInterfaceHeader(object)
    @implementConsignmentDocumentHeader(object, user###object, );
    @implementConsignmentHeader (object);
END
//--

META implementConsignmentDocumentHeaderPrefix (concrete, object, NS)

    NS.payer###concrete (concrete) += payer###object (concrete);

    NS.dataTruck###concrete (concrete) += dataTruck###object (concrete);
    NS.dataTrailer###concrete (concrete) += dataTrailer###object (concrete);
    NS.dataEmployee###concrete (concrete) += dataEmployee###object (concrete);
    NS.waybill###concrete (concrete) += waybill###object (concrete);

    NS.dataAddressSupplierStock###concrete (concrete) += dataAddressSupplierStock###object (concrete);
    NS.dataAddressCustomerStock###concrete (concrete) += dataAddressCustomerStock###object (concrete);

    NS.readdressing###concrete (concrete) += readdressing###object (concrete);

    NS.overShipmentBase###concrete (concrete) += overShipmentBase###object (concrete);

    NS.issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    NS.issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    NS.dataForwarder###concrete (concrete) += dataForwarder###object (concrete);

    NS.warrant###concrete (concrete) += warrant###object (concrete);
    NS.warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    NS.goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    NS.loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);
    NS.wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);

    NS.unloadingExecuter###concrete (concrete) += unloadingExecuter###object (concrete);
    NS.wayOfUnloading###concrete (concrete) += wayOfUnloading###object (concrete);

    NS.codeLoading###concrete (concrete) += codeLoading###object (concrete);
    
    NS.defaultArrivalTime###concrete (concrete) += defaultArrivalTime###object (concrete);        
    NS.arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    
    NS.defaultDowntime###concrete (concrete) += defaultDowntime###object (concrete);          
    NS.downtime###concrete (concrete) += downtime###object (concrete);

    NS.raceQuantity###concrete (concrete) += raceQuantity###object (concrete);

    NS.countPages###concrete (concrete) += countPages###object (concrete);
END

// -------------------------- Implement строк накладной -------------------- //

META implementConsignmentDetail (concrete, skuProp, batchProp)
    @implementConsignmentDetailInner (concrete, ###concrete, skuProp, batchProp);
END

META implementConsignmentDetailInner (concrete, concreteClass, skuProp, batchProp)
    EXTEND CLASS concreteClass##Detail : ConsignmentDetail;
    
    indexConsignmentDetail (d) += index###concrete##Detail (d);
    consignmentConsignmentDetail (d) += concrete###concrete##Detail (d);
    skipConsignmentDetail (d) += skipConsignment###concrete##Detail (d);
    skuConsignmentDetail (d) += skuProp###concrete##Detail (d);
    batchConsignmentDetail (d) += batchProp###concrete##Detail (d);    
    quantityConsignmentDetail (d) += quantity###concrete##Detail (d);

    dataPackQuantityConsignmentDetail (d) += packQuantity###concrete##Detail (d);
    dataGrossWeightConsignmentDetail (d) += sumGrossWeight###concrete##Detail (d);
END
META implementConsignmentDetail (concrete)
    @implementConsignmentDetail (concrete, sku, batch);
END

// -------------------------- Implement всей накладной -------------------- //

META implementConsignment(concrete, skuProp)
    @implementConsignmentHeader(concrete);
    @implementConsignmentDetail(concrete, skuProp, batchProp);
END

// ------------------------------------- Отпуск товара --------------------------------- //

GROUP invoiceConsignment 'Накладная' : base;

nameConsigmentLegalEntityStock 'Наименование юр. лица' = DATA VARSTRING[200] (Stock) IN invoiceConsignment PREFCHARWIDTH 30;
overFullNameSupplierConsignment(consignment) += nameConsigmentLegalEntityStock(supplierStockConsignment(consignment));
overFullNameCustomerConsignment(consignment) += nameConsigmentLegalEntityStock(customerStockConsignment(consignment));

addressConsigmentLegalEntityStock 'Юр. адрес отправителя' = DATA VARISTRING[150] (Stock) IN invoiceConsignment PREFCHARWIDTH 30;
overAddressSupplierConsignment(consignment) += addressConsigmentLegalEntityStock(supplierStockConsignment(consignment));
overAddressCustomerConsignment(consignment) += addressConsigmentLegalEntityStock(customerStockConsignment(consignment));

issuanceAllowedStock = DATA Employee (Stock);
nameIssuanceAllowedStock 'Отпуск разрешил' (stock) = nameContact(issuanceAllowedStock(stock)) IN invoiceConsignment;

issuanceExecutedStock  = DATA Employee(Stock);
nameIssuanceExecutedStock 'Отпуск произвел' (stock) = nameContact(issuanceExecutedStock(stock)) IN invoiceConsignment;

loadingExecuterStock = DATA LegalEntity(Stock);
nameLoadingExecuterStock 'Исполнитель погрузки' (stock) = nameLegalEntity(loadingExecuterStock(stock)) IN invoiceConsignment;

wayOfLoadingStock = DATA WayOfLoading(Stock);
nameWayOfLoadingStock 'Способ погрузки' (stock) = nameWayOfLoading(wayOfLoadingStock(stock)) IN invoiceConsignment;

unloadingExecuterStock = DATA LegalEntity(Stock);
nameUnloadingExecuterStock 'Исполнитель разгрузки' (stock) = nameLegalEntity(unloadingExecuterStock(stock)) IN invoiceConsignment;

wayOfUnloadingStock = DATA WayOfLoading(Stock);
nameWayOfUnloadingStock 'Способ разгрузки' (stock) = nameWayOfLoading(wayOfUnloadingStock(stock)) IN invoiceConsignment;

codeLoadingStock 'Код ПРР' = DATA STRING[3] (Stock)IN invoiceConsignment;

META deriveConsignmentProperty(objectClass, property, stockProp)
    property###objectClass (o) <- property###stock(stockProp###objectClass(o))
        WHEN CHANGED(stockProp###objectClass(o));
END
META deriveConsignmentPropertyALL(stockProp)
    @deriveConsignmentProperty(userInvoice, issuanceAllowed, stockProp);
    @deriveConsignmentProperty(userInvoice, issuanceExecuted, stockProp);
    @deriveConsignmentProperty(userInvoice, loadingExecuter, stockProp);
    @deriveConsignmentProperty(userInvoice, unloadingExecuter, stockProp);
    @deriveConsignmentProperty(userInvoice, wayOfLoading, stockProp);
    @deriveConsignmentProperty(userInvoice, wayOfUnloading, stockProp);
    @deriveConsignmentProperty(userInvoice, codeLoading, stockProp);          
END

META deriveConsignmentPropertyALL(object, stockProp)
    @deriveConsignmentProperty(object, issuanceAllowed, stockProp);
    @deriveConsignmentProperty(object, issuanceExecuted, stockProp);
    @deriveConsignmentProperty(object, loadingExecuter, stockProp);
    @deriveConsignmentProperty(object, unloadingExecuter, stockProp);
    @deriveConsignmentProperty(object, wayOfLoading, stockProp);
    @deriveConsignmentProperty(object, wayOfUnloading, stockProp);
    @deriveConsignmentProperty(object, codeLoading, stockProp);          
END

