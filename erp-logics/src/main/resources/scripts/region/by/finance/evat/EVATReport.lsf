MODULE EVATReport;

REQUIRE EVAT, SaleInvoiceEvat;

NAMESPACE EVAT;

countEVAT 'К-во док-ов' = GROUP BY EVATDocument(EVAT e) SUM 1 IF NOT statusServerStatus(e) == EVATServerStatus.cancelled CHARWIDTH 10;
countCompletedEVAT 'К-во док-ов (COMPLETED)' = GROUP BY EVATDocument(EVAT e) SUM 1 IF statusServerStatus(e) == EVATServerStatus.completed CHARWIDTH 10;
countErrorEVAT 'К-во док-ов (ERROR)' = GROUP BY EVATDocument(EVAT e) SUM 1 IF statusServerStatus(e) == EVATServerStatus.error;
VATSumEVAT 'Сумма НДС док-ов' = GROUP  BY EVATDocument(EVAT e) SUM totalVATSum(e) IF NOT statusServerStatus(e) == EVATServerStatus.cancelled CHARWIDTH 10;

filterTypeReport = DATA LOCAL EVATType ();
nameFilterTypeReport 'Тип' = name(filterTypeReport());

FORM EVATReport 'Сверка ЭСЧФ'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)
    PROPERTIES() nameFilterTypeReport
       
    OBJECTS doc = EVATDocument
    PROPERTIES (doc) READONLY nameType, isPosted, number, series, date, time, nameCustomer, name, VATSum, VATSumEVAT, countEVAT, countCompletedEVAT, objectClassName 
        ORDER date(doc)
        
    FILTERS overNeedCreateEVAT(doc),
            date(doc) >= dFrom, date(doc) <= dTo,
            type(doc) == filterTypeReport() OR ( doc IS EVATDocument AND NOT filterTypeReport()),
            VATSum(doc)
            
    OBJECTS e = EVAT
    PROPERTIES(e) in
    PROPERTIES(e) READONLY exported, captionStatus, captionServerStatus, dateEVATDocument, seriesNumberEVATDocument, unpSender, number, exportYear, exportNumber, invoice, date, dateDoc, numberDoc, seriesDoc, dateCancelled, sendToRecipient, numberInvoicePrincipal,
                  dateInvoicePrincipal, numberInvoiceVendor, dateInvoiceVendor, nameSupplier, declarationSupplier, dateReleaseSupplier, dateActualExportSupplier,
                  numberTaxesSupplier, dateTaxesSupplier, nameCustomer, declarationCustomer, numberTaxesCustomer, dateTaxesCustomer, dateImportCustomer, totalSum,
                  totalExciseSum, totalVATSum, totalSumWithVAT
    FILTERS EVATDocument(e) == doc, 
            NOT statusServerStatus(e) == EVATServerStatus.cancelled
    
    FILTERGROUP filterMoved
        FILTER 'Нет ЭСЧФ' NOT countEVAT(doc) 'F10'
        FILTER 'Не содержит COMPLETED' countEVAT(doc) AND NOT (countEVAT(doc) == countCompletedEVAT(doc)) 'F9'
        FILTER 'В статусе ERROR' countErrorEVAT(doc)
        FILTER 'Отклонения в суммах' NOT (VATSum(doc) == VATSumEVAT(doc)) 'F8'
        FILTER 'Расхождения' (NOT (countEVAT(doc) == countCompletedEVAT(doc))) OR 
                             (NOT (VATSum(doc) == VATSumEVAT(doc))) 'F7' DEFAULT      
;

DESIGN EVATReport {
    BOX {
        NEW top {
            type = CONTAINERH;
            MOVE BOX(dates) {caption = 'Период';}
            NEW type {
                caption = 'Тип счет-фактуры';
                MOVE PROPERTY (nameFilterTypeReport());
            }
        }
        NEW header {
            fill = 1;
            type = SPLITV;
            MOVE BOX(doc) {fill = 2;}
            MOVE BOX(e);
        }
        PROPERTY (VATSumEVAT(doc)){background = RGB(255,238,238);}
        PROPERTY (countEVAT(doc)){background = RGB(255,238,238);}
        PROPERTY (countCompletedEVAT(doc)){background = RGB(255,238,238);}
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    financeNavigator {
        NEW EVATReport;
    }
}

setFilterType() = {
   filterTypeReport() <- EVATType.sale; 
}  

EXTEND FORM EVATReport
    EVENTS ON INIT setFilterType()
;

// -------------------------------- Отчет с разбивкой по ставкам НДС --------------------------------------------------

docVATSumRate10 'Сумма НДС по ставке 10% документа' = GROUP BY EVATDocument e SUM VATSumRange(e, Range r) IF valueCurrentRate(r) == 10.0;
VATSumRate10 'Сумма НДС по ставке 10%' = GROUP BY evat(EVATDetail d) SUM vatSum(d) IF vatRate(d) == 10.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
sumWithVATRate10 'Сумма с НДС по ставке 10%' = GROUP BY evat(EVATDetail d) SUM sumWithVAT(d) IF vatRate(d) == 10.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
docVATSumRate10EVAT 'Сумма НДС по ставке 10% ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM VATSumRate10(e);
docSumWithVATRate10EVAT 'Сумма с НДС по ставке 10% ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM sumWithVATRate10(e);

docVATSumRate20 'Сумма НДС по ставке 20% документа' = GROUP BY EVATDocument e SUM VATSumRange(e, Range r) IF valueCurrentRate(r) == 20.0;
VATSumRate20 'Сумма НДС по ставке 20%' = GROUP BY evat(EVATDetail d) SUM vatSum(d) IF vatRate(d) == 20.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
sumWithVATRate20 'Сумма с НДС по ставке 20%' = GROUP BY evat(EVATDetail d) SUM sumWithVAT(d) IF vatRate(d) == 20.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
docVATSumRate20EVAT 'Сумма НДС по ставке 20% ЭСЧФ' =  GROUP BY EVATDocument(EVAT e) SUM VATSumRate20(e);
docSumWithVATRate20EVAT 'Сумма с НДС по ставке 20% ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM sumWithVATRate20(e);

docVATSumRate0 'Сумма НДС по ставке 0% документа' = GROUP BY EVATDocument e SUM VATSumRange(e, Range r) IF valueCurrentRate(r) == 0.0;
VATSumRate0 'Сумма НДС по ставке 0%' = GROUP BY evat(EVATDetail d) SUM vatSum(d) IF vatRate(d) == 0.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
sumWithVATRate0 'Сумма с НДС по ставке 0%' = GROUP BY evat(EVATDetail d) SUM sumWithVAT(d) IF vatRate(d) == 0.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
docVATSumRate0EVAT 'Сумма НДС по ставке 0% ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM VATSumRate0(e);
docSumWithVATRate0EVAT 'Сумма с НДС по ставке 0% ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM sumWithVATRate0(e);

docVATSumRateOther 'Сумма НДС по расчетной ставке документа' = GROUP BY EVATDocument e SUM VATSumRange(e, Range r) 
    IF valueCurrentRate(r) != 10.0 AND valueCurrentRate(r) != 20.0 AND valueCurrentRate(r) != 0.0;
VATSumRateOther 'Сумма НДС по расчетной ставке' = GROUP BY evat(EVATDetail d) SUM vatSum(d) 
    IF vatRate(d) != 10.0 AND vatRate(d) != 20.0 AND vatRate(d) != 0.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
sumWithVATRateOther 'Сумма с НДС по расчетной ставке' = GROUP BY evat(EVATDetail d) SUM sumWithVAT(d) 
    IF vatRate(d) != 10.0 AND vatRate(d) != 20.0 AND vatRate(d) != 0.0 AND NOT statusServerStatus(evat(d)) == EVATServerStatus.cancelled;
docVATSumRateOtherEVAT 'Сумма НДС по расчетной ставке ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM VATSumRateOther(e);
docSumWithVATRateOtherEVAT 'Сумма с НДС по расчетной ставке ЭСЧФ' = GROUP BY EVATDocument(EVAT e) SUM sumWithVATRateOther(e);

sumWithVAT 'Сумма с НДС' = GROUP BY EVATDocument e SUM sumWithVATRange(e, Range r);

FORM reportDistinctVAT 'Отчет о выставленных ЭСЧФ'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)
    PROPERTIES() nameFilterTypeReport 
       
    OBJECTS doc = EVATDocument
    PROPERTIES (doc) name, nameCustomer, sumWithVAT, docVATSumRate20, docVATSumRate10, docVATSumRate0, docVATSumRateOther,
                     docVATSumRate20EVAT, docSumWithVATRate20EVAT, docVATSumRate10EVAT, docSumWithVATRate10EVAT,
                     docVATSumRate0EVAT, docSumWithVATRate0EVAT, docVATSumRateOtherEVAT, docSumWithVATRateOtherEVAT
    ORDER nameCustomer(doc)
    FILTERS overNeedCreateEVAT(doc), 
            date(doc) >= dFrom, date(doc) <= dTo,
            type(doc) == filterTypeReport() OR ( doc IS EVATDocument AND NOT filterTypeReport())
    
    OBJECTS doc1 = EVATDocument //чтобы в отчёте были и документы без ЭСЧФ
    FILTERS doc1 == doc    
            
    OBJECTS e = EVAT
    PROPERTIES(e) name, VATSumRate20, sumWithVATRate20, VATSumRate10, sumWithVATRate10, VATSumRate0, sumWithVATRate0, 
                    VATSumRateOther, sumWithVATRateOther
    FILTERS EVATDocument(e) == doc,
            NOT statusServerStatus(e) == EVATServerStatus.cancelled
;

reportDistinctVAT 'Отчет о выставленных ЭСЧФ (xlsx)' (DATE df, DATE dt) = {
    PRINT reportDistinctVAT OBJECTS dFrom = df, dTo = dt XLSX;
} IMAGE 'print.png' IN print;


EXTEND FORM EVATReport
    PROPERTIES (dFrom, dTo) reportDistinctVAT
;

DESIGN EVATReport {
    top {
         NEW report{
             type = CONTAINERH;
             caption = 'Печатная форма';
             MOVE PROPERTY (reportDistinctVAT(dFrom, dTo));
         } 
    }
}
