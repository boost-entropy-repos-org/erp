MODULE PurchaseInvoiceEVAT;

REQUIRE PurchaseInvoice, EVAT, CustomsGroup;

NAMESPACE Purchase;

@defineOperationProperty(eVAT, 'Создавать ЭСЧФ', createContainer);

createEVATPurchaseInvoice 'Накладные (закупка)' (DATE dFrom, DATE dTo) = ACTION {
    FOR eVAT(operation(Invoice i)) AND date(i) >= dFrom AND date(i) <= dTo ADDOBJ e = EVAT DO {
        status(e) <- EVATStatus.original;
        
        date(e) <- date(i);
        
        numberContract (e) <- number(contractSku(i));
        dateContract (e) <- dateFrom(contractSku(i));
        codeDocType (e) <- 603;
        blankCodeDoc (e) <- '402861';
        seriesDoc(e) <- series(i);
        numberDoc(e) <- number(i);
        unpSender(e) <- UNP(supplier(i));
        
        //Поставщик
        supplier(e) <- supplier(i);
        branchCodeSupplier(e) <- branchCode(supplierStock(i));
        
        //Получатель
        customer(e) <- customer(i);
        branchCodeCustomer(e) <- branchCode(customerStock(i));
        
        FOR invoice(InvoiceDetail id) == i ADDOBJ ed = EVATDetail DO {
            evat(ed) <- e;

            name(ed) <- nameSku(id);
            code(ed) <- OVERRIDE '0000000000', codeCustomsGroup(defaultCountry(), sku(id));
            shortNameUOM(ed) <- shortNameUOM(sku(id));
            quantity(ed) <- quantity(id); 
            price(ed) <- price(id);
            sum(ed) <- sum(id);
            exciseSum(ed) <- 0;
            vatRate(ed) <- valueVAT(id);
            vatSum(ed) <- VATSum(id);
            sumWithVAT(ed) <- invoiceSum(id);
            
            IF valueVAT(id) == 0 THEN
                descriptionType(ed) <- DescriptionType.vatExcemption
            ELSE
                descriptionType(ed) <- DescriptionType.deductionInFull;
        } 
    } 
    APPLY {};
} CONFIRM;

EXTEND FORM evats
    PROPERTIES createEVATPurchaseInvoice(dFrom, dTo) TOOLBAR TODRAW e
;

DESIGN evats {
    generate {
        MOVE PROPERTY(createEVATPurchaseInvoice(dFrom, dTo));
    }
}