MODULE InvoiceConsignmentBy;

REQUIRE Invoice,
        ConsignmentBy;

NAMESPACE Invoice;

@defineConsignmentAbstractHeader(invoice);
@implementConsignmentHeader (Invoice);
@implementConsignmentDetail (invoice);

supplierConsignment (consignment) += fromLegalEntityInvoice(consignment);
supplierStockConsignment (consignment) += fromStockInvoice(consignment);

customerConsignment (consignment) += toLegalEntityInvoice(consignment);
customerStockConsignment (consignment) += toStockInvoice(consignment);

currencyConsignment (consignment) += currencyInvoice(consignment);

priceConsignmentDetail (detail) += priceInvoiceDetail(detail);
sumConsignmentDetail (detail) += sumInvoiceDetail(detail);
vatConsignmentDetail (detail) += valueVATInvoiceDetail(detail);

sumVATConsignmentDetail (detail) += VATSumInvoiceDetail(detail);
sumInvoiceConsignmentDetail (detail) += invoiceSumInvoiceDetail(detail);
noteConsignmentDetail (detail) += noteInvoiceDetail(detail);
extraDescriptionConsignmentDetail (detail) += extraDescriptionInvoiceDetail(detail);

META defineInvoiceConsignment(contactA, contactB)

    @defineConsignmentInterfaceHeader(invoice);
    @implementConsignmentDocumentHeader(invoice, userInvoice, );

    EXTEND FORM  invoices
        PROPERTIES (i)  FORCE PANEL printDoubleSidedConsignmentsVertical, printConsignmentVerticalA, printConsignmentVerticalAB, printConsignmentHorizontalA,
                         printConsignmentVerticalB, printConsignmentHorizontalB,
                         printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                         printConsignmentSimpleVertical, printConsignmentSimpleAttach
    ;

    EXTEND DESIGN invoices {
        printContainer {
            NEW consignmentRow1 {
                align = LEADING;
                type = CONTAINERH;

                NEW contOne {
                    alignment = STRETCH;
                    type = CONTAINERH;
                    caption = 'Накладная';
                    ADD PROPERTY(editConsignment) {
                        alignment = STRETCH;
                    }
                }
                NEW tn2 {
                    alignment = STRETCH;
                    type = COLUMNS;
                    columns = 3;
                    caption = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW consignmentRow2 {
                align = LEADING;
                type = COLUMNS;
                columns = 3;
                caption = 'ТТН-1';
                ADD PROPERTY(printDoubleSidedConsignmentsVertical);
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentVerticalAB);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }
        }
    }
END
