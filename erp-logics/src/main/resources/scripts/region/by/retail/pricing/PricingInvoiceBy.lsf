MODULE PricingInvoiceBy;

REQUIRE PricingInvoice, PricingBy;

NAMESPACE Pricing;

META defineInvoicePricingByAggregation(dumb)

    // Цена надбавки   
    @defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, markup, ' надбавки');
    
    markupPrice(UserInvoiceDetail detail) <- retailMarkupSum(detail)/(pricingQuantity(detail) IF pricingQuantity(detail)!=0)
                    WHEN CHANGED(retailMarkupSum(detail)) OR CHANGED (pricingQuantity(detail));

    calcRetailUserInvoiceDetail(UserInvoiceDetail userInvoiceDetail, NUMERIC[20,7] markupPrice)  = round(NUMERIC[16,4]([= (X (+) Y)*(Z+100)/100](
        pricingPrice(userInvoiceDetail) IF markupPrice IS NUMERIC[20,7],
        markupPrice AS NUMERIC[20,7] IF userInvoiceDetail IS UserInvoiceDetail,
        valueRetailVAT(userInvoiceDetail))), roundConditionMarkupPrice());                    
                    
    changeMarkupPrice(UserInvoiceDetail userInvoiceDetail) = ACTION {
        INPUTX n = NUMERIC[16,4] DO {
            retailPrice(userInvoiceDetail) <- calcRetailUserInvoiceDetail(userInvoiceDetail, n);
            retailMarkup(userInvoiceDetail) <- calcRetailMarkup(userInvoiceDetail) WHERE calcRetailMarkup(userInvoiceDetail);
        } 
    } 
    
    @defineDocumentInterfaceProperty (invoice, showMarkupPrice, 'Надбавка (руб.)');    
    
    EXTEND FORM userInvoice
        PROPERTIES(i) showMarkupPrice SHOWIF createPricing(i)
        PROPERTIES(pd) BEFORE retailMarkupSum(pd) SHOWIF showMarkupPrice(i) BACKGROUND backgroundRetail(i)
                      markupPrice ON CHANGE changeMarkupPrice(pd)         
    ;
    
    DESIGN userInvoice {
        headerCreatePricing {
            MOVE PROPERTY(showMarkupPrice(i));
        }
    }
    EXTEND FORM invoices
        PROPERTIES(d) READONLY BEFORE retailMarkupSum(d) SHOWIF showMarkupPrice(i) BACKGROUND backgroundRetail(i)
                      markupPrice
    ;    
    markupPrice 'Цена надбавки' (InvoicePricingDetail detail) = markupPrice(invoiceDetail(detail)) PERSISTENT ;
    markupPrice (InvoicePricingDetail detail) += markupPrice(detail);
    
END
