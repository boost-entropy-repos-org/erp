MODULE LabelBy;

REQUIRE Label, Item;

NAMESPACE Label;

trunc = FORMULA INTEGER 'trunc($1)';

priceRubB 'Рубли' (LabelTransactionDetail d) = trunc(price(d));
priceKopB 'Копейки' (LabelTransactionDetail d) = lpad((OVERRIDE VARSTRING[2](round0(abs(priceRubB(d) (-) price(d))*100.0)), '' IF priceRubB(d)), 2, '0');

retailPriceRubB 'Рубли (цена до)' (LabelTransactionDetail d) = trunc(retailPrice(d));
retailPriceKopB 'Копейки (цена до)' (LabelTransactionDetail d) = lpad((OVERRIDE VARSTRING[2](round0(abs(retailPriceRubB(d) (-) retailPrice(d))*100.0)), '' IF retailPriceRubB(d)), 2, '0');

discountPriceRubB 'Рубли (скидка)' (LabelTransactionDetail d) = trunc(discountSum(d));
discountPriceKopB 'Копейки (скидка)' (LabelTransactionDetail d) = lpad((OVERRIDE VARSTRING[2](round0(abs(discountPriceRubB(d) (-) discountSum(d))*100.0)), '' IF discountPriceRubB(d)), 2, '0');


priceA 'Цена' (LabelTransactionDetail d) = price(d) * 10000;
retailPriceA 'Цена до' (LabelTransactionDetail d) = retailPrice(d) * 10000;
discountPriceA 'Скидка' (LabelTransactionDetail d) = discountSum(d) * 10000;


EXTEND FORM printLabelTransaction PROPERTIES(d) READONLY priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA;
EXTEND FORM customLabelTransaction PROPERTIES(d) priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA;
EXTEND FORM labelTransactions 
    PROPERTIES(dt) READONLY priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA 
    PROPERTIES(dts) READONLY priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA
;

//цена за литр\кг

@defineItemDefaultValue(printFullPrice, 'Печатать цену за килограмм', BOOLEAN);

@defineOption(minVolumeLabelBy, 'Объем/Вес для цены за 100г (0,05 = 50г)', NUMERIC[10,3], label);

overMinVolumeLabelBy() = OVERRIDE minVolumeLabelBy(), 0.25;

basePrice 'Цена за базовую единицу' (LabelTransactionDetail d) = IF printFullPrice(skuGroup(sku(d))) THEN CASE 
                                                                                                          WHEN volume(sku(d)) <= overMinVolumeLabelBy() THEN round2(price(d) / volume(sku(d)) / 10.0 )
                                                                                                          WHEN volume(sku(d)) != 1.0 THEN round2(price(d) / volume(sku(d)))
                                                                                                          WHEN netWeight(sku(d)) <= overMinVolumeLabelBy() THEN round2(price(d) / netWeight(sku(d)) / 10.0 )
                                                                                                          WHEN netWeight(sku(d)) != 1.0 THEN round2(price(d) / netWeight(sku(d))) ;
                                                                                                                                                                                                                  
basePriceName 'Базовая единица' (LabelTransactionDetail d) = IF printFullPrice(skuGroup(sku(d))) THEN CASE 
                                                                                                          WHEN volume(sku(d)) <= overMinVolumeLabelBy() THEN '100мл'
                                                                                                          WHEN volume(sku(d)) != 1.0 THEN '1л'
                                                                                                          WHEN netWeight(sku(d)) <= overMinVolumeLabelBy() THEN '100г'
                                                                                                          WHEN netWeight(sku(d)) != 1.0 THEN '1кг' ;
                                                                                                          
basePriceRub 'Рубли (за б.е.)' (LabelTransactionDetail d) = trunc(basePrice(d));
basePriceKop 'Копейки (за б.е.)' (LabelTransactionDetail d) = OVERRIDE round0((basePrice(d) (-) basePriceRub(d)) * 100.0), 0.0 IF basePrice(d);

baseRetailPrice 'Цена за базовую единицу до' (LabelTransactionDetail d) = IF printFullPrice(skuGroup(sku(d))) THEN CASE 
                                                                                                          WHEN volume(sku(d)) <= overMinVolumeLabelBy() THEN round2(retailPrice(d) / volume(sku(d)) / 10.0 )
                                                                                                          WHEN volume(sku(d)) != 1.0 THEN round2(retailPrice(d) / volume(sku(d)))
                                                                                                          WHEN netWeight(sku(d)) <= overMinVolumeLabelBy() THEN round2(retailPrice(d) / netWeight(sku(d)) / 10.0 )
                                                                                                          WHEN netWeight(sku(d)) != 1.0 THEN round2(retailPrice(d) / netWeight(sku(d))) ;

EXTEND FORM printLabelTransaction PROPERTIES(d) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice;
EXTEND FORM customLabelTransaction PROPERTIES(d) basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice;
EXTEND FORM labelTransactions 
    PROPERTIES(dt) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice 
    PROPERTIES(dts) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice
;
