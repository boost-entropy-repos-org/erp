MODULE LabelBy;

REQUIRE Label, Item;

NAMESPACE Label;

denominationDivider = IF currentDate() < denominationDate() THEN 10000.0 ELSE 1.0;
denominationMultiplier = IF currentDate() < denominationDate() THEN 1.0 ELSE 10000.0;

trunc = FORMULA INTEGER 'trunc($1)';

priceRubB 'Рубли' (LabelTransactionDetail d) = IF currentDate() < denominationDate() THEN divideInteger(price(d), denominationDivider()) ELSE trunc(price(d));
priceKopB 'Копейки' (LabelTransactionDetail d) = lpad((OVERRIDE '' IF priceRubB(d), VARSTRING[2](round0(abs(priceRubB(d) (-) round2(price(d) / denominationDivider()))*100.0))), 2, '0');

retailPriceRubB 'Рубли (цена до)' (LabelTransactionDetail d) = IF currentDate() < denominationDate() THEN divideInteger(retailPrice(d), denominationDivider()) ELSE trunc(retailPrice(d));
retailPriceKopB 'Копейки (цена до)' (LabelTransactionDetail d) = lpad((OVERRIDE '' IF retailPriceRubB(d), VARSTRING[2](round0(abs(retailPriceRubB(d) (-) round2(retailPrice(d) / denominationDivider()))*100.0))), 2, '0');

discountPriceRubB 'Рубли (скидка)' (LabelTransactionDetail d) = IF currentDate() < denominationDate() THEN divideInteger(discountSum(d), denominationDivider()) ELSE trunc(discountSum(d));
discountPriceKopB 'Копейки (скидка)' (LabelTransactionDetail d) = lpad((OVERRIDE '' IF discountPriceRubB(d), VARSTRING[2](round0(abs(discountPriceRubB(d) (-) round2(discountSum(d) / denominationDivider()))*100.0))), 2, '0');


priceA 'Цена' (LabelTransactionDetail d) = price(d) * denominationMultiplier();
retailPriceA 'Цена до' (LabelTransactionDetail d) = retailPrice(d) * denominationMultiplier();
discountPriceA 'Скидка' (LabelTransactionDetail d) = discountSum(d) * denominationMultiplier();


EXTEND FORM printLabelTransaction PROPERTIES(d) READONLY priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA;
EXTEND FORM customLabelTransaction PROPERTIES(d) priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA;
EXTEND FORM labelTransactions 
    PROPERTIES(dt) READONLY priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA 
    PROPERTIES(dts) READONLY priceRubB, priceKopB, retailPriceRubB, retailPriceKopB, discountPriceRubB, discountPriceKopB, priceA, retailPriceA, discountPriceA
;

//цена за литр\кг

@defineItemDefaultValue(printFullPrice, 'Печатать цену за килограмм', BOOLEAN);

pricePerKG 'Цена за килограмм' (LabelTransactionDetail d) = round2(price(d) / netWeight(sku(d))) IF printFullPrice(skuGroup(sku(d)));
priceRubKG 'Рубли (за кг.)' (LabelTransactionDetail d) = trunc(pricePerKG(d));
priceKopKG 'Копейки (за кг.)' (LabelTransactionDetail d) = OVERRIDE 0.0 IF priceRubKG(d), round0((pricePerKG(d) (-) priceRubKG(d))*100.0);

pricePerL 'Цена за Литр' (LabelTransactionDetail d) = round2(price(d) / volume(sku(d))) IF printFullPrice(skuGroup(sku(d)));
priceRubL 'Рубли(за л.)' (LabelTransactionDetail d) = trunc(pricePerL(d));
priceKopL 'Копейки(за л.)' (LabelTransactionDetail d) = OVERRIDE 0.0 IF priceRubL(d), round0((pricePerL(d) (-) priceRubL(d))*100.0);

EXTEND FORM printLabelTransaction PROPERTIES(d) READONLY pricePerKG, priceRubKG, priceKopKG, pricePerL, priceRubL, priceKopL;
EXTEND FORM customLabelTransaction PROPERTIES(d) pricePerKG, priceRubKG, priceKopKG, pricePerL, priceRubL, priceKopL;
EXTEND FORM labelTransactions 
    PROPERTIES(dt) READONLY pricePerKG, priceRubKG, priceKopKG, pricePerL, priceRubL, priceKopL 
    PROPERTIES(dts) READONLY pricePerKG, priceRubKG, priceKopKG, pricePerL, priceRubL, priceKopL
;
