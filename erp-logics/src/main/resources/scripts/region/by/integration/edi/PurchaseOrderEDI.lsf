MODULE PurchaseOrderEDI;

REQUIRE PurchaseOrder, EDI;

NAMESPACE EDI;

EXTEND CLASS Purchase.UserOrder : EOrder;

isClosed(Purchase.UserOrder o) += Purchase.isClosed(o);
isOpened(Purchase.UserOrder o) += Purchase.isOpened(o);
dateTime(Purchase.UserOrder o) += Purchase.dateTime[UserOrder](o);
shipmentDateTime(Purchase.UserOrder o) += Purchase.shipmentDateTime[UserOrder](o);
number(Purchase.UserOrder o) += Purchase.number(o);
supplier(Purchase.UserOrder o) += Purchase.supplier(o);
supplierStock(Purchase.UserOrder o) += Purchase.supplierStock(o);
customer(Purchase.UserOrder o) += Purchase.customer(o);
customerStock(Purchase.UserOrder o) += Purchase.customerStock(o);
note(Purchase.UserOrder o) += Purchase.note(o);

EXTEND CLASS Purchase.UserOrderDetail : EOrderDetail;

order(Purchase.UserOrderDetail o) += Purchase.userOrder(o);
sku(Purchase.UserOrderDetail o) += Purchase.sku(o);
quantity(Purchase.UserOrderDetail o) += Purchase.quantity(o);
price(Purchase.UserOrderDetail o) += Purchase.price(o);
valueVAT(Purchase.UserOrderDetail o) += Purchase.valueVAT(o);

isEOrder(Purchase.Order o) = EDIProvider(supplier(o)) IS EDIProvider;
isEOrder(Purchase.UserOrder o) = EDIProvider(supplier(o)) IS EDIProvider;
isEOrder(Purchase.OrderDetail d) = isEOrder(order(d));
isEOrder(Purchase.UserOrderDetail d) = isEOrder(userOrder(d));

response = GROUP MAX EOrderResponse e BY eOrder(e);
responseNote 'Примечание поставщика' (Purchase.Order o) = note(response(o)) IN documentPrm;
 
responseDetail = GROUP MAX EOrderResponseDetail ed IF eOrder(orderResponse(ed)) == order(Purchase.UserOrderDetail d) AND sku(ed) == sku(d) BY d;
quantityResponseEDI 'Подтвержденное количество (EDI)'(Purchase.OrderDetail d) = quantityAccepted(responseDetail(d));
quantityResponseEDI = GROUP SUM quantityResponseEDI(UserOrderDetail d) BY userOrder(d);
 
backgroundResponseEDI = ABSTRACT COLOR (Purchase.OrderDetail);

EXTEND FORM userOrder
    PROPERTIES (o) READONLY SHOWIF isEOrder(o) responseNote 
    PROPERTIES (d) READONLY SHOWIF isEOrder(d) BEFORE quantity(d) quantityResponseEDI BACKGROUND backgroundResponseEDI(d); 

EXTEND FORM orders
    PROPERTIES (o) READONLY responseNote 
    PROPERTIES (d) READONLY SHOWIF isEOrder(d) BEFORE quantity(d) quantityResponseEDI BACKGROUND backgroundResponseEDI(d); 

WHEN SET (UserOrder o == eOrder(EOrderResponse od)) AND deliveryDateTime(od) DO {
    shipmentDate(o) <- DATE(deliveryDateTime(od));
    shipmentTime(o) <- TIME (deliveryDateTime(od));
}