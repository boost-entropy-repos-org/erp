MODULE EDI;

REQUIRE LegalEntityBy, Item, Store, Warehouse;

DESIGN options {
    NEW EDI {
        caption = 'EDI';
    }
}

@defineDoubleItemAttribute(GTIN, VARSTRING[14], 'GTIN', itemBase);

GTIN = ABSTRACT VARSTRING[15](Sku);
GTIN(Item s) += GTIN(s);

GTINBarcode 'GTIN' (Sku s) = OVERRIDE GTIN(s), idBarcode(s);
skuGTINa = GROUP NAGGR Sku s BY GTINBarcode(s);
skuGTIN(VARSTRING[15] s) = OVERRIDE skuGTINa(s), skuBarcode(s);

CLASS ABSTRACT EOrder 'EDI Заказ';
TABLE eOrder(EOrder); 

exported 'Экспортирован' = DATA BOOLEAN (EOrder);

isClosed 'Закрыт' = ABSTRACT BOOLEAN (EOrder);
isOpened = ABSTRACT BOOLEAN (EOrder);

@defineOption(sendCurrentDateTime, 'Использовать текущую дату при отправке заказа поставщику', EDI);
dateTime 'Дата/время документа' = ABSTRACT DATETIME (EOrder);
sendDateTime (EOrder o) = OVERRIDE currentDateTime() IF sendCurrentDateTime() AND o IS EOrder, dateTime(o); 

shipmentDateTime 'Дата/время поставки' = ABSTRACT DATETIME (EOrder);

number 'Номер' = ABSTRACT VARSTRING[28] (EOrder);
eOrder (number) = GROUP AGGR EOrder e BY number(e);

supplier 'Поставщик' = ABSTRACT LegalEntity (EOrder);
nameSupplier 'Поставщик' (EOrder o) = name(supplier(o));

supplierStock 'Склад поставщика' = ABSTRACT Stock (EOrder);
nameSupplierStock 'Склад поставщика' (EOrder o) = name(supplierStock(o));

customer 'Покупатель' = ABSTRACT LegalEntity (EOrder);
nameCustomer 'Покупатель' (EOrder o) = name(customer(o));

customerStock 'Склад покупателя' = ABSTRACT Stock (EOrder);
nameCustomerStock 'Склад покупателя' (EOrder o) = name(customerStock(o));

note 'Примечание' = ABSTRACT VARISTRING[100] (EOrder);

CLASS ABSTRACT EOrderDetail 'Строка EDI Заказа';
TABLE eOrderDetail(EOrderDetail); 

order = ABSTRACT EOrder(EOrderDetail);
numberOrder (EOrderDetail d) = number(order(d)); 

sku = ABSTRACT Sku(EOrderDetail);
idSku 'Код товара' (EOrderDetail o) = id(sku(o));
nameSku 'Наименование' (EOrderDetail o) = name(sku(o));
idBarcode 'Штрихкод' (EOrderDetail o) = idBarcode(sku(o));
GTINBarcode 'GTIN' (EOrderDetail o) = GTINBarcode(sku(o));

extraCodeUOM 'Ед. изм. (EDI)' = DATA VARSTRING[10] (UOM);
extraCodeUOMSku 'Ед. изм.(EDI)' (EOrderDetail o) = extraCodeUOM(UOM(sku(o)));
EXTEND FORM UOMs PROPERTIES(u) READONLY extraCodeUOM;
EXTEND FORM UOM PROPERTIES(u) extraCodeUOM;

quantity 'Кол-во' = ABSTRACT NUMERIC[16,5] (EOrderDetail);
price 'Цена' = ABSTRACT NUMERIC[16,4] (EOrderDetail);
valueVAT 'НДС, %' = ABSTRACT NUMERIC[10,5] (EOrderDetail);

CLASS EDIProvider 'Провайдер EDI';

FORM EDIProviders 'Провайдеры EDI'
    OBJECTS t=EDIProvider
    PROPERTIES (t) READONLY staticCaption
    LIST EDIProvider OBJECT t
;

DESIGN EDIProviders { 
    preferredSize = (600, 400);
    PROPERTY (staticCaption(t)){caption = 'Наименование';};
}


GLN 'GLN' = DATA VARSTRING[13] (LegalEntity);
legalEntityGLN (legalEntity) = GROUP AGGR LegalEntity legalEntity BY GLN(legalEntity);
GLNSupplier(EOrder e) = GLN(supplier(e));
GLNCustomer(EOrder e) = GLN(customer(e));

EDIProvider 'Провайдер EDI' = DATA EDIProvider(LegalEntity);
nameEDIProvider 'Провайдер EDI'(LegalEntity l) = staticCaption(EDIProvider(l));
EXTEND FORM legalEntity PROPERTIES(l) GLN, nameEDIProvider;
DESIGN legalEntity {
    column2{
        NEW EDI {
            caption = 'EDI';
            MOVE PROPERTY (GLN(l));
            MOVE PROPERTY (nameEDIProvider(l));
        }
    }
}
EXTEND FORM legalEntities PROPERTIES(l) READONLYIF isReadonly() GLN, nameEDIProvider;

nameEDIProvider'Провайдер EDI'(EOrder o) = nameEDIProvider(supplier(o));

dataGLN 'GLN' = DATA VARSTRING[13] (Stock);
GLN 'GLN' (Stock s) = OVERRIDE dataGLN(s), GLN(legalEntity(s)) IF countStock(legalEntity(s)) == 1;
stockGLN (stock) = GROUP AGGR Stock stock BY GLN(stock);
legalEntityStockGLN(VARSTRING[13] GLN) = legalEntity(stockGLN(GLN));
GLNSupplierStock 'GLN поставщика' (EOrder e) = GLN(supplierStock(e));
GLNCustomerStock 'GLN покупателя' (EOrder e) = GLN(customerStock(e));
EXTEND FORM warehouse PROPERTIES(w) GLN;
EXTEND FORM warehouses PROPERTIES(w) READONLY GLN;
EXTEND FORM departmentStore PROPERTIES(d) GLN;
EXTEND FORM departmentStores PROPERTIES(d) READONLY GLN;

send 'Отправить' = ABSTRACT LIST (EOrder);
overSend = ABSTRACT LIST (EOrder);

skipSend = ABSTRACT CASE BOOLEAN (EOrder);

sendApply 'Отправить' (EOrder o) = {
    IF NOT skipSend(o) THEN NEWSESSION {
        send(o);
        APPLY;
    } ELSE {
        overSend(o);    
    }    
}

cancel 'Отменить' = ABSTRACT LIST (EOrder);

FORM eOrders 'Заказы EDI'
    OBJECTS o = EOrder
    PROPERTIES(o) READONLY exported EDITABLE, isClosed, dateTime, shipmentDateTime, number, nameSupplier, nameSupplierStock, 
                  nameEDIProvider, GLNSupplierStock, nameCustomer, nameCustomerStock, note
    PROPERTIES(o) sendApply TOOLBAR
    FILTERGROUP isEdi 
        FILTER 'EDI поставщик' EDIProvider(supplier(o)) 'F5' DEFAULT
    FILTERGROUP orderfilters6 
        FILTER 'Открыт' isOpened(o) 'F6' DEFAULT
    
    OBJECTS d = EOrderDetail
    PROPERTIES(d) READONLY idBarcode, GTINBarcode, idSku, nameSku, extraCodeUOMSku, quantity, price
    PROPERTIES(d) NEW, DELETE GRID
    
    FILTERS order(d) == o
;

DESIGN eOrders {
    BOX {
        MOVE BOX(o);
        NEW tab {
            fill = 1;
            type = TABBED;
            MOVE BOX(d);
        }
        MOVE TOOLBARBOX;
    }
}

//-----------------------------------------Сообщение по заказу EDI-----------------------------------------//
CLASS EOrderMessage 'Сообщение по заказу EDI';
TABLE eOrderMessage(EOrderMessage);

number 'Номер' = DATA VARSTRING[24] (EOrderMessage);
eOrderMessage (number) = GROUP AGGR EOrderMessage e BY number(e);
eOrder 'Заказ' = DATA EOrder (EOrderMessage) NONULL DELETE;
numberEOrder 'Заказ' (EOrderMessage m) = number(eOrder(m));
dateTime 'Дата/время сообщения' = DATA DATETIME (EOrderMessage);
code 'Код сообщения' = DATA VARSTRING[10] (EOrderMessage);
description 'Текст сообщения' = DATA VARSTRING[200] (EOrderMessage);
good = ABSTRACT CASE BOOLEAN (EOrderMessage);

EXTEND FORM eOrders
OBJECTS m = EOrderMessage
PROPERTIES(m) READONLY numberEOrder, dateTime, code, description
FILTERS eOrder(m) == o
ORDER BY dateTime(m);

DESIGN eOrders {
    tab {
        MOVE BOX(m);
    }
}

//-----------------------------------------Ответ по заказу EDI----------------------------------------------//
CLASS EOrderResponse 'Ответ по заказу EDI';
TABLE eOrderResponse(EOrderResponse);

CLASS EOrderResponseType 'Тип ответа' {
    changed 'Принят с  изменениями',
    cancelled 'Не принят',
    accepted 'Принят без изменений'
}

@defineExternalizable(eOrderResponse, VARSTRING[100]);
dateTime 'Дата/время документа' = DATA DATETIME (EOrderResponse);
number 'Номер' = DATA VARSTRING[28] (EOrderResponse);
note 'Примечание' = DATA VARSTRING[100] (EOrderResponse);
responseType 'Тип ответа' = DATA EOrderResponseType (EOrderResponse);
captionResponseType 'Тип ответа' (EOrderResponse e) = staticCaption(responseType(e));

eOrder 'Заказ' = DATA EOrder (EOrderResponse);
numberEOrder 'Номер заказа' (EOrderResponse e) = number(eOrder(e));
deliveryDateTime 'Дата/время доставки (с)' = DATA DATETIME (EOrderResponse);
deliveryDateTimeSecond 'Дата/время доставки (по)' = DATA DATETIME (EOrderResponse);

supplier 'Поставщик' = DATA LegalEntity (EOrderResponse);
nameSupplier 'Поставщик' (EOrderResponse o) = name(supplier(o));
nameEDIProvider 'EDI провайдер' (EOrderResponse o) = nameEDIProvider(supplier(o));

customer 'Покупатель' = DATA LegalEntity (EOrderResponse);
nameCustomer 'Покупатель' (EOrderResponse o) = name(customer(o));
customerStock 'Склад покупателя' = DATA Stock (EOrderResponse);
nameCustomerStock 'Склад покупателя' (EOrderResponse o) = name(customerStock(o));

CLASS EOrderResponseDetail 'Строка ответа по заказу EDI';
TABLE eOrderResponseDetail(EOrderResponseDetail); 
@defineExternalizable(eOrderResponseDetail, VARSTRING[100]);

CLASS EOrderResponseDetailAction 'Решение' {
    added 'Добавление',
    changed 'Изменение',
    accepted 'Принято без изменений',
    cancelled 'Не принято'
}

orderResponse = DATA EOrderResponse(EOrderResponseDetail) NONULL DELETE;
dataGTIN 'GTIN (из XML)' = DATA VARSTRING[15] (EOrderResponseDetail);
sku = DATA Sku(EOrderResponseDetail) INDEXED;
nameSku 'Наименование' (EOrderResponseDetail o) = name(sku(o));
idBarcode 'Штрихкод' (EOrderResponseDetail o) = idBarcode(sku(o));
GTINBarcode 'GTIN' (EOrderResponseDetail o) = GTINBarcode(sku(o));
extraCodeUOMSku 'Ед. изм.(EDI)' (EOrderResponseDetail o) = extraCodeUOM(UOM(sku(o)));

action 'Решение' (EOrderResponseDetail) = DATA EOrderResponseDetailAction (EOrderResponseDetail);
captionAction 'Решение' (EOrderResponseDetail e) = staticCaption(action(e));
quantityOrdered 'Кол-во (заказано)' = DATA NUMERIC[16,5] (EOrderResponseDetail);
quantityAccepted 'Кол-во (принято)' = DATA NUMERIC[16,5] (EOrderResponseDetail);
price 'Цена за единицу' = DATA NUMERIC[16,4] (EOrderResponseDetail);
sumNoNDS 'Сумма без НДС' = DATA NUMERIC[16,4] (EOrderResponseDetail);
sumNDS 'Сумма с НДС' = DATA NUMERIC[16,4] (EOrderResponseDetail);

FORM eOrderResponses 'Ответы по Заказам EDI'
OBJECTS o = EOrderResponse
PROPERTIES(o) READONLY dateTime, number, numberEOrder, captionResponseType, nameSupplier, nameEDIProvider, nameCustomer, 
nameCustomerStock, deliveryDateTime, deliveryDateTimeSecond, note

OBJECTS d = EOrderResponseDetail
PROPERTIES(d) READONLY idBarcode, dataGTIN, GTINBarcode, nameSku, captionAction, quantityOrdered, quantityAccepted, price, sumNoNDS, sumNDS

FILTERS orderResponse(d) == o
;

//--------------------------------------Уведомление об отгрузке EDI-----------------------------------------//
CLASS EOrderDespatchAdvice 'Уведомление об отгрузке EDI';
TABLE eOrderDespatchAdvice(EOrderDespatchAdvice);

@defineExternalizable(eOrderDespatchAdvice, VARSTRING[100]);
dateTime 'Дата/время документа' = DATA DATETIME (EOrderDespatchAdvice);
number 'Номер' = DATA VARSTRING[28] (EOrderDespatchAdvice);

deliveryNoteNumber 'Серия и номер бумажной накладной' = DATA VARSTRING[28] (EOrderDespatchAdvice);
deliveryNoteDateTime 'Дата бумажной накладной' = DATA DATETIME (EOrderDespatchAdvice);

eOrder 'Заказ' = DATA EOrder (EOrderDespatchAdvice);
numberEOrder 'Номер заказа' (EOrderDespatchAdvice e) = number(eOrder(e));
deliveryDateTime 'Дата/время доставки' = DATA DATETIME (EOrderDespatchAdvice);
note 'Примечание' = DATA VARSTRING[100] (EOrderDespatchAdvice);

supplier 'Поставщик' = DATA LegalEntity (EOrderDespatchAdvice);
nameSupplier 'Поставщик' (EOrderDespatchAdvice o) = name(supplier(o));
nameEDIProvider 'EDI провайдер' (EOrderDespatchAdvice o) = nameEDIProvider(supplier(o));

customer 'Покупатель' = DATA LegalEntity (EOrderDespatchAdvice);
nameCustomer 'Покупатель' (EOrderDespatchAdvice o) = name(customer(o));
customerStock 'Склад покупателя' = DATA Stock (EOrderDespatchAdvice);
nameCustomerStock 'Склад покупателя' (EOrderDespatchAdvice o) = name(customerStock(o));

CLASS EOrderDespatchAdviceDetail 'Строка ответа по заказу EDI';
TABLE eOrderEOrderDespatchAdviceDetail(EOrderDespatchAdviceDetail); 
@defineExternalizable(eOrderDespatchAdviceDetail, VARSTRING[100]);

orderDespatchAdvice = DATA EOrderDespatchAdvice(EOrderDespatchAdviceDetail) NONULL DELETE;
dataGTIN 'GTIN (из XML)' = DATA VARSTRING[15] (EOrderDespatchAdviceDetail);
sku = DATA Sku(EOrderDespatchAdviceDetail) INDEXED;
nameSku 'Наименование' (EOrderDespatchAdviceDetail o) = name(sku(o));
idBarcode 'Штрихкод' (EOrderDespatchAdviceDetail o) = idBarcode(sku(o));
GTINBarcode 'GTIN' (EOrderDespatchAdviceDetail o) = GTINBarcode(sku(o));
extraCodeUOMSku 'Ед. изм.(EDI)' (EOrderDespatchAdviceDetail o) = extraCodeUOM(UOM(sku(o)));
quantityOrdered 'Кол-во заказываемого товара' = DATA NUMERIC[16,5] (EOrderDespatchAdviceDetail);
quantityDespatch 'Кол-во отгружаемого товара' = DATA NUMERIC[16,5] (EOrderDespatchAdviceDetail);
valueVAT 'НДС, %' = DATA NUMERIC[16,5] (EOrderDespatchAdviceDetail);

lineItemPrice 'Цена за единицу товара' = DATA NUMERIC[16,4] (EOrderDespatchAdviceDetail);
lineItemAmountWithoutCharges 'Стоимость товарной позиции без НДС' = DATA NUMERIC[16,4] (EOrderDespatchAdviceDetail);
lineItemAmount 'Стоимость товарной позиции с НДС' = DATA NUMERIC[16,4] (EOrderDespatchAdviceDetail);
lineItemAmountCharges 'Сумма НДС' = DATA NUMERIC[16,4] (EOrderDespatchAdviceDetail);

FORM eOrderDespatchAdvices 'Уведомления об отгрузке EDI'
OBJECTS o = EOrderDespatchAdvice
PROPERTIES(o) READONLY dateTime, number, numberEOrder, deliveryNoteNumber, deliveryNoteDateTime, nameSupplier, nameEDIProvider, nameCustomer, nameCustomerStock, deliveryDateTime, note

OBJECTS d = EOrderDespatchAdviceDetail
PROPERTIES(d) READONLY idBarcode, dataGTIN, GTINBarcode, nameSku, quantityOrdered, quantityDespatch, valueVAT, lineItemPrice, 
                lineItemAmountWithoutCharges, lineItemAmount, lineItemAmountCharges

FILTERS orderDespatchAdvice(d) == o
;

//-----------------------------------------Электронная накладная----------------------------------------------//
CLASS EInvoice 'Электронная накладная EDI';
TABLE eInvoice(EInvoice);

exported 'Экспортирован' = DATA BOOLEAN (EInvoice);

@defineExternalizable(eInvoice, VARSTRING[100]);
dateTime 'Дата/время документа' = DATA DATETIME (EInvoice);
number 'Номер' = DATA VARSTRING[28] (EInvoice);
eInvoiceNumber (number) = GROUP AGGR EInvoice e BY number(e);

blrapn = DATA VARSTRING[14] (EInvoice);
eInvoiceBlrapn (number) = GROUP MAX EInvoice e BY blrapn(e);
numberEInvoiceBlrapn(number) = number(eInvoiceBlrapn(number));
blrwbr = DATA VARSTRING[14] (EInvoice);
eInvoiceBlrwbr (number) = GROUP MAX EInvoice e BY blrwbr(e);
numberEInvoiceBlrwbr(number) = number(eInvoiceBlrwbr(number));

reportId 'Номер акта' = DATA VARSTRING[70] (EInvoice);
reportDate 'Дата составления акта' = DATA DATE (EInvoice);
reportName 'Вид  акта' = DATA VARSTRING[512] (EInvoice);

deliveryNoteNumber 'Серия и номер бумажной накладной' = DATA VARSTRING[28] (EInvoice);
deliveryNoteDateTime 'Дата бумажной накладной' = DATA DATETIME (EInvoice);
sealIDReceiver 'Номер пломбы (грузоотправитель)' = DATA VARSTRING[35] (EInvoice);

supplier 'Поставщик' = DATA LegalEntity (EInvoice);
glnSupplier 'GLN поставщика' (EInvoice i) = GLN(supplier(i));
nameSupplier 'Поставщик' (EInvoice i) = name(supplier(i));
addressSupplier 'Адрес поставщика' (EInvoice i) = address(supplier(i));
unpSupplier 'УНП поставщика' (EInvoice i) = UNP(supplier(i));

customer 'Покупатель' = DATA LegalEntity (EInvoice);
nameCustomer 'Покупатель' (EInvoice i) = name(customer(i));
glnCustomer 'GLN покупателя' (EInvoice i) = GLN(customer(i));
addressCustomer 'Адрес покупателя' (EInvoice i) = address(customer(i));
unpCustomer 'УНП покупателя' (EInvoice i) = UNP(customer(i));

customerStock 'Склад покупателя' = DATA Stock (EInvoice);
glnCustomerStock 'GLN склада покупателя' (EInvoice i) = GLN(customerStock(i));
nameCustomerStock 'Склад покупателя' (EInvoice i) = name(customerStock(i));
addressCustomerStock 'Адрес склада покупателя' (EInvoice i) = address(customerStock(i));
contactCustomerStock 'ФИО и должность лица, которое отвечает за получение груза со стороны грузополучателя' = DATA VARSTRING[150] (EInvoice);

CLASS EInvoiceDetail 'Строка электронной накладной EDI';
TABLE eInvoiceDetail(EInvoiceDetail); 
@defineExternalizable(eInvoiceDetail, VARSTRING[100]);

certificateEDI 'Сертификат (EDI)' = DATA VARSTRING[100] (Computer);
certificateEDI 'Сертификат (EDI)' () = certificateEDI(currentComputer());
passwordEDI 'Пароль (EDI)' = DATA VARSTRING[100] (Computer) ECHO;
passwordEDI 'Пароль (EDI)' () = passwordEDI(currentComputer());
signerPathEDI 'Путь EDISigner (EDI)' = DATA VARSTRING[100] (Computer);
signerPathEDI 'Путь EDISigner (EDI)' () = signerPathEDI(currentComputer());
outputEDI 'Директория подписанных файлов (EDI)' = DATA VARSTRING[100] (Computer);
outputEDI 'Директория подписанных файлов (EDI)' () = outputEDI(currentComputer());

EXTEND FORM computer PROPERTIES(c) certificateEDI, passwordEDI, signerPathEDI, outputEDI;
EXTEND FORM computers PROPERTIES(c) READONLY certificateEDI, passwordEDI, signerPathEDI, outputEDI;

in 'Вкл.' = DATA LOCAL BOOLEAN (EInvoice);
signAndSend 'Подписать и отправить' = ABSTRACT LIST (EInvoice);
signAndSendEDI 'Подписать и отправить'() = {
    FOR in(EInvoice i) DO 
        signAndSend(i);
    ELSE
        MESSAGE 'Не выбрано ни одной накладной';
    
}

eInvoice = DATA EInvoice(EInvoiceDetail) NONULL DELETE;

lineItemID 'GTIN товара' = DATA VARSTRING[100] (EInvoiceDetail);
lineItemBuyerID 'Код товара в ERP системе покупателя (грузополучателя)'= DATA VARSTRING[100] (EInvoiceDetail);
lineItemName 'Наименование товара' = DATA VARSTRING[100] (EInvoiceDetail);

quantityDespatched 'Кол-во отгружаемого товара' = DATA NUMERIC[16,5] (EInvoiceDetail);
valueVAT 'НДС, %' = DATA NUMERIC[16,5] (EInvoiceDetail);

lineItemPrice 'Цена за единицу товара' = DATA NUMERIC[16,4] (EInvoiceDetail);
lineItemAmountWithoutCharges 'Стоимость товарной позиции без НДС' = DATA NUMERIC[16,4] (EInvoiceDetail);
lineItemAmount 'Стоимость товарной позиции с НДС' = DATA NUMERIC[16,4] (EInvoiceDetail);
lineItemAmountCharges 'Сумма НДС' = DATA NUMERIC[16,4] (EInvoiceDetail);


//-----------------------------------------Сообщение по накладной EDI-----------------------------------------//
CLASS EInvoiceMessage 'Сообщение по накладной EDI';
TABLE eInvoiceMessage(EInvoiceMessage);

number 'Номер' = DATA VARSTRING[24] (EInvoiceMessage);
eInvoiceMessage (number) = GROUP AGGR EInvoiceMessage e BY number(e);
eInvoice 'Заказ' = DATA EInvoice (EInvoiceMessage) NONULL DELETE;
numberEInvoice 'Заказ' (EInvoiceMessage m) = number(eInvoice(m));
dateTime 'Дата/время сообщения' = DATA DATETIME (EInvoiceMessage);
code 'Код сообщения' = DATA VARSTRING[10] (EInvoiceMessage);
description 'Текст сообщения' = DATA VARSTRING[200] (EInvoiceMessage);
good = ABSTRACT CASE BOOLEAN (EInvoiceMessage);


FORM eInvoices 'Электронные накладные EDI'
OBJECTS e = EInvoice
PROPERTIES(e) in, exported
PROPERTIES(e) READONLY dateTime, number, deliveryNoteNumber, deliveryNoteDateTime, glnSupplier, nameSupplier, addressSupplier, unpSupplier, 
                       nameCustomer, glnCustomer, addressCustomer, unpCustomer, nameCustomerStock
PROPERTIES(e) contactCustomerStock, reportId, reportDate, reportName

OBJECTS d = EInvoiceDetail
PROPERTIES(d) READONLY lineItemID, lineItemBuyerID, lineItemName, quantityDespatched, valueVAT, lineItemPrice, lineItemAmountWithoutCharges, lineItemAmount, lineItemAmountCharges

PROPERTIES() signAndSendEDI TODRAW e TOOLBAR

FILTERS eInvoice(d) == e

OBJECTS m = EInvoiceMessage
PROPERTIES(m) READONLY numberEInvoice, dateTime, code, description
FILTERS eInvoice(m) == e
ORDER BY dateTime(m);
;

DESIGN eInvoices {
    BOX {
        MOVE BOX(e);
        NEW tab {
            fill = 1;
            type = TABBED;
            MOVE BOX(d);
            MOVE BOX(m);
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    financeNavigator {
        NEW edi 'EDI' {
            ADD eOrders;
            ADD eOrderResponses;
            ADD eOrderDespatchAdvices;
            ADD eInvoices;
        }
    }
}