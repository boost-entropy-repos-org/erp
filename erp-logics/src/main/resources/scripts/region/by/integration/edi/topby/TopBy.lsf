MODULE TopBy;

REQUIRE System, EDI, Integration, Warehouse, Store;

loginTopBy 'Имя пользователя TopBy' = DATA VARSTRING[100] ();
passwordTopBy 'Пароль TopBy' = DATA VARSTRING[100] () ECHO;
hostTopBy 'Хост' = DATA VARSTRING[100] ();
portTopBy 'Порт' = DATA INTEGER ();
receive 'Получить сообщения' = ACTION CUSTOM 'lsfusion.erp.region.by.integration.edi.topby.ReceiveMessagesActionProperty' ();

EXTEND FORM integrationData
    PROPERTIES() loginTopBy, passwordTopBy, hostTopBy, portTopBy, receive
;
    
DESIGN integrationData {
    pane {
        NEW vs {
            caption = 'TopBy';
            MOVE PROPERTY(loginTopBy());
            MOVE PROPERTY(passwordTopBy());
            MOVE PROPERTY(hostTopBy());
            MOVE PROPERTY(portTopBy());
            MOVE PROPERTY(receive());
        }
    }
}

GLN 'GLN' = DATA VARSTRING[13] (LegalEntity);
legalEntityGLN (legalEntity) = GROUP AGGR LegalEntity legalEntity BY GLN(legalEntity);
GLNSupplier(EOrder e) = GLN(supplier(e));
GLNCustomer(EOrder e) = GLN(customer(e));
EXTEND FORM legalEntity PROPERTIES(l) GLN;
EXTEND FORM legalEntities PROPERTIES(l) READONLY GLN;

GLN 'GLN' = DATA VARSTRING[13] (Stock);
stockGLN (stock) = GROUP AGGR Stock stock BY GLN(stock);
GLNSupplierStock(EOrder e) = GLN(supplierStock(e));
GLNCustomerStock(EOrder e) = GLN(customerStock(e));
EXTEND FORM warehouse PROPERTIES(w) GLN;
EXTEND FORM warehouses PROPERTIES(w) READONLY GLN;
EXTEND FORM departmentStore PROPERTIES(d) GLN;
EXTEND FORM departmentStores PROPERTIES(d) READONLY GLN;

send 'Отправить' = ACTION CUSTOM 'lsfusion.erp.region.by.integration.edi.topby.SendEOrderActionProperty' (EOrder);

EXTEND FORM eOrders
PROPERTIES(o) send FORCE PANEL TOOLBAR
;


//-----------------------------------------Сообщение по заказу EDI-----------------------------------------//
CLASS EOrderMessage 'Сообщение по заказу EDI';
TABLE eOrderMessage(EOrderMessage);

number 'Номер' = DATA VARSTRING[24] (EOrderMessage);
eOrderMessage (number) = GROUP AGGR EOrderMessage e BY number(e);
eOrder 'Заказ' = DATA EOrder (EOrderMessage); //NOT NULL
numberEOrder 'Заказ' (EOrderMessage m) = number(eOrder(m));
dateTime 'Дата/время сообщения' = DATA DATETIME (EOrderMessage);
code 'Код сообщения' = DATA VARSTRING[10] (EOrderMessage);
description 'Текст сообщения' = DATA VARSTRING[200] (EOrderMessage);

EXTEND FORM eOrders
OBJECTS m = EOrderMessage
PROPERTIES(m) numberEOrder, dateTime, code, description
FILTERS eOrder(m) == o
ORDER BY dateTime(m);

DESIGN eOrders {
    tab {
        MOVE m.box;
    }
}

//-----------------------------------------Ответ по заказу EDI----------------------------------------------//
CLASS EOrderResponse 'Ответ по заказу EDI';
TABLE eOrderResponse(EOrderResponse);

CLASS EOrderResponseType 'Тип ответа' {
    changed 'Принят с  изменениями',
    cancelled 'Не принят',
    accepted 'Принят без изменений'
}

dateTime 'Дата/время документа' = DATA DATETIME (EOrderResponse);
number 'Номер' = DATA VARSTRING[28] (EOrderResponse);
eOrderResponse (number) = GROUP AGGR EOrderResponse e BY number(e);
responseType 'Тип ответа' = DATA EOrderResponseType (EOrderResponse);
captionResponseType 'Тип ответа' (EOrderResponse e) = staticCaption(responseType(e));

eOrder 'Заказ' = DATA EOrder (EOrderResponse);
numberEOrder 'Номер заказа' (EOrderResponse e) = number(eOrder(e));
deliveryDateTime 'Дата/время доставки' = DATA DATETIME (EOrderResponse);

supplier 'Поставщик' = DATA LegalEntity (EOrderResponse);
nameSupplier 'Поставщик' (EOrderResponse o) = name(supplier(o));

customer 'Покупатель' = DATA LegalEntity (EOrderResponse);
nameCustomer 'Покупатель' (EOrderResponse o) = name(customer(o));
customerStock 'Склад покупателя' = DATA Stock (EOrderResponse);
nameCustomerStock 'Склад покупателя' (EOrderResponse o) = name(customerStock(o));

CLASS EOrderResponseDetail 'Строка ответа по заказу EDI';
TABLE eOrderResponseDetail(EOrderResponseDetail); 
@defineExternalizable(eOrderResponseDetail, VARSTRING[100]);

CLASS EOrderResponseDetailAction 'Решение' {
    added 'Добавление',
    changed 'Изменение',
    accepted 'Принято без изменений',
    cancelled 'Не принято'
}

orderResponse = DATA EOrderResponse(EOrderResponseDetail);
sku = DATA Sku(EOrderResponseDetail);
nameSku 'Наименование' (EOrderResponseDetail o) = name(sku(o));
idBarcode 'Штрихкод' (EOrderResponseDetail o) = idBarcode(sku(o));
extraCodeUOMSku 'Ед. изм.(EDI)' (EOrderResponseDetail o) = extraCodeUOM(UOM(sku(o)));

action 'Решение' (EOrderResponseDetail) = DATA EOrderResponseDetailAction (EOrderResponseDetail);
captionAction 'Решение' (EOrderResponseDetail e) = staticCaption(action(e));
quantityOrdered 'Кол-во (заказано)' = DATA NUMERIC[16,5] (EOrderResponseDetail);
quantityAccepted 'Кол-во (принято)' = DATA NUMERIC[16,5] (EOrderResponseDetail);
priceNoNDS 'Цена без НДС' = DATA NUMERIC[16,4] (EOrderResponseDetail);
priceNDS 'Цена с НДС' = DATA NUMERIC[16,4] (EOrderResponseDetail);

FORM eOrderResponses 'Ответы по Заказам EDI'
OBJECTS o = EOrderResponse
PROPERTIES(o) READONLY dateTime, number, numberEOrder, captionResponseType, nameSupplier, nameCustomer, nameCustomerStock, deliveryDateTime

OBJECTS d = EOrderResponseDetail
PROPERTIES(d) READONLY idBarcode, nameSku, captionAction, quantityOrdered, quantityAccepted, priceNoNDS, priceNDS

FILTERS orderResponse(d) == o
;

//--------------------------------------Уведомление об отгрузке EDI-----------------------------------------//
CLASS EOrderDespatchAdvice 'Уведомление об отгрузке EDI';
TABLE eOrderDespatchAdvice(EOrderDespatchAdvice);

dateTime 'Дата/время документа' = DATA DATETIME (EOrderDespatchAdvice);
number 'Номер' = DATA VARSTRING[28] (EOrderDespatchAdvice);
eOrderDespatchAdvice (number) = GROUP AGGR EOrderDespatchAdvice e BY number(e);

deliveryNoteNumber 'Серия и номер бумажной накладной' = DATA VARSTRING[28] (EOrderDespatchAdvice);
deliveryNoteDateTime 'Дата бумажной накладной' = DATA DATETIME (EOrderDespatchAdvice);

eOrder 'Заказ' = DATA EOrder (EOrderDespatchAdvice);
numberEOrder 'Номер заказа' (EOrderDespatchAdvice e) = number(eOrder(e));
deliveryDateTime 'Дата/время доставки' = DATA DATETIME (EOrderDespatchAdvice);

supplier 'Поставщик' = DATA LegalEntity (EOrderDespatchAdvice);
nameSupplier 'Поставщик' (EOrderDespatchAdvice o) = name(supplier(o));

customer 'Покупатель' = DATA LegalEntity (EOrderDespatchAdvice);
nameCustomer 'Покупатель' (EOrderDespatchAdvice o) = name(customer(o));
customerStock 'Склад покупателя' = DATA Stock (EOrderDespatchAdvice);
nameCustomerStock 'Склад покупателя' (EOrderDespatchAdvice o) = name(customerStock(o));

CLASS EOrderDespatchAdviceDetail 'Строка ответа по заказу EDI';
TABLE eOrderEOrderDespatchAdviceDetail(EOrderDespatchAdviceDetail); 
@defineExternalizable(eOrderDespatchAdviceDetail, VARSTRING[100]);

orderDespatchAdvice = DATA EOrderDespatchAdvice(EOrderDespatchAdviceDetail);
sku = DATA Sku(EOrderDespatchAdviceDetail);
nameSku 'Наименование' (EOrderDespatchAdviceDetail o) = name(sku(o));
idBarcode 'Штрихкод' (EOrderDespatchAdviceDetail o) = idBarcode(sku(o));
extraCodeUOMSku 'Ед. изм.(EDI)' (EOrderDespatchAdviceDetail o) = extraCodeUOM(UOM(sku(o)));
quantityOrdered 'Кол-во заказываемого товара' = DATA NUMERIC[16,5] (EOrderDespatchAdviceDetail);
quantityDespatch 'Кол-во отгружаемого товара' = DATA NUMERIC[16,5] (EOrderDespatchAdviceDetail);

lineItemPrice 'Цена за единицу товара' = DATA NUMERIC[16,4] (EOrderDespatchAdviceDetail);
lineItemAmountWithoutCharges 'Стоимость товарной позиции без НДС' = DATA NUMERIC[16,4] (EOrderDespatchAdviceDetail);

FORM eOrderDespatchAdvices 'Уведомления об отгрузке EDI'
OBJECTS o = EOrderDespatchAdvice
PROPERTIES(o) READONLY dateTime, number, numberEOrder, nameSupplier, nameCustomer, nameCustomerStock, deliveryDateTime

OBJECTS d = EOrderDespatchAdviceDetail
PROPERTIES(d) READONLY idBarcode, nameSku, quantityOrdered, quantityDespatch, lineItemPrice, lineItemAmountWithoutCharges

FILTERS orderDespatchAdvice(d) == o
;

NAVIGATOR {
    financeNavigator {
        edi {
            ADD eOrderResponses;
            ADD eOrderDespatchAdvices;
        }
    }
}

//REQUIRE PurchaseInvoice, Item, LegalEntityBy, Warehouse;
//
//maxMessageNumber = DATA INTEGER (); 
//
//GLN 'GLN' = DATA VARSTRING[13] (LegalEntity);
//legalGLN (legalEntity) = GROUP AGGR LegalEntity legalEntity BY GLN(legalEntity);
//GLNSupplier(UserInvoice invoice) = GLN(supplier(invoice));
//GLNCustomer(UserInvoice invoice) = GLN(customer(invoice));
//EXTEND FORM legalEntity PROPERTIES(l) GLN;
//EXTEND FORM legalEntities PROPERTIES(l) READONLY GLN;
//
//GLN 'GLN' = DATA VARSTRING[13] (Stock);
//stockGLN (stock) = GROUP AGGR Stock stock BY GLN(stock);
//EXTEND FORM warehouse PROPERTIES(w) GLN;
//EXTEND FORM warehouses PROPERTIES(w) READONLY GLN;
//
//isCancelled 'Отменена' = DATA BOOLEAN (Purchase.UserInvoice);