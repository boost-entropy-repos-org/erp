MODULE PurchaseInvoiceEDI;

REQUIRE PurchaseInvoice, PurchaseOrderEDI;

NAMESPACE Purchase;

invoice = DATA Invoice(EOrderDespatchAdvice);

digits = FORMULA VARSTRING[250] 'regexp_replace(($1), \'\\D\', \'\', \'g\')';

indexSku(EOrderDespatchAdviceDetail d) = PARTITION SUM 1 BY sku(d), eOrder(orderDespatchAdvice(d)) ORDER quantityOrdered(d), d MATERIALIZED INDEXED;
despatchAdviceDetail = GROUP MAX EOrderDespatchAdviceDetail ed 
    IF eOrder(orderDespatchAdvice(ed)) == order(Purchase.OrderDetail d) AND sku(ed) == sku(d) AND indexSku(d) == indexSku(ed) 
    BY d;

createUserInvoice 'Создать накладную' (EOrderDespatchAdvice a) = {
    
    NEWSESSION NESTED LOCAL {
        NEW i = UserInvoice {
        
            invoice(a) <- i;
            fillHeader(i, eOrder(a));
            number(i) <- VARSTRING[28](digits(deliveryNoteNumber(a)));
            series(i) <- VARSTRING[2](left(replace(deliveryNoteNumber(a), digits(deliveryNoteNumber(a)), ''), 2));
            
            FOR orderDespatchAdvice(EOrderDespatchAdviceDetail ad) == a AND despatchAdviceDetail(OrderDetail od) == ad AND NOT skipCreateUserInvoice(od)
                ORDER ad 
                NEW d = UserInvoiceDetail DO {
                    userInvoice(d) <- i;
                    orderDetail(d) <- od;
                    sku(d) <- sku(ad);
                    quantity(d) <- quantityDespatch(ad);
                    VAT(d) <- VAT(od);
                    valueVAT(d) <- valueVAT(od);
                    priceListType(d) <- priceListType(od);
                    price(d) <- lineItemPrice(ad);
                    overFill(d, od);
            }
            
            include(Order order, i) <- TRUE WHERE order == eOrder(a);
            
            SHOW userInvoice OBJECTS i = i DOCKED NOCANCEL;
        }
    }
}

EXTEND FORM eOrderDespatchAdvices 
    PROPERTIES (o) createUserInvoice TOOLBAR;


FORM selectDespatchAdvice 'Уведомления об отгрузке'
    OBJECTS o = EOrder PANEL 
    OBJECTS a = EOrderDespatchAdvice
    PROPERTIES (a) READONLY dateTime, number, deliveryNoteNumber, nameSupplier, nameCustomer, nameCustomerStock, deliveryDateTime
    FILTERS eOrder(a) == o
    FILTERGROUP invoice
        FILTER 'Не выписаны' NOT invoice(a) DEFAULT 
;
countEOrderDespatchAdvice = GROUP SUM 1 IF NOT invoice(EOrderDespatchAdvice a) BY eOrder(a);

beforeCreateInvoice(Purchase.Order o) += {

    IF isEOrder(o) AND countEOrderDespatchAdvice(o) THEN {
        DIALOG selectDespatchAdvice OBJECTS o = o, a INPUT NOMANAGESESSION DO {
            createUserInvoice(a);
            invoiceCreated(o) <- TRUE;
        }
    }

}