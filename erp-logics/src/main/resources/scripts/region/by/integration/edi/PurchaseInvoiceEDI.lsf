MODULE PurchaseInvoiceEDI;

REQUIRE PurchaseInvoice, PurchaseOrderEDI;

NAMESPACE Purchase;

invoice = DATA Invoice(EOrderDespatchAdvice);

digits = FORMULA VARSTRING[250] 'regexp_replace(($1), \'\\D\', \'\', \'g\')';
createUserInvoice 'Создать накладную' (EOrderDespatchAdvice a) = ACTION {

    FOR NEW i = UserInvoice DO {
    
        invoice(a) <- i;
        fillHeader(i, eOrder(a));
        number(i) <- digits(deliveryNoteNumber(a));
        series(i) <- left(replace(deliveryNoteNumber(a), digits(deliveryNoteNumber(a)), ''), 2);
        
        FOR orderDespatchAdvice(EOrderDespatchAdviceDetail ad) == a AND OrderDetail od == [=GROUP MAX OrderDetail d BY sku(d), order(d)](sku(ad), eOrder(a))
            ORDER ad 
            NEW d = UserInvoiceDetail DO {
                userInvoice(d) <- i;
                orderDetail(d) <- od;
                sku(d) <- sku(ad);
                quantity(d) <- quantityDespatch(ad);
                VAT(d) <- VAT(od);
                valueVAT(d) <- valueVAT(od);
                priceListType(d) <- priceListType(od);
                price(d) <- lineItemPrice(ad);
                overFill(d, od);
        }
        
        include(Order order, i) <- TRUE WHERE order == eOrder(a);
        
        SHOW userInvoice OBJECTS i = i MANAGESESSION DOCKED NOCANCEL;
    }

}

EXTEND FORM eOrderDespatchAdvices 
    PROPERTIES (o) createUserInvoice TOOLBAR;


FORM selectDespatchAdvice 'Уведомления об отгрузке'
    OBJECTS o = EOrder FIXED PANEL 
    OBJECTS a = EOrderDespatchAdvice
    PROPERTIES (a) READONLY dateTime, number, deliveryNoteNumber, nameSupplier, nameCustomer, nameCustomerStock, deliveryDateTime
    FILTERS eOrder(a) == o
    FILTERGROUP invoice
        FILTER 'Не выписаны' NOT invoice(a) DEFAULT 
;
countEOrderDespatchAdvice = GROUP SUM 1 IF NOT invoice(EOrderDespatchAdvice a) BY eOrder(a);

beforeCreateInvoice(Purchase.Order o) += ACTION {

    IF isEOrder(o) AND countEOrderDespatchAdvice(o) THEN {
        DIALOG selectDespatchAdvice OBJECTS o = o, a INPUT DO {
            createUserInvoice(a);
            invoiceCreated(o) <- TRUE;
        }
    }

}