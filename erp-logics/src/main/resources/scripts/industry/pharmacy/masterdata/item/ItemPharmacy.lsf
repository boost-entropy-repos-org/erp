MODULE ItemPharmacy;

REQUIRE Item;

NAMESPACE Item;

GROUP itemPharmacy 'Фармакологические свойства';

// Действующее вещество
CLASS Substance 'Действующее вещество';
TABLE substance(Substance);

name 'Наименование' = DATA VARISTRING[100](Substance);
substanceName = GROUP AGGR Substance s BY name(s);
@defineExternalizable(substance, VARSTRING[100]);

FORM substance 'Действующие вещества'
    OBJECTS t=Substance FIXED PANEL
    PROPERTIES(t) name, id SHOWIF showIDs()
    EDIT Substance OBJECT t
;

FORM substances 'Действующие вещества'
    OBJECTS t=Substance
    PROPERTIES(t) NEWSESSION name READONLY, id SHOWIF showIDs() READONLY, DELETE 
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER BY name(t)
    LIST Substance OBJECT t
;
DESIGN substances { main{ preferredSize = (600, 400); } }

@defineObjectItemAttribute(substance, Substance, name, 'Действующее вещество', itemPharmacy);

@defineItemFilterValue(substance, 'Действующее вещество', t);

@defineUniteAttributeItem(substance, name, 'действующее вещество', 'действующие вещества', item);

EXTEND FORM attributesItem
    PROPERTIES READONLY AFTER name(substance) id(substance) SHOWIF showIDs()
;
@defineObjectItemAttributeBatch (substance, nameSubstance, 'Действующее вещество');

@definePrimitiveItemAttribute(needPrescription, BOOLEAN, 'Необходим рецепт', itemPharmacy);

@definePrimitiveItemAttribute(blisterAmount, INTEGER, 'Кол-во блистеров', itemPharmacy);

DESIGN item {
    itemPrimaryColumn1 {
        MOVE i.itemPharmacy {
            columns = 2;
        }
    }
}

DESIGN itemGroup {
    main {
        itemGroupDetail {
            MOVE g.itemPharmacy {
                type = CONTAINERH;
            };
        }
    }
}

