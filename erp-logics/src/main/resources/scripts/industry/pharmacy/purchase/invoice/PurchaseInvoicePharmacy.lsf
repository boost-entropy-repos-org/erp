MODULE PurchaseInvoicePharmacy;

REQUIRE System, PurchaseInvoice, StockPharmacy;

NAMESPACE Purchase;

// серия лекарства
seriesPharmacyInvoiceDetail 'Серия лекарственного средства' = ABSTRACT VARSTRING[20] (InvoiceDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;
seriesPharmacyUserInvoiceDetail 'Серия лекарственного средства' = DATA VARSTRING[20] (UserInvoiceDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;
seriesPharmacyInvoiceDetail(detail) += seriesPharmacyUserInvoiceDetail(detail);

// контрактная цена и валюта
contractPriceInvoiceDetail 'Контрактная цена' = ABSTRACT VARSTRING[20] (InvoiceDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;
contractPriceUserInvoiceDetail 'Контрактная цена' = DATA VARSTRING[20] (UserInvoiceDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;
contractPriceInvoiceDetail(detail) += contractPriceUserInvoiceDetail(detail);

@defineDocumentInterfaceHeaderProperty (invoice, showContractPrice, 'Контрактная цена');

@defineOperationProperty(showContractPrice, 'Контрактная цена', commonContainer);

@deriveDocumentOperationProperty(UserInvoice, showContractPrice);

// страна ввоза
importCountryInvoiceDetail 'Страна ввоза' = ABSTRACT Country (InvoiceDetail);
nameImportCountryInvoiceDetail 'Страна ввоза' = nameCountry(importCountryInvoiceDetail(invoiceDetail)) MINCHARWIDTH 15 PREFCHARWIDTH 15;

importCountryUserInvoiceDetail 'Страна ввоза' = DATA Country (UserInvoiceDetail);
nameImportCountryUserInvoiceDetail 'Страна ввоза' = nameCountry(importCountryUserInvoiceDetail(invoiceDetail)) MINCHARWIDTH 15 PREFCHARWIDTH 15;

importCountryInvoiceDetail(detail) += importCountryUserInvoiceDetail(detail);

EXTEND FORM userInvoice
    PROPERTIES(i) showContractPriceUserInvoice
    PROPERTIES(d) BACKGROUND backgroundSkuUserInvoiceDetail(d) seriesPharmacyUserInvoiceDetail SHOWIF showSeriesPharmacy() BEFORE nameBatchUserInvoiceDetail(d) ,
                  contractPriceUserInvoiceDetail SHOWIF showContractPriceInvoice(i) BEFORE nameBatchUserInvoiceDetail(d),
                  nameImportCountryUserInvoiceDetail SHOWIF showImportCountry() BEFORE nameBatchUserInvoiceDetail(d)
;

DESIGN userInvoice {
    headerExtraParams {
        NEW headerContractPrice {
            caption = 'Контрактная цена';
            ADD PROPERTY(showContractPriceUserInvoice(i));
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(d) READONLY seriesPharmacyInvoiceDetail SHOWIF showSeriesPharmacy() BEFORE nameBatchInvoiceDetail(d),
                           contractPriceInvoiceDetail SHOWIF showContractPriceUserInvoice(i) BEFORE nameBatchInvoiceDetail(d),
                           nameImportCountryInvoiceDetail SHOWIF showImportCountry() BEFORE nameBatchInvoiceDetail(d)
;


WHEN SESSION FORMS userInvoice (CHANGED (batchUserInvoiceDetail(detail)) OR
                               CHANGED (customerStockUserInvoiceDetail(detail))) AND batchUserInvoiceDetail(detail) AND NOT CHANGED(seriesPharmacyUserInvoiceDetail(detail)) DO {
    seriesPharmacyUserInvoiceDetail(detail) <- seriesPharmacyBatch(batchUserInvoiceDetail(detail));
}

WHEN SESSION FORMS userInvoice (CHANGED (batchUserInvoiceDetail(detail)) OR
                               CHANGED (customerStockUserInvoiceDetail(detail))) AND batchUserInvoiceDetail(detail) AND NOT CHANGED(contractPriceUserInvoiceDetail(detail)) DO {
    contractPriceUserInvoiceDetail(detail) <- contractPriceBatch(batchUserInvoiceDetail(detail));
}