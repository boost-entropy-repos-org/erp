MODULE PurchaseShipmentPharmacy;

REQUIRE PurchaseShipment, PurchaseInvoicePharmacy, StockPharmacy, Country, Item, ItemPharmacyBy;

NAMESPACE Purchase;

// контрактная цена
contractPrice 'Контрактная цена' = ABSTRACT VARSTRING[20] (ShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

contractPrice 'Контрактная цена' = DATA VARSTRING[20] (UserShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

@defineDocumentAggregationDetailProperty (invoice, invoiceShipment, contractPrice, 'Контрактная цена');

contractPrice(UserShipmentDetail detail) += contractPrice(detail);
contractPrice(InvoiceShipmentDetail detail) += contractPrice(detail);

contractPrice (ShipmentBatch batch) += contractPrice(shipmentDetail(batch));

// серия лекарства
seriesPharmacy 'Серия лекарственного средства' = ABSTRACT VARSTRING[20] (ShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

seriesPharmacy 'Серия лекарственного средства' = DATA VARSTRING[20] (UserShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

@defineDocumentAggregationDetailProperty (invoice, invoiceShipment, seriesPharmacy, 'Серия лекарственного средства');

seriesPharmacy(UserShipmentDetail detail) += seriesPharmacy(detail);
seriesPharmacy(InvoiceShipmentDetail detail) += seriesPharmacy(detail);

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) += ACTION {
    ASSIGN seriesPharmacy(s) <- seriesPharmacy(i);
}

seriesPharmacy (ShipmentBatch batch) += seriesPharmacy(shipmentDetail(batch));

// страна производства
importCountry 'Страна ввоза' = ABSTRACT Country (ShipmentDetail);
nameImportCountry 'Страна ввоза' = name(importCountry(ShipmentDetail shipmentDetail)) MINCHARWIDTH 15 PREFCHARWIDTH 15;

importCountry 'Страна ввоза' = DATA Country (UserShipmentDetail);
nameImportCountry 'Страна ввоза' = name(importCountry(UserShipmentDetail shipmentDetail)) MINCHARWIDTH 15 PREFCHARWIDTH 15;

@defineDocumentAggregationDetailProperty (invoice, invoiceShipment, importCountry, 'Страна ввоза');

importCountry(UserShipmentDetail detail) += importCountry(detail);
importCountry(InvoiceShipmentDetail detail) += importCountry(detail);

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) += ACTION {
    ASSIGN importCountry(s) <- importCountry(i);
}

importCountry (ShipmentBatch batch) += importCountry(shipmentDetail(batch));


netWeightSku 'Вес нетто, кг (ед.)' (ShipmentDetail detail) = netWeight[Item](sku(detail));

changeNetWeightSku(ShipmentDetail detail) = ACTION {
    REQUEST NUMERIC[9,3] INPUT;
    
    IF requestedNumeric() THEN {
        netWeight(Item sku) <- requestedNumeric() WHERE sku(detail) == sku;    
    }
} 

manufacturerSku(ShipmentDetail detail) = manufacturer(sku(detail));
nameManufacturerSku 'Производитель лекарственного средства' (ShipmentDetail detail) = name(manufacturerSku(detail));

changeManufacturerSku(ShipmentDetail detail) = ACTION {
    DIALOG manufacturerDialog OBJECTS m = manufacturer(sku(detail)) INPUT NULL DO 
        manufacturer(Item sku) <- m WHERE sku(detail) == sku;    
} 

pharmacyPriceGroupSku(ShipmentDetail detail) = pharmacyPriceGroup[Item](sku(detail));
namePharmacyPriceGroupSku 'Тип лекарственного средства' (ShipmentDetail detail) = name(pharmacyPriceGroupSku(detail));

changePharmacyPriceGroupSku(ShipmentDetail detail) = ACTION {
    DIALOG pharmacyPriceGroups OBJECTS g = pharmacyPriceGroup(sku(detail)) INPUT NULL DO
        pharmacyPriceGroup(Item sku) <- g WHERE sku(detail) == sku;    
}

EXTEND FORM userShipment
    PROPERTIES(d) BEFORE nameBatch(d) nameManufacturerSku ON CHANGE changeManufacturerSku(d),
                                                     namePharmacyPriceGroupSku ON CHANGE changePharmacyPriceGroupSku(d), 
                                                     nameImportCountry SHOWIF showImportCountry(),
                                                     netWeightSku ON CHANGE changeNetWeightSku(d) ,
                                                     seriesPharmacy[ShipmentDetail] SHOWIF showSeriesPharmacy()
;

EXTEND FORM shipments
    PROPERTIES(d) READONLY BEFORE nameBatch(d) nameManufacturerSku, namePharmacyPriceGroupSku,
                                                          nameImportCountry SHOWIF showImportCountry(),
                                                          netWeightSku,
                                                          seriesPharmacy SHOWIF showSeriesPharmacy()
;