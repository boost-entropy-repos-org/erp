MODULE PurchaseShipmentPharmacy;

REQUIRE PurchaseShipment, PurchaseInvoicePharmacy, StockPharmacy, Country, Item, ItemPharmacyBy;

NAMESPACE Purchase;

// контрактная цена
contractPriceShipmentDetail 'Контрактная цена' = ABSTRACT VARSTRING[20] (ShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

contractPriceUserShipmentDetail 'Контрактная цена' = DATA VARSTRING[20] (UserShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

@defineDocumentAggregationDetailProperty (invoice, invoiceShipment, contractPrice, 'Контрактная цена');

contractPriceShipmentDetail(detail) += contractPriceUserShipmentDetail(detail);
contractPriceShipmentDetail(detail) += contractPriceInvoiceShipmentDetail(detail);

contractPriceBatch (batch) += contractPriceShipmentDetail(shipmentDetailShipmentBatch(batch));

// серия лекарства
seriesPharmacyShipmentDetail 'Серия лекарственного средства' = ABSTRACT VARSTRING[20] (ShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

seriesPharmacyUserShipmentDetail 'Серия лекарственного средства' = DATA VARSTRING[20] (UserShipmentDetail) MINCHARWIDTH 7 PREFCHARWIDTH 7;

@defineDocumentAggregationDetailProperty (invoice, invoiceShipment, seriesPharmacy, 'Серия лекарственного средства');

seriesPharmacyShipmentDetail(detail) += seriesPharmacyUserShipmentDetail(detail);
seriesPharmacyShipmentDetail(detail) += seriesPharmacyInvoiceShipmentDetail(detail);

overFillInvoiceUserShipmentDetailInvoiceDetail(s, i) += ACTION (s, i) {
    ASSIGN seriesPharmacyUserShipmentDetail(s) <- seriesPharmacyInvoiceDetail(i);
}

seriesPharmacyBatch (batch) += seriesPharmacyShipmentDetail(shipmentDetailShipmentBatch(batch));

// страна производства
importCountryShipmentDetail 'Страна ввоза' = ABSTRACT Country (ShipmentDetail);
nameImportCountryShipmentDetail 'Страна ввоза' = nameCountry(importCountryShipmentDetail(shipmentDetail)) MINCHARWIDTH 15 PREFCHARWIDTH 15;

importCountryUserShipmentDetail 'Страна ввоза' = DATA Country (UserShipmentDetail);
nameImportCountryUserShipmentDetail 'Страна ввоза' = nameCountry(importCountryUserShipmentDetail(shipmentDetail)) MINCHARWIDTH 15 PREFCHARWIDTH 15;

@defineDocumentAggregationDetailProperty (invoice, invoiceShipment, importCountry, 'Страна ввоза');

importCountryShipmentDetail(detail) += importCountryUserShipmentDetail(detail);
importCountryShipmentDetail(detail) += importCountryInvoiceShipmentDetail(detail);

overFillInvoiceUserShipmentDetailInvoiceDetail(s, i) += ACTION (s, i) {
    ASSIGN importCountryUserShipmentDetail(s) <- importCountryInvoiceDetail(i);
}

importCountryBatch (batch) += importCountryShipmentDetail(shipmentDetailShipmentBatch(batch));


netWeightSkuShipmentDetail 'Вес нетто, кг (ед.)' (detail) = netWeightItem(skuShipmentDetail(detail));

changeNetWeightSkuShipmentDetail = ACTION (detail) {
    REQUEST NUMERIC[9,3] INPUT;
    
    IF requestedNumeric() THEN {
        netWeightItem(sku) <- requestedNumeric() WHERE skuShipmentDetail(detail) == sku;    
    }
} 

manufacturerSkuShipmentDetail(detail) = manufacturerItem(skuShipmentDetail(detail));
nameManufacturerSkuShipmentDetail 'Производитель лекарственного средства' (detail) = nameManufacturer(manufacturerSkuShipmentDetail(detail));

changeManufacturerSkuShipmentDetail = ACTION (detail) {
    REQUEST OBJECT m FORM manufacturerDialog MODAL;
    
    IF formResult() == FormResult.ok THEN {
        manufacturerItem(sku) <- requestedObject() WHERE skuShipmentDetail(detail) == sku;    
    }
} 

pharmacyPriceGroupSkuShipmentDetail(detail) = pharmacyPriceGroupItem(skuShipmentDetail(detail));
namePharmacyPriceGroupSkuShipmentDetail 'Тип лекарственного средства' (detail) = namePharmacyPriceGroup(pharmacyPriceGroupSkuShipmentDetail(detail));

changePharmacyPriceGroupSkuShipmentDetail = ACTION (detail) {
    REQUEST OBJECT g FORM pharmacyPriceGroups MODAL;
    
    IF formResult() == FormResult.ok THEN {
        pharmacyPriceGroupItem(sku) <- requestedObject() WHERE skuShipmentDetail(detail) == sku;    
    }
}

EXTEND FORM userShipment
    PROPERTIES(d) BEFORE nameBatchUserShipmentDetail(d) nameManufacturerSkuShipmentDetail ON CHANGE changeManufacturerSkuShipmentDetail(d),
                                                     namePharmacyPriceGroupSkuShipmentDetail ON CHANGE changePharmacyPriceGroupSkuShipmentDetail(d), 
                                                     nameImportCountryUserShipmentDetail SHOWIF showImportCountry(),
                                                     netWeightSkuShipmentDetail ON CHANGE changeNetWeightSkuShipmentDetail(d) ,
                                                     seriesPharmacyShipmentDetail SHOWIF showSeriesPharmacy()
;

EXTEND FORM shipments
    PROPERTIES(d) READONLY BEFORE nameBatchShipmentDetail(d) nameManufacturerSkuShipmentDetail, namePharmacyPriceGroupSkuShipmentDetail,
                                                          nameImportCountryShipmentDetail SHOWIF showImportCountry(),
                                                          netWeightSkuShipmentDetail,
                                                          seriesPharmacyShipmentDetail SHOWIF showSeriesPharmacy()
;