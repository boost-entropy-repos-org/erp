MODULE PurchasePriceLimitPharmacy;

REQUIRE PricingPharmacy, PricingPurchase, PurchaseManufacturingPrice, PricingPriceLimit;

NAMESPACE Purchase;

//------------------ Предельные надбавки ----------------------//

@defineDocumentInterfaceProperty (invoice, showLimitPharmacyPrice, 'Предельная надбавка');
@defineDocumentInterfaceDetailMarkupPrefix (invoice, limitPharmacy, ' предельная');

WHEN SESSION FORMS userInvoice 
             (CHANGED(customerStock(UserInvoiceDetail detail)) OR
              CHANGED(sku(detail)) OR CHANGED(manufacturingPrice(detail)) OR
              SET(showLimitPharmacyPrice(detail)))
             AND showLimitPharmacyPrice(detail) DO {   
    limitPharmacyMarkup(detail) <- retailMarkupPharmacy(sku(detail), NUMERIC[16,4](manufacturingPrice(detail)), date(detail));
}

//----------------------------------------------- Операции -----------------------------------------------------//
// Вид цены для расценки для операции
showLimitPharmacyPrice 'Предельная розн. надбавка' = DATA BOOLEAN(Pricing.Operation) IN documentPrm;

EXTEND FORM Pricing.operation
    PROPERTIES(o) showLimitPharmacyPrice
;

DESIGN Pricing.operation {
    showContainer {
        MOVE PROPERTY(showLimitPharmacyPrice(o));
    }
}

// Записываем необходимо ли показывать предельную надбавку по умолчанию из операции расценки, заданной для операции закупка
showLimitPharmacyPrice (UserInvoice invoice) <- showLimitPharmacyPrice(pricingOperation(invoice))
    WHEN CHANGED(operation(invoice));

showIfLimitPrice = showLimitPharmacyPrice (UserInvoice invoice) AND createPricing(invoice);

EXTEND FORM userInvoice
    PROPERTIES(i) showLimitPharmacyPrice SHOWIF createPricing(i) BACKGROUND backgroundRetail(i)
    PROPERTIES(pd) SHOWIF showIfLimitPrice(i) BEFORE retailMarkup(pd) limitPharmacyMarkup BACKGROUND backgroundRetail(i)
;

DESIGN userInvoice{
    headerCreatePricing {
        MOVE PROPERTY (showLimitPharmacyPrice(i));

    }
}

EXTEND FORM invoices
    PROPERTIES(d) READONLY SHOWIF showLimitPharmacyPrice(i) BEFORE retailMarkup(d) limitPharmacyMarkup BACKGROUND backgroundRetail(i)
;

//расчет предельной цены
limitPrice (UserInvoiceDetail detail) =
                                       round(
                                           [= X*(Y+100.0)*(Z+100.0)/10000.0](
                                                    manufacturingPrice(detail),
                                                    limitPharmacyMarkup(detail),
                                                    valueRetailVAT(detail)), limitRetailMarkupPharmacyCondition());

WHEN SESSION FORMS userInvoice GOAFTER retailPrice[UserInvoiceDetail] 
            (SETCHANGED(retailPrice(UserInvoiceDetail d)) OR CHANGED(manufacturingPrice(d)) OR CHANGED(sku(d))  OR CHANGED(price(d))) AND //  вообще в событии должно быть это условие, но тогда не всегда срабатывает (см. skype с Vitalur за 10.07) 
             showLimitPharmacyPrice(d) NOINLINE DO {
    IF limitPrice(d) < retailPrice(d) THEN {
        retailPrice(d) <- limitPrice(d);
        retailMarkup(d) <- calcRetailMarkup(d);
    }
}
                                                    
showLimitPrice(InvoicePricing pricing) += showLimitPharmacyPrice(invoice(pricing));
limitMarkup(InvoicePricingDetail detail) += limitPharmacyMarkup(invoiceDetail(detail));                                                                                