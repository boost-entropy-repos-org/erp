MODULE ItemMaterial;

REQUIRE Item;

NAMESPACE Item;

// Материал
CLASS Material 'Материал';
TABLE material(Material);

name 'Наименование' = DATA VARISTRING[100](Material);
shortName 'Наименование (сокр.)' = DATA VARISTRING[50](Material);

@defineExternalizable(material, VARSTRING[100]);

parseComposition 'Разбить состав на материалы' = ACTION CUSTOM 'lsfusion.erp.ParseCompositionItemActionProperty' (Item);

FORM material 'Материал'
    OBJECTS t=Material FIXED PANEL
    PROPERTIES(t) name, shortName, id SHOWIF showIDs()
    EDIT Material OBJECT t
;

FORM materials 'Материалы'
    OBJECTS t=Material
    PROPERTIES(t) NEWSESSION name READONLY, shortName READONLY , id SHOWIF showIDs() READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER BY name(t)
    LIST Material OBJECT t
;
DESIGN materials { main{ preferredSize = (600, 400); } }

TABLE materialItem (Material, Item);
percent 'Процент' = DATA INTEGER (Material, Item);

percentMaterials 'Процент' (i) = GROUP SUM percent(Material m,Item i) BY i;

calcComposition 'Состав' (Item i) = VARSTRING[255](
                       [= GROUP CONCAT (percent(Material m,Item i)+'% '+name(m)) IF percent(m,i), ', ' BY i ORDER DESC percent(m,i)](i))
                       MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;

WHEN SESSION FORMS item CHANGED(calcComposition(Item i)) DO {
    composition(i) <- calcComposition(i);            
} 
backgroundMaterials (Item i) = RGB(255,0,0) IF i IS Item AND (percentMaterials(i) != 100.0);

FORM itemMaterial 'Материалы'
    OBJECTS i=Item FIXED PANEL 
    PROPERTIES(i) READONLY percentMaterials BACKGROUND  backgroundMaterials(i)
    
    OBJECTS t=Material
    PROPERTIES(t) NEWSESSION name READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t,i) percent
    
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER BY name(t)
;
DESIGN itemMaterial { main { preferredSize = (600, 400); } }     

createComposition 'Выбрать состав'(Item item) = ACTION {          
    SHOW itemMaterial OBJECTS i = item ; 
} SHORTCUT composition[Item];  

//добавление на форму атрибутов товара
in 'Отм.' = DATA BOOLEAN (Material) MAXCHARWIDTH 5;
allowReplaceMaterial = DATA LOCAL BOOLEAN ();

FORM confirmReplaceMaterial 'Объединяемые материалы'
    OBJECTS material = Material FIXED PANEL 

    OBJECTS o = Material  
    PROPERTIES(o) READONLY name
    FILTERS o==material

    OBJECTS o2 = Material
    PROPERTIES(o2) in
    PROPERTIES(o2) READONLY name
    FILTERS in(o2)
;

DESIGN confirmReplaceMaterial {
    REMOVE material.box;
    NEW splitContainer{
        type = SPLITV;
        fill = 1;
        MOVE o.box{
            caption = 'Основной материал';
        }
        MOVE o2.box{
            caption = 'Удаляемый материал';
        }           
    }
    MOVE functions.box;
}

replace 'Объединить'(Material material) = ACTION {
    DIALOG confirmReplaceMaterial OBJECTS o = material DO {
        allowReplaceMaterial() <- TRUE;
        FOR in(Material o) AND allowReplaceMaterial() DO{
            IF o != material THEN {
                FOR percent(o, Item item) DO {
                    IF percent(material, item) THEN {
                        percent(material, item) <- percent(o, item) + percent(material, item);   
                        percent(o, item) <- NULL;                        
                    } ELSE {
                        percent(material, item) <- percent(o, item);
                        percent(o, item) <- NULL;
                    }                    
                    composition(item) <- calcComposition(item);                                                                         
                }
                in(o) <- NULL;
                DELETE o;                  
            } ELSE {
                MESSAGE 'Выделенный материал не может совпадать с объединяемым';
            }
        }
        apply();
    } ELSE {
        in(Material o) <- NULL WHERE in(o);           
    }   
};

EXTEND FORM attributesItem
    OBJECTS material = Material
    PROPERTIES in(material)
    PROPERTIES(material) READONLY name
    PROPERTIES(material) READONLY shortName, id SHOWIF showIDs()
    PROPERTIES(material) NEWSESSION NEW, EDIT, DELETE FORCE PANEL TOOLBAR        
    ORDER BY name(material)
    PROPERTIES replace(material) TODRAW material FORCE PANEL TOOLBAR
;

DESIGN attributesItem {
    tabContainer {
        MOVE material.box{
            caption = 'Материалы';
        }
    }
}
