MODULE ItemMaterial;

REQUIRE Item;

NAMESPACE Item;

// Материал
CLASS Material 'Материал';
TABLE material(Material);

nameMaterial 'Наименование' = DATA VARISTRING[100](Material);
shortNameMaterial 'Наименование (сокр.)' = DATA VARISTRING[50](Material);

@defineExternalizable(material, VARSTRING[100]);

parseCompositionItem 'Разбить состав на материалы' = ACTION CUSTOM 'lsfusion.erp.ParseCompositionItemActionProperty' (Item);

FORM material 'Материал'
    OBJECTS t=Material FIXED PANEL
    PROPERTIES(t) nameMaterial, shortNameMaterial, idMaterial SHOWIF showIDs()
    EDIT Material OBJECT t
;

FORM materials 'Материалы'
    OBJECTS t=Material
    PROPERTIES(t) nameMaterial READONLY, shortNameMaterial READONLY , idMaterial SHOWIF showIDs() READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameMaterial(t)
    DIALOG Material OBJECT t
;
DESIGN materials { main{ preferredSize = (600, 400); } }

TABLE materialItem (Material, Item);
percentMaterialItem 'Процент' = DATA INTEGER (Material, Item);

percentMaterialsItem 'Процент' (i) = GROUP SUM percentMaterialItem(m,i) BY i;

calcCompositionItem 'Состав' (i) = VARSTRING[255](
                       [= GROUP CONCAT (percentMaterialItem(m,i)+'% '+nameMaterial(m)) IF percentMaterialItem(m,i), ', ' BY i ORDER DESC percentMaterialItem(m,i)](i))
                       MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;

WHEN SESSION FORMS item CHANGED(calcCompositionItem(i)) DO {
    compositionItem(i) <- calcCompositionItem(i);            
} 
backgroundMaterialsItem (i) = RGB(255,0,0) IF i IS Item AND (percentMaterialsItem(i) != 100.0);

FORM itemMaterial 'Материалы'
    OBJECTS i=Item FIXED PANEL 
    PROPERTIES(i) READONLY percentMaterialsItem BACKGROUND  backgroundMaterialsItem(i)
    
    OBJECTS t=Material
    PROPERTIES(t) nameMaterial READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t,i) percentMaterialItem
    
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameMaterial(t)
;
DESIGN itemMaterial { main { preferredSize = (600, 400); } }     

createCompositionItem 'Выбрать состав' = ACTION (item) {          
    FORM itemMaterial OBJECTS i = item MODAL; 
} SHORTCUT compositionItem;  

//добавление на форму атрибутов товара
inMaterial 'Отм.' = DATA BOOLEAN (Material) MAXCHARWIDTH 5;
allowReplaceMaterial = DATA LOCAL BOOLEAN ();

FORM confirmReplaceMaterial 'Объединяемые материалы'
    OBJECTS material = Material FIXED PANEL 

    OBJECTS o = Material  
    PROPERTIES(o) READONLY nameMaterial
    FILTERS o==material

    OBJECTS o2 = Material
    PROPERTIES(o2) inMaterial
    PROPERTIES(o2) READONLY nameMaterial
    FILTERS inMaterial(o2)
;

DESIGN confirmReplaceMaterial {
    REMOVE material.box;
    NEW splitContainer{
        type = SPLITV;
        fill = 1;
        MOVE o.box{
            caption = 'Основной материал';
        }
        MOVE o2.box{
            caption = 'Удаляемый материал';
        }           
    }
    MOVE functions.box;
}

replaceMaterial 'Объединить' = ACTION (material) {
    FORM confirmReplaceMaterial OBJECTS o=material MODAL;
    IF formResult() == FormResult.ok THEN {
        allowReplaceMaterial() <- TRUE;
        FOR inMaterial(o) AND allowReplaceMaterial() DO{
            IF o != material THEN {
                FOR percentMaterialItem(o, item) DO {
                    IF percentMaterialItem(material, item) THEN {
                        percentMaterialItem(material, item) <- percentMaterialItem(o, item) + percentMaterialItem(material, item);   
                        percentMaterialItem(o, item) <- NULL;                        
                    } ELSE {
                        percentMaterialItem(material, item) <- percentMaterialItem(o, item);
                        percentMaterialItem(o, item) <- NULL;
                    }                    
                    compositionItem(item) <- calcCompositionItem(item);                                                                         
                }
                inMaterial(o) <- NULL;
                DELETE o;                  
            } ELSE {
                MESSAGE 'Выделенный материал не может совпадать с объединяемым';
            }
        }
        apply();
    } ELSE {
        inMaterial(o) <- NULL WHERE inMaterial(o);           
    }   
};

EXTEND FORM attributesItem
    OBJECTS material = Material
    PROPERTIES inMaterial(material)
    PROPERTIES(material) READONLY nameMaterial
    PROPERTIES(material) READONLY shortNameMaterial, idMaterial SHOWIF showIDs()
    PROPERTIES(material) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR        
    ORDER BY nameMaterial(material)
    PROPERTIES replaceMaterial(material) TODRAW material FORCE PANEL TOOLBAR
;

DESIGN attributesItem {
    tabContainer {
        MOVE material.box{
            caption = 'Материалы';
        }
    }
}
