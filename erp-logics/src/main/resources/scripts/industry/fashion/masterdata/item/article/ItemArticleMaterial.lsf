MODULE ItemArticleMaterial;

REQUIRE ItemMaterial, ItemArticle;

NAMESPACE Item;

//-- Article

TABLE materialArticle(Material, Article);
percent 'Процент' = DATA INTEGER (Material, Article);

percentMaterials 'Процент' (i) = GROUP SUM percent(Material m,Article i) BY i;

calcComposition 'Состав' (Article i) = VARSTRING[255](
                       [= GROUP CONCAT (percent(Material m,Article i)+'% '+name(m)) IF percent(m,i), ', ' BY i ORDER DESC percent(m,i)](i))
                       MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;

WHEN SESSION FORMS article CHANGED(calcComposition(Article i)) DO {
    composition(i) <- calcComposition(i);            
} 
backgroundMaterials (Article i) = RGB(255,0,0) IF i IS Article AND (percentMaterials(i) != 100.0);

parseComposition 'Разбить состав на материалы' = ACTION CUSTOM 'lsfusion.erp.ParseCompositionArticleActionProperty' (Article);

FORM articleMaterial 'Материалы'
    OBJECTS i=Article FIXED PANEL 
    PROPERTIES(i) READONLY percentMaterials BACKGROUND  backgroundMaterials(i)
    
    OBJECTS t=Material
    PROPERTIES(t) name READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t,i) percent
    
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY name(t)
;
DESIGN articleMaterial { main { preferredSize = (600, 400); } }     

createComposition 'Выбрать состав'(Article article) = ACTION {          
    FORM articleMaterial OBJECTS i = article ; 
} SHORTCUT composition[Article];  