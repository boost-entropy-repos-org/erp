MODULE ItemArticleMaterial;

REQUIRE ItemMaterial, ItemArticle;

NAMESPACE Item;

//-- Article

TABLE materialArticle(Material, Article);
percent 'Процент' = DATA INTEGER (Material, Article);

percentMaterials 'Процент' (i) = GROUP SUM percent(Material m,Article i) BY i;

calcComposition 'Состав' (Article i) = VARSTRING[255](
                       [= GROUP CONCAT (percent(Material m,Article i)+'% '+name(m)) IF percent(m,i), ', ' BY i ORDER DESC percent(m,i)](i))
                       MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 MATERIALIZED;

WHEN LOCAL FORMS article CHANGED(calcComposition(Article i)) DO {
    composition(i) <- calcComposition(i);            
} 
backgroundMaterials (Article i) = RGB(255,0,0) IF i IS Article AND (percentMaterials(i) != 100.0);

parseComposition 'Разбить состав на материалы' = CUSTOM 'lsfusion.erp.ParseCompositionArticleActionProperty' (Article);

FORM articleMaterial 'Материалы'
    OBJECTS i=Article PANEL 
    PROPERTIES(i) READONLY percentMaterials BACKGROUND  backgroundMaterials(i)
    
    OBJECTS t=Material
    PROPERTIES(t) NEWSESSION name READONLY, DELETE 
    PROPERTIES(t,i) percent
    
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER name(t)
;
DESIGN articleMaterial { BOX { preferredSize = (600, 400); } }     

createComposition 'Выбрать состав'(Article article) = {          
    SHOW articleMaterial OBJECTS i = article ; 
} CONTEXTMENU composition[Article];  