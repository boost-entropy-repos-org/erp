MODULE Transport;

REQUIRE System,
        Utils,
        Employee,
        LegalEntity,
        Stock;

// Модели машины
CLASS TruckModel 'Модель';
TABLE truckModel (TruckModel);

name 'Наименование' = DATA VARISTRING[50](TruckModel);

FORM truckModel 'Модель машины'
    OBJECTS m=TruckModel PANEL
    PROPERTIES (m) name

    EDIT TruckModel OBJECT m
;

FORM truckModels 'Модели машин'
    OBJECTS m=TruckModel
    PROPERTIES(m) READONLY name
    PROPERTIES(m) NEWSESSION NEW, EDIT, DELETE 

    LIST TruckModel OBJECT m
;

// Машины
CLASS Truck 'Машина';
TABLE truck(Truck);

sid 'Номер' = DATA VARISTRING[20](Truck);
truckModel = DATA TruckModel(Truck);
nameTruckModel 'Марка' (Truck truck) = name(truckModel(truck));

name 'Наименование' (Truck truck) = VARISTRING[70](CONCAT ',', nameTruckModel(truck), 'г.н. '+sid(truck));

trailer 'Прицеп' = DATA VARISTRING[100](Truck);
weight 'Грузоподъёмность (кг)' = DATA NUMERIC[16,4](Truck);
owner 'Владелец' = DATA VARISTRING[200](Truck);
notOwnertruck = NOT owner(Truck truck);
legalEntity = DATA LegalEntity (Truck);
nameLegalEntity 'Владелец(Организация)' = fullName(legalEntity(Truck truck)); 

FORM truck 'Машина'
    OBJECTS t=Truck PANEL
    PROPERTIES (t) sid, nameTruckModel, name, trailer, owner, weight, 
                   nameLegalEntity SHOWIF notOwnertruck(t)

    EDIT Truck OBJECT t
;

DESIGN truck {
    t.panel.props {
        columns = 3;
    }
}

FORM trucks 'Машины'
    OBJECTS t=Truck
    PROPERTIES(t) READONLY sid, nameTruckModel, name, trailer, owner, 
                           nameLegalEntity, weight
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE 

    LIST Truck OBJECT t
;

// Водители

CLASS Driver 'Водитель': Employee;

sid 'Табельный номер' = DATA STRING[10](Driver);
type 'Класс' = DATA STRING[10](Driver);

defaultTruck = DATA Truck(Driver);
nameDefaultTruck 'Машина по умолчанию' = name(defaultTruck(Driver driver));


FORM driver 'Водитель'
    OBJECTS e=Driver PANEL
    PROPERTIES(e)      lastName, firstName, initialsEmployee, id SHOWIF showIDs(), namePosition,
                       sid, type, nameDefaultTruck//nameMainRoleUser

    EDIT Driver OBJECT e
;

DESIGN driver {
    main {
        preferredSize = (1024, 768);
        NEW header {
            type = CONTAINERV;
            NEW personal {
                caption = 'Личные данные';
                type = CONTAINERH;                
                MOVE PROPERTY(lastName(e));
                MOVE PROPERTY(firstName(e));   
                MOVE PROPERTY(initialsEmployee(e));                  
                MOVE PROPERTY(id(e));
                MOVE PROPERTY(namePosition(e));
            }
            NEW driver {
                caption = 'Данные водителя';
                type = CONTAINERH;
                MOVE PROPERTY(nameDefaultTruck(e));
                MOVE PROPERTY(sid(e));
                MOVE PROPERTY(type(e));
            }
        }
        MOVE functions.box;
    }
}
FORM drivers 'Водители'
    PROPERTIES isEditable()
    OBJECTS e=Driver
    PROPERTIES(e) READONLYIF isReadonly() lastName, firstName, initialsEmployee, id SHOWIF showIDs(), 
                  namePosition, shortName, sid, type, nameDefaultTruck//nameMainRoleUser
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE 
    LIST Driver OBJECT e
;

DESIGN drivers {
    MOVE functions.box{
        MOVE PROPERTY(isEditable()) BEFORE rightControls;
    }
}

NAVIGATOR {
    NEW transportNavigator 'Транспорт' BEFORE administration WINDOW toolbar IMAGE '/images/truck.png' {
        NEW transportDocuments 'Документы';
        NEW transportMasterData 'Справочники' {            
            ADD trucks;
            ADD truckModels;
            ADD drivers;
        }
    }
}