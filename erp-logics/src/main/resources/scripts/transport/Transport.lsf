MODULE Transport;

REQUIRE System,
        Utils,
        Employee,
        LegalEntity,
        Stock;

// Модели машины
CLASS TruckModel 'Модель';
TABLE truckModel (TruckModel);

nameTruckModel 'Наименование' = DATA VARISTRING[50](TruckModel);

FORM truckModel 'Модель машины'
    OBJECTS m=TruckModel FIXED PANEL
    PROPERTIES (m) nameTruckModel

    EDIT TruckModel OBJECT m
;

FORM truckModels 'Модели машин'
    OBJECTS m=TruckModel
    PROPERTIES(m) READONLY nameTruckModel
    PROPERTIES(m) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG TruckModel OBJECT m
;

// Машины
CLASS Truck 'Машина';
TABLE truck(Truck);

sidTruck 'Номер' = DATA VARSTRING[20](Truck);
truckModelTruck = DATA TruckModel(Truck);
nameTruckModelTruck 'Марка' (truck) = nameTruckModel(truckModelTruck(truck));

//nameTruck 'Наименование' (truck) = [= FORMULA VARSTRING[70] ' CAST($1 AS TEXT) || \' \' || CAST($2 AS TEXT)'](nameTruckModelTruck(truck), sidTruck(truck));
nameTruck 'Наименование' (truck) = VARSTRING[70](CONCAT ',', nameTruckModelTruck(truck), 'г.н. '+sidTruck(truck));

trailerTruck 'Прицеп' = DATA VARSTRING[100](Truck);
weightTruck 'Грузоподъёмность (кг)' = DATA NUMERIC[14,2](Truck);
ownerTruck 'Владелец' = DATA VARSTRING[100](Truck);

FORM truck 'Машина'
    OBJECTS t=Truck FIXED PANEL
    PROPERTIES (t) sidTruck, nameTruckModelTruck, nameTruck, trailerTruck, ownerTruck, weightTruck

    EDIT Truck OBJECT t
;

DESIGN truck FROM DEFAULT {
    t.panel.props {
        columns = 3;
    }
}

FORM trucks 'Машины'
    OBJECTS t=Truck
    PROPERTIES(t) READONLY sidTruck, nameTruckModelTruck, nameTruck, trailerTruck, ownerTruck, weightTruck
    PROPERTIES(t) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG Truck OBJECT t
;

// Водители

CLASS Driver 'Водитель': Employee;

sidDriver 'Табельный номер' = DATA STRING[10](Driver);
typeDriver 'Класс' = DATA STRING[10](Driver);

defaultTruckDriver = DATA Truck(Driver);
nameDefaultTruckDriver 'Машина по умолчанию' = nameTruck(defaultTruckDriver(driver));

EXTEND FORM employee
    PROPERTIES(e) sidDriver, typeDriver, nameDefaultTruckDriver
;

EXTEND DESIGN employee{
    header {
        NEW driver BEFORE access {
            caption = 'Данные водителя';
            type = COLUMNS;
            columns = 1;
            ADD PROPERTY(nameDefaultTruckDriver(e));
            ADD PROPERTY(sidDriver(e));
            ADD PROPERTY(typeDriver(e));
        }
    }
}

NAVIGATOR {
    NEW transportNavigator 'Транспорт' BEFORE administration TO toolbar IMAGE '/images/truck.png' {
        NEW transportDocuments 'Документы';
        NEW transportMasterData 'Справочники' {
            ADD trucks;
            ADD truckModels;
        }
    }
}