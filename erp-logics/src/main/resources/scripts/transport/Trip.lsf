MODULE Trip;

REQUIRE Document, Route;

NAMESPACE Trip;

CLASS Trip 'Рейс';
TABLE trip(Trip);

@defineDocumentHeaderTime(Trip);
@deriveDocumentHeaderTimePrefix(Trip, );

@defineDocumentHeaderNumber(Trip);
@defineNumeratedDefault(Trip, 'Рейсы', 'РЕ');

tripNumber (number) = GROUP AGGR trip BY numberTrip(trip);

truckTrip = DATA Truck(Trip);
sidTruckTrip 'Машина (номер)'(trip) = sidTruck(truckTrip(trip));
weightTruckTrip 'Грузоподъёмность (кг)'(trip) = weightTruck(truckTrip(trip));
ownerTruckTrip 'Владелец'(trip) = ownerTruck(truckTrip(trip));
truckModelTruckTrip (trip) = truckModelTruck(truckTrip(trip));
nameTruckModelTruckTrip 'Марка' (trip) = nameTruckModel(truckModelTruckTrip(trip));
trailerTruckTrip 'Прицеп' (trip) = trailerTruck(truckTrip(trip));
nameTruckTrip 'Автомобиль' (trip) = nameTruck(truckTrip(trip));

driverTrip = DATA Driver(Trip);
nameDriverTrip 'Водитель' (trip) = nameContact(driverTrip(trip));
sidDriverTrip 'Табельный номер' (trip) = sidDriver(driverTrip(trip));
typeDriverTrip 'Класс' (trip) = typeDriver(driverTrip(trip));

supplierTrip = DATA LegalEntity(Trip);
nameSupplierTrip 'Организация (поставщик)' (t) = nameLegalEntity(supplierTrip(t));
customerTrip = DATA LegalEntity(Trip);
nameCustomerTrip 'Организация (покупатель)' (t) = nameLegalEntity(customerTrip(t));

descriptionTrip 'Рейс' (o) = CONCAT ' ', seriesNumberTrip(o), dateTrip(o) MINCHARWIDTH 20 PREFCHARWIDTH 30; 

FORM trip 'Рейс'
    OBJECTS t=Trip FIXED PANEL
    PROPERTIES(t) nameNumeratorTrip, numberTrip, seriesTrip, dateTrip, timeTrip, 
                  nameDriverTrip, sidTruckTrip, nameTruckModelTruckTrip, trailerTruckTrip, weightTruckTrip,
                  nameSupplierTrip, nameCustomerTrip
    
    EDIT Trip OBJECT t
;


DESIGN trip {
    NEW header {
        NEW headerTop {
            caption = 'Шапка документа';
            type = CONTAINERH;
            ADD PROPERTY(nameNumeratorTrip(t));
            ADD PROPERTY(numberTrip(t));
            ADD PROPERTY(seriesTrip(t));
            ADD PROPERTY(dateTrip(t));
            ADD PROPERTY(timeTrip(t));
        }
        NEW headerCenter {
            type = CONTAINERH;
            NEW truckContainer {
                type = COLUMNS;
                columns = 3;
                caption = 'Параметры машины';
                ADD PROPERTY(nameDriverTrip(t));
                ADD PROPERTY(sidTruckTrip(t));
                ADD PROPERTY(nameTruckModelTruckTrip(t));
                ADD PROPERTY(trailerTruckTrip(t));
                ADD PROPERTY(weightTruckTrip(t));
            }

            NEW headerSum {
                caption = 'Итоги';
                type = COLUMNS;
                columns = 1;
            }
        }

        NEW headerFilter {
            type = CONTAINERH;
            caption = 'Параметры';
            ADD PROPERTY(nameSupplierTrip(t)) {
                caption = 'Поставщик';
            }
            ADD PROPERTY(nameCustomerTrip(t)){
                caption = 'Покупатель';
            }
        }
    }

    NEW pane {
        fill = 1;
        type = TABBED;
    }
    ADD functions.box;
}

overCopyTrip = ABSTRACT ACTION LIST (Trip, Trip);
copyTrip 'Копировать' = ACTION (trip) NEWSESSION {
    FOR ADDOBJ t = Trip DO {
        ASSIGN driverTrip(t) <- driverTrip(trip);
        ASSIGN truckTrip(t) <- truckTrip(trip);

        EXEC overCopyTrip(t, trip);
        FORM trip OBJECTS t = t MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

FORM trips 'Рейсы'
    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES(dt) OBJVALUE

    OBJECTS t=Trip
    PROPERTIES(t) READONLY numberTrip, seriesTrip, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameTruckModelTruckTrip, trailerTruckTrip, weightTruckTrip
    PROPERTIES(t) ADDFORM, EDITFORM, copyTrip, DELETE FORCE PANEL TOOLBAR
    
    FILTERGROUP filterTrips
        FILTER 'Рейсы на дату' dateTrip(t) == dt 'F10' DEFAULT    
;

DESIGN trips {
    ADD dt.box;
    ADD t.box;

    NEW pane {
        fill = 2;
        type = TABBED;
    }

    ADD functions.box;
}

FORM dialogDateTrips 'Рейсы'
    OBJECTS d=DATE FIXED PANEL 
    PROPERTIES(d) OBJVALUE
    
    OBJECTS t = Trip
    PROPERTIES(t) READONLY numberTrip, dateTrip, nameDriverTrip
    FILTERGROUP filters FILTER 'Рейсы на дату' dateTrip(t)>=d 'F5' DEFAULT 
;

NAVIGATOR {
    transportDocuments {
        ADD trips BEFORE routes;
    }
}