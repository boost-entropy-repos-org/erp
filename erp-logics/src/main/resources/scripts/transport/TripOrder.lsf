MODULE TripOrder;

REQUIRE Trip, Order;

NAMESPACE Trip;

tripOrder = DATA Trip(Order);
inTripOrder 'Включен' (trip, order) = tripOrder(order) == trip;
countOrderTrip 'Кол-во заказов в рейсе' (trip) = GROUP SUM 1 IF inTripOrder(trip, order) BY trip PERSISTENT; 

driverTripOrder = driverTrip(tripOrder(order));
nameDriverTripOrder 'Водитель' (order) = nameContact(driverTripOrder(order));

dateTripOrder 'Дата вывоза' = dateTrip(tripOrder(order));
numberTripOrder 'Номер рейса' = numberTrip(tripOrder(order));

filterTripOrder(trip, order) = tripOrder(order)==trip OR (trip IS Trip AND order IS Order AND NOT tripOrder(order));

quantityFromTripStock (trip, stock) = GROUP SUM 1 IF quantityFromOrderStock(order, stock) BY tripOrder(order), stock;
quantityToTripStock (trip, stock) = GROUP SUM 1 IF quantityToOrderStock(order, stock) BY tripOrder(order), stock;
quantityTripStock(trip, stock) = quantityFromTripStock(trip, stock) (+) quantityToTripStock(trip, stock);

TABLE tripStock(Trip, Stock);
numberStockTrip 'Порядковый номер' = DATA INTEGER(Stock, Trip);
numberStockTrip(stock, trip) => quantityTripStock(trip, stock)>0 RESOLVE RIGHT;

firstStockTrip = DATA Stock(Trip);
isFirstStockTrip 'Начальный пункт' (stock, trip) = firstStockTrip(trip)==stock;
addressFirstStockTrip 'Начальный пункт' (trip) = addressStock(firstStockTrip(trip));

stockPrevTripStock (trip, stock) = PARTITION PREV stock IF quantityTripStock(trip, stock) BY trip ORDER numberStockTrip(stock, trip);
addressStockPrevTripStock 'Адрес' (trip, stock) = addressStock(stockPrevTripStock (trip, stock));

maxNumberTrip(trip) = GROUP MAX numberStockTrip(stock, trip) BY trip;
indexTripOrder 'Номер' (trip, order) = maxNumberTrip(trip) - numberStockTrip(toStockOrder(order), trip) + 1 IF inTripOrder(trip, order);

CONSTRAINT numberStockTrip(stock1, tripOrder(order)) >= numberStockTrip(stock2, tripOrder(order)) IF quantityFromOrderStock(order, stock1)>0 AND quantityToOrderStock(order, stock2)>0
    MESSAGE 'Номер не соответствует направлению заказа';

grossWeightOrderedTrip 'Суммарный вес заказов' (trip) = GROUP SUM grossWeightOrderDetailOrder(order) BY tripOrder(order);

grossWeightTripToStock 'Вес' = GROUP SUM round3(grossWeightOrderDetailOrder(order)/1000) BY tripOrder(order), toStockOrder(order);

quantityOrderedTripSku 'Кол-во в заказах' (trip, sku) = GROUP SUM quantityOrderDetail(orderDetail) BY tripOrder(orderOrderDetail(orderDetail)), skuOrderDetail(orderDetail);

// отображение на карте
showPointsTrip 'Показать путь на карте' (trip) = ACTION (trip) {
    ASSIGN numberPathPOI(stock) <- OVERRIDE 1 IF quantityTripStock(trip, stock) > 0, numberStockTrip(stock, trip);
    EXEC showOnMapPath(showMapProvider());

} TOOLBAR;

createPathTrip 'Рассчитать путь' (trip) = ACTION (trip) {
    ASSIGN startPathPOI() <- firstStockTrip(trip);
    ASSIGN inPathPOI(stock) <- TRUE IF quantityTripStock(trip, stock);
    EXEC calculatePath();
    ASSIGN numberStockTrip(stock, trip) <- numberPathPOI(stock);
} TOOLBAR;

FORM ordersTrip

    OBJECTS t=Trip FIXED PANEL
    PROPERTIES (t) nameNumeratorTrip, numberTrip, seriesTrip, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameTruckModelTruckTrip, trailerTruckTrip

    OBJECTS o=Order FIXED GRID
    FILTERS isPostedOrder(o), inTripOrder(t, o)

    OBJECTS s=Sku
    PROPERTIES READONLY idBarcodeSku(s), nameSku(s), shortNameUOMSku(s)
    PROPERTIES READONLY quantityOrderedTripSku(t, s)
    PROPERTIES READONLY quantityOrderDetailSkuOrder(s, o) COLUMNS (o) HEADER seriesNumberOrder(o)
    FILTERS quantityOrderedTripSku(t, s) > 0
;

printOrdersTrip 'Печать заказов' (trip) = ACTION (trip) {
    FORM ordersTrip OBJECTS t = trip PRINT;
} TOOLBAR;

EXTEND FORM trip
    PROPERTIES (t) READONLY grossWeightOrderedTrip

    OBJECTS o=Order
    PROPERTIES(t, o) inTripOrder
    PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameSupplierOrder, nameFromStockOrder, nameCustomerOrder, nameToStockOrder, grossWeightOrderDetailOrder
    PROPERTIES(o) editOrder
    PROPERTIES printOrdersTrip(t) TODRAW o FORCE PANEL

    FILTERS isPostedOrder(o)

    FILTERS customerOrder(o) == customerTrip(t) OR (o IS Order AND NOT customerTrip(t)),
            supplierOrder(o) == supplierTrip(t) OR (o IS Order AND NOT supplierTrip(t))

    FILTERGROUP filterDateOrder
        FILTER 'Текущие' dateOrder(o) <= dateTrip(t) 'F9' DEFAULT
    FILTERGROUP filterOrder
        FILTER 'Не расписанные или в текущем рейсе' filterTripOrder(t, o) 'F11'
        FILTER 'В текущем рейсе' inTripOrder(t, o) 'F10' DEFAULT

    OBJECTS od=OrderDetail
    PROPERTIES(od) READONLY idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail, quantityOrderDetail, grossWeightOrderDetail, nameFromStockOrderDetail, nameToStockOrderDetail
    FILTERS orderOrderDetail(od)==o

    OBJECTS st=Stock
    PROPERTIES(st) READONLY nameStock, addressStock, nameLegalEntityStock
    PROPERTIES(t, st) grossWeightTripToStock
    PROPERTIES(st, t) numberStockTrip, isFirstStockTrip
    PROPERTIES (t) showPointsTrip TODRAW st
    PROPERTIES (t) createPathTrip TODRAW st

    FILTERS quantityTripStock (t, st) > 0
    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(st) 'ctrl F10' DEFAULT
    EDIT Trip OBJECT t
;

DESIGN trip {
    headerSum {
        MOVE PROPERTY(grossWeightOrderedTrip(t));
    }
    
    pane {
        NEW orders {
            fill = 1;
            caption = 'Заказы';
            MOVE o.box;
            MOVE od.box;
        }
        MOVE st.box {
            caption = 'Путевой лист';
        }
    }
}

EXTEND FORM trips
    PROPERTIES(t) READONLY grossWeightOrderedTrip
    
    OBJECTS o=Order
    PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameSupplierOrder, nameFromStockOrder, nameCustomerOrder, nameToStockOrder, grossWeightOrderDetailOrder
    FILTERS inTripOrder(t, o)
;

DESIGN trips {
    pane {
        MOVE o.box {
            caption = 'Заказы';
        }
    }
}

// Показываем отмеченные заказы на карте
filteredFromTripStock (stock) = GROUP SUM 1 IF [= FILTER trip.o](order) BY fromStockOrder(order);
filteredToTripStock (stock) = GROUP SUM 1 IF [= FILTER trip.o](order) BY toStockOrder(order);
filteredTripStock(stock) = filteredFromTripStock(stock) (+) filteredToTripStock(stock);

showPointOrders 'Показать отобранные заказы' () = ACTION () {
    numberPathPOI(stock) <- 1 IF filteredTripStock(stock);
    showOnMapPath(showMapProvider());
} TOOLBAR;

EXTEND FORM trip
    PROPERTIES showPointOrders() TODRAW o
;
