MODULE TripShipment;

REQUIRE TripOrder, OrderShipment;

NAMESPACE Trip;

// поставки
tripShipment = DATA Trip(Shipment);
inTripShipment 'Включен' (trip, shipment) = tripShipment(shipment)==trip;

filterTripShipment(trip, shipment) = tripShipment(shipment)==trip OR (trip IS Trip AND shipment IS Shipment AND NOT tripShipment(shipment));

grossWeightShippedTrip 'Суммарный вес поставок' (trip) = GROUP SUM grossWeightShipmentDetailShipment(shipment) BY tripShipment(shipment);

quantityShipmentDetailTripShipment (trip, shipment) = GROUP SUM quantityShipmentDetailOrderShipment (order, shipment) BY tripOrder(order), shipment;

quantityShippedTripSku 'Кол-во в поставках' (trip, sku) = GROUP SUM quantityShipmentDetail(shipmentDetail) BY tripShipment(shipmentShipmentDetail(shipmentDetail)), skuShipmentDetail(shipmentDetail);

EXTEND FORM trip
    PROPERTIES (t) READONLY grossWeightShippedTrip
    
    OBJECTS s=Shipment
    PROPERTIES (t, s) inTripShipment
    PROPERTIES(s) READONLY numberShipment, seriesShipment, dateShipment, nameSupplierShipment, nameFromStockShipment,
                  nameCustomerShipment, nameToStockShipment, grossWeightShipmentDetailShipment
    PROPERTIES (s) editShipment

    FILTERS isPostedShipment(s)

    FILTERS customerShipment(s) == customerTrip(t) OR (s IS Shipment AND NOT customerTrip(t)),
            supplierShipment(s) == supplierTrip(t) OR (s IS Shipment AND NOT supplierTrip(t))

    FILTERGROUP filterDateShipment
        FILTER 'Текущие' dateShipment(s) <= dateTrip(t) 'F9' DEFAULT
    FILTERGROUP filterShipment
        FILTER 'Не расписанные или в текущем рейсе' filterTripShipment(t, s) 'F11'
        FILTER 'В текущем рейсе' inTripShipment(t, s) 'F10' DEFAULT

    OBJECTS sd=ShipmentDetail
    PROPERTIES(sd) READONLY idBarcodeSkuShipmentDetail, nameSkuShipmentDetail, shortNameUOMSkuShipmentDetail, quantityShipmentDetail, grossWeightShipmentDetail, nameFromStockShipmentDetail, nameToStockShipmentDetail
    FILTERS shipmentShipmentDetail(sd)==s
;

DESIGN trip {
    headerSum {
        ADD PROPERTY(grossWeightShippedTrip(t));
    }
    
    pane {
        NEW shipments {
            fill = 1;
            caption = 'Поставки';
            ADD s.box;
            ADD sd.box;
        }
    }
}

EXTEND FORM trips
    PROPERTIES(t) READONLY grossWeightShippedTrip

    OBJECTS s=Shipment
    PROPERTIES(s) READONLY numberShipment, seriesShipment, dateShipment, nameSupplierShipment, nameFromStockShipment, nameCustomerShipment, nameToStockShipment, grossWeightShipmentDetailShipment
    FILTERS inTripShipment(t, s)
;

DESIGN trips {
    pane {
        ADD s.box {
            caption = 'Поставки';
        }
    }
}