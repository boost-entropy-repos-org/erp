MODULE TripPurchaseInvoiceCharge;

REQUIRE PurchaseInvoiceCharge, TripInvoice;

NAMESPACE  Trip;

sumCharge 'Сумма услуг' = DATA NUMERIC[18,4] (Trip) PREFCHARWIDTH 15;

sumGrossWeightPurchaseInvoiced 'Суммарный вес накладных' (trip) = GROUP SUM sumGrossWeightInvoiceDetail(UserInvoice invoice) IF invoice IS Purchase.UserInvoice BY trip(invoice);
calcGrossSum 'Сумма услуг' (Purchase.Invoice invoice, Trip trip)= NUMERIC[18,4](sumCharge(trip)* 
                                                        sumGrossWeightInvoiceDetail(invoice) /
                                                        (sumGrossWeightPurchaseInvoiced(trip) IF sumGrossWeightPurchaseInvoiced(trip)!=0.0));

signChargeInvoices 'Расписать сумму услуг'(Trip trip) = ACTION {
    INPUTX n = NUMERIC[18,4] DO {
        sumCharge(trip) <- n;
        FOR trip(UserInvoice invoice) == trip DO {
            setChargePercent(invoice, calcGrossSum (invoice, trip) * 100.000000 / (incomeSum(invoice) IF incomeSum(invoice) != 0));
            chargePrice(UserInvoiceDetail d) <- NUMERIC[16,4](chargePercent(d) * incomePrice(d) / 100.000000) WHERE userInvoice(d) == invoice;
        }
    }   
}

EXTEND FORM trip
    PROPERTIES(t) sumCharge ON CHANGE signChargeInvoices(t)
    PROPERTIES(i) READONLY GRID chargeSumInvoiceDetail
    PROPERTIES(id) READONLY GRID chargeSum[Purchase.InvoiceDetail]
;

DESIGN trip {
    invoiceSum1 {       
        MOVE PROPERTY(sumCharge(t));
    }
}