MODULE TripInvoice;

REQUIRE OrderInvoice, Shipment, Trip;

NAMESPACE Trip;

tripInvoice = DATA Trip(Invoice);

inTripInvoice 'Включен' (trip, invoice) = tripInvoice(invoice) == trip;

countInvoiceTrip 'Кол-во накладных в рейсе' (trip) = GROUP SUM 1 IF inTripInvoice(trip, invoice) BY trip PERSISTENT;

quantityInvoicedTripSku 'Кол-во в накладных' (trip, sku) = GROUP SUM quantityInvoiceDetail(invoiceDetail) BY tripInvoice(invoiceInvoiceDetail(invoiceDetail)), skuInvoiceDetail(invoiceDetail);

grossWeightInvoicedTrip 'Суммарный вес накладных' (trip) = GROUP SUM sumGrossWeightInvoiceDetailInvoice(invoice) BY tripInvoice(invoice);
netWeightInvoicedTrip 'Суммарный вес накладных (нетто)' (trip) = GROUP SUM sumNetWeightInvoiceDetailInvoice(invoice) BY tripInvoice(invoice);

EXTEND FORM trip

    PROPERTIES (t) READONLY grossWeightInvoicedTrip

    OBJECTS i=Invoice
    PROPERTIES (t, i) inTripInvoice
    PROPERTIES (i) READONLY numberInvoice, seriesInvoice, dateInvoice, nameFromInvoice, nameFromStockInvoice, nameToInvoice, 
                   nameToStockInvoice, sumGrossWeightInvoiceDetailInvoice, sumNetWeightInvoiceDetailInvoice
    PROPERTIES (i) editInvoice SHOWIF isOpenedInvoice(i)
    FILTERGROUP filterDateInvoice
        FILTER 'Текущие' dateInvoice(i) <= dateTrip(t) 'F9' DEFAULT
    FILTERGROUP filterInvoice
        FILTER 'Не расписанные или в текущем рейсе' inTripInvoice(t, i) OR NOT tripInvoice(i) 'F11'
        FILTER 'В текущем рейсе' inTripInvoice(t, i) 'F10' DEFAULT

    FILTERS customerInvoice(i) == customerTrip(t) OR (i IS Invoice AND NOT customerTrip(t)),
            supplierInvoice(i) == supplierTrip(t) OR (i IS Invoice AND NOT supplierTrip(t))

    OBJECTS id=InvoiceDetail
    PROPERTIES (id) READONLY idBarcodeSkuInvoiceDetail, nameSkuInvoiceDetail, shortNameUOMSkuInvoiceDetail, quantityInvoiceDetail, 
                    nameFromStockInvoiceDetail, nameToStockInvoiceDetail, sumGrossWeightInvoiceDetail, sumNetWeightInvoiceDetail
    FILTERS invoiceInvoiceDetail(id)==i
;

DESIGN trip {
    headerSum {
        MOVE PROPERTY(grossWeightInvoicedTrip(t));
    }

    pane {
        NEW invoices {
            caption = 'Накладные';
            NEW invoiceSum {
                type = CONTAINERV;
                caption = 'Итоги';
                NEW invoiceSum1 {type = CONTAINERH;}
                NEW invoiceSum2 {type = CONTAINERH;}
            }
            MOVE i.box;
            MOVE id.box;
        }
    }
}

EXTEND FORM trips
    PROPERTIES (t) READONLY grossWeightInvoicedTrip
    OBJECTS i=Invoice
    PROPERTIES (i) READONLY numberInvoice, seriesInvoice, dateInvoice, nameFromInvoice, nameFromStockInvoice, nameToInvoice, nameToStockInvoice
    FILTERS inTripInvoice(t, i)
;

DESIGN trips {
    pane {
        MOVE i.box {
            caption = 'Накладные';
        }
    }
}