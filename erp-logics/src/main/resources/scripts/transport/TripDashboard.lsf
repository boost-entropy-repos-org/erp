MODULE TripDashboard;

REQUIRE Dashboard, OrderInvoice, TripOrder, OrderPickingOrder, TripInvoiceConsignmentBy;

NAMESPACE Trip;

inCreatingTripOrder 'Отметить' = DATA SESSION BOOLEAN (Order);
inCreatingTripStock = GROUP SUM 1 IF inCreatingTripOrder(order) BY toStockOrder(order);

countInCreatingTripOrder = GROUP SUM 1 IF inCreatingTripOrder(order);

editTripOrder 'Редактировать рейс' = ACTION (order) {
    FOR t == tripOrder(order) DO {
        FORM trip OBJECTS t=t MODAL;
        IF formResult() == FormResult.ok THEN {
            apply();
        } ELSE {
            cancel();
        }
    }
}

FORM optionTrip 'Настройки рейса'
    OBJECTS t = Trip FIXED PANEL
    PROPERTIES (t) dateTrip, timeTrip, nameDriverTrip, nameTruckTrip
    PROPERTIES (t) nameIssuanceAllowedTrip, nameIssuanceExecutedTrip, nameForwarderTrip, nameLoadingExecuterTrip, 
                   nameWayOfLoadingTrip, nameUnloadingExecuterTrip, nameWayOfUnloadingTrip, codeLoadingTrip, documentNameCurrencyTrip,
                   timeOfArrivalTrip, downtimeTrip        
;
DESIGN optionTrip FROM DEFAULT {
//    t.panel.props {
//        columns = 2;
//    }
    NEW consignmentContainer {
        caption = 'Атрибуты накладных';
        NEW issuanceContainer {
            caption = 'Отпуск';
            ADD PROPERTY(nameIssuanceAllowedTrip(t));
            ADD PROPERTY(nameIssuanceExecutedTrip(t));
            ADD PROPERTY(nameForwarderTrip(t));
        }
        NEW loadingContainer {
            caption = 'ППР';
            ADD PROPERTY(nameLoadingExecuterTrip(t));
            ADD PROPERTY(nameWayOfLoadingTrip(t));
            ADD PROPERTY(nameUnloadingExecuterTrip(t));
            ADD PROPERTY(nameWayOfUnloadingTrip(t));
            ADD PROPERTY(codeLoadingTrip(t));
            ADD PROPERTY(timeOfArrivalTrip(t));
            ADD PROPERTY(downtimeTrip(t));
        }
        NEW dopContainer {
            caption = 'Дополнительно';
            ADD PROPERTY(documentNameCurrencyTrip(t));
        }

    }
    ADD functions.box;
}

overPrintTrip = ABSTRACT ACTION LIST (Trip);
overCreateTripOrder = ABSTRACT ACTION LIST (Trip);
createTripOrder 'Взять в рейс' = ACTION () {
    FOR ADDOBJ t = Trip DO {
        dateTrip(t) <- currentDate();
        timeTrip(t) <- currentTime();
        overCreateTripOrder(t);
        FORM optionTrip OBJECTS t = t MODAL;

        IF formResult() == FormResult.ok THEN {
            FOR inCreatingTripOrder(order) 
                    AND NOT tripOrder(order) DO {
                tripOrder(order) <- t;
            }
            overPrintTrip(t);
            apply();
        } ELSE {
            cancel();
        }
    }
} TOOLBAR EDITKEY 'ctrl ENTER';

cancelTripOrder 'Отменить' = ACTION (order) {
    ASSIGN tripOrder (order) <- NULL;
    EXEC apply();
} TOOLBAR EDITKEY 'ctrl DELETE';

hideInCreatingTripOrder = TRUE AND NOT (calcPartOrder(o) OR calcFullOrder(o));

FORM tripDashboard 'Загрузка транспорта' AUTOREFRESH 60

    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) date = OBJVALUE

    OBJECTS o = Order FIXED GRID
    PROPERTIES(o) inCreatingTripOrder READONLYIF hideInCreatingTripOrder(o)
    PROPERTIES(o) READONLY BACKGROUND backgroundStatusPickingOrder(o) nameDriverTripOrder, numberOrder, dateOrder,
                            nameToOrder, nameToStockOrder, addressToStockOrder, packQuantityOrder, 
                            namePerformerOrder, startDateTimeOrder, finishDateTimeOrder
    PROPERTIES() createTripOrder SHOWIF countInCreatingTripOrder() TODRAW o
    //FILTERS calcPartOrder(o) AND NOT tripOrder(o)
    FILTERS countPickingOrderOrder(o) AND NOT tripOrder(o)

    OBJECTS to = Order FIXED GRID
    PROPERTIES(to) READONLY nameDriverTripOrder, dateTripOrder, numberTripOrder,
                            numberOrder, dateOrder, finishDateTimeOrder,
                            nameToOrder, nameToStockOrder, addressToStockOrder, packQuantityOrder, quantityOrderDetailOrder
                            
    PROPERTIES(to) editTripOrder TODRAW to FORCE PANEL TOOLBAR, cancelTripOrder
    FILTERS tripOrder(to) //AND driverTripOrder(to) == currentUser()
    FILTERGROUP filters FILTER 'Заказы на дату' 'F5' dateOrder(o)<=d DEFAULT
    FILTERGROUP filters1 FILTER 'Рейсы на дату' 'F5' dateTripOrder(to)==d DEFAULT

;

DESIGN tripDashboard FROM DEFAULT {
    main{
    NEW baseContainer {
        fill = 1;
        type = SPLITV;
        NEW firstContainer {
            fill = 1;
            ADD to.box{
                caption = 'Вывезенные заказы';
                PROPERTY(addressToStockOrder(to)){
                    caption = 'Пункт разгрузки';
                    minimumCharWidth = 40;
                }
                PROPERTY(nameToOrder(to)){
                    minimumCharWidth = 45;
                }                
                PROPERTY(nameToStockOrder(to)){
                    minimumCharWidth = 45;
                }
                PROPERTY(numberOrder(to)) {
                    caption = 'Номер заказа';
                }
                PROPERTY(dateOrder(to)) {
                    caption = 'Дата заказа';
                }
            }
        }
        NEW secondContainer BEFORE firstContainer {
            fill = 1;
            ADD o.box{
                caption = 'Заказы к вывозу';
                PROPERTY(addressToStockOrder(o)){
                    caption = 'Пункт разгрузки';
                    minimumCharWidth = 40;
                }
                PROPERTY(nameToOrder(o)){
                    minimumCharWidth = 45;
                }
                PROPERTY(nameToStockOrder(o)){
                    minimumCharWidth = 45;
                }
                PROPERTY(numberOrder(o)) {
                    caption = 'Номер заказа';
                }
                PROPERTY(dateOrder(o)) {
                    caption = 'Дата заказа';
                    }
                PROPERTY(namePerformerOrder(o)) {
                    minimumCharWidth = 25;
                    }    
                PROPERTY(inCreatingTripOrder(o)) {
                    editKey = 'SPACE';
                    }
                }
            }
    }
    NEW thirdContainer BEFORE baseContainer {
        type = CONTAINERH;
        caption = 'Шапка';
        ADD PROPERTY(date);

    }

    ADD functions.box;
    }
}

NAVIGATOR {
    saleDashboardNavigator {
        ADD tripDashboard;
    }
}