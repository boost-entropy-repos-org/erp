MODULE ContractLedgerSchedule;

REQUIRE ContractLedger;

PRIORITY Contract;

sumContractLedgerDate(date) = GROUP SUM sumContractLedger(ledger) //IF dateContractALedger(conractALedger) > dateFrom AND dateContractALedger(conractALedger) < dateTo
                                     BY dateContractLedger(ledger);

sumContractDateFromDateTo(contract, dateFrom, dateTo) = GROUP SUM sumContractLedger(ledger) IF dateContractLedger(ledger) > dateFrom AND dateContractLedger(ledger) < dateTo
                                                               BY contractContractLedger(ledger), dateFrom, dateTo;

sumContractDate(contract, date) = GROUP SUM sumContractLedger(ledger)
                                         BY contractContractLedger(ledger), dateContractLedger(ledger);

toString10Date(date) = STRING[10](date);

toStringG999 = FORMULA TEXT PG 'to_char($1,\'999G999G999G999\')' MS 'REPLACE(REPLACE(PARSENAME(CONVERT(nvarchar(max), CONVERT(MONEY,$1), 1), 2), \'.\', \' \'), \',\', \' \')';
sumsContractDate 'Суммы' (contract, date) = CONCAT ' \n', toStringG999(balanceAContractDate(contract, date)), toStringG999(sumContractDate(contract, date));

EXTEND FORM contractLedger

    OBJECTS dateFrom = DATE FIXED PANEL
    PROPERTIES(dateFrom) date1 = OBJVALUE
    OBJECTS dateTo = DATE FIXED PANEL
    PROPERTIES(dateTo) date2 = OBJVALUE

    OBJECTS dateIn = DATE FIXED GRID
    FILTERS sumContractLedgerDate(dateIn) AND dateFrom < dateIn AND dateIn < dateTo

    OBJECTS cc = Contract
    PROPERTIES(cc) READONLY seriesNumberContract, namePartyAContract, namePartyBContract
    ORDER BY seriesNumberContract(c)
    FILTERS sumContractDateFromDateTo(cc, dateFrom, dateTo),
            partyBContract(cc) == partyB() OR (cc IS Contract AND NOT partyB()),
            partyAContract(cc) == partyA() OR (cc IS Contract AND NOT partyA())

    PROPERTIES READONLY sumsContractDate(cc, dateIn) COLUMNS (dateIn) HEADER toString10Date(dateIn)

    OBJECTS cl2 = ContractLedger
    PROPERTIES (cl2) READONLY dateContractLedger, sumContractLedger, descriptionContractLedger
    FILTERS contractContractLedger(cl2) == cc

;

DESIGN contractLedger {
    detail {
        NEW schedule {
            caption = 'План платежей';
            NEW dateContainer {
                type = CONTAINERH;
                caption = 'Период';
                MOVE PROPERTY(date1) {caption = 'Дата с';}
                MOVE PROPERTY(date2) {caption = 'Дата по';};
            }
            NEW schedule1 {
                type = SPLITV;
                fill = 1;
                MOVE cc.box {
                    fill = 2;
                    PROPERTY(sumsContractDate(cc,dateIn)) {
                        minimumSize = ( -1, 34);
                        preferredSize = ( -1, 34);
                        maximumSize = ( -1, 34);
                    }
                }
                MOVE cl2.box;
            }

        }
    }
}
