MODULE ContractLedgerSchedule;

REQUIRE ContractLedger;

PRIORITY Contract;

sumContractLedger(date) = GROUP SUM sum(ContractLedger ledger) //IF dateContractALedger(conractALedger) > dateFrom AND dateContractALedger(conractALedger) < dateTo
                                     BY date(ledger);

sum(contract, dateFrom, dateTo) = GROUP SUM sum(ContractLedger ledger) IF date(ledger) > DATE dateFrom AND date(ledger) < DATE dateTo
                                                               BY contract(ledger), dateFrom, dateTo;

sum(contract, date) = GROUP SUM sum(ContractLedger ledger)
                                         BY contract(ledger), date(ledger);

toString10(date) = STRING[10](date);

toStringG999 = FORMULA TEXT PG 'to_char($1,\'999G999G999G999\')', MS 'REPLACE(REPLACE(PARSENAME(CONVERT(nvarchar(max), CONVERT(MONEY,$1), 1), 2), \'.\', \' \'), \',\', \' \')';
sums 'Суммы' (Contract contract, DATE date) = CONCAT ' \n', toStringG999(balanceA(contract, date)), toStringG999(sum(contract, date));

EXTEND FORM contractLedger

    OBJECTS dateFrom = DATE PANEL
    PROPERTIES(dateFrom) date1 = OBJVALUE
    OBJECTS dateTo = DATE PANEL
    PROPERTIES(dateTo) date2 = OBJVALUE

    OBJECTS dateIn = DATE GRID
    FILTERS sumContractLedger(dateIn) AND dateFrom < dateIn AND dateIn < dateTo

    OBJECTS cc = Contract
    PROPERTIES(cc) READONLY seriesNumber, namePartyA, namePartyB
    ORDER BY seriesNumber(cc)
    FILTERS sum(cc, dateFrom, dateTo),
            partyB(cc) == partyB() OR (cc IS Contract AND NOT partyB()),
            partyA(cc) == partyA() OR (cc IS Contract AND NOT partyA())

    PROPERTIES READONLY sums(cc, dateIn) COLUMNS (dateIn) HEADER toString10(dateIn)

    OBJECTS cl2 = ContractLedger
    PROPERTIES (cl2) READONLY date, sum, description
    FILTERS contract(cl2) == cc

;

DESIGN contractLedger {
    detail {
        NEW schedule {
            caption = 'План платежей';
            NEW dateContainer {
                type = CONTAINERH;
                caption = 'Период';
                MOVE PROPERTY(date1) {caption = 'Дата с';}
                MOVE PROPERTY(date2) {caption = 'Дата по';};
            }
            NEW schedule1 {
                type = SPLITV;
                fill = 1;
                MOVE cc.box {
                    fill = 2;
                    PROPERTY(sums(cc,dateIn)) {
                        minimumSize = ( -1, 34);
                        preferredSize = ( -1, 34);
                        maximumSize = ( -1, 34);
                    }
                }
                MOVE cl2.box;
            }

        }
    }
}
