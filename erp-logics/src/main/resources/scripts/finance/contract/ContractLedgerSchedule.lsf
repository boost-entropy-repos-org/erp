MODULE ContractLedgerSchedule;

REQUIRE ContractLedger;

PRIORITY Contract;

sumContractLedgerDate(date) = GROUP SUM sumContractLedger(ledger) //IF dateContractALedger(conractALedger) > dateFrom AND dateContractALedger(conractALedger) < dateTo
                                     BY dateContractLedger(ledger);

sumContractDateFromDateTo(contract, dateFrom, dateTo) = GROUP SUM sumContractLedger(ledger) IF dateContractLedger(ledger) > dateFrom AND dateContractLedger(ledger) < dateTo
                                                               BY contractContractLedger(ledger), dateFrom, dateTo;

sumContractDate(contract, date) = GROUP SUM sumContractLedger(ledger)
                                         BY contractContractLedger(ledger), dateContractLedger(ledger);

toString10Date(date) = STRING[10](date);

toStringG999 = FORMULA TEXT 'to_char($1,\'999G999G999G999\')';
sumsContractDate 'Суммы' (contract, date) = CONCAT ' \n', toStringG999(balanceAContractDate(contract, date)), toStringG999(sumContractDate(contract, date));

EXTEND FORM contractLedger

    OBJECTS dateFrom = DATE FIXED PANEL
    PROPERTIES(dateFrom) date1 = OBJVALUE
    OBJECTS dateTo = DATE FIXED PANEL
    PROPERTIES(dateTo) date2 = OBJVALUE

    OBJECTS dateIn = DATE FIXED GRID
    FILTERS sumContractLedgerDate(dateIn) AND dateFrom < dateIn AND dateIn < dateTo

    OBJECTS cc = Contract
    PROPERTIES(cc) READONLY seriesNumberContract, namePartyAContract, namePartyBContract
    ORDER BY seriesNumberContract
    FILTERS sumContractDateFromDateTo(cc, dateFrom, dateTo),
            partyBContract(cc) == partyB() OR (cc IS Contract AND NOT partyB()),
            partyAContract(cc) == partyA() OR (cc IS Contract AND NOT partyA())

    PROPERTIES READONLY sumsContractDate(cc, dateIn) COLUMNS (dateIn) HEADER toString10Date(dateIn)

    OBJECTS cl2 = ContractLedger
    PROPERTIES (cl2) READONLY dateContractLedger, sumContractLedger, descriptionContractLedger
    FILTERS contractContractLedger(cl2) == cc

;

EXTEND DESIGN contractLedger {
    detail {
        NEW schedule {
            caption = 'План платежей';
            NEW dateContainer {
                type = CONTAINERH;
                caption = 'Период';
                ADD PROPERTY(date1) {caption = 'Дата с';}
                ADD PROPERTY(date2) {caption = 'Дата по';};
            }

            ADD cc.box {
                fill = 2;
                PROPERTY(sumsContractDate) {
                    minimumSize = ( -1, 34);
                    preferredSize = ( -1, 34);
                    maximumSize = ( -1, 34);
                }
            }
            ADD cl2.box;
        }
    }
}
