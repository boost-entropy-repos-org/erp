MODULE ContractPayment;

REQUIRE ContractLedger, Payment;

EXTEND CLASS Payment;

contract 'Контракт' (payment) = DATA Contract (Payment);
seriesNumberContract 'Номер договора' (Payment payment) = seriesNumber(contract(payment));

currencyContract(Payment payment) = currency(contract(payment)) PERSISTENT @@currency;
nameCurrencyContract 'Валюта' (Payment payment) = name(currencyContract(payment));

typeExchangeContract (payment) = DATA TypeExchange (Payment);
typeExchangeContract (Payment payment) <- typeExchange(contract(payment))
    WHEN CHANGED(contract(payment));
nameTypeExchangeContract 'Тип обмена' (Payment payment) = name(typeExchangeContract(payment));
rateExchangeContract 'Курс' (payment) = DATA NUMERIC[15,8] (Payment) PREFCHARWIDTH 15 @@denominationRate;
rateExchangeContract (Payment payment) <- OVERRIDE 1.0 IF payment IS Payment AND currencyContract(payment) == currencyAccountTo(payment) AND NOT typeExchangeContract(payment),
                                                  rateOn(typeExchangeContract(payment), currencyAccountTo(payment), date(payment)) IF currencyContract(payment) != currencyAccountTo(payment)
    WHEN CHANGED(currencyContract(payment)) OR
         CHANGED(typeExchangeContract(payment)) OR
         CHANGED(date(payment)) OR
         CHANGED(contract(payment)) OR
         CHANGED(currencyAccountTo(payment));

sumContract 'Сумма платежа по контракту' (payment) = DATA NUMERIC[18,4] (Payment) PREFCHARWIDTH 16 @@denominationCurrency;
sumContract(Payment payment) <- IF NOT currencyContract(payment) == currencyAccountTo(payment)
                               THEN rateExchangeContract(payment)*sum(payment)
                               ELSE sum(payment)
    WHEN CHANGED(sum(payment)) OR
         CHANGED(currencyContract(payment)) OR
         CHANGED(currencyAccountTo(payment)) OR
         CHANGED(typeExchangeContract(payment)) OR
         CHANGED(rateExchangeContract(payment));

autoWriteFIFO 'Автоматически расписывать по ФИФО' = DATA BOOLEAN (PaymentOperation.Operation);

EXTEND FORM PaymentOperation.operation
    PROPERTIES (o) autoWriteFIFO
;

DESIGN PaymentOperation.operation{
    propertyContainer {
        MOVE PROPERTY(autoWriteFIFO(o));
    }    
}

WHEN SESSION FORMS payment CHANGED(sum(Payment payment)) OR 
                           CHANGED(currencyContract(payment)) OR
                           CHANGED(currencyAccountTo(payment)) OR
                           CHANGED(typeExchangeContract(payment)) OR
                           CHANGED(rateExchangeContract(payment))DO {
    sumContract(payment) <- IF currencyContract(payment) == currencyAccountTo(payment)
                               THEN sum(payment)
                               ELSE rateExchangeContract(payment)*sum(payment);
    IF autoWriteFIFO(operation(payment)) THEN writeFIFO(payment);    
}

CONSTRAINT currency(typeExchangeContract(Payment payment)) != currencyContract(payment)
    CHECKED BY typeExchangeContract[Payment] MESSAGE 'Валюта договора должна совпадать с валютой типа обмена';

CONSTRAINT contract(Payment payment) IS Contract AND NOT isReturn(payment) AND NOT partyA(contract(payment)) == beneficiary(payment)
    CHECKED BY contract[Payment] MESSAGE 'Выбранный договор не действителен для бенефициара';

CONSTRAINT contract(Payment payment) IS Contract AND NOT isReturn(payment) AND NOT partyB(contract(payment)) == payer(payment)
    CHECKED BY contract[Payment] MESSAGE 'Выбранный договор не действителен для плательщика';

CONSTRAINT contract(Payment payment) IS Contract AND isReturn(payment) AND NOT partyB(contract(payment)) == beneficiary(payment)
    CHECKED BY contract[Payment] MESSAGE 'Выбранный договор не действителен для бенефициара';

CONSTRAINT contract(Payment payment) IS Contract AND isReturn(payment) AND NOT partyA(contract(payment)) == payer(payment)
    CHECKED BY contract[Payment] MESSAGE 'Выбранный договор не действителен для плательщика';
        
@implementContractLedger( , Payment, contract);

overSumContract(Payment payment) = IF isReturn(payment) 
                                    THEN sumContract(payment)
                                    ELSE -sumContract(payment);
                                    
sum[ContractLedger](Payment contractLedger) += overSumContract(contractLedger);

@implementContractLedger(a , Payment, contract);
sum[ContractALedger](Payment contractALedger) += overSumContract(contractALedger);

@implementOutContractLedger(Payment, contract);
isReturn[OutContractLedger](Payment outContractLedger) += isReturn(outContractLedger);

//sum[OutContractLedger](Payment outContractLedger) += sumContract(outContractLedger);
sum[OutContractLedger](Payment outContractLedger) += IF isReturn(outContractLedger) THEN -sumContract(outContractLedger)
                                                                                    ELSE sumContract(outContractLedger);

//------------------------------------------- Actions --------------------------------------------------------------//

setRateExchangeContract(Payment payment) = ACTION {
    REQUEST NUMERIC[18,4] INPUT;
    ASSIGN rateExchangeContract(payment) <- requestedNumeric()/sum(payment);
}

//----------------------------------------------- Цвета --------------------------------------------------------------//

backgroundSum 'Цвет' (ContractLedger contractLedger, DATE date) = OVERRIDE RGB(255,238,165) IF date(contractLedger) <= date,
                                                                         RGB(255,160,160) IF date(contractLedger) > date;
background 'Цвет' (ContractLedger contractLedger, DATE date) = RGB(255,160,160) IF date(contractLedger) > date;
//backgroundSum 'Цвет' (Contract contract) = RGB(255,238,165) IF contract IS Contract;
backgroundSum 'Цвет' (ContractALedger contractALedger, DATE date) = OVERRIDE RGB(232,184,146) IF date(contractALedger) <= date,
                                                                           RGB(255,160,160) IF date(contractALedger) > date;
background 'Цвет' (ContractALedger contractALedger, DATE date) = RGB(255,160,160) IF date(contractALedger) > date;
//backgroundSumA 'Цвет' (Contract contract) = RGB(232,184,146) IF contract IS Contract;
backgroundBonusSum 'Цвет' (InContractLedger inContractLedger) = RGB(213,249,185) IF inContractLedger IS InContractLedger;
backgroundBonusSum 'Цвет' (Contract contract) = RGB(213,249,185) IF contract IS Contract;
background 'Цвет' (OutContractLedger outContractLedger, DATE date) = RGB(255,160,160) IF date(outContractLedger) > date;
background 'Цвет' (InContractLedger inContractLedger, DATE date) = RGB(255,160,160) IF date(inContractLedger) > date;

showTypeExchangeContract(Payment payment) = currencyContract(payment) != currencyAccountTo(payment);

//-------------------------------------------- Форма платежей -------------------------------------------------------//

EXTEND FORM payment
    OBJECTS i = InContractLedger
    PROPERTIES(p) seriesNumberContract,
                  costedInContractLedger TODRAW i FORCE PANEL TOOLBAR READONLY,
                  writeFIFO TODRAW i FORCE PANEL TOOLBAR,
                  writeLIFO TODRAW i FORCE PANEL TOOLBAR,
                  nameTypeExchangeContract SHOWIF showTypeExchangeContract(p),
                  rateExchangeContract SHOWIF showTypeExchangeContract(p),
                  sumContract ON CHANGE setRateExchangeContract(p),
                  nameCurrencyContract

    PROPERTIES(i) READONLY date, description, debt
    PROPERTIES(p, i) cost
    ORDER BY date(i)

    FILTERS contract(i) == contract(p)
    FILTERGROUP filters1 
        FILTER 'Актуальные' debt(i) OR cost(p,i) 'F6' DEFAULT
;

DESIGN payment{
    main{
        columnContainer{
            NEW thirdColumn{
                type = CONTAINERH;
                caption = 'Контракт';
                NEW thirdOneColumn{
                    type = COLUMNS;
                    columns = 1;
                    MOVE PROPERTY(seriesNumberContract(p));
                    MOVE PROPERTY(nameCurrencyContract(p));
                }
                NEW thirdSecondColumn{
                    type = COLUMNS;
                    columns = 1;
                    MOVE PROPERTY(nameTypeExchangeContract(p));
                    MOVE PROPERTY(rateExchangeContract(p));
                    MOVE PROPERTY(sumContract(p));
                }
            }
        }
        MOVE i.box BEFORE functions.box;
    }
}

EXTEND FORM payments
    PROPERTIES(p) READONLYIF isReadonly()
                  seriesNumberContract, nameCurrencyContract, sumContract
;

currentInContractLedger = DATA LOCAL InContractLedger ();
WHEN SESSION FORMS payment
    CHANGED(sumContract(Payment p)) AND currentInContractLedger()
        DO ASSIGN cost(p, InContractLedger i) <- sumContract(p) WHERE i == currentInContractLedger();

//------------------------------------- Платежи по договору --------------------------------------------------------//
overSum = ABSTRACT NUMERIC[18,4] (Currency, DATE);

pay 'Оплатить по договору'(Contract contract, DATE date) = ACTION NEWSESSION {
    FOR ADDOBJ pm = Payment DO {
        date(pm) <- date;
        payer(pm) <- partyB(contract);
        beneficiary(pm) <- partyA(contract);
        contract(pm) <- contract;
        sumContract(pm) <- OVERRIDE balanceA(contract, date), overSum(currency(contract), date);
        sum(pm) <- round2(OVERRIDE balanceA(contract, date), overSum(currency(contract), date)) IF currencyContract(pm) == currencyAccountFrom(pm);
        writeFIFO(pm);
        FORM payment OBJECTS p = pm MANAGESESSION DOCKEDMODAL NOCANCEL;
    }
}

//------------------------------------------- Платеж по договору --------------------------------------------//

addPayment 'Оплатить'(Contract contract) = ACTION NEWSESSION{
    FOR ADDOBJ p = Payment DO{
        payer(p) <- partyB(contract);
        beneficiary(p) <- partyA(contract);
        contract(p) <- contract;
        FORM payment OBJECTS p=p MANAGESESSION DOCKEDMODAL NOCANCEL;
    }
}

EXTEND FORM contractLedger
    PROPERTIES(c, d) FORCE PANEL TOOLBAR pay

    OBJECTS pm = Payment
    PROPERTIES(pm) READONLY number BACKGROUND background[OutContractLedger,DATE](pm, d),
                            series BACKGROUND background[OutContractLedger,DATE](pm, d),
                            date BACKGROUND background[OutContractLedger,DATE](pm, d),
                            time BACKGROUND background[OutContractLedger,DATE](pm, d),
                            namePayer BACKGROUND background[OutContractLedger,DATE](pm, d),
                            nameBeneficiary BACKGROUND background[OutContractLedger,DATE](pm, d),
                            nameCurrencyContract BACKGROUND background[OutContractLedger,DATE](pm, d),
                            nameTypeExchangeContract BACKGROUND background[OutContractLedger,DATE](pm, d),
                            rateExchangeContract BACKGROUND background[OutContractLedger,DATE](pm, d),
                            sumContract BACKGROUND background[OutContractLedger,DATE](pm, d),
                            costedInContractLedger BACKGROUND background[OutContractLedger,DATE](pm, d),
                            note BACKGROUND background[OutContractLedger,DATE](pm, d)

    PROPERTIES(pm) FORCE PANEL TOOLBAR writeFIFO, writeLIFO
    PROPERTIES addPayment(c) TODRAW pm FORCE PANEL TOOLBAR
    PROPERTIES(pm) EDITFORM, DELETE FORCE PANEL TOOLBAR
    ORDER BY date(pm), time(pm)

    FILTERS contract(pm) == c
;

DESIGN contractLedger {
    firstSecondContainer {
        MOVE pm.box AFTER debt.box;
    }
}
