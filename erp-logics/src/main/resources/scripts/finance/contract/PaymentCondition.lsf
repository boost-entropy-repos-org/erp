MODULE PaymentCondition;

REQUIRE System, Integration;

NAMESPACE Contract;

CLASS PaymentCondition 'Условия оплаты';
TABLE paymentCondition(PaymentCondition);

@defineExternalizable(paymentCondition, VARSTRING[100]);

// Порядок оплаты

CLASS AgreementPaymentType 'Порядок оплаты' {
    prepayment 'С отсрочкой платежа',
    implement 'По факту реализации',
    requirement 'Платежное требование'
}
FORM agreementPaymentTypes 'Порядок оплаты'
    OBJECTS t = AgreementPaymentType
    PROPERTIES(t) READONLY staticCaption
    DIALOG AgreementPaymentType OBJECT t
;

bankingDaysPaymentCondition 'Банковских дней' = DATA BOOLEAN (PaymentCondition);

typePaymentCondition = DATA AgreementPaymentType (PaymentCondition);
nameTypePaymentCondition 'Тип оплаты' (paymentCondition) = staticCaption(typePaymentCondition(paymentCondition));

isPrepaymentTypePaymentCondition(paymentCondition) = typePaymentCondition(paymentCondition) == AgreementPaymentType.prepayment;

// Периоды рассрочки

CLASS PaymentPeriod 'Период рассрочки';
TABLE paymentPeriod(PaymentPeriod);

countDaysPaymentPeriod 'Кол-во дней' = DATA INTEGER (PaymentPeriod);
percentPaymentPeriod 'Процент' = DATA NUMERIC[8,3] (PaymentPeriod);
paymentConditionPaymentPeriod 'Условия оплаты периода рассрочки' = DATA PaymentCondition(PaymentPeriod);
bankingDaysPaymentPeriod 'Банковских дней'(p) = bankingDaysPaymentCondition(paymentConditionPaymentPeriod(p));

descriptionPaymentPeriod(paymentPeriod) =
    countDaysPaymentPeriod(paymentPeriod) + (IF bankingDaysPaymentPeriod(paymentPeriod) THEN ' банковских дн.' ELSE ' календарных дн.')+  ' ( ' + percentPaymentPeriod(paymentPeriod) + ' %)';


descriptionPeriodPaymentCondition 'Условия оплаты' (paymentCondition) =
    GROUP CONCAT descriptionPaymentPeriod(paymentPeriod) IF paymentConditionPaymentPeriod(paymentPeriod) == paymentCondition, ', '
                BY paymentCondition
                ORDER paymentPeriod MINCHARWIDTH 20 PREFCHARWIDTH 30;

descriptionPaymentCondition 'Условия оплаты' (paymentCondition) =
    OVERRIDE 'По факту реализации' IF paymentCondition IS PaymentCondition , 
             descriptionPeriodPaymentCondition(paymentCondition) MINCHARWIDTH 20 PREFCHARWIDTH 30;

// Бонусы

CLASS BonusPeriod 'Период бонуса';
TABLE bonusPeriod(BonusPeriod);

countDaysBonusPeriod 'Кол-во дней' = DATA INTEGER (BonusPeriod);
percentBonusPeriod 'Процент' = DATA NUMERIC[8,3] (BonusPeriod);
paymentConditionBonusPeriod 'Условия оплаты периода бонуса' = DATA PaymentCondition (BonusPeriod);

bonusPeriodPaymentConditionDays(paymentCondition, days) = GROUP AGGR bonusPeriod BY paymentConditionBonusPeriod(bonusPeriod),
                                                                                      countDaysBonusPeriod(bonusPeriod)
                                                                                   WHERE bonusPeriod IS BonusPeriod;

sumPercentPaymentCondition(paymentCondition) = GROUP SUM percentPaymentPeriod(paymentPeriod) BY paymentConditionPaymentPeriod(paymentPeriod);
CONSTRAINT sumPercentPaymentCondition(paymentCondition) != 100.0 CHECKED BY sumPercentPaymentCondition
    MESSAGE 'Сумма процентов рассрочки должна быть равна 100';

FORM paymentCondition 'Условие оплаты'

    OBJECTS pc = PaymentCondition FIXED PANEL
    PROPERTIES(pc) idPaymentCondition SHOWIF showIDs(), nameTypePaymentCondition, bankingDaysPaymentCondition 

    OBJECTS pp = PaymentPeriod FIXED GRID
    PROPERTIES(pp) countDaysPaymentPeriod SHOWIF isPrepaymentTypePaymentCondition(pc),
                   percentPaymentPeriod SHOWIF isPrepaymentTypePaymentCondition(pc)
    PROPERTIES(pp) ADDOBJ SHOWIF isPrepaymentTypePaymentCondition(pc),
                   DELETESESSION SHOWIF isPrepaymentTypePaymentCondition(pc)

    OBJECTS bp = BonusPeriod
    PROPERTIES(bp) countDaysBonusPeriod, percentBonusPeriod
    PROPERTIES(bp) ADDOBJ, DELETESESSION

    FILTERS paymentConditionPaymentPeriod(pp) == pc,
            paymentConditionBonusPeriod(bp) == pc

    EDIT PaymentCondition OBJECT pc
;

DESIGN paymentCondition FROM DEFAULT{
    main{
        preferredSize = (1024, 768);
    }
}

FORM paymentConditions 'Условия оплаты'

    OBJECTS pc = PaymentCondition
    PROPERTIES(pc) READONLY idPaymentCondition SHOWIF showIDs(), descriptionPaymentCondition, nameTypePaymentCondition
    PROPERTIES(pc) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG PaymentCondition OBJECT pc
;
