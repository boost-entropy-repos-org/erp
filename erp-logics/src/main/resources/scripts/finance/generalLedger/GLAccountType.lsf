MODULE GLAccountType;

REQUIRE System,
        Utils,
        Document,
        LegalEntity,
        Historizable,
        Hierarchy,
        Currency,
        Finance;

PRIORITY Utils;

//------------------------- План счетов -------------------------------//

CLASS GLAccountType 'План счетов';
TABLE GLAccountType(GLAccountType);

nameGLAccountType 'Наименование' = DATA VARISTRING[150](GLAccountType);

currencyGLAccountType = DATA Currency (GLAccountType);
nameCurrencyGLAccountType 'Валюта' (GLAccountType)= nameCurrency(currencyGLAccountType(GLAccountType));
idGLAccountType 'Идентификатор' = DATA VARSTRING[20] (GLAccountType) MINCHARWIDTH 15 PREFCHARWIDTH 20;

GLAccountTypeIdGLAccountType (string1) = GROUP AGGR GLAccountType
    BY idGLAccountType(GLAccountType) WHERE GLAccountType IS GLAccountType;

FORM GLAccountType 'План счетов'
    OBJECTS g = GLAccountType FIXED PANEL
    PROPERTIES(g) nameGLAccountType, idGLAccountType, nameCurrencyGLAccountType
    EDIT GLAccountType OBJECT g
;

FORM GLAccountTypes 'Планы счетов'
    OBJECTS g = GLAccountType
    PROPERTIES(g) READONLY nameGLAccountType, idGLAccountType, nameCurrencyGLAccountType
    PROPERTIES(g) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    DIALOG GLAccountType OBJECT g
;

//-------------------------- Счета -------------------------//

CLASS GLAccount 'Счет';
TABLE GLAccount(GLAccount);

nameGLAccount 'Наименование' = DATA VARSTRING[255] (GLAccount) MINCHARWIDTH 40 MAXCHARWIDTH 80 PREFCHARWIDTH 80;
GLAccountTypeGLAccount = DATA GLAccountType (GLAccount) AUTOSET;
nameGLAccountTypeGLAccount 'План счетов' (GLAccount)= nameGLAccountType(GLAccountTypeGLAccount(GLAccount));
nameCurrencyGLAccount 'Валюта' (GLAccount) = nameCurrencyGLAccountType(GLAccountTypeGLAccount(GLAccount));

idGLAccount 'Идентификатор' = DATA VARSTRING[20] (GLAccount) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

TABLE GLAccountGLAccount(GLAccount, GLAccount);
@defineHierarchy(GLAccount);

canonicalIdGLAccount 'Канонический код' (GLAccount) = VARSTRING[255](
                           [= GROUP CONCAT idGLAccount(parent), ' / ' BY child ORDER DESC levelGLAccountGLAccount(child, parent)](GLAccount))
                           MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;
canonicalNamesGLAccount 'Каноническое имя' (GLAccount) = VARSTRING[255](
                           [= GROUP CONCAT nameGLAccount(parent), ' / ' BY child ORDER DESC levelGLAccountGLAccount(child, parent)](GLAccount))
                           MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;

nameAccountParentGLAccount 'Родительский объект' (GLAccount)= nameGLAccount(parentGLAccount(GLAccount));
idGLAccountParentGLAccount 'Родительский объект' (GLAccount)= idGLAccount(parentGLAccount(GLAccount));

CONSTRAINT GLAccountTypeGLAccount(parentGLAccount(GLAccount)) != GLAccountTypeGLAccount(GLAccount)
           CHECKED BY parentGLAccount MESSAGE 'План счетов родительской объекта должна совпадать с планом счетов объекта';

GLAccountIdTypeIdGLAccount (string1, string2) = GROUP AGGR GLAccount
    BY idGLAccountType(GLAccountTypeGLAccount(GLAccount)), idGLAccount(GLAccount) WHERE GLAccount IS GLAccount;

FORM GLAccount 'Счет'
    OBJECTS g=GLAccount FIXED PANEL
    PROPERTIES(g) nameGLAccountTypeGLAccount, nameGLAccount, idGLAccount, idGLAccountParentGLAccount
    EDIT GLAccount OBJECT g
;

FORM GLAccounts 'Счета'
    OBJECTS t = GLAccountType FIXED PANEL
    PROPERTIES(t) SELECTOR nameGLAccountType

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS l = LegalEntity FIXED PANEL
    PROPERTIES(l) SELECTOR nameLegalEntity

    TREE treeGroups g=GLAccount PARENT parentGLAccount
    PROPERTIES(g) READONLY nameGLAccount, idGLAccount
    PROPERTIES(g) READONLY canonicalIdGLAccount
    PROPERTIES(g) ADDFORM, EDITFORM, deletea=DELETE FORCE PANEL TOOLBAR
    FILTERS GLAccountTypeGLAccount(g) == t
    ORDER BY canonicalIdGLAccount
;

DESIGN GLAccounts FROM DEFAULT {
    main {
        type = CONTAINERV;
        NEW primary {
            fill = 1;
            type = SPLITV;
            NEW row {
                type = CONTAINERV;
                fill = 1;
                NEW row1 {
                    type = CONTAINERH;
                    ADD t.box;
                    ADD l.box;
                }
                ADD dates.box {
                    type = CONTAINERH;
                }
                ADD treeGroups.tree.box{ caption = 'Счет';}
            }
            NEW wor {
                type = TABBED;
                fill = 1;
            }
        }
        ADD functions.box;
    }
}

FORM GLAccountDialog 'Счета'
    OBJECTS t = GLAccountType FIXED PANEL
    PROPERTIES(t) SELECTOR nameGLAccountType

    TREE treeGroups g=GLAccount PARENT parentGLAccount
    PROPERTIES(g) READONLY nameGLAccount, idGLAccount
    PROPERTIES(g) READONLY canonicalIdGLAccount
    PROPERTIES(g) ADDFORM, EDITFORM, deletea=DELETE FORCE PANEL TOOLBAR
    FILTERS GLAccountTypeGLAccount(g) == t
    ORDER BY canonicalIdGLAccount
    DIALOG GLAccount OBJECT g
;

overCopyGLAccount = ABSTRACT ACTION LIST (GLAccount, GLAccount);
copyGLAccountType 'Копировать' = ACTION (GLAccountType) NEWSESSION {
    LOCAL mapping = GLAccount (GLAccount);

    FOR ADDOBJ t = GLAccountType DO {
        ASSIGN currencyGLAccountType(t) <- currencyGLAccountType(GLAccountType);

        FOR GLAccountTypeGLAccount(GLAccount) == GLAccountType DO {
            FOR ADDOBJ d=GLAccount DO {
                ASSIGN GLAccountTypeGLAccount(d) <- t;
                ASSIGN nameGLAccount(d) <- nameGLAccount(GLAccount);
                ASSIGN idGLAccount(d) <- idGLAccount(GLAccount);
                ASSIGN parentGLAccount(d) <- mapping(parentGLAccount(GLAccount));
                ASSIGN mapping(GLAccount) <- d;
                EXEC overCopyGLAccount(GLAccount, d);
            }
        }

        FORM GLAccountType OBJECTS g = t MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

EXTEND FORM GLAccountTypes
    PROPERTIES(g) copyGLAccountType
;

NAVIGATOR {
    financeNavigator {
        NEW accountType 'Бухгалтерский учет' {
            ADD GLAccountTypes;
            ADD GLAccounts;
        }
    }
}

// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultGLAccountType 'Добавить значение плана счетов' = ACTION (iname, isid, isidCurrency) {
    FOR ADDOBJ t = GLAccountType DO {
        ASSIGN nameGLAccountType(t) <- iname;
        ASSIGN idGLAccountType (t) <- isid;
        ASSIGN currencyGLAccountType(t) <- currencyShortName(isidCurrency);
    }
}
loadDefaultGLAccount 'Добавить значение счета' = ACTION (iname, isid, sidType, sidParent) {
    FOR ADDOBJ g = GLAccount DO {
        ASSIGN nameGLAccount(g) <- iname;
        ASSIGN idGLAccount(g) <- isid;
        ASSIGN GLAccountTypeGLAccount(g) <- GLAccountTypeIdGLAccountType(sidType);
        ASSIGN parentGLAccount (g) <- GLAccountIdTypeIdGLAccount(sidType, sidParent);
    }
}
loadDefaultGLAccounts 'Загрузить стандарный план счетов и счета' () = ABSTRACT ACTION LIST ()  IN loadDefault;

@implementLoadDefaultData(loadDefaultGLAccounts);