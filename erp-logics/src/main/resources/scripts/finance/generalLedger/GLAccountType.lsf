MODULE GLAccountType;

REQUIRE System,
        Utils,
        Document,
        LegalEntity,
        Historizable,
        Hierarchy,
        Currency,
        Finance;

PRIORITY Utils;

//------------------------- План счетов -------------------------------//

CLASS GLAccountType 'План счетов';
TABLE GLAccountType(GLAccountType);

name 'Наименование' = DATA VARISTRING[150](GLAccountType);

currency = DATA Currency (GLAccountType);
nameCurrency 'Валюта' (GLAccountType GLAccountType)= name(currency(GLAccountType));
id 'Идентификатор' = DATA VARSTRING[20] (GLAccountType) MINCHARWIDTH 15 PREFCHARWIDTH 20;

GLAccountTypeIdGLAccountType (string1) = GROUP AGGR GLAccountType GLAccountType
    BY id(GLAccountType) WHERE GLAccountType IS GLAccountType;

FORM GLAccountType 'План счетов'
    OBJECTS g = GLAccountType PANEL
    PROPERTIES(g) name, id, nameCurrency
    EDIT GLAccountType OBJECT g
;

FORM GLAccountTypes 'Планы счетов'
    OBJECTS g = GLAccountType
    PROPERTIES(g) READONLY name, id, nameCurrency
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE 
    LIST GLAccountType OBJECT g
;

//-------------------------- Счета -------------------------//

CLASS GLAccount 'Счет';
TABLE GLAccount(GLAccount);

name 'Наименование' = DATA VARSTRING[255] (GLAccount) MINCHARWIDTH 40 MAXCHARWIDTH 80 PREFCHARWIDTH 80;
GLAccountType = DATA GLAccountType (GLAccount) AUTOSET;
nameGLAccountType 'План счетов' (GLAccount GLAccount)= name(GLAccountType(GLAccount));
nameCurrency 'Валюта' (GLAccount GLAccount) = nameCurrency(GLAccountType(GLAccount));

id 'Идентификатор' = DATA VARSTRING[20] (GLAccount) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

TABLE GLAccountGLAccount(GLAccount, GLAccount);
@defineHierarchy(GLAccount);

canonicalId 'Канонический код' (GLAccount GLAccount) = VARSTRING[255](
                           [= GROUP CONCAT id(GLAccount parent), ' / ' BY GLAccount child ORDER DESC level(child, parent)](GLAccount))
                           MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 MATERIALIZED;
canonicalNames 'Каноническое имя' (GLAccount GLAccount) = VARSTRING[255](
                           [= GROUP CONCAT name(GLAccount parent), ' / ' BY GLAccount child ORDER DESC level(child, parent)](GLAccount))
                           MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 MATERIALIZED;

nameAccountParent 'Родительский объект' (GLAccount GLAccount)= name(parent(GLAccount));
idGLAccountParent 'Родительский объект' (GLAccount GLAccount)= id(parent(GLAccount));

CONSTRAINT GLAccountType(parent(GLAccount GLAccount)) != GLAccountType(GLAccount)
           CHECKED BY parent[GLAccount] MESSAGE 'План счетов родительской объекта должна совпадать с планом счетов объекта';

GLAccountIdTypeIdGLAccount (string1, string2) = GROUP AGGR GLAccount GLAccount
    BY id(GLAccountType(GLAccount)), id(GLAccount) WHERE GLAccount IS GLAccount;

FORM GLAccount 'Счет'
    OBJECTS g=GLAccount PANEL
    PROPERTIES(g) nameGLAccountType, name, id, idGLAccountParent
    EDIT GLAccount OBJECT g
;

FORM GLAccounts 'Счета'
    OBJECTS t = GLAccountType PANEL
    PROPERTIES(t) SELECTOR name

    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)

    OBJECTS l = LegalEntity PANEL
    PROPERTIES(l) SELECTOR name

    TREE treeGroups g=GLAccount PARENT parent
    PROPERTIES(g) READONLY name, id
    PROPERTIES(g) READONLY canonicalId
    PROPERTIES(g) NEWSESSION NEW, EDIT, deletea=DELETE 
    FILTERS GLAccountType(g) == t
    ORDER BY canonicalId(g)
;

DESIGN GLAccounts {
    main {
        type = CONTAINERV;
        NEW primary {
            fill = 1;
            type = SPLITV;
            NEW row {
                type = CONTAINERV;
                fill = 1;
                NEW row1 {
                    type = CONTAINERH;
                    MOVE t.box;
                    MOVE l.box;
                }
                MOVE dates.box {
                    type = CONTAINERH;
                }
                MOVE treeGroups.tree.box{ caption = 'Счет';}
            }
            NEW wor {
                type = TABBED;
                fill = 1;
            }
        }
        MOVE functions.box;
    }
}

FORM GLAccountDialog 'Счета'
    OBJECTS t = GLAccountType PANEL
    PROPERTIES(t) SELECTOR name

    TREE treeGroups g=GLAccount PARENT parent
    PROPERTIES(g) READONLY name, id
    PROPERTIES(g) READONLY canonicalId
    PROPERTIES(g) NEWSESSION NEW, EDIT, deletea=DELETE 
    FILTERS GLAccountType(g) == t
    ORDER BY canonicalId(g)
    LIST GLAccount OBJECT g
;

overCopy = ABSTRACT LIST (GLAccount, GLAccount);
copy 'Копировать'(GLAccountType GLAccountType) = {
	NEWSESSION {
	    LOCAL mapping = GLAccount (GLAccount);
	
	    NEW t = GLAccountType {
	        ASSIGN currency(t) <- currency(GLAccountType);
	
	        FOR GLAccountType(GLAccount GLAccount) == GLAccountType DO {
	            NEW d=GLAccount {
	                ASSIGN GLAccountType(d) <- t;
	                ASSIGN name(d) <- name(GLAccount);
	                ASSIGN id(d) <- id(GLAccount);
	                ASSIGN parent(d) <- mapping(parent(GLAccount));
	                ASSIGN mapping(GLAccount) <- d;
	                EXEC overCopy(GLAccount, d);
	            }
	        }
	
	        SHOW GLAccountType OBJECTS g = t MANAGESESSION DOCKED;
	    }
	}
} TOOLBAR;

EXTEND FORM GLAccountTypes
    PROPERTIES(g) copy
;

NAVIGATOR {
    financeNavigator {
        NEW accountType 'Бухгалтерский учет' {
            ADD GLAccountTypes;
            ADD GLAccounts;
        }
    }
}

// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultGLAccountType 'Добавить значение плана счетов'(VARISTRING[150] iname, VARSTRING[20] isid, STRING[3] isidCurrency) = {
    NEW t = GLAccountType {
        ASSIGN name(t) <- iname;
        ASSIGN id (t) <- isid;
        ASSIGN currency(t) <- currencyShortName(isidCurrency);
    }
}
loadDefaultGLAccount 'Добавить значение счета'(VARSTRING[255] iname, VARSTRING[20] isid, VARSTRING[20] sidType, VARSTRING[20] sidParent) = {
    NEW g = GLAccount {
        ASSIGN name(g) <- iname;
        ASSIGN id(g) <- isid;
        ASSIGN GLAccountType(g) <- GLAccountTypeIdGLAccountType(sidType);
        ASSIGN parent (g) <- GLAccountIdTypeIdGLAccount(sidType, sidParent);
    }
}
loadDefaultGLAccounts 'Загрузить стандартный план счетов и счета' () = ABSTRACT LIST ()  IN loadDefault;

@implementLoadDefaultData(loadDefaultGLAccounts);