MODULE Dimension;

REQUIRE GeneralLedger;

//------------------------- Субконто -------------------------------//

CLASS ABSTRACT Dimension 'Субконто';
TABLE dimension(Dimension);
nameDimension 'Название' = ABSTRACT VARISTRING[255] (Dimension) MINCHARWIDTH 40 PREFCHARWIDTH 80;

FORM dimensions 'Субконто'
    OBJECTS d = Dimension
    PROPERTIES(d) READONLY objectClassName, nameDimension
    DIALOG Dimension OBJECT d
;

CLASS DimensionType 'Тип субконто';
TABLE dimensionType(DimensionType);

nameDimensionType 'Наименование' (type) = staticCaption(type) IF type IS DimensionType;

idDimensionType 'Идентификатор' = DATA VARSTRING[20] (DimensionType) MINCHARWIDTH 3 MAXCHARWIDTH 10 PREFCHARWIDTH 7;
dimensionTypeDimension = ABSTRACT CASE EXCLUSIVE DimensionType (Dimension) PERSISTENT;
nameDimensionTypeDimension 'Тип субконто' (dimension) = nameDimensionType(dimensionTypeDimension(dimension));

TABLE GLAccountDimensionType(GLAccount, DimensionType);
orderGLAccountDimensionType 'Порядковый номер' = DATA INTEGER (GLAccount,DimensionType);
dimensionsTypeGLAccount 'Субконто' (GLAccount) = GROUP CONCAT VARSTRING[255](nameDimensionType(dimensionType)) IF orderGLAccountDimensionType (GLAccount,dimensionType) , ', '
                                                BY GLAccount
                                                ORDER orderGLAccountDimensionType (GLAccount,dimensionType) MINCHARWIDTH 40 PREFCHARWIDTH 80 PERSISTENT;

overCopyGLAccount(from, to) += ACTION (from, to) {
    ASSIGN orderGLAccountDimensionType(to, dimensionType) <- orderGLAccountDimensionType(from, dimensionType);
}

prevGLAccountDimensionType (account, type) = PARTITION PREV type IF orderGLAccountDimensionType(account, type) BY account
                                                                  ORDER orderGLAccountDimensionType(account, type);
namePrevGLAccountDimensionType 'Предок' (account, type) = nameDimensionType(prevGLAccountDimensionType(account, type));

TABLE generalLedgerDimensionType(GeneralLedger, DimensionType);
debitGeneralLedgerDimensionType = ABSTRACT CASE EXCLUSIVE Dimension (GeneralLedger, DimensionType) PERSISTENT;
nameDebitGeneralLedgerDimensionType 'Субконто (дебет)' (generalLedger,dimensionType)= nameDimension(debitGeneralLedgerDimensionType(generalLedger,dimensionType));
debitUserGeneralLedgerDimensionType = DATA Dimension (UserGeneralLedger, DimensionType);
nameDebitUserGeneralLedgerDimensionType 'Субконто (дебет)' (userGeneralLedger,dimensionType)= nameDimension(debitUserGeneralLedgerDimensionType(userGeneralLedger,dimensionType));
debitGeneralLedgerDimensionType(generalLedger,dimensionType) += WHEN CLASS(debitUserGeneralLedgerDimensionType(generalLedger,dimensionType)) THEN debitUserGeneralLedgerDimensionType(generalLedger,dimensionType);
CONSTRAINT debitUserGeneralLedgerDimensionType(userGeneralLedger,dimensionType) IF NOT dimensionTypeDimension(debitUserGeneralLedgerDimensionType(userGeneralLedger,dimensionType)) == dimensionType
    CHECKED BY debitUserGeneralLedgerDimensionType
        MESSAGE 'Субконто (дебет) проводки должен соответствовать субконто';

creditGeneralLedgerDimensionType = ABSTRACT CASE EXCLUSIVE Dimension (GeneralLedger, DimensionType) PERSISTENT;
nameCreditGeneralLedgerDimensionType 'Субконто (кредит)' (generalLedger,dimensionType)= nameDimension(creditGeneralLedgerDimensionType(generalLedger,dimensionType));
creditUserGeneralLedgerDimensionType = DATA Dimension (UserGeneralLedger,DimensionType);
nameCreditUserGeneralLedgerDimensionType 'Субконто (кредит)' (userGeneralLedger,dimensionType)= nameDimension(creditUserGeneralLedgerDimensionType(userGeneralLedger,dimensionType));
creditGeneralLedgerDimensionType(generalLedger,dimensionType) += WHEN CLASS(creditUserGeneralLedgerDimensionType(generalLedger,dimensionType)) THEN creditUserGeneralLedgerDimensionType(generalLedger,dimensionType);
CONSTRAINT creditUserGeneralLedgerDimensionType(userGeneralLedger,dimensionType) IF NOT dimensionTypeDimension(creditUserGeneralLedgerDimensionType(userGeneralLedger,dimensionType)) == dimensionType
    CHECKED BY creditUserGeneralLedgerDimensionType
        MESSAGE 'Субконто (кредит) проводки должен соответствовать субконто';

overCopyGeneralLedger(from, to) += ACTION (from, to) {
    debitUserGeneralLedgerDimensionType(to, dimensionType) <- debitGeneralLedgerDimensionType(from, dimensionType);
    creditUserGeneralLedgerDimensionType(to, dimensionType) <- creditGeneralLedgerDimensionType(from, dimensionType);
}

dimensionTypeIdDimensionType (string1) = GROUP AGGR dimensionType BY idDimensionType(dimensionType) WHERE dimensionType IS DimensionType;

// Отбор субконто

inDebitGeneralLedgerDimension (gl, d)= GROUP SUM 1 BY gl, debitGeneralLedgerDimensionType(gl, dt);  
inCreditGeneralLedgerDimension (gl, d) = GROUP SUM 1 BY gl, creditGeneralLedgerDimensionType(gl, dt);

inGeneralLedgerDimension (gl, d) = inDebitGeneralLedgerDimension(gl, d) OR inCreditGeneralLedgerDimension(gl, d);  

//-- Подсчет сумм одно субконто
TABLE GLAccountLegalEntityDDate(GLAccount, LegalEntity, Dimension, DATE);
sumCreditGLAccountCompanyDimensionDate 'Сумма (кредит)' (GLAccount, company, d1, date)= GROUP SUM
    sumGeneralLedger(ledger) IF isPostedGeneralLedger(ledger) AND t1 IS DimensionType
        BY creditGeneralLedger(ledger), legalEntityGeneralLedger(ledger), creditGeneralLedgerDimensionType(ledger,t1), dateGeneralLedger(ledger); // PERSISTENT
sumDebitGLAccountCompanyDimensionDate 'Сумма (дебет)' (GLAccount, company, d1, date)= GROUP SUM
    sumGeneralLedger(ledger) IF isPostedGeneralLedger(ledger) AND t1 IS DimensionType
        BY debitGeneralLedger(ledger), legalEntityGeneralLedger(ledger), debitGeneralLedgerDimensionType(ledger,t1), dateGeneralLedger(ledger); // PERSISTENT
balanceGLAccountCompanyDimensionDate 'Сальдо' (GLAccount, company, d1, date)=
    sumDebitGLAccountCompanyDimensionDate(GLAccount, company, d1, date) (-) sumCreditGLAccountCompanyDimensionDate(GLAccount, company, d1, date);

sumCreditGLAccountCompanyDimensionDateFromTo 'Сумма (кредит)' (GLAccount, company, d1, dateFrom, dateTo) = GROUP SUM
    sumCreditGLAccountCompanyDimensionDate(GLAccount, company, d1, date) IF date >= dateFrom AND date <= dateTo
        BY GLAccount, company, d1, dateFrom, dateTo;
sumDebitGLAccountCompanyDimensionDateFromTo 'Сумма (дебет)' (GLAccount, company, d1, dateFrom, dateTo) = GROUP SUM
    sumDebitGLAccountCompanyDimensionDate(GLAccount, company, d1, date) IF date >= dateFrom AND date <= dateTo
        BY GLAccount, company, d1, dateFrom, dateTo;
balanceGLAccountCompanyDimensionDateFromTo 'Сальдо' (GLAccount, company, d1, dateFrom, dateTo)=
    sumDebitGLAccountCompanyDimensionDateFromTo(GLAccount, company, d1, dateFrom, dateTo) (-) sumCreditGLAccountCompanyDimensionDateFromTo(GLAccount, company, d1, dateFrom, dateTo);

//-- Подсчет сумм два субконто
TABLE GLAccountLegalEntityDDDate(GLAccount, LegalEntity, Dimension, Dimension, DATE);
sumCreditGLAccountCompanyDimensionDimensionDate 'Сумма (кредит)' (GLAccount, company, d1, d2, date)= GROUP SUM
    sumGeneralLedger(ledger) IF isPostedGeneralLedger(ledger) AND t1 IS DimensionType AND t2 IS DimensionType  AND t1 != t2
        BY creditGeneralLedger(ledger), legalEntityGeneralLedger(ledger), creditGeneralLedgerDimensionType(ledger,t1), creditGeneralLedgerDimensionType(ledger,t2), dateGeneralLedger(ledger); // PERSISTENT
sumDebitGLAccountCompanyDimensionDimensionDate 'Сумма (дебет)' (GLAccount, company, d1, d2, date)= GROUP SUM
    sumGeneralLedger(ledger) IF isPostedGeneralLedger(ledger) AND t1 IS DimensionType AND t2 IS DimensionType  AND t1 != t2
        BY debitGeneralLedger(ledger), legalEntityGeneralLedger(ledger), debitGeneralLedgerDimensionType(ledger,t1), debitGeneralLedgerDimensionType(ledger,t2), dateGeneralLedger(ledger); // PERSISTENT
balanceGLAccountCompanyDimensionDimensionDate 'Сальдо' (GLAccount, company, d1, d2, date)=
    sumDebitGLAccountCompanyDimensionDimensionDate(GLAccount, company, d1, d2, date) (-) sumCreditGLAccountCompanyDimensionDimensionDate(GLAccount, company, d1, d2, date);

sumCreditGLAccountCompanyDimensionDimensionDateFromTo 'Сумма (кредит)' (GLAccount, company, d1, d2, dateFrom, dateTo) = GROUP SUM
    sumCreditGLAccountCompanyDimensionDimensionDate(GLAccount, company, d1, d2, date) IF date >= dateFrom AND date <= dateTo
        BY GLAccount, company, d1, d2, dateFrom, dateTo;
sumDebitGLAccountCompanyDimensionDimensionDateFromTo 'Сумма (дебет)' (GLAccount, company, d1, d2, dateFrom, dateTo) = GROUP SUM
    sumDebitGLAccountCompanyDimensionDimensionDate(GLAccount, company, d1, d2, date) IF date >= dateFrom AND date <= dateTo
        BY GLAccount, company, d1, d2, dateFrom, dateTo;
balanceGLAccountCompanyDimensionDimensionDateFromTo 'Сальдо' (GLAccount, company, d1, d2, dateFrom, dateTo)=
    sumDebitGLAccountCompanyDimensionDimensionDateFromTo(GLAccount, company, d1, d2, dateFrom, dateTo) (-) sumCreditGLAccountCompanyDimensionDimensionDateFromTo(GLAccount, company, d1, d2, dateFrom, dateTo);

//-- Подсчет сумм три субконто
TABLE GLAccountLegalEntityDDDDate(GLAccount, LegalEntity, Dimension, Dimension, Dimension, DATE);
sumCreditGLAccountCompanyDimensionDimensionDimensionDate 'Сумма (кредит)' (GLAccount, company, d1, d2, d3, date) = GROUP SUM
    sumGeneralLedger(ledger) IF isPostedGeneralLedger(ledger) AND
    t1 IS DimensionType AND t2 IS DimensionType AND t3 IS DimensionType AND
    t1 != t2 AND t2 != t3 AND t1 != t3
        BY creditGeneralLedger(ledger), legalEntityGeneralLedger(ledger), creditGeneralLedgerDimensionType(ledger,t1), creditGeneralLedgerDimensionType(ledger,t2), creditGeneralLedgerDimensionType(ledger,t3), dateGeneralLedger(ledger); // PERSISTENT
sumDebitGLAccountCompanyDimensionDimensionDimensionDate 'Сумма (дебет)' (GLAccount, company, d1, d2, d3, date) = GROUP SUM
    sumGeneralLedger(ledger) IF isPostedGeneralLedger(ledger) AND
    t1 IS DimensionType AND t2 IS DimensionType AND t3 IS DimensionType AND
    t1 != t2 AND t2 != t3 AND t1 != t3
        BY debitGeneralLedger(ledger), legalEntityGeneralLedger(ledger), creditGeneralLedgerDimensionType(ledger,t1), creditGeneralLedgerDimensionType(ledger,t2), creditGeneralLedgerDimensionType(ledger,t3), dateGeneralLedger(ledger); // PERSISTENT
balanceGLAccountCompanyDimensionDimensionDimensionDate 'Сальдо' (GLAccount, company, d1, d2, d3, date)=
    sumDebitGLAccountCompanyDimensionDimensionDimensionDate(GLAccount, company, d1, d2, d3, date) (-) sumCreditGLAccountCompanyDimensionDimensionDimensionDate(GLAccount, company, d1, d2, d3, date);

sumCreditGLAccountCompanyDimensionDimensionDimensionDateFromTo 'Сумма (кредит)' (GLAccount, company, d1, d2, d3, dateFrom, dateTo) = GROUP SUM
    sumCreditGLAccountCompanyDimensionDimensionDimensionDate(GLAccount, company, d1, d2, d3, date) IF date >= dateFrom AND date <= dateTo
        BY GLAccount, company, d1, d2, d3, dateFrom, dateTo;
sumDebitGLAccountCompanyDimensionDimensionDimensionDateFromTo 'Сумма (дебет)' (GLAccount, company, d1, d2, d3, dateFrom, dateTo) = GROUP SUM
    sumDebitGLAccountCompanyDimensionDimensionDimensionDate(GLAccount, company, d1, d2, d3, date) IF date >= dateFrom AND date <= dateTo
        BY GLAccount, company, d1, d2, d3, dateFrom, dateTo;
balanceGLAccountCompanyDimensionDimensionDimensionDateFromTo 'Сальдо' (GLAccount, company, d1, d2, d3, dateFrom, dateTo)=
    sumDebitGLAccountCompanyDimensionDimensionDimensionDateFromTo(GLAccount, company, d1, d2, d3, dateFrom, dateTo) (-) sumCreditGLAccountCompanyDimensionDimensionDimensionDateFromTo(GLAccount, company, d1, d2, d3, dateFrom, dateTo);

//--
orderDebitGeneralLedgerDimensionType 'Порядковый номер' (ledger, dimensionType)=  orderGLAccountDimensionType(debitGeneralLedger(ledger), dimensionType);
orderCreditGeneralLedgerDimensionType 'Порядковый номер' (ledger, dimensionType)=  orderGLAccountDimensionType(creditGeneralLedger(ledger), dimensionType);

dimensionsDebitGeneralLedger 'Субконто (дебет)' (ledger) = GROUP CONCAT VARSTRING[255](nameDebitGeneralLedgerDimensionType(ledger, dimensionType)) IF orderDebitGeneralLedgerDimensionType(ledger, dimensionType) , ', '
                                                BY ledger
                                                ORDER orderDebitGeneralLedgerDimensionType (ledger, dimensionType) MINCHARWIDTH 40 PREFCHARWIDTH 80;
dimensionsCreditGeneralLedger 'Субконто (кредит)' (ledger) = GROUP CONCAT VARSTRING[255](nameCreditGeneralLedgerDimensionType(ledger, dimensionType)) IF orderCreditGeneralLedgerDimensionType(ledger, dimensionType) , ', '
                                                BY ledger
                                                ORDER orderCreditGeneralLedgerDimensionType (ledger, dimensionType) MINCHARWIDTH 40 PREFCHARWIDTH 80;

FORM dimensionType 'Тип субконто'
    OBJECTS d = DimensionType FIXED PANEL
    PROPERTIES(d) nameDimensionType, idDimensionType
    EDIT DimensionType OBJECT d
;

FORM dimensionTypes 'Тип субконто'
    OBJECTS d = DimensionType
    PROPERTIES(d) READONLY nameDimensionType, idDimensionType
    PROPERTIES(d) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    DIALOG DimensionType OBJECT d
;

EXTEND FORM GLAccount

    OBJECTS d = DimensionType
    PROPERTIES(d) READONLY nameDimensionType
    PROPERTIES orderGLAccountDimensionType(g,d), namePrevGLAccountDimensionType(g,d)
;
DESIGN GLAccount {
    MOVE d.box BEFORE functions.box;
}
EXTEND FORM GLAccounts
    PROPERTIES(g) READONLY dimensionsTypeGLAccount BEFORE deletea
    PROPERTIES(gl) READONLY dimensionsDebitGeneralLedger AFTER idDebitGeneralLedger(gl)
    PROPERTIES(gl) READONLY dimensionsCreditGeneralLedger AFTER idCreditGeneralLedger(gl)
    OBJECTS w = DimensionType FIXED PANEL
    PROPERTIES(w) SELECTOR nameDimensionType
    FILTERS orderGLAccountDimensionType(g,w)
    OBJECTS ww = DimensionType FIXED PANEL
    PROPERTIES(ww) SELECTOR nameDimensionType
    FILTERS orderGLAccountDimensionType(g,ww)
    OBJECTS www = DimensionType FIXED PANEL
    PROPERTIES(www) SELECTOR nameDimensionType
    FILTERS orderGLAccountDimensionType(g,www)

    OBJECTS d = Dimension
    PROPERTIES READONLY nameDimension(d), sumDebitGLAccountCompanyDimensionDateFromTo(g,l,d,dFrom,dTo), sumCreditGLAccountCompanyDimensionDateFromTo(g,l,d,dFrom,dTo), balanceGLAccountCompanyDimensionDateFromTo(g,l,d,dFrom,dTo)
    FILTERS dimensionTypeDimension(d) == w
    FILTERGROUP filter1
            FILTER 'С суммами' sumCreditGLAccountCompanyDimensionDateFromTo(g,l,d,dFrom,dTo) OR sumDebitGLAccountCompanyDimensionDateFromTo(g,l,d,dFrom,dTo) 'F11' DEFAULT
    OBJECTS dd = Dimension
    PROPERTIES READONLY nameDimension(dd), sumDebitGLAccountCompanyDimensionDimensionDateFromTo(g,l,d,dd,dFrom,dTo), sumCreditGLAccountCompanyDimensionDimensionDateFromTo(g,l,d,dd,dFrom,dTo), balanceGLAccountCompanyDimensionDimensionDateFromTo(g,l,d,dd,dFrom,dTo)
    FILTERS dimensionTypeDimension(dd) == ww
    FILTERGROUP filter2
            FILTER 'С суммами' sumCreditGLAccountCompanyDimensionDimensionDateFromTo(g,l,d,dd,dFrom,dTo) OR sumDebitGLAccountCompanyDimensionDimensionDateFromTo(g,l,d,dd,dFrom,dTo) 'F10' DEFAULT
    OBJECTS ddd = Dimension
    PROPERTIES READONLY nameDimension(ddd), sumDebitGLAccountCompanyDimensionDimensionDimensionDateFromTo(g,l,d,dd,ddd,dFrom,dTo), sumCreditGLAccountCompanyDimensionDimensionDimensionDateFromTo(g,l,d,dd,ddd,dFrom,dTo), balanceGLAccountCompanyDimensionDimensionDimensionDateFromTo(g,l,d,dd,ddd,dFrom,dTo)
    FILTERS dimensionTypeDimension(ddd) == www
    FILTERGROUP filter3
            FILTER 'С суммами' sumCreditGLAccountCompanyDimensionDimensionDimensionDateFromTo(g,l,d,dd,ddd,dFrom,dTo) OR sumCreditGLAccountCompanyDimensionDimensionDimensionDateFromTo(g,l,d,dd,ddd,dFrom,dTo) 'F9' DEFAULT

;
DESIGN GLAccounts {
    wor{
        NEW dimension BEFORE gl.box {
            caption = 'Аналитика';
            type = CONTAINERH;
            fill = 1;
            NEW dimension1 {
                fill = 1;
                caption = 'Субконто 1';
                MOVE w.box;
                MOVE d.box;
            }
            NEW dimension2 {
                fill = 1;
                caption = 'Субконто 2';
                MOVE ww.box;
                MOVE dd.box;
            }
            NEW dimension3 {
                fill = 1;
                caption = 'Субконто 3';
                MOVE www.box;
                MOVE ddd.box;
            }
        }
    }
}


EXTEND FORM GLAccountDialog
    PROPERTIES(g) READONLY dimensionsTypeGLAccount BEFORE deletea
;

EXTEND FORM userGeneralLedger

    OBJECTS d = DimensionType
    PROPERTIES READONLY nameDimensionType(d), orderDebitGeneralLedgerDimensionType(g,d)
    PROPERTIES nameDebitUserGeneralLedgerDimensionType(g,d)
    FILTERS orderDebitGeneralLedgerDimensionType(g,d)
    ORDER BY orderDebitGeneralLedgerDimensionType(g,d)

    OBJECTS c = DimensionType
    PROPERTIES READONLY nameDimensionType(c), orderCreditGeneralLedgerDimensionType(g,c)
    PROPERTIES nameCreditUserGeneralLedgerDimensionType(g,c)
    FILTERS orderCreditGeneralLedgerDimensionType(g,c)
    ORDER BY orderCreditGeneralLedgerDimensionType(g,c)
;
DESIGN userGeneralLedger {
    NEW row BEFORE functions.box{
        type = CONTAINERH;
        caption = 'Субконто';
        fill = 1;
        MOVE d.box {caption = 'Дебет';}
        MOVE c.box {caption = 'Кредит';}
    }
}

EXTEND FORM generalLedgers
    PROPERTIES(g) READONLY AFTER idDebitGeneralLedger(g) dimensionsDebitGeneralLedger
    PROPERTIES(g) READONLY AFTER idCreditGeneralLedger(g) dimensionsCreditGeneralLedger
;



//------------------- Пользовательское субконто ---------------------//
CLASS UserDimension 'Субконто (польз.)' : Dimension;

TABLE userDimension(UserDimension);
nameUserDimension 'Название' = DATA VARISTRING[255] (UserDimension) MINCHARWIDTH 40 PREFCHARWIDTH 80;
nameDimension(dimension)+= nameUserDimension(dimension);

dimensionTypeUserDimension = DATA DimensionType (UserDimension);
nameDimensionTypeUserDimension 'Тип субконто' (dimension) = nameDimensionType(dimensionTypeUserDimension(dimension));

dimensionTypeDimension (dimension) += WHEN dimension IS UserDimension THEN dimensionTypeUserDimension(dimension);

EXTEND CLASS DimensionType { userDimensionType 'Пользовательский тип' }


FORM userDimension 'Субконто (польз.)'
    OBJECTS t=UserDimension FIXED PANEL
    PROPERTIES(t) nameUserDimension, nameDimensionTypeUserDimension
    EDIT UserDimension OBJECT t
;

FORM userDimensions 'Субконто (польз.)'
    OBJECTS t=UserDimension
    PROPERTIES(t) READONLY staticCaption, nameUserDimension, nameDimensionTypeUserDimension, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameUserDimension(t)
    DIALOG UserDimension OBJECT t
;
DESIGN userDimensions { main{ preferredSize = (600, 400); } }

NAVIGATOR {
    accountType {
        ADD dimensionTypes;
        ADD userDimensions;
    }
}
// ------------------- Субконто для проводок --------------- //

idDimension = ABSTRACT VARISTRING[100](Dimension) PERSISTENT;
//idDimension(dimension) += idGLAccount(dimension);
idDimension(dimension) += VARISTRING[20](staticCaption(dimension)) IF dimension IS UserDimension;

overIdDebitGeneralLedgerDimensionType = ABSTRACT CASE EXCLUSIVE VARISTRING[20](GeneralLedger, DimensionType) PERSISTENT;
idDebitGeneralLedgerDimensionType 'Субконто (дебет)' (generalLedger, dimensionType) =
    OVERRIDE idDimension(debitGeneralLedgerDimensionType(generalLedger, dimensionType)),
             overIdDebitGeneralLedgerDimensionType(generalLedger, dimensionType);

overIdCreditGeneralLedgerDimensionType = ABSTRACT CASE EXCLUSIVE VARISTRING[20](GeneralLedger, DimensionType) PERSISTENT;
idCreditGeneralLedgerDimensionType 'Субконто (кредит)' (generalLedger, dimensionType) =
    OVERRIDE idDimension(creditGeneralLedgerDimensionType(generalLedger, dimensionType)),
             overIdCreditGeneralLedgerDimensionType(generalLedger, dimensionType);

idDimensionsDebitGeneralLedger 'Субконто (дебет)' (ledger) = GROUP CONCAT VARSTRING[255](idDebitGeneralLedgerDimensionType(ledger, dimensionType)) IF orderDebitGeneralLedgerDimensionType(ledger, dimensionType) , ', '
                                                                       BY ledger
                                                                    ORDER orderDebitGeneralLedgerDimensionType (ledger, dimensionType) MINCHARWIDTH 5 PREFCHARWIDTH 15;

idDimensionsCreditGeneralLedger 'Субконто (кредит)' (ledger) = GROUP CONCAT VARSTRING[255](idCreditGeneralLedgerDimensionType(ledger, dimensionType)) IF orderCreditGeneralLedgerDimensionType(ledger, dimensionType) , ', '
                                                                         BY ledger
                                                                      ORDER orderCreditGeneralLedgerDimensionType (ledger, dimensionType) MINCHARWIDTH 5 PREFCHARWIDTH 15;
// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultDimensionGLAccount 'Добавить субконто' = ACTION (integer, sidType, sidAccount, type) {
    ASSIGN orderGLAccountDimensionType(g,type) <- integer WHERE g == GLAccountIdTypeIdGLAccount(sidType, sidAccount);
}
loadDefaultDimensionGLAccounts 'Загрузить стандарный субконто для счетов' () = ABSTRACT ACTION LIST () IN loadDefault;

@implementLoadDefaultData(loadDefaultDimensionGLAccounts);
