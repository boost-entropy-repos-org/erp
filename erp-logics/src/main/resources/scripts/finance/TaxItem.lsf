MODULE TaxItem;

REQUIRE System, Historizable, Item, StockTax;

NAMESPACE Item;

//------------------------- НДС -------------------------//

@defineHistorizableCustom (VATItemCountry, 'НДС', Range, numberRange, number, item, nameAttributeItem, country, nameCountry, 15, base);
                        //(property,            caption, type, typeIdentity, object1, object1Identity, object2, object2Identity, fixedCharWidth, group)
VATSkuCountryDate (item, country, date) += VATItemCountryDate (item, country, date);

CONSTRAINT taxRange(dataVATItemCountryDate(item, country, date)) != Tax.taxVAT OR
           countryRange(dataVATItemCountryDate(item, country, date)) != country AS Country
           CHECKED BY dataVATItemCountryDate
           MESSAGE 'ошибка: Шкала и страна строки должна соответствовать шкале и строке НДС : TaxItem';

valueVATItemCountryDate 'НДС, %' (item, country, date) = valueRateRangeDate(VATItemCountryDate(item, country, date), date);
valueCurrentRateVATItemCountry 'НДС, %' (item, country) = valueCurrentRateRange(VATItemCountry(item, country));

overCopyItem(s, d) += ACTION (s, d) {
    FOR dataVATItemCountryDate(s, country, date) DO {
        ASSIGN dataVATItemCountryDate(d, country, date) <- dataVATItemCountryDate(s, country, date);
    }
}

overNumberVATItemDefaultCountryDate 'НДС' (item, date)= numberRange(overVATItemCountryDate(item, defaultCountry(), date));
valueVATItemDefaultCountryDate 'НДС, %' (item, date) = valueRateRangeDate(VATItemCountryDate(item, defaultCountry(), date),date);

changeVATItemDefaultCountryDate = ACTION (item, date) {
    REQUEST OBJECT r
    FORM rangeDialog OBJECTS c = defaultCountry() MODAL SHOWDROP;
    IF formResult() == FormResult.ok THEN {
        dataVATItemCountryDate(item, country, date) <- requestedObject() WHERE country == defaultCountry();

    } ELSE IF formResult() == FormResult.drop THEN {
        dataVATItemCountryDate(item, country, date) <- NULL WHERE country == defaultCountry();
    }
}

EXTEND FORM item
    PROPERTIES (i, c, dtr) overNumberVATItemCountryDate, valueVATItemCountryDate READONLY
    PROPERTIES (i, dtr) overNumberVATItemDefaultCountryDate ON CHANGE changeVATItemDefaultCountryDate(i, dtr), valueVATItemDefaultCountryDate READONLY
    FILTERGROUP filters1
            FILTER 'Страны, у которых есть НДС' 'F11' countRangeTaxCountry (Tax.taxVAT, c) DEFAULT
;
EXTEND DESIGN item { regionPrm  { ADD PROPERTY(overNumberVATItemDefaultCountryDate(i,dtr));  ADD PROPERTY(valueVATItemDefaultCountryDate(i,dtr));} }

