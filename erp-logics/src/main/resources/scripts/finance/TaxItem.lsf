MODULE TaxItem;

REQUIRE System, Historizable, Item, StockTax;

NAMESPACE Item;

//------------------------- НДС -------------------------//

VAT 'НДС' = DATA Range (Item, Country);
numberVAT 'НДС, номер' (Item i, Country c) = number(VAT(i, c));

VAT (Item item, Country country) += VAT (item, country);

CONSTRAINT tax(VAT(Item item, Country country)) != Tax.taxVAT OR
           country(VAT(item, country)) != country AS Country
           CHECKED BY VAT[Item,Country]
           MESSAGE 'ошибка: Шкала и страна строки должна соответствовать шкале и строке НДС : TaxItem';

valueVAT 'НДС, %' (Item item, Country country, DATE date) = valueRate(VAT(item, country), date);

VAT 'НДС' (Item item) = VAT(item, defaultCountry()) PERSISTENT;
numberVAT 'НДС' (Item item) = number(VAT(item));

valueVAT 'НДС, %' (Item item, DATE date) = valueRate(VAT(item), date);
valueVAT 'НДС, %' (Item item) = valueVAT(item, currentDate());

changeNumberVAT(Item item) = {
    DIALOG rangeDialog OBJECTS c = defaultCountry(), r = VAT(item, defaultCountry()) INPUT NULL DO
        VAT(item, Country country) <- r WHERE country == defaultCountry();
}
changeValueVAT(Item item) = {
    INPUTX n = NUMERIC[10,5] DO
        VAT(item, Country country) <- valueCurrentVAT(country, n) WHERE country == defaultCountry();
}

EXTEND FORM item
    PROPERTIES (i, c)      numberVAT 
    PROPERTIES (i, c, dtr) valueVAT READONLY
    PROPERTIES (i)         numberVAT ON CHANGE changeNumberVAT(i) 
    PROPERTIES (i)         valueVAT ON CHANGE changeValueVAT(i)
    FILTERGROUP filters1
            FILTER 'Страны, у которых есть НДС' countRange (Tax.taxVAT, c) 'F11' DEFAULT
;
DESIGN item { regionPrm  { MOVE PROPERTY(numberVAT(i));  MOVE PROPERTY(valueVAT(i));} }

EXTEND FORM items
    PROPERTIES (i) READONLYIF  isReadonly() numberVAT ON CHANGE changeNumberVAT(i), 
                                            valueVAT ON CHANGE changeValueVAT(i)    
;

overCopy(Item s, Item d) += {
    FOR VAT(s, Country country) DO {
        VAT(d, country) <- VAT(s, country);
    }
}

// Миграция
dataVAT 'НДС' = DATA Range (Item, Country, DATE);

VAT 'НДС' (item, country, date) =
     GROUP LAST dataVAT(Item item, Country country, DATE dateIn)
           BY item, country, DATE date
           ORDER dateIn
           WHERE dataVAT(item, country, dateIn) AND dateIn <= (date AS DATE) COMPLEX;

migrateVATItem 'Перенести НДС'() = {
    VAT(Item item, Country country) <- VAT(item, country, currentDate()) WHERE NOT VAT(item, country);
}
EXTEND FORM migrationData
    PROPERTIES() migrateVATItem
;