MODULE TaxItem;

REQUIRE System, Historizable, Item, StockTax;

NAMESPACE Item;

//------------------------- НДС -------------------------//

VATItemCountry 'НДС' = DATA Range (Item, Country);
numberVATItemCountry 'НДС, номер' (i, c) = numberRange(VATItemCountry(i, c));

VATSkuCountry (item, country) += VATItemCountry (item, country);

CONSTRAINT taxRange(VATItemCountry(item, country)) != Tax.taxVAT OR
           countryRange(VATItemCountry(item, country)) != country AS Country
           CHECKED BY VATItemCountry
           MESSAGE 'ошибка: Шкала и страна строки должна соответствовать шкале и строке НДС : TaxItem';

valueVATItemCountryDate 'НДС, %' (item, country, date) = valueRateRangeDate(VATItemCountry(item, country), date);

VATItem 'НДС' (item) = VATItemCountry(item, defaultCountry()) PERSISTENT;
numberVATItem 'НДС' (item) = numberRange(VATItem(item));

valueVATItemDate 'НДС, %' (item, date) = valueRateRangeDate(VATItem(item), date);
valueVATItem 'НДС, %' (item) = valueVATItemDate(item, currentDate());

changeNumberVATItem = ACTION (item) {
    REQUEST OBJECT r
        FORM rangeDialog OBJECTS c = defaultCountry(), r=VATItemCountry(item, defaultCountry()) DIALOG SHOWDROP;
    IF formResult() == FormResult.ok THEN {
        VATItemCountry(item, country) <- requestedObject() WHERE country == defaultCountry();

    } ELSE IF formResult() == FormResult.drop THEN {
        VATItemCountry(item, country) <- NULL WHERE country == defaultCountry();
    }
}
changeValueVATItem = ACTION (item) {
    REQUEST NUMERIC[10,5] INPUT;
    VATItemCountry(item, country) <- valueCurrentVATCountryValue(country, requestedNumeric()) WHERE country == defaultCountry();
}

EXTEND FORM item
    PROPERTIES (i, c)      numberVATItemCountry 
    PROPERTIES (i, c, dtr) valueVATItemCountryDate READONLY
    PROPERTIES (i)         numberVATItem ON CHANGE changeNumberVATItem(i) 
    PROPERTIES (i)         valueVATItem ON CHANGE changeValueVATItem(i)
    FILTERGROUP filters1
            FILTER 'Страны, у которых есть НДС' countRangeTaxCountry (Tax.taxVAT, c) 'F11' DEFAULT
;
DESIGN item { regionPrm  { MOVE PROPERTY(numberVATItem(i));  MOVE PROPERTY(valueVATItem(i));} }

EXTEND FORM items
    PROPERTIES (i) READONLYIF  isReadonly() numberVATItem ON CHANGE changeNumberVATItem(i), 
                                            valueVATItem ON CHANGE changeValueVATItem(i)    
;

overCopyItem(s, d) += ACTION (s, d) {
    FOR VATItemCountry(s, country) DO {
        VATItemCountry(d, country) <- VATItemCountry(s, country);
    }
}

// Миграция
dataVATItemCountryDate 'НДС' = DATA Range (Item, Country, DATE);

VATItemCountryDate 'НДС' (item, country, date) =
     GROUP LAST dataVATItemCountryDate(item, country, dateIn)
           BY item, country, date
           ORDER dateIn
           WHERE dataVATItemCountryDate(item, country, dateIn) AND dateIn <= (date AS DATE) COMPLEX;

migrateVATItem 'Перенести НДС' = ACTION () {
    VATItemCountry(item, country) <- VATItemCountryDate(item, country, currentDate()) WHERE NOT VATItemCountry(item, country);
}
EXTEND FORM migrationData
    PROPERTIES() migrateVATItem
;