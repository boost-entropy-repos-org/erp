MODULE ConsignmentDashboard;

REQUIRE Dashboard, ConsignmentBy, OrderInvoice, TripInvoice,  OrderInvoicePickingOrder; 

NAMESPACE Order;

inConsignmentPrintOrder 'Отметить' = DATA SESSION BOOLEAN (Order);

countInConsignmentPrintOrder = GROUP SUM 1 IF inConsignmentPrintOrder(order);

hidePrintAction = TRUE AND NOT countInConsignmentPrintOrder();

toInvoicedPickedOrderDetail 'Не выписано' (orderDetail) = pickingQuantityOrderDetail(orderDetail) (-) invoicedOrderDetail(orderDetail);
toInvoicedPickedOrder (order) = GROUP SUM toInvoicedPickedOrderDetail(orderDetail) IF toInvoicedPickedOrderDetail(orderDetail) >0  BY orderOrderDetail(orderDetail);

countOrderConsignmentPrintTrip = GROUP SUM 1 IF inConsignmentPrintOrder(order) BY tripOrder(order);

countInConsignmentPrintTrip = GROUP SUM 1 IF countOrderConsignmentPrintTrip(trip);

inConsignmentPrintTrip = GROUP MAX trip IF countOrderConsignmentPrintTrip(trip);

printConsignmentOrder 'Создать накладные' = ACTION (order) {
    IF countInConsignmentPrintOrder() THEN {
        IF countInConsignmentPrintTrip() > 1 THEN {
            MESSAGE 'Выберите заказы одного рейса';
        } ELSE {
                FOR inConsignmentPrintOrder(o) DO {
                    createPickingUserInvoicePostedOrder(o);
                    FOR createdUserInvoiceOrder(i, o) DO {
                        tripInvoice(i) <- tripOrder(o);
                    }
                }
                apply();
        }
    } ELSE {
        createPickingUserInvoicePostedOrder(order);  
        FOR createdUserInvoiceOrder(i, order) DO {
            tripInvoice(i) <- tripOrder(order);
        }
        apply();
    }
} TOOLBAR EDITKEY 'ctrl ENTER';


backgroundStatusPickingPickingOrder 'Цвет' (order) =  CASE
      WHEN tripOrder(order) THEN RGB(212,255,212)
      WHEN calcPartOrder(order) THEN RGB(255,200,216)
      WHEN calcFullOrder(order) THEN RGB(255,238,165)
; 

changeTripOrder = ACTION (order){
    REQUEST OBJECT t 
    FORM dialogDateTrips OBJECTS d=dateOrder(order) MODAL SHOWDROP;
    IF formResult() == FormResult.ok THEN {
            tripOrder (order) <- requestedObject();
        } ELSE IF formResult() == FormResult.drop THEN {
            tripOrder (order) <- NULL;   
    }        
}

changeNumberInvoice (invoice) = ACTION (invoice) {
    REQUEST STRING[18] INPUT;
    IF requestedString() THEN {
        numberInvoice(invoice) <- requestedString();         
        apply();
    }    
}

changeSeriesInvoice (invoice) = ACTION (invoice) {
    REQUEST STRING[2] INPUT;
    IF requestedString() THEN {
        seriesInvoice(invoice) <- requestedString();
        apply();
    }    
}

FORM consignmentDashboard 'Выписка накладных' AUTOREFRESH 60

    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES(dt) date = OBJVALUE

    OBJECTS o=Order FIXED GRID
    PROPERTIES(o) inConsignmentPrintOrder
    PROPERTIES(o) READONLY BACKGROUND backgroundStatusPickingPickingOrder(o) nameDriverTripOrder, dateTripOrder
    PROPERTIES(o) BACKGROUND backgroundStatusPickingPickingOrder(o) numberTripOrder ON CHANGE changeTripOrder(o)   
    PROPERTIES(o) READONLY BACKGROUND backgroundStatusPickingPickingOrder(o) numberOrder, dateOrder,
                  nameToOrder, nameToStockOrder, addressToStockOrder, grossWeightOrderPickingOrder, packQuantityOrder, quantityOrderDetailOrder,
                  startDateTimeOrder, finishDateTimeOrder, noteOrder
    PROPERTIES(o) printConsignmentOrder FORCE PANEL TOOLBAR
    FILTERS isPostedOrder (o) AND toInvoicedPickedOrder(o) AND NOT isClosedOrder(o)
    //FILTERGROUP toInvoiced FILTER 'Не выписаны' 'F6' toInvoicedPickedOrder(o) DEFAULT  
    FILTERS outOrder (o)
    
    OBJECTS d = OrderDetail FIXED GRID
    PROPERTIES(d) READONLY nameSkuOrderDetail 
    FILTERS orderOrderDetail(d) == o

    OBJECTS i = Invoice FIXED GRID
    PROPERTIES (i) FORCE PANEL numInv=numberInvoice ON CHANGE changeNumberInvoice(i), serInv=seriesInvoice ON CHANGE changeSeriesInvoice(i)
    PROPERTIES (i) READONLY isPostedInvoice 
    PROPERTIES (i) READONLY numberInvoice, seriesInvoice
    PROPERTIES (i) READONLY dateInvoice, timeInvoice, nameToInvoice, nameToStockInvoice, addressToStockInvoice, countInvoiceDetailInvoice, 
                   quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                   VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice, grossWeightInvoiceDetailInvoice, noteInvoice, noteOrderInvoice
    PROPERTIES (i) editInvoice
    PROPERTIES (i) deletei=DELETE FORCE PANEL TOOLBAR

    PROPERTIES (i)  FORCE PANEL printConsignmentVerticalA, printConsignmentVerticalAB, printConsignmentHorizontalA,
                                printConsignmentVerticalB, printConsignmentHorizontalB,
                                printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                                printConsignmentSimpleVertical, printConsignmentSimpleAttach, printConsignmentAttachXLS
                                
                               
    FILTERS outInvoice (i)                            
    FILTERGROUP filters FILTER 'Заказы на дату' 'F5' dateOrder(o)<=dt DEFAULT
    FILTERGROUP filters1 FILTER 'Накладные на дату' 'F5' dateInvoice(i)==dt DEFAULT
;

DESIGN consignmentDashboard FROM DEFAULT {
    main{
        NEW base1Container {
            fill = 1;

             NEW base2Container {
                fill = 4;
                type = SPLITV;
                    NEW tabContainer{
                        type = TABBED;
                        fill = 1;
                        ADD i.box{
                            caption = 'Накладные';
                            PROPERTY(nameToInvoice){
                                minimumCharWidth = 40;
                            }
                            PROPERTY(addressToStockOrder){
                                caption = 'Адрес получателя';
                            }
                            PROPERTY(nameToStockInvoice){                  
                                minimumCharWidth = 45;
                            }
                            PROPERTY(numInv) {
                                font = 'bold 18';
                            }
                            PROPERTY(serInv) {
                                font = 'bold 18';
                            }
                        }
                        ADD d.box{
                            caption = 'Сертификаты'; 
                        }
                    }
                    NEW secondContainer BEFORE tabContainer {
                        fill = 1;
                        ADD o.box{
                            caption = 'Скомплектованные заказы';
                                PROPERTY(nameToOrder){
                                    minimumCharWidth = 40;
                                }
                                PROPERTY(nameToStockOrder){
                                    minimumCharWidth = 45;
                                }
                                PROPERTY(dateOrder){
                                    caption = 'Дата заказа';
                                }
                                PROPERTY(numberOrder){
                                    caption = 'Номер заказа';
                                }
                                PROPERTY(addressToStockOrder){
                                    caption = 'Адрес получателя';
                                }
                                PROPERTY(inConsignmentPrintOrder){
                                    editKey = 'SPACE';
                                }
                            }
                        }
                    }
                    NEW printContainer {
                        NEW consignmentRow1 {
                            align = LEADING;
                            type = CONTAINERH;

                            NEW contOne {
                                caption = 'Накладная';
                                ADD PROPERTY(editConsignment);
                            }
                            NEW tn{
                                type = CONTAINERH;
                                caption = 'ТН-2';
                                ADD PROPERTY(printConsignmentSimpleVertical);
                                ADD PROPERTY(printConsignmentSimpleHorizontal);
                                ADD PROPERTY(printConsignmentSimpleAttach);
                                ADD PROPERTY(printConsignmentSimpleAttach);
                            }
                        }
                        NEW consignmentRow2 {
                            type = COLUMNS;
                            columns = 5;
                            caption = 'ТТН-1';
                            ADD PROPERTY(printConsignmentVerticalA);
                            ADD PROPERTY(printConsignmentVerticalAB);
                            ADD PROPERTY(printConsignmentHorizontalA);
                            ADD PROPERTY(printConsignmentVerticalB);
                            ADD PROPERTY(printConsignmentHorizontalB);
                            ADD PROPERTY(printConsignmentAttach);
                        }
                        NEW export {
                            type = CONTAINERH;
                            caption = 'Экспорт';  
                            ADD PROPERTY(printConsignmentAttachXLS);                
                        }
                    }
                }
                NEW thirdContainer BEFORE base1Container {
                    type = CONTAINERH;
                    caption = 'Шапка';
                    ADD PROPERTY(date);
                }
        ADD functions.box;
    }
}

NAVIGATOR {
    saleDashboardNavigator {
        ADD consignmentDashboard;
    }
}
