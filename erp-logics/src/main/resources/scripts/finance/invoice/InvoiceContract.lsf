MODULE InvoiceContract;

REQUIRE StockContract, Invoice;

META defineInvoicePaymentCondition (sign)

    paymentCondition 'Условия оплаты' = ABSTRACT PaymentCondition (Invoice) PERSISTENT;
    paymentCondition 'Условия оплаты' = DATA PaymentCondition (UserInvoice);
    paymentCondition(UserInvoice prop) += paymentCondition(prop);
    paymentCondition[Invoice.Invoice](Invoice prop) += paymentCondition(prop);
    descriptionPaymentCondition 'Условия оплаты' (Invoice i) = description(paymentCondition(i)) IN base MINCHARWIDTH 10 PREFCHARWIDTH 15;
    descriptionPaymentCondition 'Условия оплаты' (UserInvoice i) = description(paymentCondition(i)) MINCHARWIDTH 10 PREFCHARWIDTH 15;

    paymentCondition(UserInvoice prop) <- OVERRIDE paymentCondition(contractSku(prop)), paymentCondition(agreement(prop))
        WHEN CHANGED(agreement(prop)) OR
             CHANGED(contractSku(prop));

    EXTEND FORM userInvoice
        PROPERTIES(i) descriptionPaymentCondition
    ;

    DESIGN userInvoice{
        i.documentPrm{
            MOVE PROPERTY(descriptionPaymentCondition(i));
        }
    }

END

paymentCondition 'Условия оплаты' = ABSTRACT PaymentCondition (Invoice) PERSISTENT;
descriptionPaymentCondition 'Условия оплаты' (Invoice i) = description(paymentCondition(i)) IN base MINCHARWIDTH 10 PREFCHARWIDTH 15;
