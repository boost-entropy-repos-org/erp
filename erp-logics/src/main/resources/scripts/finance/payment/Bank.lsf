MODULE Bank;

REQUIRE System, Integration, LegalEntity, Finance;

CLASS Bank 'Банк';
TABLE bank (Bank);
TABLE bankDate (Bank, DATE);

GROUP bank 'Информация о банке' : base;

@defineExternalizable(bank, VARSTRING[100]);

nameBank 'Наименование' = DATA VARISTRING[100](Bank);

MFOBank 'Код МФО' = DATA STRING[9] (Bank) IN bank;
departmentBank 'Отдел банка' = DATA VARSTRING[100] (Bank) IN bank;
CBUBank 'ЦБУ' = DATA STRING[3] (Bank) IN bank;
infoBank 'Дополнительные сведения' = DATA VARSTRING[100] (Bank) IN bank;

countryBank = DATA Country (Bank);
nameCountryBank 'Страна' (bank) = nameCountry(countryBank(bank)) IN bank MINCHARWIDTH 10 PREFCHARWIDTH 15 MAXCHARWIDTH 20;

@defineHistorizable(address, , 'Адрес банка', VARSTRING[150], bank, nameBank, bank);

FORM bank 'Банк'
    OBJECTS b=Bank FIXED PANEL
    PROPERTIES(b) nameBank, idBank SHOWIF showIDs(), addressBank, MFOBank, departmentBank, CBUBank, nameCountryBank, infoBank
    EDIT Bank OBJECT b
;

DESIGN bank FROM DEFAULT {
    NEW topContainer{
        type = COLUMNS;
        columns = 3;
        caption = 'Информация о банке';
        ADD PROPERTY(nameBank(b));
        ADD PROPERTY(idBank(b));
        ADD PROPERTY(addressBank(b));
        ADD PROPERTY(MFOBank(b));
        ADD PROPERTY(departmentBank(b));
        ADD PROPERTY(CBUBank(b));
        ADD PROPERTY(nameCountryBank(b));
        ADD PROPERTY(infoBank(b));
    }
    ADD functions.box;
}

FORM banks 'Банки'
    OBJECTS b=Bank
    PROPERTIES(b) READONLY nameBank, idBank SHOWIF showIDs(), addressBank, MFOBank, departmentBank, CBUBank, nameCountryBank, infoBank
    PROPERTIES(b) ADDFORM, EDITFORM, deleteb=DELETE FORCE PANEL TOOLBAR

    DIALOG Bank OBJECT b
;

NAVIGATOR {
    financeMasterData {
        ADD banks;
    }
}

// ----------------------------------- Расчетный счет ------------------------------------------ //

CLASS Account 'Расчетный счет';
TABLE account (Account);

GROUP account 'Банковская информация' : base;

numberAccount 'Номер расчетного счета'  = DATA VARSTRING[20] (Account) IN account FIXEDCHARWIDTH 13;
accountNumber (number) = GROUP MAX account BY numberAccount(account); // WHERE account IS Account;

// Банк
bankAccount = DATA Bank (Account);
nameBankAccount 'Наименование банка' (account) = nameBank(bankAccount(account)) IN account;
addressBankAccount 'Адрес банка' (account) = addressBank(bankAccount(account)) IN account;
MFOBankAccount 'Код МФО банка' (account) = MFOBank(bankAccount(account)) IN account;
departmentBankAccount 'Отдел банка' (account) = departmentBank(bankAccount(account)) IN account;
CBUBankAccount 'ЦБУ банка' (account) = CBUBank(bankAccount(account)) IN account;

// Организация
legalEntityAccount = DATA LegalEntity (Account) NOT NULL DELETE;
nameLegalEntityAccount 'Организация' (account) = nameLegalEntity(legalEntityAccount(account));

defaultAccountLegalEntity(legalEntity) = GROUP MAX account BY legalEntityAccount(account);

userAccountLegalEntity = DATA Account (LegalEntity);
CONSTRAINT legalEntityAccount(userAccountLegalEntity(legalEntity)) != legalEntity CHECKED MESSAGE 'ошибка: Р/сч. по умолчанию должен соответствовать р/сч. Ю.Л.';

accountLegalEntity (legalEntity) =  OVERRIDE defaultAccountLegalEntity(legalEntity), userAccountLegalEntity(legalEntity);
numberAccountLegalEntity 'Основной р/сч.' (legalEntity) = numberAccount(accountLegalEntity(legalEntity)) IN law;

equalsLegalEntityAccount 'Основной' (legalEntity, account) = userAccountLegalEntity(legalEntity) == account;

accountNumberLegalEntityID 'Расчетный счет по номеру' (number, legalEntityId) = GROUP AGGR account BY numberAccount(account), idLegalEntity(legalEntityAccount(account)) WHERE account IS Account;
legalEntityAccountNumber (number) = legalEntityAccount(accountNumber(number)); 

// Валюта
currencyAccount = DATA Currency (Account) NOT NULL;
currencyAccount(account) <- currencyLegalEntity(legalEntityAccount(account))
    WHEN SET(legalEntityAccount(account));
nameCurrencyAccount 'Валюта счета' = nameCurrency(currencyAccount(account)) IN account;

noteAccount 'Примечание'  = DATA VARSTRING[50] (Account) IN account;

FORM account 'Расчетный счет'
    OBJECTS a = Account FIXED PANEL
    PROPERTIES(a) numberAccount, nameCurrencyAccount, nameLegalEntityAccount, nameBankAccount, addressBankAccount,
                  MFOBankAccount, departmentBankAccount, CBUBankAccount, noteAccount

    EDIT Account OBJECT a
;

DESIGN account FROM DEFAULT{
    main{
        ADD PROPERTY(numberAccount(a));
        ADD PROPERTY(nameCurrencyAccount(a));
        ADD PROPERTY(nameLegalEntityAccount(a));
        ADD PROPERTY(nameBankAccount(a));
        ADD PROPERTY(addressBankAccount(a));
        ADD PROPERTY(MFOBankAccount(a));
        ADD PROPERTY(departmentBankAccount(a));
        ADD PROPERTY(CBUBankAccount(a));
        ADD PROPERTY(noteAccount(a));

        ADD functions.box;
    }
}

addAccount 'Добавить расчетный счет' = ACTION ADDFORM Bank.Account;
editAccount 'Редактировать' (account) = ACTION EDITFORM Bank.Account;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY numberAccount, nameCurrencyAccount, nameLegalEntityAccount, nameBankAccount, addressBankAccount,
                           MFOBankAccount, departmentBankAccount, CBUBankAccount, noteAccount
    PROPERTIES(a) editAccount FORCE PANEL TOOLBAR, DELETE FORCE PANEL TOOLBAR
    PROPERTIES() addAccount TODRAW a FORCE PANEL

    DIALOG Account OBJECT a
;

// ------------------------ Расширяем форму организаций ------------------------ //

EXTEND FORM legalEntity
    PROPERTIES(l)  SHOWIF toShowLegalEntity(l) numberAccountLegalEntity

    OBJECTS a=Account
    PROPERTIES(a)  numberAccount, nameCurrencyAccount, nameBankAccount, addressBankAccount, departmentBankAccount,
                   CBUBankAccount, MFOBankAccount, noteAccount, ADDOBJ, DELETESESSION
    FILTERS legalEntityAccount(a) == l
    PROPERTIES equalsLegalEntityAccount(l, a)
;

EXTEND DESIGN legalEntity {
    extendContainer {
        ADD a.box {
            caption = 'Расчетные счета';
        }
    }
}

// ------------------------ Макросы ------------------------ //

META defineDocumentAccount(object, contact, caption)
    numberAccount###contact###object 'Расчетный счет'###caption (object) = numberAccountLegalEntity(contact###object(object)) IN documentPrm;
    nameBank###contact###object 'Банк'###caption (object) = nameBankAccount(accountLegalEntity(contact###object(object))) IN documentPrm;
    addressBank###contact###object 'Адрес банка'###caption (object) = addressBankAccount(accountLegalEntity(contact###object(object))) IN documentPrm;
    departmentBank###contact###object 'Отдел банка'###caption (object) = departmentBankAccount(accountLegalEntity(contact###object(object))) IN documentPrm;
    CBUBank###contact###object 'ЦБУ банка'###caption (object) = CBUBankAccount(accountLegalEntity(contact###object(object))) IN documentPrm;
    MFOBank###contact###object 'Код МФО банка'###caption (object) = MFOBankAccount(accountLegalEntity(contact###object(object))) IN documentPrm;
END
