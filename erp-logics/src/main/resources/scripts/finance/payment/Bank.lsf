MODULE Bank;

REQUIRE System, Integration, LegalEntity, Finance;

CLASS Bank 'Банк';
TABLE bank (Bank);
TABLE bankDate (Bank, DATE);

GROUP bank 'Информация о банке' : base;

@defineExternalizable(bank, VARSTRING[100]);

name 'Наименование' = DATA VARISTRING[100](Bank);

MFO 'Код МФО' = DATA STRING[9] (Bank) IN bank;
department 'Отдел банка' = DATA VARSTRING[100] (Bank) IN bank;
CBU 'ЦБУ' = DATA STRING[3] (Bank) IN bank;
info 'Дополнительные сведения' = DATA VARSTRING[100] (Bank) IN bank;

country = DATA Country (Bank);
nameCountry 'Страна' (Bank bank) = name(country(bank)) IN bank MINCHARWIDTH 10 PREFCHARWIDTH 15 MAXCHARWIDTH 20;

@defineHistorizable(address, , 'Адрес банка', VARSTRING[150], bank, nameBank, bank);

code 'Код банка' (Bank b) = right(MFO(b),3);
//changeCodeBank = ACTION (b) {
//    REQUEST STRING[9] INPUT ;
//    IF requestedString() THEN {
//        MFOBank(b)<- requestedString();    
//    }   
//}

FORM bank 'Банк'
    OBJECTS b=Bank FIXED PANEL
    PROPERTIES(b) name, id SHOWIF showIDs(), address, MFO, department, CBU, nameCountry, info
    EDIT Bank OBJECT b
;

DESIGN bank {
    NEW topContainer{
        type = COLUMNS;
        columns = 3;
        caption = 'Информация о банке';
        MOVE PROPERTY(name(b));
        MOVE PROPERTY(id(b));
        MOVE PROPERTY(address(b));
        MOVE PROPERTY(MFO(b));
        MOVE PROPERTY(department(b));
        MOVE PROPERTY(CBU(b));
        MOVE PROPERTY(nameCountry(b));
        MOVE PROPERTY(info(b));
    }
    MOVE functions.box;
}

FORM dialogBanks 'Банки'
    OBJECTS b=Bank
    PROPERTIES(b) READONLY name, id SHOWIF showIDs(), address, MFO, department, CBU, nameCountry, info

    DIALOG Bank OBJECT b
;

FORM banks 'Банки'
    OBJECTS b=Bank
    PROPERTIES(b) READONLY name, id SHOWIF showIDs(), address, MFO, department, CBU, nameCountry, info
    PROPERTIES(b) ADDFORM, EDITFORM, deleteb=DELETE FORCE PANEL TOOLBAR
;

NAVIGATOR {
    financeMasterData {
        ADD banks;
    }
}

// ----------------------------------- Расчетный счет ------------------------------------------ //

CLASS Account 'Расчетный счет';
TABLE account (Account);

GROUP account 'Банковская информация' : base;

number 'Номер расчетного счета'  = DATA VARSTRING[50] (Account) IN account FIXEDCHARWIDTH 13;
account (number) = GROUP MAX Account account BY number(account); // WHERE account IS Account;

// Банк
bank = DATA Bank (Account);
nameBank 'Наименование банка' (Account account) = name(bank(account)) IN account;
addressBank 'Адрес банка' (Account account) = address(bank(account)) IN account;
MFOBank 'Код МФО банка' (Account account) = MFO(bank(account)) IN account;
departmentBank 'Отдел банка' (Account account) = department(bank(account)) IN account;
CBUBank 'ЦБУ банка' (Account account) = CBU(bank(account)) IN account;

// Организация
legalEntity = DATA LegalEntity (Account) NOT NULL DELETE AUTOSET;
nameLegalEntity 'Организация' (Account account) = name(legalEntity(account));

defaultAccount(legalEntity) = GROUP MAX Account account BY legalEntity(account);

userAccount = DATA Account (LegalEntity);
CONSTRAINT legalEntity(userAccount(LegalEntity legalEntity)) != legalEntity CHECKED MESSAGE 'ошибка: Р/сч. по умолчанию должен соответствовать р/сч. Ю.Л.';

account (LegalEntity legalEntity) =  OVERRIDE defaultAccount(legalEntity), userAccount(legalEntity);
numberAccount 'Основной р/сч.' (LegalEntity legalEntity) = number(account(legalEntity)) IN law;
nameBank 'Банк покупателя' (LegalEntity legalEntity) = nameBank(account(legalEntity));
addressBank 'Адрес банка покупателя' (LegalEntity legalEntity) = addressBank(account(legalEntity));
departmentBank 'Отдел банка покупателя' (LegalEntity legalEntity) = departmentBank(account(legalEntity));
CBUBank 'ЦБУ банка покупателя' (LegalEntity legalEntity) = CBUBank(account(legalEntity));
MFOBank 'Код МФО банка покупателя' (LegalEntity legalEntity) = MFOBank(account(legalEntity));
    
equals 'Основной' (LegalEntity legalEntity, Account account) = userAccount(legalEntity) == account;

accountID 'Расчетный счет по номеру' (number, legalEntityId) = GROUP AGGR Account account BY number(account), id(legalEntity(account)) WHERE account IS Account;
legalEntityAccount (VARSTRING[20] number) = legalEntity(account(number)); 

// Валюта
currency = DATA Currency (Account) NOT NULL;
currency(Account account) <- currency(legalEntity(account))
    WHEN SET(legalEntity(account));
nameCurrency 'Валюта счета' = name(currency(Account account)) IN account;

note 'Примечание'  = DATA VARSTRING[50] (Account) IN account;

FORM account 'Расчетный счет'
    OBJECTS a = Account FIXED PANEL
    PROPERTIES(a) number, nameCurrency, nameLegalEntity, nameBank, addressBank,
                  MFOBank, departmentBank, CBUBank, note

    EDIT Account OBJECT a
;

DESIGN account {
    main{
        MOVE PROPERTY(number(a));
        MOVE PROPERTY(nameCurrency(a));
        MOVE PROPERTY(nameLegalEntity(a));
        MOVE PROPERTY(nameBank(a));
        MOVE PROPERTY(addressBank(a));
        MOVE PROPERTY(MFOBank(a));
        MOVE PROPERTY(departmentBank(a));
        MOVE PROPERTY(CBUBank(a));
        MOVE PROPERTY(note(a));

        MOVE functions.box;
    }
}

addAccount 'Добавить расчетный счет' = ACTION ADDFORM Bank.Account;
edit 'Редактировать' (account) = ACTION EDITFORM Bank.Account;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY number, nameCurrency, nameLegalEntity, nameBank, addressBank,
                           MFOBank, departmentBank, CBUBank, note
    PROPERTIES(a) edit FORCE PANEL TOOLBAR, DELETE FORCE PANEL TOOLBAR
    PROPERTIES() addAccount TODRAW a FORCE PANEL

    DIALOG Account OBJECT a
;

// ------------------------ Расширяем форму организаций ------------------------ //

EXTEND FORM legalEntity
    PROPERTIES(l)  SHOWIF toShow(l) numberAccount

    OBJECTS a=Account
    PROPERTIES(a)  number, nameCurrency, nameBank, addressBank, departmentBank,
                   CBUBank, MFOBank, note, ADDOBJ, DELETESESSION
    FILTERS legalEntity(a) == l
    PROPERTIES equals(l, a)
;

DESIGN legalEntity {
    extendContainer {
        MOVE a.box {
            caption = 'Расчетные счета';
        }
    }
}

// ------------------------ Макросы ------------------------ //

META defineDocumentAccount(object, contact, caption)
    numberAccount###contact 'Расчетный счет'###caption (###object object) = numberAccount(contact(object)) IN documentPrm;
    nameBank###contact 'Банк'###caption (###object object) = nameBank(account(contact(object))) IN documentPrm;
    addressBank###contact 'Адрес банка'###caption (###object object) = addressBank(account(contact(object))) IN documentPrm;
    departmentBank###contact 'Отдел банка'###caption (###object object) = departmentBank(account(contact(object))) IN documentPrm;
    CBUBank###contact 'ЦБУ банка'###caption (###object object) = CBUBank(account(contact(object))) IN documentPrm;
    MFOBank###contact 'Код МФО банка'###caption (###object object) = MFOBank(account(contact(object))) IN documentPrm;
END
