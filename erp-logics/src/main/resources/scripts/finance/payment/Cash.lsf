MODULE Cash;

REQUIRE LegalEntity;

// ----------------------------------- Кассовый счет ------------------------------------------ //

CLASS Account 'Наличный счет';
TABLE account (Account);

// Неактивный
inactive 'Неактивная' = DATA BOOLEAN (Account);
active 'Активная' (Account a) = a IS Account AND NOT inactive(a);

number 'Номер наличного счета'  = DATA VARSTRING[20] (Account) FIXEDCHARWIDTH 13;

legalEntity = DATA LegalEntity (Account);
nameLegalEntity 'Организация' = name(legalEntity(Account account));
defaultAccount(legalEntity) = GROUP MAX Account account IF active(account) BY legalEntity(account);

currency = DATA Currency (Account) NOT NULL;
currency(Account account) <- currency(legalEntity(account))
    WHEN SET(legalEntity(account));
nameCurrency 'Валюта счета' = name(currency(Account account));

FORM account 'Наличный счет'
    OBJECTS a = Account PANEL
    PROPERTIES(a) number, nameCurrency, nameLegalEntity, inactive

    EDIT Account OBJECT a
;

edit 'Редактировать' (account) = ACTION EDITFORM Cash.Account;

FORM accounts 'Наличные счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY number, nameCurrency, nameLegalEntity
    PROPERTIES(a) NEWSESSION edit TOOLBAR, DELETE 
    PROPERTIES(a) NEWSESSION NEW[Cash.Account]
    FILTERGROUP inactive FILTER 'Активный' active(a) 'ctrl F10' DEFAULT  
    LIST Account OBJECT a
;

EXTEND FORM legalEntity
    OBJECTS ca=Account
    PROPERTIES(ca)  inactive, number, nameCurrency, NEW, DELETE GRID
    FILTERS legalEntity(ca) == l
    FILTERGROUP inactiveca FILTER 'Активный' active(ca) 'ctrl F10' DEFAULT  
;

DESIGN legalEntity {
    extendContainer {
        MOVE ca.box {
            caption = 'Наличные счета';
        }
    }
}