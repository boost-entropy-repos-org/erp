MODULE Cash;

REQUIRE LegalEntity;

// ----------------------------------- Кассовый счет ------------------------------------------ //

CLASS Account 'Наличный счет';
TABLE account (Account);

number 'Номер наличного счета'  = DATA VARSTRING[20] (Account) FIXEDCHARWIDTH 13;

legalEntity = DATA LegalEntity (Account);
nameLegalEntity 'Организация' = name(legalEntity(Account account));
defaultAccount(legalEntity) = GROUP MAX Account account BY legalEntity(account);

currency = DATA Currency (Account) NOT NULL;
currency(Account account) <- currency(legalEntity(account))
    WHEN SET(legalEntity(account));
nameCurrency 'Валюта счета' = name(currency(Account account));

FORM account 'Наличный счет'
    OBJECTS a = Account FIXED PANEL
    PROPERTIES(a) number, nameCurrency, nameLegalEntity

    EDIT Account OBJECT a
;

edit 'Редактировать' (account) = ACTION EDITFORM Cash.Account;

FORM accounts 'Наличные счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY number, nameCurrency, nameLegalEntity
    PROPERTIES(a) edit FORCE PANEL TOOLBAR, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(a) ADDFORM[Cash.Account]

    DIALOG Account OBJECT a
;

EXTEND FORM legalEntity
    OBJECTS ca=Account
    PROPERTIES(ca)  number, nameCurrency, ADDOBJ, DELETESESSION
    FILTERS legalEntity(ca) == l
;

DESIGN legalEntity {
    extendContainer {
        MOVE ca.box {
            caption = 'Наличные счета';
        }
    }
}