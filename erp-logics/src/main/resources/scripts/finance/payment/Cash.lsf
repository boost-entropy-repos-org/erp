MODULE Cash;

REQUIRE LegalEntity;

// ----------------------------------- Кассовый счет ------------------------------------------ //

CLASS Account 'Наличный счет';
TABLE account (Account);

numberAccount 'Номер наличного счета'  = DATA VARSTRING[20] (Account) FIXEDCHARWIDTH 13;

legalEntityAccount = DATA LegalEntity (Account);
nameLegalEntityAccount 'Организация' = nameLegalEntity(legalEntityAccount(account));
defaultAccountLegalEntity(legalEntity) = GROUP MAX account BY legalEntityAccount(account);

currencyAccount = DATA Currency (Account) NOT NULL;
currencyAccount(account) <- currencyLegalEntity(legalEntityAccount(account))
    WHEN SET(legalEntityAccount(account));
nameCurrencyAccount 'Валюта счета' = nameCurrency(currencyAccount(account));

FORM account 'Наличный счет'
    OBJECTS a = Account FIXED PANEL
    PROPERTIES(a) numberAccount, nameCurrencyAccount, nameLegalEntityAccount

    EDIT Account OBJECT a
;

addAccount 'Добавить' = ACTION ADDFORM Cash.Account;
editAccount 'Редактировать' (account) = ACTION EDITFORM Cash.Account;

FORM accounts 'Наличные счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY numberAccount, nameCurrencyAccount, nameLegalEntityAccount
    PROPERTIES(a) editAccount FORCE PANEL TOOLBAR, DELETE FORCE PANEL TOOLBAR
    PROPERTIES() addAccount TODRAW a FORCE PANEL

    DIALOG Account OBJECT a
;

EXTEND FORM legalEntity
    OBJECTS ca=Account
    PROPERTIES(ca)  numberAccount, nameCurrencyAccount, ADDOBJ, DELETESESSION
    FILTERS legalEntityAccount(ca) == l
;

DESIGN legalEntity {
    extendContainer {
        MOVE ca.box {
            caption = 'Наличные счета';
        }
    }
}