MODULE TaxStoreDashboard;

REQUIRE TaxReverse, Store;

NAMESPACE Tax;

addVATRange 'Добавить ставку'(TaxUnit taxUnit) = ACTION {
    IF range(Tax.taxVAT, taxUnit) THEN {
        FOR NEW rt = Rate DO {
            range (rt) <- range(Tax.taxVAT, taxUnit);
        }    
    } ELSE {    
        FOR NEW r = Range DO {
            tax(r) <- Tax.taxVAT;
            rangeType(r) <- RangeType.units;
            reverse(r) <- TRUE;
            in(r, taxUnit) <- TRUE;
            country(r) <- defaultCountry();
            FOR NEW rt = Rate DO {
                range (rt) <- r;
            }
        }    
    }
} TOOLBAR IMAGE 'add.png';

FORM storeTaxUnit 'Налоговые субъекты'
    OBJECTS u = TaxUnit
    PROPERTIES (u) READONLY description
    FILTERGROUP store FILTER 'Магазины' u IS Store 'F9' DEFAULT
    LIST TaxUnit OBJECT u
;

FORM VATUnitDashboard 'Ввод расчетной ставки'
    OBJECTS u = TaxUnit FIXED PANEL 
    PROPERTIES (u) description SELECTOR
    FILTERS u IS Store
        
    OBJECTS rt = Rate
    PROPERTIES (rt) value, dateFrom, DELETE 
    PROPERTIES (u) addVATRange TODRAW rt
    FILTERS in(rt, u)
    ORDER BY dateFrom(rt)
;

DESIGN VATUnitDashboard {
    PROPERTY (description(u)) {
        caption = 'Магазин';
    }
    rt.box {
        caption = 'Расчетные ставки НДС';
    }
}

NAVIGATOR {
    saleDashboardNavigator {
        ADD VATUnitDashboard;
    }    
}