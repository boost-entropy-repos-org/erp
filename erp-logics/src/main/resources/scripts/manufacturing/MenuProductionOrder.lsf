MODULE  MenuProductionOrder;

REQUIRE Menu,
        ProductionOrder,
        BOMNutrition;

// -- Заполнение на основе производственных заказов ---- //

FORM menuOrders 'Производственные заказы'

    OBJECTS o = Order
    PROPERTIES(o) READONLY isPostedOrder, numberOrder, seriesOrder, dateOrder, timeOrder,
                  nameComponentsStockOrder, nameProductsStockOrder, fromDateOrder, toDateOrder, quantityProductDetailOrder,
                  sumProductDetailOrder, 
                  nameCalcPriceListTypeOrder, namePriceListTypeOrder, noteOrder
    FILTERS isPostedOrder(o)

    OBJECTS pd=ProductDetail
    PROPERTIES(pd) READONLY indexProductDetail, nameSkuProductDetail, nameProductProductDetail, shortNameUOMProductDetail, quantityProductDetail,
                   priceProductDetail, sumProductDetail
    FILTERS orderProductDetail(pd)==o
;

DESIGN menuOrders FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        NEW documentContainer BEFORE functions.box {
            fill = 1;
            type = CONTAINERV;
            ADD o.box;
            ADD pd.box {
                caption = 'Изделия';
            }
        }
    }
}

fillOrderUserMenu 'Заполнить на основе производственного заказа' =  ACTION (userMenu) {
    FORM menuOrders MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL productionOrder = Order();
        ASSIGN productionOrder() <- chosenObject('o');
        ASSIGN noteUserMenu(userMenu) <- 'Основание: ' + descriptionOrder(productionOrder());
        FOR orderProductDetail(productDetail) == productionOrder() ADDOBJ d = UserMenuDetail DO {
            ASSIGN userMenuUserMenuDetail(d) <- userMenu;
            ASSIGN priceUserMenuDetail(d) <- priceProductDetail(productDetail);
            ASSIGN skuUserMenuDetail(d) <- skuProductDetail(productDetail);
            ASSIGN productYieldUserMenuDetail(d) <- productYieldProduct(productProductDetail(productDetail));
            ASSIGN compositionUserMenuDetail(d) <- compositionProduct(productProductDetail(productDetail));
        }
    }
}

EXTEND FORM userMenu
    PROPERTIES(m) fillOrderUserMenu
;
EXTEND DESIGN userMenu {
    headerRow1 {
        NEW row2 BEFORE m.documentPrm {
            caption = 'Производственный заказ';
            type = CONTAINERH;

            ADD PROPERTY(fillOrderUserMenu(m));
        }
    }
}
