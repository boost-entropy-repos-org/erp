MODULE  FoodSupplement;

REQUIRE BOMNutrition, PurchaseShipment, ProductionConsumption;

NAMESPACE Production;

supplementQuantity = GROUP BY order(ComponentDetail cd), sku(cd) SUM consumedQuantity(cd) * overNetWeight(sku(cd))*1000.0 IF isSupplement(sku(cd)); 
supplementQuantity  = GROUP 
        BY ProductDetail pd, sku(ComponentDetail cd) SUM consumedQuantity(cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(pd) AND BOM(cd) == BOM(pd); 
supplementQuantity  = GROUP 
        BY ProductDetail pd SUM consumedQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(pd) AND BOM(cd) == BOM(pd); 
        
supplementQuantityPlan = GROUP BY order(ComponentDetail cd), sku(cd) SUM bruttoQuantity(cd) * overNetWeight(sku(cd))*1000.0 IF isSupplement(sku(cd)); 
supplementQuantityPlan  = GROUP 
        BY ProductDetail pd, sku(ComponentDetail cd) SUM bruttoQuantity(cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(pd) AND BOM(cd) == BOM(pd); 
supplementQuantityPlan  = GROUP 
        BY ProductDetail pd SUM bruttoQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(pd) AND BOM(cd) == BOM(pd);   
             
isPlan 'План'  = DATA LOCAL BOOLEAN (Order);       
        
FORM  foodSupplement 'Расход пищевых добавок'
    OBJECTS o =Order PANEL 
    PROPERTIES(o) date, nameProductsStock, nameComponentsStock, isPlan 
    
    OBJECTS sk =Sku
    FILTERS (supplementQuantity(o,sk) IF NOT isPlan(o)) OR (supplementQuantityPlan(o,sk) IF isPlan(o))

    OBJECTS pd =ProductDetail
    PROPERTIES nameSku(pd), outputQuantity(pd), supplementQuantity(pd,sk) COLUMNS (sk) HEADER name(sk) FOOTER supplementQuantity(o,sk)
    PROPERTIES quantity(pd), supplementQuantityPlan(pd,sk) COLUMNS (sk) HEADER name(sk) FOOTER supplementQuantityPlan(o,sk)
    FILTERS order(pd) == o,
            (supplementQuantity(pd) IF NOT isPlan(o)) OR  (supplementQuantityPlan(pd) IF isPlan(o))           
;

foodSupplement 'Расход пищевых добавок (факт)' (Order o) = {
    isPlan(o) <- NULL;
    PRINT foodSupplement OBJECTS o = o ;
}IMAGE 'print.png' IN print;

foodSupplementPlan 'Расход пищевых добавок (план)' (Order o) = {
    isPlan(o) <- TRUE;    
    PRINT foodSupplement OBJECTS o = o ; 
} IMAGE 'print.png' IN print;
EXTEND FORM orders
    PROPERTIES (o)  foodSupplement, foodSupplementPlan
;