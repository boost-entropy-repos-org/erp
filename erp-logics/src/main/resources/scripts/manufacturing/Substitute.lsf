MODULE  Substitute;

REQUIRE BOM;

// ---------------- Замена ------------------- //

CLASS Substitute 'Замена';
TABLE substitute(Substitute);

fromSkuSubstitute = DATA Sku(Substitute);
nameFromSkuSubstitute 'Товар с' (substitute)= nameSku(fromSkuSubstitute(substitute)) IN recognize;

toSkuSubstitute = DATA Sku(Substitute);
nameToSkuSubstitute 'Товар на' (substitute)= nameSku(toSkuSubstitute(substitute)) IN recognize;

uniqueSubstituteFromToSku = GROUP AGGR substitute BY fromSkuSubstitute(substitute), toSkuSubstitute(substitute) WHERE substitute IS Substitute;
countSubstituteFromToSku = GROUP SUM 1 BY fromSkuSubstitute(substitute), toSkuSubstitute(substitute);

multiplierSubstitute 'Коэффициент' = DATA NUMERIC[8,3] (Substitute) IN recognize;

allBOMsSubstitute 'Во всех спецификациях'= DATA BOOLEAN (Substitute);
allBOMsSubstitute(substitute) <- TRUE WHEN SET(substitute IS Substitute);

//TABLE substituteComponent(Substitute, Component);
//includeSubstituteComponent 'Вкл.'= DATA BOOLEAN (Substitute, Component);
//excludeSubstituteComponent 'Искл.'= DATA BOOLEAN (Substitute, Component);
//toShowIncludeSubstitute (substitute) = substitute IS Substitute AND NOT allBOMsSubstitute(substitute);
//
//useSubstituteComponent 'Можно использовать' (substitute, component) = 
//    (allBOMsSubstitute(substitute) AND component IS Component AND NOT excludeSubstituteComponent(substitute, component)) OR 
//    (includeSubstituteComponent(substitute, component) AND NOT allBOMsSubstitute(substitute));
//
//nameBOMComponent 'Спецификация' (component) = nameBOM(BOMComponent(component));
//seriesNumberBOMComponent 'Серия/номер спецификации' (component)= seriesNumberBOM(BOMComponent(component));
//
//countComponentMaterial (material) = GROUP SUM 1 BY materialComponent(component);

TABLE substituteBOM(Substitute, BOM);
includeSubstituteBOM 'Вкл.'= DATA BOOLEAN (Substitute, BOM);
excludeSubstituteBOM 'Искл.'= DATA BOOLEAN (Substitute, BOM);
toShowIncludeSubstitute (substitute) = substitute IS Substitute AND NOT allBOMsSubstitute(substitute);

useSubstituteBOM 'Можно использовать' (substitute, BOM) = 
    (allBOMsSubstitute(substitute) AND BOM IS BOM AND NOT excludeSubstituteBOM(substitute, BOM)) OR 
    (includeSubstituteBOM(substitute, BOM) AND NOT allBOMsSubstitute(substitute));

countComponentMaterial (material) = GROUP SUM 1 BY materialComponent(component);

relationMaterialBOM (material, BOM)  = GROUP SUM 1 BY materialComponent(c), BOMComponent(c); 

FORM substitute 'Замена'

    OBJECTS sk = Sku
    PROPERTIES(sk) READONLY nameSku, idBarcodeSku, shortNameUOMSku
    FILTERGROUP filters
        FILTER 'Есть в спецификациях' countComponentMaterial(sk) 'F11' DEFAULT

    OBJECTS s=Substitute
    PROPERTIES(s) nameFromSkuSubstitute, nameToSkuSubstitute, multiplierSubstitute, allBOMsSubstitute
    PROPERTIES(s) ADDOBJ, DELETESESSION

    FILTERS fromSkuSubstitute(s)==sk

    OBJECTS b = BOM
    PROPERTIES(b) READONLY seriesNumberBOM, productsBOM
    PROPERTIES(s,b) SHOWIF toShowIncludeSubstitute(s) includeSubstituteBOM
    PROPERTIES(s,b) SHOWIF allBOMsSubstitute(s) excludeSubstituteBOM
    ORDER BY seriesNumberBOM(b)

    FILTERS relationMaterialBOM(sk,b)
;

DESIGN substitute {
    main {
        preferredSize = (1024, 768);

        NEW documentContainer BEFORE functions.box {
            fill = 1;
            type = CONTAINERV;
            MOVE sk.box;
            NEW row {
                fill = 1;
                type = CONTAINERH;
                MOVE s.box {fill = 1.5;}
                MOVE b.box;
            }
        }
    }
}
NAVIGATOR {
    manufacturingMasterData {
        ADD substitute;
    }
}
