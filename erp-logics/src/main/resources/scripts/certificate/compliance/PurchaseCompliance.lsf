MODULE PurchaseCompliance;

REQUIRE Compliance, PurchaseCertificate;

NAMESPACE Purchase;

@defineDocumentCertificate(compliance, invoice, i, 'Сертификат соответствия');

WHEN SESSION (CHANGED(batch(UserInvoiceDetail detail)) OR
              CHANGED (sku(detail)) OR
              CHANGED (customerStock(detail))) AND NOT autoDeriveLastCertificate() DO {
    dataCompliance(detail)  <- IF batch(detail) 
                                  THEN prevCompliance(batch(detail))
                                  ELSE prevCompliance(prevLastOrderBatch(sku(detail), customerStock(detail)));
              
}
fromDateCompliance 'Дата с' (InvoiceDetail detail) = fromDate(compliance(detail));
toDateCompliance 'Дата по' (InvoiceDetail detail) = toDate(compliance(detail));

sumInvoiceDetail 'Сумма (по накладным)' = GROUP SUM sum(InvoiceDetail d)
                                                         BY compliance(d) MINCHARWIDTH 15;
sumUserInvoiceDetail 'Сумма (по накладным)' = GROUP SUM sum(UserInvoiceDetail d)
                                                             BY compliance(d) MINCHARWIDTH 15;

image 'Файл сертификата' (UserInvoiceDetail userInvoiceDetail) = image(compliance(userInvoiceDetail));
openImage 'Просмотреть сертификат' (UserInvoiceDetail userInvoiceDetail) = { OPENFILE image(userInvoiceDetail); }

EXTEND FORM userInvoice
    PROPERTIES(dc) TOOLBAR openImage
;

@defineDocumentCertificate(compliance, shipment, s, 'Сертификат соответствия');

// Проставляем автоматически сертификат от строки инвойса
WHEN CHANGED(compliance(invoiceDetail(UserShipmentDetail d))) AND 
     NOT CHANGED(compliance(d)) DO
     dataCompliance(d) <- compliance(invoiceDetail(d)); 

@deriveDocumentPurchaseCertificate(compliance, shipment, customerStock, data);

compliance(InvoiceShipmentDetail detail) += compliance(invoiceDetail(detail));

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) += {
    ASSIGN dataCompliance(s) <- compliance(i);
}

compliance (ShipmentBatch batch) += compliance(shipmentDetail(batch));
idCompliance (Batch batch) = number(compliance(batch));
