MODULE SaleCompliance;

REQUIRE Compliance, SaleCertificate;

NAMESPACE Sale;

CLASS ABSTRACT PrintPageCompliance 'Страница сертификата соответствия';
compliancePrintPageCompliance = ABSTRACT Compliance (PrintPageCompliance);
imagePrintPageCompliance = ABSTRACT IMAGEFILE (PrintPageCompliance);

EXTEND CLASS Compliance : PrintPageCompliance;
compliancePrintPageCompliance(compliance) += compliance IF compliance IS Compliance;
imagePrintPageCompliance(compliance) += imageCompliance(compliance);

EXTEND CLASS ExtraPageComplianceDetail : PrintPageCompliance;
compliancePrintPageCompliance(detail) += complianceExtraPageComplianceDetail(detail);
imagePrintPageCompliance(detail) += imageExtraPageComplianceDetail(detail);

@defineDocumentCertificate(compliance, invoice, i, 'Сертификат соответствия');
@defineDocumentCertificateConstraint(compliance, invoice, 'Сертификат соответствия');
@deriveDocumentSaleCertificate(compliance, invoice, supplierStock, data);

countComplianceInvoice (compliance, invoice) =
    GROUP SUM 1
    BY complianceInvoiceDetail(detail), invoiceInvoiceDetail(detail);

imageInvoiceDetail 'Файл сертификата' (invoiceDetail) = imageCompliance(complianceInvoiceDetail(invoiceDetail));
openImageInvoiceDetail 'Просмотреть сертификат' (invoiceDetail) = ACTION OPENFILE imageInvoiceDetail(invoiceDetail);

// todo : при попытке распечатать выдет сообщение "Форма не подходит для данных параметров"
FORM imageComplianceInvoice 'Файл сертификата'
    OBJECTS i = Invoice FIXED PANEL

    OBJECTS c = Compliance
    FILTERS countComplianceInvoice(c, i)
    
    OBJECTS p = PrintPageCompliance
    PROPERTIES(p) imagePrintPageCompliance
    FILTERS compliancePrintPageCompliance(p) == c    
;
printAllImageInvoice 'Печать сертификатов' = ACTION (invoice) {
    FORM imageComplianceInvoice OBJECTS i = invoice PRINT;
}
printAllImageInvoiceAuto 'Печать сертификатов' = ACTION (invoice) {
    FORM imageComplianceInvoice OBJECTS i = invoice PRINT AUTO;
}

EXTEND FORM userInvoice
    PROPERTIES(i) TODRAW dc FORCE PANEL TOOLBAR printAllImageInvoice
    PROPERTIES(dc) TODRAW dc FORCE PANEL TOOLBAR openImageInvoiceDetail
;

EXTEND FORM invoices
    PROPERTIES(i) TODRAW dc FORCE PANEL TOOLBAR printAllImageInvoice
    PROPERTIES(dc) TODRAW dc FORCE PANEL TOOLBAR openImageInvoiceDetail
;

@defineDocumentCertificate(compliance, shipment, s, 'Сертификат соответствия');
complianceShipmentDetail(detail) += complianceInvoiceDetail(invoiceDetailInvoiceShipmentDetail(detail));

overFillInvoiceUserShipmentDetailInvoiceDetail(s, i) += ACTION (s, i) {
    ASSIGN dataComplianceUserShipmentDetail(s) <- complianceInvoiceDetail(i);
}
@defineDocumentCertificateConstraint(compliance, shipment, 'Сертификат соответствия');
@deriveDocumentSaleCertificate(compliance, shipment, supplierStock, data);