MODULE ComplianceEmail;

REQUIRE Compliance;


countDaysToEndCompliance 'За сколько дней уведомлять об истечении сертификата' = DATA INTEGER ();
emailToNotification 'E-mail через точку с запятой' = DATA VARSTRING[300] () MINCHARWIDTH 30 PREFCHARWIDTH 50;

EXTEND FORM options PROPERTIES countDaysToEndCompliance(), emailToNotification();
EXTEND DESIGN options { 
    pane { 
        NEW notification {
            caption = 'Сертификаты';
            ADD PROPERTY(countDaysToEndCompliance()); 
            ADD PROPERTY(emailToNotification());        
        } 
    } 
}



countToNotificationComplianceDate 'Сертификаты, которым нужно сообщение' (compliance, date) = GROUP SUM 1 
    IF currentBalanceBatch(b) AND subtractDate(toDateComplianceBatch(b), countDaysToEndCompliance()) <= date
        BY complianceBatch(b), date;
    
countToNotificationDate 'Кол-во сертификатов' (date) = GROUP SUM 1 IF countToNotificationComplianceDate(compliance, date) BY date;   
    
compliancesToNotificationDate 'Сертификаты' (date) = GROUP CONCAT descriptionCompliance(c) IF countToNotificationComplianceDate(c, date), ' , '
           BY date
           ORDER descriptionCompliance(c)
;  
FORM emailCompliance 'Сертификаты'
 
    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES  val = OBJVALUE(dt)
    PROPERTIES int = countDaysToEndCompliance() TODRAW dt

    OBJECTS c = Compliance
    PROPERTIES(c) READONLY numberCompliance, seriesCompliance, dateCompliance, timeCompliance,
                  fromDateCompliance, toDateCompliance, nameLegalEntityCompliance

    FILTERS countToNotificationComplianceDate(c,dt) 
;

emailComplianceDate 'Выслать уведомление об окончании срока действия сертификатов' (date) = ACTION EMAIL
    SUBJECT 'Уведомление об окончании срока действия сертификатов на ' + countToNotificationDate(date) + ' шт.'
    TO emailToNotification()
    INLINE emailCompliance OBJECTS dt = date AS DATE
;
  
  
WHEN CHANGED (currentDate()) AND countToNotificationDate(currentDate()) DO {
    emailComplianceDate(currentDate());        
}



