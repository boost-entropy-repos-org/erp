MODULE ComplianceEmail;

REQUIRE Compliance;


countDaysToEndCompliance 'За сколько дней уведомлять об истечении сертификата' = DATA INTEGER ();
emailToNotification 'E-mail через точку с запятой' = DATA VARSTRING[300] () CHARWIDTH 30;

EXTEND FORM options PROPERTIES countDaysToEndCompliance(), emailToNotification();
DESIGN options { 
    pane { 
        certificate {
            MOVE PROPERTY(countDaysToEndCompliance()); 
            MOVE PROPERTY(emailToNotification());        
        } 
    } 
}



countToNotification 'Сертификаты, которым нужно сообщение' (compliance, date) = GROUP SUM 1 
    IF currentBalance(Batch b) AND subtract(toDateCompliance(b), countDaysToEndCompliance()) <= DATE date
        BY compliance(b), date;
    
countToNotification 'Кол-во сертификатов' (date) = GROUP SUM 1 IF countToNotification(Compliance compliance, DATE date) BY date;   
    
compliancesToNotification 'Сертификаты' (date) = GROUP CONCAT description(Compliance c) IF countToNotification(c, DATE date), ' , '
           BY date
           ORDER description(c)
;  
FORM emailCompliance 'Сертификаты'
 
    OBJECTS dt = DATE PANEL
    PROPERTIES  val = VALUE(dt)
    PROPERTIES int = countDaysToEndCompliance() TODRAW dt

    OBJECTS c = Compliance
    PROPERTIES(c) READONLY number, series, date, time,
                  fromDate, toDate, nameLegalEntity
    ORDER toDate(c)                  

    FILTERS countToNotification(c,dt) 
;

emailCompliance 'Выслать уведомление об окончании срока действия сертификатов' (DATE date) = { EMAIL
    SUBJECT 'Уведомление об окончании срока действия сертификатов на ' + countToNotification(date) + ' шт.'
    TO emailToNotification()
    INLINE emailCompliance OBJECTS dt = date AS DATE
; }
  
  
WHEN CHANGED (currentDate()) AND countToNotification(currentDate()) DO {
    emailCompliance(currentDate());        
}



