MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate;
TABLE compliance(Compliance);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
dateCertificate(certificate) += dateCompliance(certificate);
timeCertificate(certificate) += timeCompliance(certificate);

fromDateCompliance 'Дата с' = DATA DATE (Compliance) IN documentHeader;
fromDateCertificate(certificate) += fromDateCompliance(certificate);

toDateCompliance 'Дата по' = DATA DATE (Compliance) IN documentHeader;
toDateCertificate(certificate) += toDateCompliance(certificate);

// ------ Номер ----------- //

numberCompliance 'Номер' = DATA STRING[100] (Compliance) NOT NULL IN numbered MINCHARWIDTH 10;
seriesCompliance 'Серия' = DATA STRING[2] (Compliance) IN numbered FIXEDCHARWIDTH 3; 

seriesNumberCompliance 'Серия/Номер' (o) = 
    CONCAT '', seriesCompliance(o), numberCompliance(o) 
    MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

seriesCertificate(compliance) += seriesCompliance(compliance);
numberCertificate(compliance) += numberCompliance(compliance);

complianceNumber (number) = GROUP AGGR compliance BY numberCompliance (compliance);

// Описание
descriptionCompliance 'Сертификат соответствия' (compliance) = CONCAT ' ', seriesNumberCompliance(compliance),
                                                 'от ' + fromDateCompliance(compliance), 'по ' + toDateCompliance(compliance);
// История                                                 
@defineDocumentHeaderCreated(Compliance);                                                 

// Текст
textCompliance 'Текст сертификата' = DATA TEXT (Compliance);

@defineBatchCertificate(compliance, Compliance, 'Сертификат соответствия');
fromDateComplianceBatch 'Дата с' (batch) = fromDateCompliance(complianceBatch(batch));
toDateComplianceBatch 'Дата по' (batch) = toDateCompliance(complianceBatch(batch));

legalEntityCompliance = DATA LegalEntity(Compliance);
nameLegalEntityCompliance 'Держатель' (compliance) = nameLegalEntity(legalEntityCompliance(compliance));

// Файл сертификата
imageCompliance 'Файл сертификата'  = DATA IMAGEFILE (Compliance);
saveImageCompliance 'Загрузить сертификат' (compliance) = ACTION LOADFILE imageCompliance(compliance);
openImageCompliance 'Просмотреть сертификат' (compliance) = ACTION OPENFILE imageCompliance(compliance);

CLASS ExtraPageComplianceDetail 'Дополнительная страница сертификата';
TABLE extraPageComplianceDetail (ExtraPageComplianceDetail);

complianceExtraPageComplianceDetail = DATA Compliance (ExtraPageComplianceDetail);

imageExtraPageComplianceDetail 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageComplianceDetail);
saveImageExtraPageComplianceDetail 'Загрузить сертификат' (extraPageComplianceDetail) = ACTION LOADFILE imageExtraPageComplianceDetail(extraPageComplianceDetail);
openImageExtraPageComplianceDetail 'Просмотреть сертификат' (extraPageComplianceDetail) = ACTION OPENFILE imageExtraPageComplianceDetail(extraPageComplianceDetail);

@defineDocumentDetailIndex(compliance, ExtraPageComplianceDetail);

extraPageComplianceDetailIndexCompliance (index, compliance) = GROUP AGGR extraPageComplianceDetail BY indexExtraPageComplianceDetail (extraPageComplianceDetail), complianceExtraPageComplianceDetail(extraPageComplianceDetail) WHERE extraPageComplianceDetail IS ExtraPageComplianceDetail;

@defineDocumentDetailNote(ExtraPageComplianceDetail);

addExtraPageComplianceDetail 'Добавить' = ACTION (compliance){
    FOR ADDOBJ d = ExtraPageComplianceDetail DO {
        complianceExtraPageComplianceDetail(d) <- compliance;
        EXEC saveImageExtraPageComplianceDetail(d);
        EXEC apply();
    }    
} IMAGE 'add.png';

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance FIXED PANEL
    PROPERTIES(c) numberCompliance, seriesCompliance, dateCompliance, timeCompliance,
                  fromDateCompliance, toDateCompliance, nameLegalEntityCompliance, saveImageCompliance, openImageCompliance
                      
    OBJECTS d = ExtraPageComplianceDetail
    PROPERTIES(d) indexExtraPageComplianceDetail, noteExtraPageComplianceDetail
    PROPERTIES(c) addExtraPageComplianceDetail TODRAW d FORCE PANEL TOOLBAR
    PROPERTIES(d) openImageExtraPageComplianceDetail TODRAW d FORCE PANEL TOOLBAR,
                  saveImageExtraPageComplianceDetail TODRAW d FORCE PANEL TOOLBAR,
                  DELETE TODRAW d FORCE PANEL TOOLBAR
    FILTERS complianceExtraPageComplianceDetail(d) == c                   

    EDIT Compliance OBJECT c
;

DESIGN compliance {
    main {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                MOVE PROPERTY (numberCompliance(c));
                MOVE PROPERTY (seriesCompliance(c));
                MOVE PROPERTY (dateCompliance(c));
                MOVE PROPERTY (timeCompliance(c));

            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDateCompliance(c));
                    MOVE PROPERTY (toDateCompliance(c));
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                    MOVE PROPERTY(nameLegalEntityCompliance(c));
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл сертификата';
                MOVE PROPERTY(openImageCompliance(c));
                MOVE PROPERTY(saveImageCompliance(c));
            }
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                MOVE d.box{
                    caption = 'Дополнительные страницы сертификата';
                    PROPERTY(indexExtraPageComplianceDetail(d)){
                        caption = 'Номер файла';
                    }
                }
            }
        }    
        MOVE functions.box;
    }
}

overCompliance = ABSTRACT ACTION LIST (Compliance, Compliance);
overCopyComplianceDetail = ABSTRACT ACTION LIST (ExtraPageComplianceDetail, ExtraPageComplianceDetail);
    
copyCompliance 'Копировать' = ACTION (compliance) NEWSESSION {
    FOR ADDOBJ c = Compliance DO {
        fromDateCompliance(c) <- fromDateCompliance(compliance);
        toDateCompliance(c) <- toDateCompliance(compliance);
        legalEntityCompliance(c) <- legalEntityCompliance(compliance);
        textCompliance(c) <- textCompliance(compliance);
        imageCompliance(c) <- imageCompliance(compliance);
        
        overCompliance(compliance, c);

        FOR complianceExtraPageComplianceDetail(detail) == compliance DO {
            FOR ADDOBJ d=ExtraPageComplianceDetail DO {
                complianceExtraPageComplianceDetail(d) <- c;
                imageExtraPageComplianceDetail(d) <- imageExtraPageComplianceDetail(detail);

                
                overCopyComplianceDetail(d, detail);
            }
        }

        FORM compliance OBJECTS c = c MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

skipShowEditCompliance = ABSTRACT BOOLEAN (Compliance);
showEditCompliance = c IS Compliance AND NOT skipShowEditCompliance(c);

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY numberCompliance, seriesCompliance, dateCompliance, timeCompliance, fromDateCompliance, toDateCompliance,
                           nameLegalEntityCompliance
    PROPERTIES(c) copyCompliance

    PROPERTIES (c) READONLY FORCE PANEL createdNameUserCompliance, createdTimeCompliance, createdHostnameComputerCompliance
    PROPERTIES(c) ADDFORM, EDITFORM SHOWIF showEditCompliance(c), DELETE SHOWIF showEditCompliance(c) FORCE PANEL TOOLBAR
    
    DIALOG Compliance OBJECT c
;

DESIGN compliances {
    main{
        preferredSize = (1024, 768);
        NEW header {
            fill = 1;
            type = SPLITV;
            MOVE c.box { fill = 2; }
            NEW documentDetail {
                fill = 1; 
                NEW documentHistory {
                    caption = 'История';
                    fill = 1; 
                    type = CONTAINERV;
        
                    MOVE c.created;
                }
            }
        }
        MOVE functions.box;
    }
}

NAVIGATOR {
    customsDocuments {
        ADD compliances;
    }
}

editCompliance 'Редактировать' (compliance) = ACTION EDITFORM Compliance;    