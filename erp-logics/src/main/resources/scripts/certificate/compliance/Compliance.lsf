MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate;
TABLE compliance(Compliance);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
date(Compliance certificate) += date(certificate);
time(Compliance certificate) += time(certificate);

fromDate 'Дата с' = DATA DATE (Compliance) IN documentHeader;
fromDate(Compliance certificate) += fromDate(certificate);

toDate 'Дата по' = DATA DATE (Compliance) IN documentHeader;
toDate(Compliance certificate) += toDate(certificate);

// ------ Номер ----------- //

number 'Номер' = DATA STRING[100] (Compliance) NOT NULL IN numbered MINCHARWIDTH 10;
series 'Серия' = DATA STRING[2] (Compliance) IN numbered FIXEDCHARWIDTH 3; 

seriesNumber 'Серия/Номер' (Compliance o) = 
    CONCAT '', series(o), number(o) 
    MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

series(Compliance compliance) += series(compliance);
number(Compliance compliance) += number(compliance);

compliance (number) = GROUP NAGGR Compliance compliance BY number (compliance);

// Описание
description 'Сертификат соответствия' (Compliance compliance) = CONCAT ' ', seriesNumber(compliance),
                                                 'от ' + fromDate(compliance), 'по ' + toDate(compliance) MINCHARWIDTH 20 PREFCHARWIDTH 30;
// История                                                 
@defineDocumentHeaderCreated(Compliance);                                                 

// Текст
text 'Текст сертификата' = DATA TEXT (Compliance);

@defineBatchCertificate(compliance, Compliance, 'Сертификат соответствия');
fromDateCompliance 'Дата с' (Batch batch) = fromDate(compliance(batch));
toDateCompliance 'Дата по' (Batch batch) = toDate(compliance(batch));

legalEntity = DATA LegalEntity(Compliance);
nameLegalEntity 'Держатель' (Compliance compliance) = name(legalEntity(compliance));

// Файл сертификата
image 'Файл сертификата'  = DATA IMAGEFILE (Compliance);
saveImage 'Загрузить сертификат' (Compliance compliance) = ACTION LOADFILE image(compliance);
openImage 'Просмотреть сертификат' (Compliance compliance) = ACTION OPENFILE image(compliance);

CLASS ExtraPageComplianceDetail 'Дополнительная страница сертификата';
TABLE extraPageComplianceDetail (ExtraPageComplianceDetail);

compliance = DATA Compliance (ExtraPageComplianceDetail);

image 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageComplianceDetail);
saveImage 'Загрузить сертификат' (ExtraPageComplianceDetail extraPageComplianceDetail) = ACTION LOADFILE image(extraPageComplianceDetail);
openImage 'Просмотреть сертификат' (ExtraPageComplianceDetail extraPageComplianceDetail) = ACTION OPENFILE image(extraPageComplianceDetail);

@defineDocumentDetailIndex(compliance, ExtraPageComplianceDetail);

extraPageComplianceDetail (index, compliance) = GROUP AGGR ExtraPageComplianceDetail extraPageComplianceDetail BY index (extraPageComplianceDetail), compliance(extraPageComplianceDetail) WHERE extraPageComplianceDetail IS ExtraPageComplianceDetail;

@defineDocumentDetailNote(ExtraPageComplianceDetail);

addExtraPageDetail 'Добавить'(Compliance compliance) = ACTION {
    FOR NEW d = ExtraPageComplianceDetail DO {
        compliance(d) <- compliance;
        EXEC saveImage(d);
        EXEC apply();
    }    
} IMAGE 'add.png';

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance FIXED PANEL
    PROPERTIES(c) number, series, date, time,
                  fromDate, toDate, nameLegalEntity, saveImage, openImage
                      
    OBJECTS d = ExtraPageComplianceDetail
    PROPERTIES(d) index, note
    PROPERTIES(c) addExtraPageDetail TODRAW d TOOLBAR
    PROPERTIES(d) NEWSESSION openImage TODRAW d TOOLBAR,
                  saveImage TODRAW d TOOLBAR,
                  DELETE TODRAW d TOOLBAR
    FILTERS compliance(d) == c                   

    EDIT Compliance OBJECT c
;

DESIGN compliance {
    main {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                MOVE PROPERTY (number(c));
                MOVE PROPERTY (series(c));
                MOVE PROPERTY (date(c));
                MOVE PROPERTY (time(c));

            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDate(c));
                    MOVE PROPERTY (toDate(c));
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                    MOVE PROPERTY(nameLegalEntity(c));
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл сертификата';
                MOVE PROPERTY(openImage(c));
                MOVE PROPERTY(saveImage(c));
            }
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                MOVE d.box{
                    caption = 'Дополнительные страницы сертификата';
                    PROPERTY(index(d)){
                        caption = 'Номер файла';
                    }
                }
            }
        }    
        MOVE functions.box;
    }
}

over = ACTION ABSTRACT LIST (Compliance, Compliance);
overCopy = ACTION ABSTRACT LIST (ExtraPageComplianceDetail, ExtraPageComplianceDetail);
    
copy 'Копировать'(Compliance compliance) = ACTION NEWSESSION {
    FOR NEW c = Compliance DO {
        fromDate(c) <- fromDate(compliance);
        toDate(c) <- toDate(compliance);
        legalEntity(c) <- legalEntity(compliance);
        text(c) <- text(compliance);
        image(c) <- image(compliance);
        
        over(compliance, c);

        FOR compliance(ExtraPageComplianceDetail detail) == compliance DO {
            FOR NEW d=ExtraPageComplianceDetail DO {
                compliance(d) <- c;
                image(d) <- image(detail);

                
                overCopy(d, detail);
            }
        }

        SHOW compliance OBJECTS c = c MANAGESESSION DOCKED;
    }
} TOOLBAR;

skipShowEdit = ABSTRACT BOOLEAN (Compliance);
showEdit = Compliance c IS Compliance AND NOT skipShowEdit(c);

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY number, series, date, time, fromDate, toDate,
                           nameLegalEntity
    PROPERTIES(c) copy

    PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(c) NEWSESSION NEW, EDIT SHOWIF showEdit(c), DELETE SHOWIF showEdit(c) TOOLBAR
    
    LIST Compliance OBJECT c
;

DESIGN compliances {
    preferredSize = (1024, 768);
    NEW header {
        fill = 1;
        type = SPLITV;
        MOVE c.box { fill = 2; }
        NEW documentDetail {
            fill = 1;
            type = TABBED;
            NEW documentHistory {
                caption = 'История';
                fill = 1; 
                type = CONTAINERV;
    
                MOVE c.created;
            }
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    customsDocuments {
        ADD compliances;
    }
}

edit 'Редактировать' (compliance) = ACTION EDITFORM Compliance;    