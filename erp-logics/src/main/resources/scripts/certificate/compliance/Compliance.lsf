MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate, NumeratedObject;
TABLE compliance(Compliance);

@defineExternalizable(compliance, VARSTRING[100]);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
dateCertificate(certificate) += dateCompliance(certificate);
timeCertificate(certificate) += timeCompliance(certificate);

@defineDocumentHeaderTimePrefix(Compliance, from, ' с');
fromDateCertificate(certificate) += fromDateCompliance(certificate);
fromTimeCertificate(certificate) += fromTimeCompliance(certificate);

@defineDocumentHeaderTimePrefix(Compliance, to, ' по');
toDateCertificate(certificate) += toDateCompliance(certificate);
toTimeCertificate(certificate) += toTimeCompliance(certificate);

// ------ Номер ----------- //

@defineNumeratedObjectDefault(Compliance, 'Нумератор для сертификатов', 'СС');

seriesCertificate(compliance) += seriesObject(compliance) IF compliance IS Compliance;
numberCertificate(compliance) += numberObject(compliance) IF compliance IS Compliance;
numberToCompliance (number) = GROUP AGGR compliance BY numberCertificate(compliance) WHERE compliance IS Compliance;

// Описание
descriptionCompliance 'Сертификат соответствия' (compliance) = CONCAT ' ', '№ ' + seriesNumberObject(compliance),
                                                 'от ' + fromDateCompliance(compliance), 'действует по ' + toDateCompliance(compliance);

// Текст
textCompliance 'Текст сертификата' = DATA TEXT (Compliance);

@defineCertificateObjectBatch(compliance, 'Сертификат соответствия');

legalEntityCompliance = DATA LegalEntity(Compliance);
nameLegalEntityCompliance 'Держатель' (compliance) = nameLegalEntity(legalEntityCompliance(compliance));

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance FIXED PANEL
    PROPERTIES(c) objectClassName, nameNumeratorObject, numberObject, seriesObject, dateCompliance, timeCompliance,
                  fromDateCompliance, fromTimeCompliance, toDateCompliance, toTimeCompliance, nameLegalEntityCompliance
    EDIT Compliance OBJECT c
;

DESIGN compliance FROM DEFAULT{
    main {
        NEW pane {
            type = CONTAINERV;
            NEW header {
                caption = 'Шапка документа';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY (objectClassName);
                ADD PROPERTY (nameNumeratorObject);
                ADD PROPERTY (numberObject);
                ADD PROPERTY (seriesObject);
                ADD PROPERTY (dateCompliance);
                ADD PROPERTY (timeCompliance);

            }
            NEW params {
                childConstraints = TO THE RIGHTBOTTOM;
                NEW timeContainer{
                    caption = 'Период действия';
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY (fromDateCompliance);
                    ADD PROPERTY (fromTimeCompliance);
                    ADD PROPERTY (toDateCompliance);
                    ADD PROPERTY (toTimeCompliance);
                }
                NEW documentParams {
                    caption = 'Параметры документа';
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY(nameLegalEntityCompliance);
                }
            }
        }
        ADD functions.box;
    }
}

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY numberObject, seriesObject, dateCompliance, timeCompliance,
                  fromDateCompliance, fromTimeCompliance, toDateCompliance, toTimeCompliance,
                  nameLegalEntityCompliance, objectClassName
    PROPERTIES(c) ADDFORM, EDITFORM, DELETE

    DIALOG Compliance OBJECT c
;

NAVIGATOR {
    customsDocuments {
        ADD compliances;
    }
}