MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate, NumeratedObject;
TABLE compliance(Compliance);

@defineExternalizable(compliance, VARSTRING[100]);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
dateCertificate(certificate) += dateCompliance(certificate);
timeCertificate(certificate) += timeCompliance(certificate);

fromDateCompliance 'Дата с' = DATA DATE (Compliance) IN documentHeaderGroup;
fromDateCertificate(certificate) += fromDateCompliance(certificate);

toDateCompliance 'Дата по' = DATA DATE (Compliance) IN documentHeaderGroup;
toDateCertificate(certificate) += toDateCompliance(certificate);

// ------ Номер ----------- //

@defineDocumentHeaderNumber(Compliance);

seriesCertificate(compliance) += seriesCompliance(compliance);
numberCertificate(compliance) += numberCompliance(compliance);
numberToCompliance (number) = GROUP AGGR compliance BY numberCompliance(compliance) WHERE compliance IS Compliance;

// Описание
descriptionCompliance 'Сертификат соответствия' (compliance) = CONCAT ' ', '№ ' + seriesNumberCompliance(compliance),
                                                 'от ' + fromDateCompliance(compliance), 'действует по ' + toDateCompliance(compliance);

// Текст
textCompliance 'Текст сертификата' = DATA TEXT (Compliance);

@defineCertificateObjectBatch(compliance, 'Сертификат соответствия');

legalEntityCompliance = DATA LegalEntity(Compliance);
nameLegalEntityCompliance 'Держатель' (compliance) = nameLegalEntity(legalEntityCompliance(compliance));

// Файл сертификата
imageCompliance 'Файл сертификата'  = DATA IMAGEFILE (Compliance);
saveImageCompliance 'Загрузить сертификат' (compliance) = ACTION LOADFILE imageCompliance(compliance);
openImageCompliance 'Просмотреть сертификат' (compliance) = ACTION OPENFILE imageCompliance(compliance);

CLASS AdditionComplianceDetail 'Дополнительная страница сертификата';
complianceAdditionComplianceDetail = DATA Compliance (AdditionComplianceDetail);
imageAdditionComplianceDetail 'Файл сертификата'  = DATA IMAGEFILE (AdditionComplianceDetail);
saveImageAdditionComplianceDetail 'Загрузить сертификат' (additionComplianceDetail) = ACTION LOADFILE imageAdditionComplianceDetail(additionComplianceDetail);
openImageAdditionComplianceDetail 'Просмотреть сертификат' (additionComplianceDetail) = ACTION OPENFILE imageAdditionComplianceDetail(additionComplianceDetail);
@defineDocumentDetailIndex(compliance, AdditionComplianceDetail, base);
@defineDocumentDetailNote(AdditionComplianceDetail);

addAdditionComplianceDetail 'Добавить' = ACTION (compliance){
    FOR ADDOBJ d = AdditionComplianceDetail DO {
        complianceAdditionComplianceDetail(d) <- compliance;
        EXEC saveImageAdditionComplianceDetail(d);
        EXEC apply();
    }    
}

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance FIXED PANEL
    PROPERTIES(c) objectClassName, numberCompliance, seriesCompliance, dateCompliance, timeCompliance,
                  fromDateCompliance, toDateCompliance, nameLegalEntityCompliance, saveImageCompliance, openImageCompliance
                      
    OBJECTS d = AdditionComplianceDetail
    PROPERTIES(d) indexAdditionComplianceDetail, noteAdditionComplianceDetail
    PROPERTIES(c) addAdditionComplianceDetail TODRAW d FORCE PANEL TOOLBAR
    PROPERTIES(d) openImageAdditionComplianceDetail TODRAW d FORCE PANEL TOOLBAR,
                  saveImageAdditionComplianceDetail TODRAW d FORCE PANEL TOOLBAR,
                  DELETE TODRAW d FORCE PANEL TOOLBAR
    FILTERS complianceAdditionComplianceDetail(d) == c                   

    EDIT Compliance OBJECT c
;

DESIGN compliance FROM DEFAULT{
    main {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                ADD PROPERTY (objectClassName);
                ADD PROPERTY (numberCompliance);
                ADD PROPERTY (seriesCompliance);
                ADD PROPERTY (dateCompliance);
                ADD PROPERTY (timeCompliance);

            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    ADD PROPERTY (fromDateCompliance);
                    ADD PROPERTY (toDateCompliance);
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                    ADD PROPERTY(nameLegalEntityCompliance);
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл сертификата';
                ADD PROPERTY(openImageCompliance);
                ADD PROPERTY(saveImageCompliance);
            }
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                ADD d.box{
                    caption = 'Дополнительные файлы сертификата';
                    PROPERTY(indexAdditionComplianceDetail){
                        caption = 'Номер файла';
                    }
                }
            }
        }    
        ADD functions.box;
    }
}

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY numberCompliance, seriesCompliance, dateCompliance, timeCompliance, fromDateCompliance, toDateCompliance,
                  nameLegalEntityCompliance, objectClassName
    PROPERTIES(c) ADDFORM, EDITFORM, DELETE

    DIALOG Compliance OBJECT c
;

DESIGN compliances FROM DEFAULT {
    main{
        preferredSize = (640, 480);
    }
}

NAVIGATOR {
    customsDocuments {
        ADD compliances;
    }
}