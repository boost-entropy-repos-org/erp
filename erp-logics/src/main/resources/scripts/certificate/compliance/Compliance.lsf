MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate;
TABLE compliance(Compliance);

@defineExternalizable(compliance, VARSTRING[100]);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
dateCertificate(certificate) += dateCompliance(certificate);
timeCertificate(certificate) += timeCompliance(certificate);

fromDateCompliance 'Дата с' = DATA DATE (Compliance) IN documentHeader;
fromDateCertificate(certificate) += fromDateCompliance(certificate);

toDateCompliance 'Дата по' = DATA DATE (Compliance) IN documentHeader;
toDateCertificate(certificate) += toDateCompliance(certificate);

// ------ Номер ----------- //

numberCompliance 'Номер' = DATA STRING[30] (Compliance) IN numbered MINCHARWIDTH 10;
seriesCompliance 'Серия' = DATA STRING[2] (Compliance) IN numbered FIXEDCHARWIDTH 3; 

seriesNumberCompliance 'Серия/Номер' (o) = 
    CONCAT '', seriesCompliance(o), numberCompliance(o) 
    MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

seriesCertificate(compliance) += seriesCompliance(compliance);
numberCertificate(compliance) += numberCompliance(compliance);

// Описание
descriptionCompliance 'Сертификат соответствия' (compliance) = CONCAT ' ', seriesNumberCompliance(compliance),
                                                 'от ' + fromDateCompliance(compliance), 'по ' + toDateCompliance(compliance);

// Текст
textCompliance 'Текст сертификата' = DATA TEXT (Compliance);

@defineBatchCertificate(compliance, Compliance, 'Сертификат соответствия');

legalEntityCompliance = DATA LegalEntity(Compliance);
nameLegalEntityCompliance 'Держатель' (compliance) = nameLegalEntity(legalEntityCompliance(compliance));

// Файл сертификата
imageCompliance 'Файл сертификата'  = DATA IMAGEFILE (Compliance);
saveImageCompliance 'Загрузить сертификат' (compliance) = ACTION LOADFILE imageCompliance(compliance);
openImageCompliance 'Просмотреть сертификат' (compliance) = ACTION OPENFILE imageCompliance(compliance);

CLASS ExtraPageComplianceDetail 'Дополнительная страница сертификата';
TABLE extraPageComplianceDetail (ExtraPageComplianceDetail);

complianceExtraPageComplianceDetail = DATA Compliance (ExtraPageComplianceDetail);

imageExtraPageComplianceDetail 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageComplianceDetail);
saveImageExtraPageComplianceDetail 'Загрузить сертификат' (extraPageComplianceDetail) = ACTION LOADFILE imageExtraPageComplianceDetail(extraPageComplianceDetail);
openImageExtraPageComplianceDetail 'Просмотреть сертификат' (extraPageComplianceDetail) = ACTION OPENFILE imageExtraPageComplianceDetail(extraPageComplianceDetail);

@defineDocumentDetailIndex(compliance, ExtraPageComplianceDetail, base);

extraPageComplianceDetailIndexCompliance (index, compliance) = GROUP AGGR extraPageComplianceDetail BY indexExtraPageComplianceDetail (extraPageComplianceDetail), complianceExtraPageComplianceDetail(extraPageComplianceDetail) WHERE extraPageComplianceDetail IS ExtraPageComplianceDetail;

@defineDocumentDetailNote(ExtraPageComplianceDetail);

addExtraPageComplianceDetail 'Добавить' = ACTION (compliance){
    FOR ADDOBJ d = ExtraPageComplianceDetail DO {
        complianceExtraPageComplianceDetail(d) <- compliance;
        EXEC saveImageExtraPageComplianceDetail(d);
        EXEC apply();
    }    
} IMAGE 'add.png';

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance FIXED PANEL
    PROPERTIES(c) numberCompliance, seriesCompliance, dateCompliance, timeCompliance,
                  fromDateCompliance, toDateCompliance, nameLegalEntityCompliance, saveImageCompliance, openImageCompliance
                      
    OBJECTS d = ExtraPageComplianceDetail
    PROPERTIES(d) indexExtraPageComplianceDetail, noteExtraPageComplianceDetail
    PROPERTIES(c) addExtraPageComplianceDetail TODRAW d FORCE PANEL TOOLBAR
    PROPERTIES(d) openImageExtraPageComplianceDetail TODRAW d FORCE PANEL TOOLBAR,
                  saveImageExtraPageComplianceDetail TODRAW d FORCE PANEL TOOLBAR,
                  DELETE TODRAW d FORCE PANEL TOOLBAR
    FILTERS complianceExtraPageComplianceDetail(d) == c                   

    EDIT Compliance OBJECT c
;

DESIGN compliance FROM DEFAULT{
    main {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                ADD PROPERTY (numberCompliance);
                ADD PROPERTY (seriesCompliance);
                ADD PROPERTY (dateCompliance);
                ADD PROPERTY (timeCompliance);

            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    ADD PROPERTY (fromDateCompliance);
                    ADD PROPERTY (toDateCompliance);
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                    ADD PROPERTY(nameLegalEntityCompliance);
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл сертификата';
                ADD PROPERTY(openImageCompliance);
                ADD PROPERTY(saveImageCompliance);
            }
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                ADD d.box{
                    caption = 'Дополнительные страницы сертификата';
                    PROPERTY(indexExtraPageComplianceDetail){
                        caption = 'Номер файла';
                    }
                }
            }
        }    
        ADD functions.box;
    }
}

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY numberCompliance, seriesCompliance, dateCompliance, timeCompliance, fromDateCompliance, toDateCompliance,
                           nameLegalEntityCompliance
    PROPERTIES(c) ADDFORM, EDITFORM, DELETE

    DIALOG Compliance OBJECT c
;

DESIGN compliances FROM DEFAULT {
    main{
        preferredSize = (640, 480);
    }
}

NAVIGATOR {
    customsDocuments {
        ADD compliances;
    }
}