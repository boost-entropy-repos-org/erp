MODULE ComplianceSkuLedger;

REQUIRE Compliance;

NAMESPACE Compliance;

currentBalanceCompliance (compliance) = GROUP SUM  currentBalanceBatch(b) BY complianceBatch(b); 

FORM complianceBatch 'Текущие остатки по сертификатам'
    
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY numberCompliance, seriesCompliance, dateCompliance, timeCompliance, fromDateCompliance, toDateCompliance,
                           nameLegalEntityCompliance
    FILTERGROUP filterBalanceCompliance
        FILTER 'Сертификаты с остатком' currentBalanceCompliance(c) 'F11' DEFAULT    
                   
    FILTERGROUP filterCompliance
        FILTER 'Сертификаты с истекшим сроком действия' toDateCompliance(c) < currentDate() 'F7'
        FILTER 'Сертификаты со сроком действия, истекающим через 7 дней' toDateCompliance(c) < sumDate(currentDate(), 7) 'F6'
        FILTER 'Сертификаты со сроком действия, истекающим через 30 дней' toDateCompliance(c) < sumDate(currentDate(), 30) 'F5'
        FILTER 'Сертификаты со сроком действия, истекающим через 90 дней' toDateCompliance(c) < sumDate(currentDate(), 90) 'F4'                           

    OBJECTS b = Batch
    PROPERTIES(b) READONLY nameSkuBatch, idBatch, nameBatch, numberBatch, seriesBatch, dateTimeBatch, shippedQuantityBatch,
                  currentBalanceBatch, expiryDateBatch
    
    FILTERS complianceBatch(b) == c
    
    FILTERGROUP filterBalance
        FILTER 'Партии с остатком' currentBalanceBatch(b) 'F10' DEFAULT
;

NAVIGATOR {
    customsNavigator {
        NEW customsReports 'Отчеты' AFTER customsMasterData {
            ADD complianceBatch;    
        }
    }
}

EXTEND FORM batches
    PROPERTIES(bt) seriesNumberComplianceBatch, fromDateComplianceBatch, toDateComplianceBatch
;