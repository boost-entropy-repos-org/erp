MODULE PurchaseRegistration;

REQUIRE Registration, PurchaseCertificate;

NAMESPACE Purchase;

@defineDocumentCertificate(registration, invoice, i, 'Свидетельство государственной регистрации');
@deriveDocumentPurchaseCertificate(registration, invoice, customerStock, data);

@defineDocumentCertificate(registration, shipment, s, 'Свидетельство государственной регистрации');

// Проставляем автоматически сертификат от строки инвойса
WHEN CHANGED(registration(invoiceDetail(UserShipmentDetail d))) AND 
     NOT CHANGED(registration(d)) DO
     dataRegistration(d) <- registration(invoiceDetail(d)); 

@deriveDocumentPurchaseCertificate(registration, shipment, customerStock, data);

registration(InvoiceShipmentDetail detail) += registration(invoiceDetail(detail));

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) += {
    ASSIGN dataRegistration(s) <- registration(i);
}

registration (ShipmentBatch batch) += registration(shipmentDetail(batch));

//---------------------------------- накладные для сертификата --------------------------------------------------------//

in 'Вкл.' (Registration registration, UserInvoice invoice) = registration(invoice)==registration;

in 'Вкл.' (Registration registration, UserInvoiceDetail invoiceDetail) = dataRegistration(invoiceDetail)==registration;

invoices 'Накладные' (registration) =
    GROUP CONCAT shortDescription(UserInvoice invoice) IF in (Registration registration, invoice), ','
              BY registration ORDER invoice MATERIALIZED MINCHARWIDTH 40 PREFCHARWIDTH 80 MAXCHARWIDTH 120;

// ------------------------- Расширение формы сертификатов данными из инвойсов ----------------------------- //

EXTEND FORM registration
    PROPERTIES(c) READONLY invoices

    OBJECTS id=UserInvoiceDetail
    PROPERTIES(id) READONLY seriesNumber, dateTime, idBarcodeSku, nameSku, quantity, price, homePrice, homeSum

    FILTERS registration(id) == c
;

DESIGN registration {
    documentParams{
        MOVE PROPERTY(invoices(c));
    }
    pane {
        MOVE id.box {
            fill = 1;
        }
    }
}

// ------------------------------------------ Выбор накладных непосредственно в форме сертификата -------------------------------------- //

FORM registrationInvoices 'Накладные сертификации'
    OBJECTS c=Registration PANEL
    PROPERTIES(c) READONLY seriesNumber, dateTime, fromDate, toDate

    OBJECTS i=UserInvoice
    PROPERTIES(c, i)       in
    PROPERTIES(i) READONLY number, series, date, time, nameSupplier, nameCustomer
    FILTERS in(c, i) OR c IS Registration AND NOT registration(i)

    OBJECTS id=UserInvoiceDetail
    PROPERTIES(c, id) in
    PROPERTIES(id) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku, quantity, price
    FILTERS userInvoice(id)==i
;

selectInvoices 'Выбрать накладные'(Registration registration) = {
    SHOW registrationInvoices OBJECTS c=registration ;
} TOOLBAR;

EXTEND FORM registration
    PROPERTIES (c) selectInvoices TODRAW id 
;