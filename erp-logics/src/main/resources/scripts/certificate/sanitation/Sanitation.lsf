MODULE Sanitation;

REQUIRE Certificate, Shipment;

CLASS Sanitation 'ГГР' : Certificate;
TABLE sanitation(Sanitation);

@defineDocumentHeaderTimePrefix(Sanitation, ,' документа');
@deriveDocumentHeaderTimePrefix(Sanitation, );
dateCertificate(sanitation) += dateSanitation(sanitation);
timeCertificate(sanitation) += timeSanitation(sanitation);

@defineDocumentHeaderTimePrefix(Sanitation, from, ' с');
fromDateCertificate(certificate) += fromDateSanitation(certificate);
fromTimeCertificate(certificate) += fromTimeSanitation(certificate);

@defineDocumentHeaderTimePrefix(Sanitation, to, ' по');
//toDateSanitation 'Дата по' = DATA DATE(Sanitation);
//toTimeSanitation 'Время по' = DATA TIME(Sanitation);
//toDateTimeSanitation 'Дата/время по'(sanitation) = dateTimeToDateTime(toDateSanitation(sanitation), toTimeSanitation(sanitation));
toDateCertificate(certificate) += toDateSanitation(certificate);
toTimeCertificate(certificate) += toTimeSanitation(certificate);

@defineDocumentHeaderNumber(Sanitation);
seriesCertificate(sanitation) += seriesSanitation(sanitation);
numberCertificate(sanitation) += numberSanitation(sanitation);

descriptionSanitation 'ГГР' (sanitation) = CONCAT ' ', '№ ' + seriesNumberSanitation(sanitation), 'от ' + fromDateSanitation(sanitation), 'действует по ' + toDateSanitation(sanitation);

@defineCertificateObjectBatch(sanitation, 'ГГР');

FORM sanitation 'ГГР'
    OBJECTS s = Sanitation FIXED PANEL
    PROPERTIES(s) objectClassName, numberSanitation, seriesSanitation, dateSanitation, timeSanitation,
                  fromDateSanitation, fromTimeSanitation, toDateSanitation, toTimeSanitation

    EDIT Sanitation OBJECT s
;

DESIGN sanitation FROM DEFAULT{
    main{
        NEW topContainer{
            type = CONTAINERV;
            NEW headContainer {
                caption = 'Шапка документа';
                type = CONTAINERH;
                ADD PROPERTY (objectClassName);
                ADD PROPERTY (numberSanitation);
                ADD PROPERTY (seriesSanitation);
                ADD PROPERTY (dateSanitation);
                ADD PROPERTY (timeSanitation);

            }
            NEW row {
                NEW timeContainer{
                    caption = 'Период действия';
                    type = CONTAINERH;
                    ADD PROPERTY (fromDateSanitation);
                    ADD PROPERTY (fromTimeSanitation);
                    ADD PROPERTY (toDateSanitation);
                    ADD PROPERTY (toTimeSanitation);
                }
            }
        }
    }
    ADD functions.box;
}

FORM sanitations 'ГГР'
    OBJECTS s = Sanitation
    PROPERTIES(s) READONLY numberSanitation, seriesSanitation, dateSanitation, timeSanitation,
                  fromDateSanitation, fromTimeSanitation, toDateSanitation, toTimeSanitation, objectClassName
    PROPERTIES(s) ADDFORM, EDITFORM, DELETE
;

FORM sanitationsDialog 'ГГР'
    OBJECTS s = Sanitation
    PROPERTIES(s) READONLY numberSanitation, seriesSanitation, dateSanitation, timeSanitation,
                  fromDateSanitation, fromTimeSanitation, toDateSanitation, toTimeSanitation, objectClassName

    DIALOG Sanitation OBJECT s
;

NAVIGATOR {
    customsDocuments {
        ADD sanitations;
    }
}