MODULE PurchaseSanitation;

REQUIRE Sanitation, PurchaseCertificate;

NAMESPACE Purchase;

@defineDocumentCertificate(sanitation, invoice, i, 'Удостоверение гигиенической регистрации');
@deriveDocumentPurchaseCertificate(sanitation, invoice, customerStock, data);

@defineDocumentCertificate(sanitation, shipment, s, 'Удостоверение гигиенической регистрации');

// Проставляем автоматически сертификат от строки инвойса
WHEN CHANGED(sanitation(invoiceDetail(UserShipmentDetail d))) AND 
     NOT CHANGED(sanitation(d)) DO
     dataSanitation(d) <- sanitation(invoiceDetail(d)); 

@deriveDocumentPurchaseCertificate(sanitation, shipment, customerStock, data);

sanitation(InvoiceShipmentDetail detail) += sanitation(invoiceDetail(detail));

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) += ACTION {
    ASSIGN dataSanitation(s) <- sanitation(i);
}

sanitation (ShipmentBatch batch) += sanitation(shipmentDetail(batch));

//---------------------------------- накладные для сертификата --------------------------------------------------------//

in 'Вкл.' (Sanitation sanitation, UserInvoice invoice) = sanitation(invoice)==sanitation;

in 'Вкл.' (Sanitation sanitation, UserInvoiceDetail invoiceDetail) = dataSanitation(invoiceDetail)==sanitation;

invoices 'Накладные' (sanitation) =
    GROUP CONCAT shortDescription(UserInvoice invoice) IF in (Sanitation sanitation, invoice), ','
              BY sanitation ORDER invoice PERSISTENT MINCHARWIDTH 40 PREFCHARWIDTH 80 MAXCHARWIDTH 120;

// ------------------------- Расширение формы сертификатов данными из инвойсов ----------------------------- //

EXTEND FORM sanitation
    PROPERTIES(c) READONLY invoices

    OBJECTS id=UserInvoiceDetail
    PROPERTIES(id) READONLY seriesNumber, dateTime, idBarcodeSku, nameSku, quantity, price, homePrice, homeSum

    FILTERS sanitation(id) == c
;

DESIGN sanitation {
    documentParams{
        MOVE PROPERTY(invoices(c));
    }
    pane {
        MOVE id.box {
            fill = 1;
        }
    }
}

// ------------------------------------------ Выбор накладных непосредственно в форме сертификата -------------------------------------- //

FORM sanitationInvoices 'Накладные сертификации'
    OBJECTS c=Sanitation FIXED PANEL
    PROPERTIES(c) READONLY seriesNumber, dateTime, fromDate, toDate

    OBJECTS i=UserInvoice
    PROPERTIES(c, i)       in
    PROPERTIES(i) READONLY number, series, date, time, nameSupplier, nameCustomer
    FILTERS in(c, i) OR c IS Sanitation AND NOT sanitation(i)

    OBJECTS id=UserInvoiceDetail
    PROPERTIES(c, id) in
    PROPERTIES(id) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku, quantity, price
    FILTERS userInvoice(id)==i
;

selectInvoices 'Выбрать накладные'(Sanitation sanitation) = ACTION {
    SHOW sanitationInvoices OBJECTS c=sanitation ;
} TOOLBAR;

EXTEND FORM sanitation
    PROPERTIES (c) selectInvoices TODRAW id 
;