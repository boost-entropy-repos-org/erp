MODULE PurchaseDeclarationShipment;

REQUIRE PurchaseShipment, PurchaseDeclaration;

NAMESPACE Purchase;

createUserShipmentDeclaration 'Создать поставку по декларации'() = {
	NEWSESSION {
	    DIALOG declarations OBJECTS d INPUT DO {
	        NEW s = UserShipment {
	            isPosted(s) <- TRUE;
	            supplier(s) <- [=GROUP MAX supplier(UserInvoiceDetail d) BY declaration(d)](d);
	            supplierStock(s) <- [=GROUP MAX supplierStock(UserInvoiceDetail d) BY declaration(d)](d);
	            customer(s) <- [=GROUP MAX customer(UserInvoiceDetail d) BY declaration(d)](d);
	            customerStock(s) <- [=GROUP MAX customerStock(UserInvoiceDetail d) BY declaration(d)](d);
	            operation(s) <- [=GROUP MAX operation(UserInvoiceDetail d) BY declaration(d)](d);
	        
	            FOR declaration(UserInvoiceDetail detail) == d NEW sd = UserShipmentDetail DO {
	                userShipment(sd) <- s;
	                invoiceDetail(sd) <- detail;
	                sku(sd) <- sku(detail);
	                quantity(sd) <- quantity(detail);
	                price(sd) <- shipmentPrice(detail);
	            }
	                                     
	            mergeBatch(s);
	        }  
	    }
	    apply();
	}
} TOOLBAR;

EXTEND FORM shipments 
    PROPERTIES  TODRAW s createUserShipmentDeclaration()   
;