MODULE PurchaseDeclarationShipment;

REQUIRE PurchaseShipment, PurchaseDeclaration;

NAMESPACE Purchase;

createUserShipmentDeclaration 'Создать поставку по декларации'() = {
    DIALOG declarations OBJECTS d INPUT DO {
    	NEWSESSION {
	        NEW s = UserShipment {
	            isPosted(s) <- TRUE;
	            supplier(s) <- [=GROUP BY declaration(UserInvoiceDetail d) MAX supplier(d)](d);
	            supplierStock(s) <- [=GROUP BY declaration(UserInvoiceDetail d) MAX supplierStock(d)](d);
	            customer(s) <- [=GROUP BY declaration(UserInvoiceDetail d) MAX customer(d)](d);
	            customerStock(s) <- [=GROUP BY declaration(UserInvoiceDetail d) MAX customerStock(d)](d);
	            operation(s) <- [=GROUP BY declaration(UserInvoiceDetail d) MAX operation(d)](d);
	        
	            FOR declaration(UserInvoiceDetail detail) == d NEW sd = UserShipmentDetail DO {
	                userShipment(sd) <- s;
	                invoiceDetail(sd) <- detail;
	                sku(sd) <- sku(detail);
	                quantity(sd) <- quantity(detail);
	                price(sd) <- shipmentPrice(detail);
	            }
	                                     
	            mergeBatch(s);
	        }  
    	    APPLY;
	    }
	}
} TOOLBAR;

EXTEND FORM shipments 
    PROPERTIES  TODRAW s createUserShipmentDeclaration()   
;