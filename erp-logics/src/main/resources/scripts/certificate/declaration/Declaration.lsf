MODULE Declaration;

REQUIRE Certificate, Shipment, CustomsGroup;

CLASS Declaration 'Таможенная декларация' : Certificate;
TABLE declaration(Declaration);

@defineExternalizable(declaration, VARSTRING[100]);

// Номер
number 'Номер' = DATA STRING[100] (Declaration) IN numbered MINCHARWIDTH 10;
series 'Серия' = DATA STRING[2] (Declaration) IN numbered FIXEDCHARWIDTH 3; 

seriesNumber 'Серия/Номер' (Declaration o) = 
    CONCAT '', series(o), number(o) 
    MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;
    
series(Declaration declaration) += series(declaration);
number(Declaration declaration) += number(declaration);

// Валюта
@defineDocumentHeaderTimePrefix(Declaration, ,' документа');
@deriveDocumentHeaderTimePrefix(Declaration, );
date(Declaration declaration) += date(declaration);
time(Declaration declaration) += time(declaration);

customsHouse = DATA LegalEntity (Declaration);
nameCustomsHouse 'Таможня' (Declaration declaration) = name(customsHouse(declaration));

description 'Таможенная декларация' (Declaration declaration) = CONCAT ' ', seriesNumber(declaration), 'от ' + date(declaration) MINCHARWIDTH 20 PREFCHARWIDTH 30;

@defineBatchCertificate(declaration, Declaration, 'Таможенная декларация');

FORM declaration 'Таможенная декларация'
    OBJECTS d = Declaration FIXED PANEL
    PROPERTIES(d) number, series, date, time, nameCustomsHouse
    EDIT Declaration OBJECT d
;
DESIGN declaration {
    main {
        NEW header FIRST {
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(number(d));
            MOVE PROPERTY(series(d));
            MOVE PROPERTY(date(d));
            MOVE PROPERTY(time(d));
            MOVE PROPERTY(nameCustomsHouse(d));
        }
        MOVE functions.box;
    }
}

FORM declarations 'Таможенные декларации'
    OBJECTS d = Declaration LAST
    PROPERTIES(d) READONLY number, series, date, time, nameCustomsHouse
    PROPERTIES(d) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG Declaration OBJECT d
;

NAVIGATOR {
    customsDocuments {
        ADD declarations;
    }
}