MODULE Declaration;

REQUIRE Certificate, Shipment, CustomsGroup;

CLASS Declaration 'Таможенная декларация' : Certificate;
TABLE declaration(Declaration);

@defineExternalizable(declaration, VARSTRING[100]);

// Номер
numberDeclaration 'Номер' = DATA STRING[100] (Declaration) IN numbered MINCHARWIDTH 10;
seriesDeclaration 'Серия' = DATA STRING[2] (Declaration) IN numbered FIXEDCHARWIDTH 3; 

seriesNumberDeclaration 'Серия/Номер' (o) = 
    CONCAT '', seriesDeclaration(o), numberDeclaration(o) 
    MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;
    
seriesCertificate(declaration) += seriesDeclaration(declaration);
numberCertificate(declaration) += numberDeclaration(declaration);

// Валюта
@defineDocumentHeaderTimePrefix(Declaration, ,' документа');
@deriveDocumentHeaderTimePrefix(Declaration, );
dateCertificate(declaration) += dateDeclaration(declaration);
timeCertificate(declaration) += timeDeclaration(declaration);

customsHouseDeclaration = DATA LegalEntity (Declaration);
nameCustomsHouseDeclaration 'Таможня' (declaration) = nameLegalEntity(customsHouseDeclaration(declaration));

descriptionDeclaration 'Таможенная декларация' (declaration) = CONCAT ' ', seriesNumberDeclaration(declaration), 'от ' + dateDeclaration(declaration);

@defineBatchCertificate(declaration, Declaration, 'Таможенная декларация');

FORM declaration 'Таможенная декларация'
    OBJECTS d = Declaration FIXED PANEL
    PROPERTIES(d) numberDeclaration, seriesDeclaration, dateDeclaration, timeDeclaration, nameCustomsHouseDeclaration
    EDIT Declaration OBJECT d
;
DESIGN declaration {
    main {
        NEW header FIRST {
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(numberDeclaration(d));
            MOVE PROPERTY(seriesDeclaration(d));
            MOVE PROPERTY(dateDeclaration(d));
            MOVE PROPERTY(timeDeclaration(d));
            MOVE PROPERTY(nameCustomsHouseDeclaration(d));
        }
        MOVE functions.box;
    }
}

FORM declarations 'Таможенные декларации'
    OBJECTS d = Declaration
    PROPERTIES(d) READONLY numberDeclaration, seriesDeclaration, dateDeclaration, timeDeclaration, nameCustomsHouseDeclaration
    PROPERTIES(d) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG Declaration OBJECT d
;

NAVIGATOR {
    customsDocuments {
        ADD declarations;
    }
}