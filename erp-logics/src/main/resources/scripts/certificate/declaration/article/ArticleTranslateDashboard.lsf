MODULE ArticleTranslateDashboard;

REQUIRE ImageArticle, Dashboard, CustomsGroupArticle, PurchaseInvoice;

NAMESPACE Item;

// Обработка статуса

completedTranslate 'Обработан' = DATA BOOLEAN (Item);
notCompletedTranslate 'Не обработан' (article) = GROUP SUM 1 IF Item i IS Item AND NOT completedTranslate(i) BY article(i);
completedTranslate 'Обработан' = Article a IS Article AND NOT notCompletedTranslate(a);
prevCompletedTranslate (Article article) = PREV(completedTranslate(article));

changeCompletedTranslate (Article article) = {
    INPUT b = BOOLEAN DO
        completedTranslate(Item item) <- b WHERE article(item) == article;
}

// Не заполнены атрибуты
nullTranslate 'Без атрибутов' (Item item) = item IS Item AND NOT (caption(item) AND composition(item)) AND NOT completedTranslate(item);
nullTranslate 'Без атрибутов' (article) = GROUP SUM 1 IF Item item IS Item AND NOT (caption(item) AND composition(item)) AND NOT completedTranslate(item) BY article(item);

hintEditableBackground = RGB(224, 224, 255) IF TRUE;

TABLE invoiceUser (Purchase.Invoice, User);
inPurchase 'Вкл.' = DATA BOOLEAN  (Purchase.Invoice, User);
inPurchaseInvoices (user) = GROUP SUM 1 IF inPurchase(Purchase.Invoice invoice, User user) BY user;

seriesNumberDate 'Номер/дата накладной' (Purchase.Invoice o) = 
    CONCAT '', seriesNumber(o), 
               ' от ' +date(o) 
    ;  

seriesNumberDateInvoices 'Накладные' =  GROUP CONCAT seriesNumberDate(Purchase.Invoice invoice) IF inPurchase(invoice, User user), ', ' 
    BY user 
    ORDER date(invoice) 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;
seriesNumberDateInvoicesCurrentUser 'Накладные' = seriesNumberDateInvoices(currentUser()) MINCHARWIDTH 30 PREFCHARWIDTH 50;

countInvoices 'Кол-во накладных' (user)= GROUP SUM 1 IF inPurchase(Purchase.Invoice invoice, User user) BY user;

FORM purchaseInvoices 'Выбрать накладные'
    OBJECTS u=User PANEL                           
//    PROPERTIES(u) READONLY seriesNumberDeclaration, nameLegalEntityDeclaration, nameCurrencyDeclaration, dateTimeDeclaration

    OBJECTS i=Purchase.Invoice
    PROPERTIES (i, u) inPurchase
    PROPERTIES (i) READONLY number, series, date, time, 
                   nameSupplier, nameCustomer, countInvoiceDetail,
                   quantityInvoiceDetail, sumInvoiceDetail
    FILTERS isOpened(i)
    ORDER date(i) DESC
//    FILTERS inDeclarationUserInvoice(d, i) OR d IS Declaration AND NOT declarationUserInvoice(i)

    OBJECTS id=Purchase.InvoiceDetail
    PROPERTIES(id) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku, 
                            quantity, price
    FILTERS invoice(id) == i

    FILTERGROUP filtersInvoiceInclude
        FILTER 'Только включенные' inPurchase(i, u) 'F10'

;

DESIGN purchaseInvoices {
    BOX {
        preferredSize = (1024, 768);
        REMOVE BOX(u);
        NEW tabContainer{
            type = TABBED;
            fill = 1;
            NEW invoiceContainer {
                fill = 1;
                type = SPLITV;
                caption = 'Накладные';                
                NEW headerInvoiceContainer {
                    fill = 1;
                    type = SPLITV;                
                    MOVE BOX(i);
                }
                NEW paneInvoiceContainer {
                    type = TABBED;
                    fill = 0.5;
                    MOVE BOX(id);
                }
            }            
        }
        MOVE TOOLBARBOX;
    }
}

selectPurchaseInvoices 'Выбрать накладные'(User user) = {
    SHOW purchaseInvoices OBJECTS u=user ;
} TOOLBAR;
selectPurchaseInvoicesCurrentUser 'Выбрать накладные' = { selectPurchaseInvoices(currentUser()); }

quantityInvoiceDetail 'Кол-во товара в документе' (sku, user) = 
    GROUP SUM quantityInvoiceDetail(Sku sku, Purchase.Invoice invoice) IF inPurchase(invoice, User user)
        BY sku, user;

quantityInvoiceDetail 'Кол-во товара в документе' (article, invoice) = 
    GROUP SUM quantity(Purchase.InvoiceDetail idetail)
          BY  article(sku(idetail)),
              invoice(idetail);
quantityInvoiceDetail 'Кол-во товара в документе' (article, user) = 
    GROUP SUM quantityInvoiceDetail(Article article, Purchase.Invoice invoice) IF inPurchase(invoice, User user)
        BY article, user;
        
defaultPurchaseInvoice (sku) =
    GROUP LAST invoice(Purchase.InvoiceDetail detail)
          BY sku(detail)
          ORDER date(detail), detail
          WHERE isPosted(detail) AND quantity(detail) 
                AND (inPurchase(invoice(detail), currentUser()) OR NOT inPurchaseInvoices(currentUser()))
                AND NOT isCompanySupplier(detail) COMPLEX;

descriptionDefaultPurchaseInvoice 'Последний инвойс' (Sku sku)= CONCAT ' ', seriesNumber(defaultPurchaseInvoice(sku)),
                                                                          'от ' + date(defaultPurchaseInvoice(sku)) MINCHARWIDTH 10 PREFCHARWIDTH 15 MAXCHARWIDTH 30;
// Подсчет непереведенных артикулов для групп
countNotCompletedTranslateArticles 'Не обработано (к-во)' (group) = 
    GROUP SUM 1 IF notCompletedTranslate(Article article)  AND ((inPurchaseInvoices(currentUser()) AND quantityInvoiceDetail(article, currentUser())) OR NOT inPurchaseInvoices(currentUser())) BY itemGroup(article);
recCountNotCompletedTranslateArticles 'Не обработано (к-во)' (parent) = GROUP SUM countNotCompletedTranslateArticles(ItemGroup child) IF isParent(child, ItemGroup parent) BY parent;
        
dictionaryComposition = DATA Dictionary ();   
nameDictionaryComposition 'Словарь для состава' = name(dictionaryComposition());

dictionaryCaption = DATA Dictionary ();   
nameDictionaryCaption 'Словарь для наименования' = name(dictionaryCaption());   
                 
FORM articleTranslateDashboard 'Перевод'

    PROPERTIES seriesNumberDateInvoicesCurrentUser() READONLY, selectPurchaseInvoicesCurrentUser(), nameDictionaryCaption(),
               nameDictionaryComposition()

    TREE groupTree g=ItemGroup PARENT parent
    PROPERTIES READONLY order(g), name(g), recCountNotCompletedTranslateArticles(g)
    ORDER order(g), name(g)
    FILTERGROUP inactive FILTER 'Активные' active(g) 'F5' DEFAULT
    FILTERGROUP notTranslateFilters
        FILTER 'Не обработан' recCountNotCompletedTranslateArticles(g) 'F6'

    OBJECTS a=Article
    PROPERTIES(a) READONLY nameBrand, id, canonicalNameItemGroupPanel=canonicalNameItemGroup PANEL
    PROPERTIES(a) originalCaption, originalComposition, originalCustomsGroup 
    PROPERTIES(a) BACKGROUND hintEditableBackground() nameUOM, caption, nameGender, composition,
                                                      netWeight, grossWeight, nameCountry
    PROPERTIES(a) completedTranslate ON CHANGE changeCompletedTranslate(a)
    PROPERTIES(a) NEWSESSION EDIT
    PROPERTIES(a) PANEL searchImage, urlImage
    PROPERTIES uploadAllImagesArticle() TODRAW a         

    FILTERGROUP attributesFilters
        FILTER 'Без атрибутов' nullTranslate(a) 'F7' 
        FILTER 'С атрибутами' NOT nullTranslate(a) 'F6' 

    FILTERS isParent(g, a) 
//    FILTERGROUP groupFilters
//        FILTER 'По группам' 'F11' isParentGroupArticle(g, a) DEFAULT
//        FILTER 'Без групп' 'ctrl F11' captionArticle(a) IF NOT itemGroupArticle(a)

    FILTERGROUP translateFilters
        FILTER 'Не обработан' notCompletedTranslate(a) 'F9' DEFAULT
        FILTER 'Обработан' completedTranslate(a) 'F8'

    FILTERS quantityInvoiceDetail(a, currentUser()) OR (a IS Article AND NOT countInvoices(currentUser()))

    OBJECTS i=Item
    PROPERTIES(i) READONLY descriptionDefaultPurchaseInvoice, idBarcode, id, originalCaptionGrid = originalCaption, 
                           originalCustomsGroup, nameColor, nameSize
    PROPERTIES(i) BACKGROUND hintEditableBackground() originalSize, nameUOM, caption, canonicalNameItemGroup, 
                             composition, netWeight, grossWeight, nameCountry,
                             image READONLY PANEL
    PROPERTIES(a)            loadImage 
    PROPERTIES(i) completedTranslate
    ORDER id(i)
    PROPERTIES(i) NEWSESSION EDIT SHOWIF enableEditing(i)
    
    FILTERGROUP attributesFilters2
        FILTER 'Без атрибутов' nullTranslate(i) 'F7'
        FILTER 'С атрибутами' NOT nullTranslate(i) 'F6'
             
    FILTERGROUP itemFilters
        FILTER 'По артикулам' article(i) == a 'F4' DEFAULT
        FILTER 'По группам (не обработан)' isParent(g,i) AND NOT completedTranslate(i) 'shift F4'
        FILTER 'По группам' isParent(g,i) 'ctrl F4'
        FILTER 'По артикулам/инвойсам' article(i) == a AND (quantityInvoiceDetail(i, currentUser()) OR (i IS Item AND NOT countInvoices(currentUser()))) 'shift F5'
;
                                                                                                         
DESIGN articleTranslateDashboard {
    NEW top{
        type = CONTAINERH;
        NEW filter {
            caption = 'Фильтр';
            type = CONTAINERH;
            MOVE PROPERTY(seriesNumberDateInvoicesCurrentUser());
            MOVE PROPERTY(selectPurchaseInvoicesCurrentUser());
        }
        NEW dictionary {
            caption = 'Словари';
            type = CONTAINERH;
            MOVE PROPERTY(nameDictionaryCaption());
            MOVE PROPERTY(nameDictionaryComposition());        
        }
    }
    NEW pane {
        type = SPLITH;
        fill = 1;
        NEW tree {
            type = SPLITV;
            fill = 1;
            MOVE BOX(TREE groupTree) {fill = 2;}
            NEW imageBox {
                fill = 3;
                caption = 'Изображение';                     
                MOVE PROPERTY(image(i)) {                           
                    caption = '';
                    fill = 1;
                }
                MOVE PROPERTY(loadImage(a));
            }
        }
        NEW row11 {
            fill = 3;
            type = SPLITV;
            MOVE BOX(a) {
                defaultComponent = TRUE;
                fill = 2;
                NEW imageContainer{
                    type = CONTAINERH;
                    MOVE PROPERTY(searchImage(a));
                    MOVE PROPERTY(urlImage(a));
                    MOVE PROPERTY(uploadAllImagesArticle());
                }
            }
            MOVE BOX(i);
        }
    }

    PROPERTY(id(a)) { minimumCharWidth = 6; preferredCharWidth = 10;}
    PROPERTY(id(i)) { minimumCharWidth = 6; preferredCharWidth = 10;}
    PROPERTY(composition(i)) { minimumCharWidth = 30; preferredCharWidth = 40;}
    PROPERTY(originalCustomsGroup(i)) { minimumCharWidth = 11; preferredCharWidth = 15;}
    PROPERTY(caption(i)) { minimumCharWidth = 20; preferredCharWidth = 40;}
    PROPERTY(composition(i)) { minimumCharWidth = 30; preferredCharWidth = 40;}

    MOVE TOOLBARBOX;
}
      
WHEN LOCAL FORMS articleTranslateDashboard CHANGED(caption(article(Item i))) AND NOT CHANGED(caption(i)) DO         
    caption(i) <- caption(article(i));           
           
completeArticleTranslateDashboard 'Подтвердить все'() = {   
    LOCAL k = INTEGER ();
    k() <- 0;
    
    FOR [= FILTER articleTranslateDashboard.a](Article article) AND nullTranslate(article) DO{   
        k() <- k() + 1;              
    }    
    IF k() > 0 THEN {
        MESSAGE 'Для артикула не задано название или состав ('+k()+' шт.)';
    } ELSE {
        FOR [= FILTER articleTranslateDashboard.a](Article article) DO {
            completedTranslate(Item item) <- TRUE WHERE article(item) == article;
        }
    }      
} TOOLBAR CONFIRM;

EXTEND FORM  articleTranslateDashboard
    PROPERTIES() TODRAW a completeArticleTranslateDashboard
;

// ------------------------- Автоматическое заполнение свойств --------------------------- //
countArticleCaption (string, original) = GROUP SUM 1 BY caption(Article article), originalCaption(article);
defaultCaptionArticleOriginalCaption (composition) = GROUP LAST VARISTRING[255] caption 
                                                           BY VARISTRING[100] original
                                                           ORDER countArticleCaption(caption, original), caption
                                                           WHERE countArticleCaption(caption, original);

fillDefaultCaption 'Перевести название'(Article article) = {
    caption(article) <- defaultCaptionArticleOriginalCaption(originalCaption(article))
        WHERE defaultCaptionArticleOriginalCaption(originalCaption(article));
} ASON CONTEXTMENU caption[Article];

fillDefaultCaptionArticles 'Перевести для всех название'() = {
    FOR [= FILTER articleTranslateDashboard.a](Article article) DO {
        fillDefaultCaption(article);
    }
} ASON CONTEXTMENU caption[Article];

  
translateCaption 'Перевести название по словарю'(Article article) = {
    translate(dictionaryCaption(), originalCaption(article));
    caption(article) <- VARISTRING[100](translationResult());
} ASON CONTEXTMENU caption[Article];  

translateCaptionArticles 'Перевести для всех название по словарю'() = {
    FOR [= FILTER articleTranslateDashboard.a](Article article) DO {
        translateCaption(article);
    }
} ASON CONTEXTMENU caption[Article];
//--
translateComposition 'Перевести состав по словарю'(Article article) = {
    translate(dictionaryComposition(), originalComposition(article));
    composition(article) <- VARSTRING[255](translationResult());
} ASON CONTEXTMENU composition[Article];  

translateCompositionArticles 'Перевести для всех состав по словарю'() = {
    FOR [= FILTER articleTranslateDashboard.a](Article article) DO {
        translateComposition(article);
    }
} ASON CONTEXTMENU composition[Article];

//--
countArticleComposition (string, original) = GROUP SUM 1 BY composition(Article article), originalComposition(article);
defaultCompositionArticleOriginal (composition) = GROUP LAST VARSTRING[255] composition 
                                                                   BY VARSTRING[255] original
                                                                   ORDER countArticleComposition(composition, original), composition
                                                                   WHERE countArticleComposition(composition, original);

fillDefaultComposition 'Перевести состав'(Article article) = {
    composition(article) <- defaultCompositionArticleOriginal(originalComposition(article))
        WHERE defaultCompositionArticleOriginal(originalComposition(article));
} ASON CONTEXTMENU composition[Article];

fillDefaultCompositionArticles 'Перевести для всех состав'() = {
   FOR [= FILTER articleTranslateDashboard.a](Article article) DO {
        fillDefaultComposition(article);
   }
} ASON CONTEXTMENU composition[Article];

NAVIGATOR {
    customsDashboardNavigator {
        ADD articleTranslateDashboard;
    }
}