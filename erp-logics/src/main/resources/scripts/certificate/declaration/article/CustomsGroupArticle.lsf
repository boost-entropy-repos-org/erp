MODULE CustomsGroupArticle;

REQUIRE CustomsGroupItem, ItemArticle;

NAMESPACE CustomsGroup;

//-- ТН ВЭД
TABLE countryArticle(Country, Article);
customsGroupCountryArticle = DATA CustomsGroup(Country, Article);
codeCustomsGroupCountryArticle 'ТН ВЭД' (country, article) = codeCustomsGroup(customsGroupCountryArticle(country, article)) MINCHARWIDTH 10 PREFCHARWIDTH 15;
nameCustomsGroupCountryArticle 'Позиция ТН ВЭД' (country, article) = nameCustomsGroup(customsGroupCountryArticle(country, article)) MINCHARWIDTH 30 PREFCHARWIDTH 40;
codeCustomsGroupDefaultCountryArticle 'ТН ВЭД' (article) = codeCustomsGroup(customsGroupCountryArticle(defaultCountry(), article)) MINCHARWIDTH 10 PREFCHARWIDTH 15;

originalCustomsGroupArticle 'Код ТНВЭД (ориг.)' = DATA STRING[10] (Article) IN base FIXEDCHARWIDTH 10;

// Действие по изменению кода ТНВЭД через ручной ввод
changeCodeCustomsGroupCountryArticle = ACTION (country, article) {
    IF requestInputChangeCustomsCode() THEN {
        REQUEST STRING[10] INPUT;
        IF requestedString() THEN {
            IF customsGroupCode(requestedString()) AND charLength(requestedString()) == 10 THEN {
                customsGroupCountryArticle(country, article) <- customsGroupCode(requestedString()); 
            } ELSE {
                REQUEST OBJECT cg
                    FORM customsGroups OBJECTS cz = customsZoneCountry(country), cg = nearestCustomsGroupCode(requestedString()) MODAL;
                IF formResult() == FormResult.ok THEN {
                    customsGroupCountryArticle(country, article) <- chosenObject('cg');
                }
            }
        } ELSE
            customsGroupCountryArticle(country, article) <- NULL;
    } ELSE {
        REQUEST OBJECT cg
            FORM customsGroups OBJECTS cz = customsZoneCountry(country), cg = customsGroupCountryArticle(country, article) CONTEXTFILTER cg = customsGroupCountryArticle(country, article) DIALOG SHOWDROP;
        IF formResult() == FormResult.ok THEN {
            customsGroupCountryArticle(country, article) <- requestedObject();
        } ELSE IF formResult() == FormResult.drop THEN {
            customsGroupCountryArticle(country, article) <- NULL;
        }
    }
}

changeCustomsGroupDefaultCountryArticle = ACTION (article) {
    REQUEST OBJECT cg
    FORM customsGroups OBJECTS cz = customsZoneDefaultCountry() DIALOG SHOWDROP;
    IF formResult() == FormResult.ok THEN {
        customsGroupCountryArticle(country, article) <- requestedObject() WHERE country == defaultCountry();

    } ELSE IF formResult() == FormResult.drop THEN {
        customsGroupCountryArticle(country, article) <- NULL WHERE country == defaultCountry();
    }
}

CONSTRAINT customsZoneCustomsGroup(customsGroupCountryArticle(country, article)) != customsZoneCountry(country) CHECKED BY customsGroupCountryArticle
    MESSAGE 'Таможенная зона должна соответствовать стране';

EXTEND FORM article
    PROPERTIES(a)    originalCustomsGroupArticle,
                     codeCustomsGroupDefaultCountryArticle  ON CHANGE changeCustomsGroupDefaultCountryArticle(a)
    PROPERTIES(c, a) codeCustomsGroupCountryArticle
    
    PROPERTIES(i) BEFORE deletei codeCustomsGroupDefaultCountryItem ON CHANGE changeCustomsGroupDefaultCountryItem(i) 
;
EXTEND DESIGN article  {
    regionPrm {
        ADD PROPERTY(originalCustomsGroupArticle(a));
        ADD PROPERTY(codeCustomsGroupDefaultCountryArticle(a));
    }
}

EXTEND FORM articles
    PROPERTIES(a) originalCustomsGroupArticle
;

WHEN SESSION FORMS article CHANGED(customsGroupCountryArticle(country, article)) DO         
    customsGroupCountryItem(country, i) <- customsGroupCountryArticle(country, article) WHERE articleItem(i) == article;      

WHEN SESSION FORMS article SET(i IS Item) DO
    customsGroupCountryItem(country, i) <- customsGroupCountryArticle(country, articleItem(i));      
    

EXTEND FORM articles
    PROPERTIES(a)  READONLYIF isReadonly() BEFORE deletea  codeCustomsGroupDefaultCountryArticle
;
