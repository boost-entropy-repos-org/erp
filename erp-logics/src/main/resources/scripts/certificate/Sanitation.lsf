MODULE Sanitation;

REQUIRE Certificate, Shipment;

CLASS Sanitation 'ГГР' : Certificate, NumberedObject;
TABLE sanitation(Sanitation);

@defineDocumentHeaderTimePrefix(Sanitation, ,' документа');
@deriveDocumentHeaderTimePrefix(Sanitation, );
dateCertificate(sanitation) += dateSanitation(sanitation);
timeCertificate(sanitation) += timeSanitation(sanitation);

@defineDocumentHeaderTimePrefix(Sanitation, from, ' с');
fromDateCertificate(certificate) += fromDateSanitation(certificate);
fromTimeCertificate(certificate) += fromTimeSanitation(certificate);

@defineDocumentHeaderTimePrefix(Sanitation, to, ' по');
//toDateSanitation 'Дата по' = DATA DATE(Sanitation);
//toTimeSanitation 'Время по' = DATA TIME(Sanitation);
//toDateTimeSanitation 'Дата/время по'(sanitation) = dateTimeToDateTime(toDateSanitation(sanitation), toTimeSanitation(sanitation));
toDateCertificate(certificate) += toDateSanitation(certificate);
toTimeCertificate(certificate) += toTimeSanitation(certificate);

seriesCertificate(sanitation) += seriesObject(sanitation) IF sanitation IS Sanitation;
numberCertificate(sanitation) += numberObject(sanitation) IF sanitation IS Sanitation;

descriptionSanitation 'ГГР' (sanitation) = CONCAT ' ', '№ ' + seriesNumberObject(sanitation), 'от ' + fromDateSanitation(sanitation), 'действует по ' + toDateSanitation(sanitation);

@defineCertificateObjectBatch(sanitation, 'ГГР');

FORM sanitation 'ГГР'
    OBJECTS s = Sanitation FIXED PANEL
    PROPERTIES(s) objectClassName, numberObject, seriesObject, dateSanitation, timeSanitation,
                  fromDateSanitation, fromTimeSanitation, toDateSanitation, toTimeSanitation

    EDIT Sanitation OBJECT s
;

DESIGN sanitation FROM DEFAULT{
    main{
        NEW topContainer{
            childConstraints = TO THE BOTTOM;
            NEW headContainer {
                caption = 'Шапка документа';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY (objectClassName);
                ADD PROPERTY (numberObject);
                ADD PROPERTY (seriesObject);
                ADD PROPERTY (dateSanitation);
                ADD PROPERTY (timeSanitation);

            }
            NEW row {
                NEW timeContainer{
                    caption = 'Период действия';
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (fromDateSanitation);
                    ADD PROPERTY (fromTimeSanitation);
                    ADD PROPERTY (toDateSanitation);
                    ADD PROPERTY (toTimeSanitation);
                }
            }
        }
    }
    ADD functions.box;
}

FORM sanitations 'ГГР'
    OBJECTS s = Sanitation
    PROPERTIES(s) READONLY numberObject, seriesObject, dateSanitation, timeSanitation,
                  fromDateSanitation, fromTimeSanitation, toDateSanitation, toTimeSanitation, objectClassName
    PROPERTIES(s) ADDFORM, EDITFORM, DELETE
;

FORM sanitationsDialog 'ГГР'
    OBJECTS s = Sanitation
    PROPERTIES(s) READONLY numberObject, seriesObject, dateSanitation, timeSanitation,
                  fromDateSanitation, fromTimeSanitation, toDateSanitation, toTimeSanitation, objectClassName

    DIALOG Sanitation OBJECT s
;

NAVIGATOR {
    customsDocuments {
        ADD sanitations;
    }
}