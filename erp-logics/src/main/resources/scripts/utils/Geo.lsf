MODULE Geo;

REQUIRE Country;

CLASS ABSTRACT POI 'POI';
TABLE POI(POI);

autoSynchronizeCoordinates 'Синхронизировать координаты' = DATA BOOLEAN ();

CLASS MapProvider 'Использовать карты' {
    google 'Google',
    yandex 'Yandex'
}
FORM mapProviders
    OBJECTS m = MapProvider
    PROPERTIES(m) staticCaption
    DIALOG MapProvider OBJECT m
;

userMapProvider = DATA MapProvider ();
mapProvider = OVERRIDE MapProvider.yandex, userMapProvider();

nameUserMapProvider 'Использовать карты' () = staticCaption(userMapProvider());

namePOI 'Название'= ABSTRACT STRING[200] (POI) PERSISTENT;

countryPOI = ABSTRACT Country (POI) PERSISTENT;
nameCountryPOI 'Страна' (poi) = nameCountry(countryPOI(poi));

addressPOI 'Адрес' = ABSTRACT STRING[100] (POI) PERSISTENT;

latitudePOI 'Координата X' = ABSTRACT NUMERIC[10,5] (POI);
longitudePOI 'Координата Y' = ABSTRACT NUMERIC[10,5] (POI);

// Получение координат
readLatitude 'Координата X' = DATA SESSION NUMERIC[10,5] ();
readLongitude 'Координата Y' = DATA SESSION NUMERIC[10,5] ();

getCoordinatesAddressMapProvider = ACTION CUSTOM 'fdk.utils.geo.GetCoordinatesAddressActionProperty';
getCoordinatesAddress = getCoordinatesAddressMapProvider(address, mapProvider());

changeCoordinatesPOI 'Изменить координаты'= ACTION (POI) {

    EXEC getCoordinatesAddress (CONCAT ',', nameCountryPOI(POI), addressPOI(POI));
    SET latitudePOI(POI) <- readLatitude();
    SET longitudePOI(POI) <- readLongitude();

}
WHEN SESSION (SETCHANGED(countryPOI(POI)) OR SETCHANGED(addressPOI(POI))) AND autoSynchronizeCoordinates() DO EXEC changeCoordinatesPOI(POI);

// Показать на карте одну точку
showOnMapMapProvider 'Показать на карте'= ACTION CUSTOM 'fdk.utils.geo.ShowOnMapActionProperty';
showOnMap = showOnMapMapProvider (latitude, longitude, mapProvider(), address);

showOnMapPOI 'Показать на карте'= ACTION (POI) {
    EXEC showOnMap (latitudePOI (POI), longitudePOI (POI), addressPOI(POI));
}

// Показать на карте путь
numberPathPOI 'Номер' = DATA SESSION INTEGER (POI);
descriptionPathPOI 'Описание' = DATA SESSION STRING[200] (POI);

showOnMapPath 'Показать точки на карте'= ACTION CUSTOM 'fdk.utils.geo.ShowOnMapPathActionProperty';

inPathPOI 'Вкл' = DATA SESSION BOOLEAN (POI);
startPathPOI 'Место старта'= DATA SESSION POI();
calculatePath 'Рассчитать путь'= ACTION CUSTOM 'fdk.utils.geo.CalculatePathActionProperty';

EXTEND FORM options
PROPERTIES() autoSynchronizeCoordinates, nameUserMapProvider
;

EXTEND DESIGN options {
    commons {
        ADD PROPERTY(autoSynchronizeCoordinates);
        ADD PROPERTY(nameUserMapProvider);
    }
}

