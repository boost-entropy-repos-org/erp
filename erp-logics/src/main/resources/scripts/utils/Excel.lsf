MODULE Excel;

REQUIRE System, MasterData;

CLASS Template 'Шаблон';
TABLE template(Template);

nameTemplate 'Имя' = DATA STRING[100] (Template);
fileTemplate 'Файл шаблона' (Template) = DATA EXCELFILE (Template);
loadFileTemplate 'Загрузить шаблон'(t) = ACTION LOADFILE fileTemplate(t);
openFileTemplate 'Открыть шаблон'(t) = ACTION OPENFILE fileTemplate(t);
idTemplate 'Идентификатор' = DATA STRING[20] (Template) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

processTemplate = ACTION CUSTOM 'fdk.utils.excel.ProcessTemplateActionProperty';

resultTemplate = DATA SESSION EXCELFILE ();
openResultTemplate () = ACTION OPENFILE resultTemplate();

openProcessTemplate 'Сгенерировать документ' = ACTION (template) {
    processTemplate(template);
    openResultTemplate();
}

CLASS TemplateEntry 'Строка шаблона';
TABLE templateEntry(TemplateEntry);

templateTemplateEntry 'Шаблон' = DATA Template (TemplateEntry);
keyTemplateEntry 'Параметр' = DATA STRING[100](TemplateEntry);
valueTemplateEntry 'Значение' = DATA SESSION TEXT (TemplateEntry);

FORM template 'Шаблон Excel'

    OBJECTS t = Template FIXED PANEL
    PROPERTIES (t) nameTemplate, idTemplate
    PROPERTIES (t) loadFileTemplate, openFileTemplate

    OBJECTS te = TemplateEntry
    PROPERTIES (te) keyTemplateEntry, valueTemplateEntry, ADDOBJ, DELETESESSION
    PROPERTIES (t) TODRAW te FORCE PANEL TOOLBAR openProcessTemplate

    FILTERS templateTemplateEntry (te) == t

    EDIT Template OBJECT t
;

DESIGN template FROM DEFAULT{
    main{
        preferredSize = (1024, 768);
        childConstraints = TO THE BOTTOM;
        ADD t.box {
            childConstraints = TO THE RIGHT;
            NEW row1 {
                ADD PROPERTY(nameTemplate);
                ADD PROPERTY(idTemplate);
            };
            NEW row2 {
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(loadFileTemplate);
                ADD PROPERTY(openFileTemplate);
            };
        }
        ADD te.box;
    }
    ADD functions.box;
}

FORM templates 'Шаблоны Excel'

    OBJECTS t = Template
    PROPERTIES (t) READONLY nameTemplate, idTemplate
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    OBJECTS te = TemplateEntry
    PROPERTIES (te) READONLY keyTemplateEntry, valueTemplateEntry

    FILTERS templateTemplateEntry (te) == t
;

FORM dialogTemplates 'Шаблоны Excel'

    OBJECTS t = Template
    PROPERTIES (t) READONLY nameTemplate, idTemplate
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    DIALOG Template OBJECT t
;
DESIGN dialogTemplates FROM DEFAULT{
    main {
        preferredSize = (1024, 768);
    }
}

NAVIGATOR {
    masterData {
        templatesNavigator {
            ADD templates;
        }
    }
}



