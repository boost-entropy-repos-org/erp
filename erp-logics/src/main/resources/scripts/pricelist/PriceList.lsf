MODULE PriceList;

REQUIRE PriceListLedger, StockTax;

NAMESPACE PriceList;

// --------------------------- Пользовательские виды цен ------------------------- //

CLASS DataPriceListType 'Пользовательский вид цены' : LedgerPriceListType;

@defineExternalizable(dataPriceListType, VARSTRING[100]);
idPriceListType (p) += idDataPriceListType(p);

batchLedgerDataPriceListType 'Использовать для партий свои цены' = DATA BOOLEAN (DataPriceListType);
batchLedgerPriceListTypeStock (priceListType, stock) += WHEN priceListType IS DataPriceListType AND isCompanyStock(stock)
                                                        THEN batchLedgerDataPriceListType(priceListType);

nameDataPriceListType 'Наименование' = DATA VARISTRING[50](DataPriceListType);
nameLedgerPriceListType(type) += nameDataPriceListType(type) IF type IS DataPriceListType;

includeVATDataPriceListType 'Цена с НДС' = DATA BOOLEAN (DataPriceListType);
includeVATLedgerPriceListType (priceListType) += includeVATDataPriceListType(priceListType);

@defineDocumentHeaderCurrency(dataPriceListType);
currencyPriceListType(dataPriceListType) += currencyDataPriceListType(dataPriceListType);

FORM dataPriceListType 'Пользовательский вид цен'
    OBJECTS d = DataPriceListType FIXED PANEL
    PROPERTIES(d) nameDataPriceListType, idDataPriceListType SHOWIF showIDs(), includeVATPriceListType,
                  nameCurrencyDataPriceListType, nameRoundConditionPriceListType, batchLedgerDataPriceListType

    EDIT DataPriceListType OBJECT d
;

DESIGN dataPriceListType FROM DEFAULT {
    d.box{
        ADD PROPERTY(nameDataPriceListType);
        ADD PROPERTY(idDataPriceListType);
        ADD PROPERTY(includeVATPriceListType);
        ADD PROPERTY(nameCurrencyDataPriceListType);
        ADD PROPERTY(nameRoundConditionPriceListType);
        ADD PROPERTY(batchLedgerDataPriceListType);
    }
}

FORM dataPriceListTypes 'Пользовательские виды цен'
    OBJECTS p = DataPriceListType
    PROPERTIES(p) READONLY nameDataPriceListType, nameCurrencyDataPriceListType, nameRoundConditionPriceListType
    PROPERTIES(p) READONLY includeVATDataPriceListType

    DIALOG DataPriceListType OBJECT p
;

addDataPriceListType 'Добавить пользовательскую цену' = ACTION ADDFORM DataPriceListType;
editDataPriceListType 'Редактировать' = ACTION EDITFORM DataPriceListType;
editPriceListType(priceListType) += editDataPriceListType(priceListType);

EXTEND FORM priceListTypes
    PROPERTIES() TODRAW pt FORCE PANEL addDataPriceListType
//    PROPERTIES(pt) SHOWIF showIDs() READONLY idDataPriceListType
;

// --------------------------- Прайс-листы ------------------------- //

CLASS ABSTRACT PriceList 'Прайс' : Document;
CLASS ABSTRACT PriceListDetail 'Строка прайса' : DocumentDetail;

CLASS UserPriceList 'Прайс (польз.)' : PriceList;
CLASS UserPriceListDetail 'Строка прайса (польз.)' : PriceListDetail;

@defineDocumentInterface(priceList);
@deriveDocumentHeaderTimePrefix(UserPriceList, );

@defineExternalizable(userPriceList, VARSTRING[100]);

// Шапка

@defineDocumentInterfaceDataTimePrefix(priceList, from, ' c');
@deriveDocumentHeaderTimePrefix(UserPriceList, from);

toDatePriceList 'Дата по' = ABSTRACT DATE (PriceList) IN documentHeaderGroup PERSISTENT;
toTimePriceList 'Время по' = ABSTRACT TIME (PriceList) IN documentHeaderGroup PERSISTENT;
toDateTimePriceList 'Дата/время по'(priceList) = dateTimeToDateTime(toDatePriceList(priceList), toTimePriceList(priceList)) PERSISTENT;

toDateUserPriceList 'Дата по' = DATA DATE(UserPriceList);
toTimeUserPriceList 'Время по' = DATA TIME(UserPriceList);
toDateTimeUserPriceList 'Дата/время'(userPriceList) = dateTimeToDateTime(toDateUserPriceList(userPriceList), toTimeUserPriceList(userPriceList));

toDatePriceList(userPriceList) += toDateUserPriceList(userPriceList);
toTimePriceList(userPriceList) += toTimeUserPriceList(userPriceList);

@defineDocumentInterfaceNumber(priceList);
@defineNumeratedObjectDefault(UserPriceList, 'Прайсы', 'ПЛ');

@defineDocumentInterfacePosted(priceList);

@defineDocumentInterfaceDescription(priceList, 'Прайс');

//@defineDocumentInterfaceLegalEntity(priceList, company, 'Организация');
@defineDocumentInterfaceDataLegalEntity(priceList, company, 'Организация');
userPriceListLegalEntity (legalEntity) = GROUP MAX userPriceList BY companyUserPriceList(userPriceList);

@defineDocumentInterfaceCurrency(priceList);

groupTypeUserPriceList = DATA GroupType (UserPriceList);
nameGroupTypeUserPriceList 'Тип классификатора' = nameGroupType(groupTypeUserPriceList(UserPriceList));

// Строки

@defineDocumentInterfaceDetailDataTimePrefix(priceList, to, ' по');

@defineDocumentInterfaceDetailSku(priceList, sku);

@defineDocumentInterfaceDetailQuantity(priceList);
@defineAddDetailDialogBarcode(userPriceList, sku);

// ---------------- Цены для видов цен

// Шапка

TABLE priceListPriceListType (PriceList, PriceListType);
showPriceListPriceListType 'Показывать цены' = ABSTRACT BOOLEAN (PriceList, PriceListType) PERSISTENT;
showUserPriceListPriceListType 'Показывать цены' = DATA BOOLEAN (UserPriceList, PriceListType);
showPriceListPriceListType (priceList, priceListType) += showUserPriceListPriceListType(priceList, priceListType);

TABLE priceListDataPriceListType (PriceList, DataPriceListType);
inPriceListDataPriceListType 'Изменять цены' = ABSTRACT BOOLEAN (PriceList, DataPriceListType) PERSISTENT;
inUserPriceListDataPriceListType 'Изменять цены' = DATA BOOLEAN (UserPriceList, DataPriceListType);
inPriceListDataPriceListType (priceList, dataPriceListType) += inUserPriceListDataPriceListType(priceList, dataPriceListType);

priceListTypesPriceList 'Цены' (priceList) = GROUP CONCAT namePriceListType(priceListType) IF inPriceListDataPriceListType(priceList, priceListType) , ', '
                                               BY priceList
                                               ORDER priceListType MINCHARWIDTH 10 PREFCHARWIDTH 20 PERSISTENT;

// Строки

TABLE priceListDetailPriceListType (PriceListDetail, PriceListType);

TABLE priceListDetailDataPriceListType (PriceListDetail, DataPriceListType);
pricePriceListDetailDataPriceListType 'Цена (новая)' = ABSTRACT NUMERIC[14,2] (PriceListDetail, DataPriceListType) PERSISTENT;
priceUserPriceListDetailDataPriceListType 'Цена (новая)' = DATA NUMERIC[14,2] (PriceListDetail, DataPriceListType);
pricePriceListDetailDataPriceListType (priceListDetail, dataPriceListType) += priceUserPriceListDetailDataPriceListType(priceListDetail, dataPriceListType);

inPriceListDetailDataPriceListType (detail, type) = inPriceListDataPriceListType(priceListPriceListDetail(detail), type) PERSISTENT;
pricePriceListDetailDataPriceListType(detail, type) => inPriceListDetailDataPriceListType (detail, type) RESOLVE FALSE;

//НДС

valueVATPriceListDetail 'НДС' (priceListDetail) =
    valueRateRangeDate(VATSkuCountryDate(skuPriceListDetail(priceListDetail),
                       countryLegalEntity(companyPriceListDetail(priceListDetail)),
                       fromDatePriceListDetail(priceListDetail)) ,
    fromDatePriceListDetail(priceListDetail));
valueVATUserPriceListDetail 'НДС' (userPriceListDetail) =
    valueRateRangeDate(VATSkuCountryDate(skuUserPriceListDetail(userPriceListDetail),
                       countryLegalEntity(companyUserPriceListDetail(userPriceListDetail)),
                       fromDateUserPriceListDetail(userPriceListDetail)) ,
    fromDateUserPriceListDetail(userPriceListDetail));

// ---------------------  Склады, для которых действует прайс

// Шапка
allStocksPriceList 'Отм' = ABSTRACT BOOLEAN (PriceList) PERSISTENT;
allStocksUserPriceList 'Отм' = DATA BOOLEAN (UserPriceList);
allStocksPriceList (priceList) += allStocksUserPriceList(priceList);

TABLE priceListStockGroup(PriceList, StockGroup);
dataInPriceListStockGroup 'Отм' = ABSTRACT BOOLEAN (PriceList, StockGroup) PERSISTENT;
dataInUserPriceListStockGroup 'Отм' = DATA BOOLEAN (UserPriceList, StockGroup);
dataInPriceListStockGroup (priceList, stockGroup) += dataInUserPriceListStockGroup(priceList, stockGroup);

TABLE priceListStock(PriceList, Stock);
dataInPriceListStock 'Отм' = ABSTRACT BOOLEAN (PriceList, Stock) PERSISTENT;
dataInUserPriceListStock 'Отм' = DATA BOOLEAN (UserPriceList, Stock);
dataInPriceListStock (priceList, stock) += dataInUserPriceListStock(priceList, stock);

levelParentPriceListStockGroup (priceList, stockGroup) = GROUP MIN levelStockGroupStockGroup(stockGroup, parent) IF dataInPriceListStockGroup(priceList, parent)
                                                               BY priceList, stockGroup PERSISTENT;

inParentPriceListStockGroup (priceList, stockGroup) = TRUE IF levelParentPriceListStockGroup (priceList, stockGroup) PERSISTENT;

inPriceListStockGroup 'Отм' (priceList, stockGroup) = OVERRIDE
    allStocksPriceList(priceList) AND stockGroup IS StockGroup,
    inParentPriceListStockGroup (priceList, stockGroup),
    dataInPriceListStockGroup(priceList, stockGroup);

inPriceListStock 'Отм' (priceList, stock) =
    (OVERRIDE allStocksPriceList(priceList) AND stock IS Stock,
              inParentPriceListStockGroup(priceList, stockGroupStock(stock)),
              dataInPriceListStock(priceList, stock))
    AND isCompanyStock(stock);

stocksPriceList 'Склады' (priceList) = GROUP CONCAT nameStock(stock) IF inPriceListStock(priceList, stock) , ', '
                                       BY priceList
                                       ORDER stock MINCHARWIDTH 20 PREFCHARWIDTH 40 PERSISTENT;
// История
@defineCreatedAbstract(PriceListDetail);

createdTimePriceListDetail (d) += createdTimeUserPriceList(userPriceListUserPriceListDetail(d));
createdUserPriceListDetail (d) += createdUserUserPriceList(userPriceListUserPriceListDetail(d));
createdComputerPriceListDetail (d) += createdComputerUserPriceList(userPriceListUserPriceListDetail(d));

// Партии
@defineDocumentInterfaceDetailBatch(priceList, batch);
//@deriveDocumentDetailPricePriceListTypeVATBatch(userPriceList, stockProp, userPriceList);

changePurchaseBatchUserPriceListDetail = ACTION (userPriceListDetail) {
    FORM dialogBatch OBJECTS sk = skuUserPriceListDetail(userPriceListDetail) MODAL SHOWDROP;

    IF formResult() == FormResult.ok THEN {
        ASSIGN batchUserPriceListDetail(userPriceListDetail) <- chosenObject('bt');
    } ELSE IF formResult() == FormResult.drop THEN {
        ASSIGN batchUserPriceListDetail(userPriceListDetail) <- NULL;
    }

};

//@defineBalancesBatch(priceListDetail); //-- показываем по нажатию правой клавиши остатки партии
//@defineBalancesBatch(userPriceListDetail); //-- показываем по нажатию правой клавиши остатки партии
//@defineMovementBatch(priceListDetail, stockProp); //-- показываем по нажатию правой клавиши движение по партии
//@defineMovementBatch(userPriceListDetail, stockProp); //-- показываем по нажатию правой клавиши движение по партии

// ------- Проведение по priceListLedger ----- //

EXTEND CLASS PriceListDetail : PriceListLedger;

fromDateTimePriceListLedger (ledger) += fromDateTimePriceListDetail(ledger);
toDateTimePriceListLedger (ledger) += toDateTimePriceListDetail(ledger);

isPostedPriceListLedger(ledger) += isPostedPriceListDetail(ledger);

skuPriceListLedger (ledger) += skuPriceListDetail(ledger);
batchPriceListLedger (ledger) += batchPriceListDetail(ledger);
inPriceListLedgerBatch (ledger, batch) += batchPriceListDetail(ledger) == batch; 

companyPriceListLedger (ledger) += companyPriceListDetail(ledger);

descriptionPriceListLedger (ledger) += descriptionPriceList(priceListPriceListDetail(ledger));

inPriceListLedgerLedgerPriceListType (ledger, type) += inPriceListDetailDataPriceListType(ledger, type);
pricePriceListLedgerLedgerPriceListType (ledger, type) += pricePriceListDetailDataPriceListType(ledger, type);
inPriceListLedgerStock (ledger, stock) += inPriceListStock(priceListPriceListDetail(ledger), stock);

createdTimePriceListLedger (d) += createdTimePriceListDetail(d);
createdUserPriceListLedger (d) += createdUserPriceListDetail(d);
createdComputerPriceListLedger (d) += createdComputerPriceListDetail(d);

// -------------- Текущая действующая цена --------------- //

currentPricePriceListDetailDataPriceListTypeStock 'Действующая цена' (priceListDetail, dataPriceListType, stock) =
    IF inPriceListDetailDataPriceListType(priceListDetail, dataPriceListType) THEN
        prevPriceBPriceListTypeSkuStockDateTime(dataPriceListType, skuPriceListDetail(priceListDetail), stock, fromDateTimePriceListDetail(priceListDetail))
    ELSE
        prevPriceAPriceListTypeSkuStockDateTime(dataPriceListType, skuPriceListDetail(priceListDetail), stock, fromDateTimePriceListDetail(priceListDetail));

currentPriceSkuPriceListDataPriceListTypeStock 'Действующая цена' (sku, priceList, dataPriceListType, stock) =
    IF inPriceListDataPriceListType(priceList, dataPriceListType) THEN
        prevPriceBPriceListTypeSkuStockDateTime(dataPriceListType, sku, stock, fromDateTimePriceList(priceList))
    ELSE
        prevPriceAPriceListTypeSkuStockDateTime(dataPriceListType, sku, stock, fromDateTimePriceList(priceList));

changePricePriceListDetailDataPriceListTypeStock 'Изменение, %' (priceListDetail, dataPriceListType, stock) =
    pricePriceListDetailDataPriceListType(priceListDetail, dataPriceListType) * 100.0 /
    (currentPricePriceListDetailDataPriceListTypeStock (priceListDetail, dataPriceListType, stock) IF currentPricePriceListDetailDataPriceListTypeStock (priceListDetail, dataPriceListType, stock) != 0) - 100.0;

// Заголовки колонок
headerNameCurrentDataPriceListType (userPriceList, dataPriceListType) =
    [FORMULA VARSTRING[50]  ' CAST($1 AS TEXT) || \' (старая) \''](
    namePriceListType(dataPriceListType)) IF showPriceListPriceListType(userPriceList, dataPriceListType) MINCHARWIDTH 30 MAXCHARWIDTH 50;

headerNameChangeDataPriceListType (userPriceList, dataPriceListType) =
    [FORMULA VARSTRING[50]  'CAST($1 AS TEXT) || \' (изменение, %) \''](
    namePriceListType(dataPriceListType)) IF inUserPriceListDataPriceListType (userPriceList, dataPriceListType) AND
                                             showUserPriceListPriceListType (userPriceList, dataPriceListType) MINCHARWIDTH 30 MAXCHARWIDTH 50;

headerNameDataPriceListType (userPriceList, dataPriceListType) =
    [FORMULA VARSTRING[50]  ' CAST($1 AS TEXT) || \' (новая) \''](
    namePriceListType(dataPriceListType)) IF inPriceListDataPriceListType(userPriceList, dataPriceListType) MINCHARWIDTH 30 MAXCHARWIDTH 50;

// Цвета колонок
backgroundCurrentDataPriceListType 'Цвет' (dataPriceListType) = RGB(255,238,165) IF dataPriceListType IS DataPriceListType;
backgroundChangeDataPriceListType 'Цвет' (dataPriceListType) = RGB(232,184,146) IF dataPriceListType IS DataPriceListType;
backgroundDataPriceListType 'Цвет' (dataPriceListType) = RGB(213,249,185) IF dataPriceListType IS DataPriceListType;

// Подбор товаров
detailSkuUserPriceList(sku, price) = GROUP SUM 1 IF sku == skuUserPriceListDetail(detail) AND price == userPriceListUserPriceListDetail(detail)
                                           BY skuUserPriceListDetail(detail), userPriceListUserPriceListDetail(detail);

inSkuUserPriceList 'Вкл.' (sku, price) = TRUE IF detailSkuUserPriceList(sku, price);

changeInSkuUserPriceList = ACTION (sku, priceList) {
    REQUEST BOOLEAN INPUT;
    IF NOT requestedLogical() THEN {
        IF detailSkuUserPriceList(sku, priceList) THEN {
            DELETE detail WHERE sku == skuUserPriceListDetail(detail) AND priceList == userPriceListUserPriceListDetail(detail);
        }
    } ELSE {
        FOR ADDOBJ d = UserPriceListDetail DO {
           userPriceListUserPriceListDetail(d) <- priceList;
           skuUserPriceListDetail(d) <- sku;
        }
    }
}

// ------------- Подбор документов ----------------- //

numberDocument(priceList) += numberPriceList(priceList);
seriesDocument(priceList) += seriesPriceList(priceList);
dateDocument(priceList) += datePriceList(priceList);
supplierDocument(priceList) += companyPriceList(priceList);

indexDocumentDetail(priceList) += indexPriceListDetail(priceList);
skuDocumentDetail(priceList) += skuPriceListDetail(priceList);

documentDocumentDetail(detail) += priceListPriceListDetail(detail);

fillDocumentPriceList 'Подбор документа' = ACTION (userPriceList) {
    FORM documents MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL chosenDocument = Document();
        ASSIGN chosenDocument() <- chosenObject('d');
        FOR documentDocumentDetail(documentDetail) == chosenDocument() ADDOBJ i = UserPriceListDetail DO {
            ASSIGN userPriceListUserPriceListDetail(i) <- userPriceList;
            ASSIGN skuUserPriceListDetail(i) <- skuDocumentDetail(documentDetail);
        }
    }
} TOOLBAR;

// ---------------------- Активность --------------------------- //

isActivePriceList(priceList) = (fromDateTimePriceList(priceList) < currentDateTime() AND toDateTimePriceList(priceList) > currentDateTime()) OR
                               (fromDateTimePriceList(priceList) < currentDateTime() AND NOT toDateTimePriceList(priceList));
isActivePriceListDetail(priceListDetail) = (fromDateTimePriceListDetail(priceListDetail) < currentDateTime() AND toDateTimePriceListDetail(priceListDetail) > currentDateTime()) OR
                               (fromDateTimePriceListDetail(priceListDetail) < currentDateTime() AND NOT toDateTimePriceListDetail(priceListDetail));

// Цвета
backgroundPriceList 'Цвет' (priceList) =
    IF currentDateTime() > toDateTimePriceList(priceList)
        THEN RGB(255, 226, 226) IF priceList IS PriceList
    ELSE
        RGB(226, 255, 226) IF fromDateTimePriceList(priceList) > currentDateTime();

backgroundPriceListDetail 'Цвет' (priceListDetail) =
    IF currentDateTime() > toDateTimePriceListDetail(priceListDetail)
        THEN RGB(255, 226, 226) IF priceListDetail IS PriceListDetail
    ELSE
        RGB(226, 255, 226) IF fromDateTimePriceListDetail(priceListDetail) > currentDateTime();

// --------------- Фильтры для подбора товаров ------------------ //

// Вид цены
filterPriceListTypeUserPriceList = DATA SESSION PriceListType (UserPriceList);
nameFilterPriceListTypeUserPriceList 'Вид цены' (userPriceList) = namePriceListType(filterPriceListTypeUserPriceList(userPriceList));

currentPriceSkuPriceListStock 'Текущая цена' (sku, userPriceList, stock) =
    currentPriceSkuPriceListDataPriceListTypeStock(sku, userPriceList, filterPriceListTypeUserPriceList(userPriceList), stock);
filterPriceListTypeUserPriceListSkuStock (userPriceList, sku, stock) =
    currentPriceSkuPriceListStock (sku, userPriceList, stock) OR
    (sku IS Sku AND stock IS Stock AND NOT filterPriceListTypeUserPriceList(userPriceList));

FORM userPriceList 'Прайс'
    OBJECTS p = UserPriceList FIXED PANEL
    PROPERTIES(p) isPostedUserPriceList, nameNumeratorUserPriceList, numberUserPriceList, seriesUserPriceList,
                  dateUserPriceList, timeUserPriceList,
                  fromDateUserPriceList, fromTimeUserPriceList, toDateUserPriceList, toTimeUserPriceList,
                  nameCurrencyUserPriceList, nameCompanyUserPriceList, noteUserPriceList,
                  nameGroupTypeS = nameGroupTypeUserPriceList, nameGroupTypeP = nameGroupTypeUserPriceList

    OBJECTS t = PriceListType
    PROPERTIES(t) namePriceListType READONLY, includeVATLedgerPriceListType
    PROPERTIES(p, t) inPriceListDataPriceListType TODRAW t FORCE GRID, showPriceListPriceListType
    FILTERS (currencyUserPriceList(p) == currencyPriceListType(t)) OR (p IS UserPriceList AND t IS PriceListType AND NOT currencyPriceListType(t))

    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES READONLY skuTreeName = nameGroup(sk)
    ORDER BY skuTreeName
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeGroup(sk) DEFAULT

    OBJECTS d = UserPriceListDetail
    PROPERTIES(d) BACKGROUND backgroundPriceListDetail(d)
                  indexUserPriceListDetail, idBarcodeSkuUserPriceListDetail, nameSkuUserPriceListDetail,
                  shortNameUOMSkuUserPriceListDetail, nameBatchUserPriceListDetail ON CHANGE changePurchaseBatchUserPriceListDetail(d),
                  valueVATUserPriceListDetail, fromDateUserPriceListDetail, fromTimeUserPriceListDetail,
                  toDateUserPriceListDetail, toTimeUserPriceListDetail, ADDOBJ, DELETESESSION
    PROPERTIES(d, t) pricePriceListDetailDataPriceListType COLUMNS (t) HEADER headerNameDataPriceListType(p, t) SHOWIF headerNameDataPriceListType(p, t) BACKGROUND backgroundDataPriceListType(t) TODRAW d FORCE GRID
    PROPERTIES(p) TODRAW d FORCE PANEL TOOLBAR fillDocumentPriceList, addDetailInputBarcodeUserPriceListDetailUserPriceList
    FILTERS userPriceListUserPriceListDetail(d) == p,
            isParentGroupSku(sk, skuPriceListDetail(d)) OR (sk IS Group AND d IS PriceListDetail AND NOT skuPriceListDetail(d)),
            groupTypeUserPriceList(p) == groupTypeGroup(sk)
    FILTERGROUP filters
        FILTER 'Активные строки' 'F10' isActivePriceListDetail(d)

    TREE stockTree a=STRING[3], sg = StockGroup PARENT parentStockGroup
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = nameStockGroup(sg)
    PROPERTIES(p) allStocksPriceList TODRAW a FORCE GRID
    PROPERTIES(p, sg) inPriceListStockGroup
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)
    FILTERS countCompanyStockStockGroup(sg)

    OBJECTS ts = Stock
    PROPERTIES READONLY  nameStock(ts)
    PROPERTIES(p, ts)    inPriceListStock
    PROPERTIES(d, t, ts) currentPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER headerNameCurrentDataPriceListType(p, t) SHOWIF headerNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t)
    PROPERTIES(d, t, ts) changePricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER headerNameChangeDataPriceListType(p, t) SHOWIF headerNameChangeDataPriceListType(p, t) BACKGROUND backgroundChangeDataPriceListType(t)
    FILTERS ts IS Stock AND NOT sg IS StockGroup OR isParentStockGroupStock(sg, ts),
            isCompanyStock(ts)
    FILTERGROUP filters2
            FILTER 'Выбранные склады' 'F10' inPriceListStock(p, ts) DEFAULT

    OBJECTS tt = Stock FIXED PANEL
    PROPERTIES SELECTOR stockName = nameStock(tt) FORCE PANEL
    PROPERTIES(d, t, tt) currentPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER headerNameCurrentDataPriceListType(p, t) SHOWIF headerNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t) TODRAW d FORCE GRID
    PROPERTIES(d, t, tt) changePricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER headerNameChangeDataPriceListType(p, t) SHOWIF headerNameChangeDataPriceListType(p, t) BACKGROUND backgroundChangeDataPriceListType(t) TODRAW d FORCE GRID

    PROPERTIES(d)        nameCompanyUserPriceListDetail
    FILTERS isCompanyStock(tt)

    TREE skuTree2 sk2 = Group PARENT parentGroup
    PROPERTIES READONLY skuTreeName2 = nameGroup(sk2)
    ORDER BY skuTreeName2
    FILTERGROUP inactive1 FILTER 'Активные' 'F5' activeGroup(sk2) DEFAULT

    OBJECTS ts2 = Stock FIXED PANEL
    PROPERTIES(ts2) SELECTOR nameStock
    FILTERS inPriceListStock(p, ts2),
            isCompanyStock(ts2)
    PROPERTIES(p) FORCE PANEL nameFilterPriceListTypeUserPriceList

    OBJECTS s2=Sku
    PROPERTIES inSkuUserPriceList(s2, p) ON CHANGE changeInSkuUserPriceList(s2, p)
    PROPERTIES READONLY inputName2 = nameSku(s2), idBarcodeSku(s2), currentBalanceSkuStock(s2, ts2)
    PROPERTIES READONLY currentPriceSkuPriceListStock(s2, p, ts2) SHOWIF filterPriceListTypeUserPriceList(p)
    PROPERTIES(s2, p, t, ts2) currentPriceSkuPriceListDataPriceListTypeStock COLUMNS (t) HEADER headerNameCurrentDataPriceListType(p, t) SHOWIF headerNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t)
    FILTERS filterPriceListTypeUserPriceListSkuStock(p, s2, ts2),
            groupTypeUserPriceList(p) == groupTypeGroup(sk2)
    FILTERGROUP filters3
        FILTER 'С остатком' 'F10' currentBalanceSkuStock(s2, ts2)

    FILTERS isParentGroupSku(sk2, s2)
    ORDER BY inputName2

    EVENTS
        ON OK prePostUserPriceList(p)

    EDIT UserPriceList OBJECT p
;

// Оптимизация с преподсчетом видов цен в сессионное свойство

//viewCurrentPricePriceListDetailDataPriceListTypeStock 'Действующая цена' = DATA SESSION NUMERIC[14,2] (UserPriceListDetail, DataPriceListType, Stock);
//
//currentUserPriceList = DATA SESSION UserPriceList ();
//setCurrentUserPriceList (u) = ACTION currentUserPriceList() <- u;
//
//EXTEND FORM userPriceList
//    PROPERTIES(d, t, ts) viewCurrentPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER headerNameCurrentDataPriceListType(p, t) SHOWIF headerNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t)
//    PROPERTIES(d, t, tt) viewCurrentPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER headerNameCurrentDataPriceListType(p, t) SHOWIF headerNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t) TODRAW d FORCE GRID
//    EVENTS
//        ON CHANGE p setCurrentUserPriceList(p)
//;
//
//WHEN SESSION FORMS userPriceList SET([VIEW userPriceList.d](d)) AND p == currentUserPriceList() DO
//    FOR showUserPriceListPriceListType(p, pt) AND isCompanyStock(s) NOINLINE (pt) DO
//        viewCurrentPricePriceListDetailDataPriceListTypeStock(d, pt, s) <- currentPricePriceListDetailDataPriceListTypeStock(d, pt, s);  
//
//WHEN SESSION FORMS userPriceList CHANGED(showUserPriceListPriceListType(p, pt)) NOINLINE (pt) DO
//    viewCurrentPricePriceListDetailDataPriceListTypeStock(d, pt, s) <- currentPricePriceListDetailDataPriceListTypeStock(d, pt, s) 
//        WHERE [VIEW userPriceList.d](d) AND isCompanyStock(s);  
//
//WHEN SESSION FORMS userPriceList CHANGED(skuUserPriceListDetail(d)) OR CHANGED(fromDateTimeUserPriceListDetail(d)) DO
//    viewCurrentPricePriceListDetailDataPriceListTypeStock(d, pt, s) <- currentPricePriceListDetailDataPriceListTypeStock(d, pt, s) 
//        WHERE showUserPriceListPriceListType(userPriceListUserPriceListDetail(d), pt) AND isCompanyStock(s);  
//

DESIGN userPriceList FROM DEFAULT {
    NEW headContainer {
        caption = 'Шапка документа';       
        type = CONTAINERV;
        
        NEW first {
            type = CONTAINERH;
            ADD PROPERTY(isPostedUserPriceList) { preferredCharWidth = 40; }
        }
        NEW second { 
            type = CONTAINERH;
            ADD PROPERTY (nameNumeratorUserPriceList);
            ADD PROPERTY (numberUserPriceList);
            ADD PROPERTY (seriesUserPriceList);
            ADD PROPERTY (dateUserPriceList);
            ADD PROPERTY (timeUserPriceList);
        }        

    }
    NEW timeContainer{
        caption = 'Период действия';
        type = CONTAINERH;
        ADD PROPERTY (fromDateUserPriceList);
        ADD PROPERTY (fromTimeUserPriceList);
        ADD PROPERTY (toDateUserPriceList);
        ADD PROPERTY (toTimeUserPriceList);
    }
    ADD p.documentPrmGroup{
        caption = 'Параметры документа';
        type = COLUMNS;
        columns = 3;
        ADD PROPERTY(nameCurrencyUserPriceList);
        ADD PROPERTY(nameCompanyUserPriceList);
        ADD PROPERTY(noteUserPriceList);
    }
    NEW detailContainer{
        fill = 1;
        type = TABBED;
        NEW firstContainer{
            fill = 1;
            caption = 'Спецификация';

            type = SPLITH;
            NEW leftColumnContainer{
                fill = 1;
                ADD tt.box;
                ADD PROPERTY(nameGroupTypeS);
                ADD skuTree.tree.box {
                    caption = 'Товары';
                }
            }
            NEW skuContainer{
                fill = 3;
                NEW rowContainer {
                    type = CONTAINERH;
                    ADD PROPERTY(addDetailInputBarcodeUserPriceListDetailUserPriceList);
                }
                ADD d.box;
            }
        }

        NEW secondContainer {
            fill = 1;
            caption = 'Склады';
            type = SPLITH;
            ADD stockTree.tree.box {
                caption = 'Группы складов';
            }
            ADD ts.box {
                fill = 2;
            }
        }

        NEW thirdContainer{
            fill = 1;
            caption = 'Вид цен';
            ADD t.box;
        }

        NEW fourthContainer {
            caption = 'Подбор';
            NEW topThirdContainer{
                type = CONTAINERH;
                ADD ts2.box;
                NEW filtersContainer {
                    caption = 'Фильтры';
                    type = CONTAINERH;
                    ADD PROPERTY(nameFilterPriceListTypeUserPriceList);
                }
            }
            NEW detailFourthContainer{
                fill = 1;
                type = SPLITH;
                NEW leftColumnContainer2 {
                    fill = 1;
                    ADD PROPERTY(nameGroupTypeP);
                    ADD skuTree2.tree.box {
                        caption = 'Товары';
                    }
                }
                ADD s2.box {
                    fill = 3;
                }
            }
        }

    }
    ADD functions.box;

    PROPERTY(formOk) {
        caption = 'Провести';
    }
}

// -------------------------------- Изменение цен для строк ------------------------------ //

CLASS TypeMarkUpSkuUserPriceList 'Метод изменения цены'{
    percent 'Наценка, %',
    multiplication 'Умножение',
    division 'Деление'
}

FORM typeMarkUpSkuUserPriceList 'Метод изменения цены'
    OBJECTS m = TypeMarkUpSkuUserPriceList
    PROPERTIES(m) READONLY staticCaption

    DIALOG TypeMarkUpSkuUserPriceList OBJECT m
;

DESIGN typeMarkUpSkuUserPriceList FROM DEFAULT{
    PROPERTY(staticCaption(m)){caption = 'Метод изменения цены';}
}

headerNumericCaption(type) =
    CASE
        WHEN type == TypeMarkUpSkuUserPriceList.percent
            THEN '% наценки' IF type IS TypeMarkUpSkuUserPriceList
        WHEN type == TypeMarkUpSkuUserPriceList.multiplication
            THEN 'Множитель' IF type IS TypeMarkUpSkuUserPriceList
        WHEN type == TypeMarkUpSkuUserPriceList.division
            THEN 'Делитель' IF type IS TypeMarkUpSkuUserPriceList
        ELSE '% наценки' IF type IS TypeMarkUpSkuUserPriceList
    ;

FORM chooseMarkUpSkuUserPriceList 'Наценка'

    OBJECTS p = UserPriceList FIXED PANEL

    OBJECTS m = TypeMarkUpSkuUserPriceList FIXED PANEL
    PROPERTIES(m) SELECTOR staticCaption

    OBJECTS pt = PriceListType FIXED PANEL
    PROPERTIES(pt) SELECTOR namePriceListType

    OBJECTS n = NUMERIC[20,7] FIXED PANEL
    PROPERTIES(n) objValue = OBJVALUE HEADER headerNumericCaption(m)

    OBJECTS nt = DataPriceListType FIXED PANEL
    PROPERTIES(nt) SELECTOR nameDataPriceListType
    FILTERS inPriceListDataPriceListType(p, nt)
;

DESIGN chooseMarkUpSkuUserPriceList FROM DEFAULT {

    REMOVE p.box;
    NEW topContainer {
        type = CONTAINERV;
        ADD PROPERTY(staticCaption(m)){caption = 'Метод изменения цены';}
        ADD PROPERTY(namePriceListType(pt));
        PROPERTY(namePriceListType(pt)){caption = 'Старая цена';}
        ADD PROPERTY(objValue);
        PROPERTY(objValue){caption = 'Изменение';}
        ADD PROPERTY(nameDataPriceListType(nt));
        PROPERTY(nameDataPriceListType(nt)){caption = 'Новая цена';}
    }
    ADD functions.box;
}

// ----------------------- Для отмеченных строк ------------------------ //
isSelectedUserPriceListDetail 'Отм' = DATA SESSION BOOLEAN (UserPriceListDetail);
changeMarkUpChosenSkuUserPriceList 'Изменить цену для отмеченных строк' = ACTION (userPriceList, stock) {
    FORM chooseMarkUpSkuUserPriceList OBJECTS p = userPriceList MODAL;
    IF formResult() == FormResult.ok THEN {
        IF NOT [GROUP SUM 1 IF isSelectedUserPriceListDetail(priceListDetail)
                      BY userPriceListUserPriceListDetail(priceListDetail)](userPriceList) THEN
            MESSAGE 'Выберите хотя бы одну позицию'
        ELSE {
            LOCAL prevPrice = NUMERIC[14,2] (UserPriceListDetail);
            IF chosenObject('pt') != chosenObject('nt') AND inUserPriceListDataPriceListType(userPriceList, chosenObject('pt')) THEN {
                prevPrice(detail) <- priceUserPriceListDetailDataPriceListType(detail, chosenObject('pt'))
                                     WHERE userPriceListUserPriceListDetail(detail) == userPriceList;
            } ELSE {
                prevPrice(detail) <- currentPricePriceListDetailDataPriceListTypeStock(detail, chosenObject('pt'), stock)
                                     WHERE userPriceListUserPriceListDetail(detail) == userPriceList;
            }
            IF chosenObject('m') == TypeMarkUpSkuUserPriceList.percent AND includeVATPriceListType(chosenObject('nt')) AND NOT includeVATPriceListType(chosenObject('pt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (valueVATUserPriceListDetail(detail) + 100.0) / 100.0 * (chosenNumeric('n') + 100.0) / 100.0, roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.percent AND includeVATPriceListType(chosenObject('pt')) AND NOT includeVATPriceListType(chosenObject('nt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / (valueVATUserPriceListDetail(detail) + 100.0) * 100.0 * (chosenNumeric('n') + 100.0) / 100.0, roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.percent THEN{
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (chosenNumeric('n') + 100.0) / 100.0, roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            }
            IF chosenObject('m') == TypeMarkUpSkuUserPriceList.multiplication AND includeVATPriceListType(chosenObject('nt')) AND NOT includeVATPriceListType(chosenObject('pt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (valueVATUserPriceListDetail(detail) + 100.0) / 100.0 * chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.multiplication AND includeVATPriceListType(chosenObject('pt')) AND NOT includeVATPriceListType(chosenObject('nt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / (valueVATUserPriceListDetail(detail) + 100.0) * 100.0 * chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.multiplication THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            }
            IF chosenObject('m') == TypeMarkUpSkuUserPriceList.division AND includeVATPriceListType(chosenObject('nt')) AND NOT includeVATPriceListType(chosenObject('pt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (valueVATUserPriceListDetail(detail) + 100.0) / 100.0 / chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.division AND includeVATPriceListType(chosenObject('pt')) AND NOT includeVATPriceListType(chosenObject('nt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / (valueVATUserPriceListDetail(detail) + 100.0) * 100.0 / chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.division THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE isSelectedUserPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            }
        }
    }
}

EXTEND FORM userPriceList
    PROPERTIES(d) isSelectedUserPriceListDetail BEFORE indexUserPriceListDetail
    PROPERTIES(p, tt) TODRAW d FORCE PANEL TOOLBAR changeMarkUpChosenSkuUserPriceList
    FILTERGROUP filterSelected
        FILTER 'Отмеченные строки' 'F11' isSelectedUserPriceListDetail(d)
;
EXTEND DESIGN userPriceList{
    rowContainer{
        ADD PROPERTY(changeMarkUpChosenSkuUserPriceList);
    }
}

// ---------------------- Для всех строк ----------------------- //
changeMarkUpSkuUserPriceList 'Изменить цену для всех строк' = ACTION (userPriceList, stock) {
    FORM chooseMarkUpSkuUserPriceList OBJECTS p = userPriceList MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL prevPrice = NUMERIC[14,2] (UserPriceListDetail);
        IF chosenObject('pt') != chosenObject('nt') AND inUserPriceListDataPriceListType(userPriceList, chosenObject('pt')) THEN {
            prevPrice(detail) <- priceUserPriceListDetailDataPriceListType(detail, chosenObject('pt'))
                                 WHERE userPriceListUserPriceListDetail(detail) == userPriceList;
        } ELSE {
            prevPrice(detail) <- currentPricePriceListDetailDataPriceListTypeStock(detail, chosenObject('pt'), stock)
                                 WHERE userPriceListUserPriceListDetail(detail) == userPriceList;
        }

        FOR [FILTER userPriceList.d](detail) DO {
            IF chosenObject('m') == TypeMarkUpSkuUserPriceList.percent AND includeVATPriceListType(chosenObject('nt')) AND NOT includeVATPriceListType(chosenObject('pt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (valueVATUserPriceListDetail(detail) + 100.0) / 100.0 * (chosenNumeric('n') + 100.0) / 100.0, roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.percent AND includeVATPriceListType(chosenObject('pt')) AND NOT includeVATPriceListType(chosenObject('nt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / (valueVATUserPriceListDetail(detail) + 100.0) * 100.0 * (chosenNumeric('n') + 100.0) / 100.0, roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.percent THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (chosenNumeric('n') + 100.0) / 100.0, roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            }
            IF chosenObject('m') == TypeMarkUpSkuUserPriceList.multiplication AND includeVATPriceListType(chosenObject('nt')) AND NOT includeVATPriceListType(chosenObject('pt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (valueVATUserPriceListDetail(detail) + 100.0) / 100.0 * chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.multiplication AND includeVATPriceListType(chosenObject('pt')) AND NOT includeVATPriceListType(chosenObject('nt')) THEN{
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / (valueVATUserPriceListDetail(detail) + 100.0) * 100.0 * chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.multiplication THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            }
            IF chosenObject('m') == TypeMarkUpSkuUserPriceList.division AND includeVATPriceListType(chosenObject('nt')) AND NOT includeVATPriceListType(chosenObject('pt')) THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) * (valueVATUserPriceListDetail(detail) + 100.0) / 100.0 / chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.division AND includeVATPriceListType(chosenObject('pt')) AND NOT includeVATPriceListType(chosenObject('nt')) THEN{
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / (valueVATUserPriceListDetail(detail) + 100.0) * 100.0 / chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            } ELSE IF chosenObject('m') == TypeMarkUpSkuUserPriceList.division THEN {
                ASSIGN priceUserPriceListDetailDataPriceListType(detail, t) <-
                    roundPriceRoundCondition(prevPrice(detail) / chosenNumeric('n'), roundConditionPriceListType(t))
                    WHERE userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('nt');
            }
        }
    }
}

EXTEND FORM userPriceList
    PROPERTIES(p, tt) TODRAW d FORCE PANEL TOOLBAR changeMarkUpSkuUserPriceList
;

EXTEND DESIGN userPriceList{
    rowContainer{
        ADD PROPERTY(changeMarkUpSkuUserPriceList) BEFORE PROPERTY(changeMarkUpChosenSkuUserPriceList);
    }
}

addUserPriceList 'Добавить' = ACTION ADDFORM UserPriceList;
editUserPriceList 'Редактировать' = ACTION EDITFORM UserPriceList;
editPriceList 'Редактировать' = ABSTRACT ACTION LIST (PriceList) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;
editPriceList (p) += editUserPriceList(p);

// ------------------ Копирование прайс-листов ------------------- //

copyAbstractDataPriceList = ABSTRACT ACTION LIST (PriceList, UserPriceList);

copyDataPriceList = ACTION (priceList, userPriceList){
        ASSIGN currencyUserPriceList(userPriceList) <- currencyUserPriceList(priceList);
        ASSIGN companyUserPriceList(userPriceList) <- companyUserPriceList(priceList);
        ASSIGN dataInUserPriceListStock(userPriceList, stock) <- dataInUserPriceListStock(priceList, stock);
        ASSIGN dataInUserPriceListStockGroup(userPriceList, stockGroup) <- dataInUserPriceListStockGroup(priceList, stockGroup);
        ASSIGN allStocksUserPriceList(userPriceList) <- allStocksUserPriceList(priceList);
        ASSIGN inUserPriceListDataPriceListType(userPriceList, priceListType) <- inPriceListDataPriceListType(priceList, priceListType);
        EXEC copyAbstractDataPriceList(priceList, userPriceList);
        FOR userPriceListUserPriceListDetail(detail) == priceList DO {
            FOR ADDOBJ d = UserPriceListDetail DO {
                ASSIGN userPriceListUserPriceListDetail(d) <- userPriceList;
                ASSIGN skuUserPriceListDetail(d) <- skuUserPriceListDetail(detail);
                ASSIGN priceUserPriceListDetailDataPriceListType(d, priceListType) <- pricePriceListDetailDataPriceListType(detail, priceListType);
            }
        }
}

copySessionPriceList 'Копировать' = ACTION (priceList){

    FOR ADDOBJ u = UserPriceList DO {
        EXEC copyDataPriceList(priceList, u);
        FORM userPriceList OBJECTS p = u MANAGESESSION DOCKEDMODAL;
    }
}

copyPriceList 'Копировать' = ACTION (priceList) NEWSESSION {

    FOR ADDOBJ u = UserPriceList DO {
        EXEC copyDataPriceList(priceList, u);
        FORM userPriceList OBJECTS p = u MANAGESESSION DOCKEDMODAL;
    }
}

updateSessionPriceList 'Обновить' = ACTION (priceList) {

    FOR ADDOBJ u = UserPriceList DO {
        EXEC copyDataPriceList(priceList, u);
        FORM userPriceList OBJECTS p = u DOCKEDMODAL;
        IF formResult() == FormResult.ok THEN {
            ASSIGN toDateUserPriceList(priceList) <- fromDateUserPriceList(u);
            ASSIGN toTimeUserPriceList(priceList) <- fromTimeUserPriceList(u);
        } ELSE {
            DELETE u;
        }
    }
}

updatePriceList 'Обновить' = ACTION (priceList) NEWSESSION{

    FOR ADDOBJ u = UserPriceList DO {
        EXEC copyDataPriceList(priceList, u);
        FORM userPriceList OBJECTS p = u DOCKEDMODAL;
        IF formResult() == FormResult.ok THEN {
            ASSIGN toDateUserPriceList(priceList) <- fromDateUserPriceList(u);
            ASSIGN toTimeUserPriceList(priceList) <- fromTimeUserPriceList(u);
        } ELSE {
            DELETE u;
        }
    }
}

printPriceList 'Распечатать' = ABSTRACT ACTION LIST (PriceList);

FORM priceLists 'Прайсы'
    OBJECTS p = PriceList
    PROPERTIES(p) READONLY BACKGROUND backgroundPriceList(p)
           isPostedPriceList FORCE GRID, numberPriceList, seriesPriceList, datePriceList, timePriceList,
           fromDatePriceList, fromTimePriceList, toDatePriceList, toTimePriceList,
           nameCurrencyPriceList, nameCompanyPriceList, priceListTypesPriceList, stocksPriceList,
           notePriceList

    PROPERTIES ()  addUserPriceList TODRAW p
    PROPERTIES (p) editPriceList, copyPriceList FORCE PANEL TOOLBAR, printPriceList FORCE PANEL TOOLBAR
    PROPERTIES (p) DELETE FORCE PANEL TOOLBAR SHOWIF isUserPriceList(p)

    OBJECTS t = DataPriceListType FIXED GRID

    OBJECTS d = PriceListDetail
    PROPERTIES(d) READONLY indexPriceListDetail, idBarcodeSkuPriceListDetail, nameSkuPriceListDetail,
                           shortNameUOMSkuPriceListDetail, nameBatchPriceListDetail, nameCompanyPriceListDetail

    OBJECTS ts = Stock
    PROPERTIES(ts) READONLY nameStock
    PROPERTIES(p, ts) READONLY inPriceListStock

    PROPERTIES(d, t) READONLY pricePriceListDetailDataPriceListType COLUMNS (t) HEADER nameDataPriceListType(t)

    FILTERGROUP filters
        FILTER 'Активные прайсы' 'F10' isActivePriceList(p) DEFAULT


    FILTERS priceListPriceListDetail(d) == p,
            inPriceListDataPriceListType(p, t),
            inPriceListStock(p, ts),
            currencyUserPriceList(p) == currencyDataPriceListType(t),
            isCompanyStock(ts)

    DIALOG PriceList OBJECT p
;

DESIGN priceLists FROM DEFAULT{
    NEW topContainer{
        fill = 1;
        type = SPLITV;
        REMOVE t.box;
        ADD p.box {
            fill = 2;
        }
        NEW detailContainer{
            fill = 1;
            type = TABBED;
            NEW firstContainer{
                caption = 'Спецификация';
                type = SPLITH;
                ADD d.box {
                    fill = 4;
                }
                ADD ts.box;
            }
            NEW printTab {
                caption = 'Печатные формы';
                NEW printContainer {
                    caption = 'Печать';
                    ADD PROPERTY(printPriceList);
                }
            }
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    priceListDocuments 'Документы' {
        ADD priceLists;
    }
}

// Заполнение на основании

addUnderPriceListUserPriceList 'Заполнить на основании' = ACTION (p) {
    FORM priceLists MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR priceListPriceListDetail(detail) == chosenObject('p') ADDOBJ d = UserPriceListDetail DO {
            userPriceListUserPriceListDetail(d) <- p;
            skuUserPriceListDetail(d) <- skuPriceListDetail(detail);
            priceUserPriceListDetailDataPriceListType(d, type) <- pricePriceListDetailDataPriceListType(detail, type)
                                                                  WHERE inUserPriceListDataPriceListType(p, type); 
        }
    }
}

EXTEND FORM userPriceList
    PROPERTIES(p) TODRAW d FORCE PANEL TOOLBAR addUnderPriceListUserPriceList
;

//печать прайсов

FORM printPriceList 'Прайс'
    OBJECTS p = PriceList FIXED PANEL
    PROPERTIES(p) READONLY numberPriceList, seriesPriceList, datePriceList, timePriceList,
           fromDatePriceList, fromTimePriceList, toDatePriceList, toTimePriceList,
           nameCurrencyPriceList, nameCompanyPriceList, priceListTypesPriceList, stocksPriceList,
           notePriceList, isPostedPriceList

    OBJECTS t = PriceListType
    PROPERTIES(t) READONLY namePriceListType
    FILTERS inPriceListDataPriceListType(p, t),
            (currencyUserPriceList(p) == currencyPriceListType(t)) OR (p IS PriceList AND t IS PriceListType AND NOT currencyPriceListType(t))

    OBJECTS d = UserPriceListDetail
    PROPERTIES(d) READONLY indexPriceListDetail, idBarcodeSkuPriceListDetail, nameSkuPriceListDetail,
                           shortNameUOMSkuPriceListDetail, nameCompanyPriceListDetail
    PROPERTIES(d, t) pricePriceListDetailDataPriceListType COLUMNS (t) HEADER headerNameDataPriceListType(p, t) SHOWIF headerNameDataPriceListType(p, t) BACKGROUND backgroundDataPriceListType(t) TODRAW d FORCE GRID

    FILTERS priceListPriceListDetail(d) == p
;

printPrintPriceList 'Распечатать' (priceList) =
    ACTION FORM printPriceList OBJECTS p = priceList PRINT  IMAGE 'print.png' IN printGroup;

printPriceList(priceList) += printPrintPriceList(priceList);

// ------------------------- Расширяем форму LegalEntity ----------------------- //

addUserPriceListLegalEntity 'Добавить' = ACTION (legalEntity) {

    FOR ADDOBJ u = UserPriceList DO {
        ASSIGN companyUserPriceList(u) <- legalEntity;
        FORM userPriceList OBJECTS p = u DOCKEDMODAL;
        IF NOT formResult() == FormResult.ok THEN {
            DELETE u;
        }
    }
} TOOLBAR;

EXTEND FORM legalEntity

    OBJECTS p = UserPriceList
    PROPERTIES(p) READONLY isPostedUserPriceList, numberUserPriceList, seriesUserPriceList, dateUserPriceList, timeUserPriceList,
                           fromDateUserPriceList, fromTimeUserPriceList, toDateUserPriceList, toTimeUserPriceList,
                           nameCurrencyUserPriceList, priceListTypesPriceList,
                           stocksPriceList, noteUserPriceList
    PROPERTIES addUserPriceListLegalEntity(l) TODRAW p FORCE PANEL, updateSessionPriceList(p) TODRAW p FORCE PANEL TOOLBAR
    PROPERTIES(p) EDITSESSIONFORM, DELETESESSION FORCE PANEL TOOLBAR
    FILTERS companyUserPriceList(p) == l
;

EXTEND DESIGN legalEntity{
    extendContainer {
        ADD p.box{caption = 'Прайсы';}
    }
}

// ------------------------------------------------- Сравнение прайсов ---------------------------- //

countSkuPriceList (sku, priceList) = GROUP SUM 1 IF skuPriceListDetail(detail) == sku AND priceListPriceListDetail(detail) == priceList
    BY sku, priceList;

countSkusPriceList (priceList) = GROUP SUM 1 IF countSkuPriceList(sku, priceList)
    BY priceList;

countSkuPriceListPriceList (priceList1, priceList2) = GROUP SUM 1 IF countSkuPriceList(sku, priceList1) == countSkuPriceList(sku, priceList2)
    BY priceList1, priceList2;

equalsPriceListPriceList (priceList1, priceList2) = companyPriceList(priceList1) == companyPriceList(priceList2) AND
                                                    currencyPriceList(priceList1) == currencyPriceList(priceList2) AND
                                                    datePriceList(priceList1) == datePriceList(priceList2) AND
                                                    countSkuPriceListPriceList (priceList1, priceList2) == countSkusPriceList(priceList1) AND
                                                    countSkuPriceListPriceList (priceList1, priceList2) == countSkusPriceList(priceList2) AND
                                                    priceList1 != priceList2;

//FORM test 'Test'
//    OBJECTS p1 = PriceList FIXED PANEL
//    PROPERTIES(p1) READONLY numberPriceList
//
//    OBJECTS d1 = PriceListDetail
//    PROPERTIES(d1) READONLY idBarcodeSkuPriceListDetail, nameSkuPriceListDetail
//    FILTERS priceListPriceListDetail(d1) == p1
//
//    OBJECTS p2 = PriceList FIXED PANEL
//    PROPERTIES(p2) READONLY numberPriceList
//
//    OBJECTS d2 = PriceListDetail
//    PROPERTIES(d2) READONLY idBarcodeSkuPriceListDetail, nameSkuPriceListDetail
//    FILTERS priceListPriceListDetail(d2) == p2
//;
//
//DESIGN test FROM DEFAULT{
//    NEW topContainer{
//        type = CONTAINERH;
//        NEW leftContainer{
//            type = CONTAINERV;
//            ADD p1.box;
//            ADD d1.box;
//        }
//        NEW rightContainer{
//            type = CONTAINERV;
//            ADD p2.box;
//            ADD d2.box;
//        }
//    }
//    ADD functions.box;
//}

findEqualsPriceLists 'Найти дубликаты прайсов' = ACTION () {
    FOR p1 IS UserPriceList NOINLINE DO {
        FOR equalsPriceListPriceList(p1, p2) NOINLINE DO {
            ASSIGN dataInUserPriceListStock(p2, s) <- dataInUserPriceListStock(p1, s) WHERE dataInUserPriceListStock(p1, s);
            DELETE p1;
            apply();
            BREAK;
        }
    }
}

EXTEND FORM migrationData
    PROPERTIES() findEqualsPriceLists
;