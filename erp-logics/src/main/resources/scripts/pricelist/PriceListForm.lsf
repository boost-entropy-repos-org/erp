MODULE PriceListForm;

REQUIRE PriceListLedger, StockMovement;

NAMESPACE PriceList;
// -------------------------- Текущие цены -------------------- //

FORM currentPrices 'Текущие цены'
    
    OBJECTS pt = PriceListType
    PROPERTIES(pt) READONLY name
     
    OBJECTS s = Stock FIXED PANEL
    PROPERTIES(s) name SELECTOR
    FILTERS isCompany(s)
        
    OBJECTS dt = DATETIME FIXED PANEL
    PROPERTIES(dt) OBJVALUE

    TREE skuTree sg = SkuGroup PARENT parent
    PROPERTIES READONLY order(sg), skuTreeName = name(sg)
    ORDER BY order(sg), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(sg) 'F5' DEFAULT
     
    OBJECTS sk = Sku 
    PROPERTIES(sk) READONLY name ON SHORTCUT showMovementSku(sk, s) ON SHORTCUT showMovementPriceSku(sk, s), idBarcode, id SHOWIF showIDs()
    PROPERTIES(sk, s, dt) READONLY balanceA
    PROPERTIES(pt, sk, s, dt) READONLY priceA, nameCompanyA PANEL, descriptionA PANEL
    ORDER BY name(sk)
    FILTERS isParent(sg, sk),
            priceA(pt, sk, s, dt)
           
    OBJECTS pp = PriceListType
    PROPERTIES(pp) READONLY name, nameCurrency 
    PROPERTIES(pp, sk, s, dt) READONLY priceA
    FILTERS  priceA(pp, sk, s, dt)
    
    TREE skuTree2 sg2 = SkuGroup PARENT parent
    PROPERTIES READONLY order(sg2), skuTreeName2 = name(sg2)
    ORDER BY order(sg2), skuTreeName2
    FILTERGROUP inactive1 FILTER 'Активные' active(sg2) 'F5' DEFAULT

    OBJECTS b = Batch
    PROPERTIES(b) READONLY isPosted, date, time, id SHOWIF showIDs(), nameSku, nameStock, name, cost
    PROPERTIES(b, s, dt) READONLY balanceA
    ORDER BY date(b), time(b)
    PROPERTIES(pt, b, s, dt) READONLY priceA, nameCompanyA PANEL, descriptionA PANEL
    FILTERGROUP filters
    FILTER 'Проведенные партии' isPosted(b) 'F10' DEFAULT
    FILTERS isParent(sg2, b),
            priceA(pt, b, s, dt)
    
    OBJECTS pb = PriceListType
    PROPERTIES(pb) READONLY name, nameCurrency
    PROPERTIES(pb, b, s, dt) READONLY priceA
    FILTERS  priceA(pb, b, s, dt)
;
@extendFormFilterStockAccess(s, currentPrices);

DESIGN currentPrices {
     NEW pane {
         fill = 1;
         type = CONTAINERV;
             NEW firstContainer{
                type = CONTAINERV;
                NEW topContainer{
                    type = CONTAINERH;
                    MOVE s.box;
                    MOVE dt.box{
                        caption = 'Дата/время';
                    }
                }
             }
             NEW rightContainer {
                 fill = 1;
                 type = CONTAINERV;
                 NEW bottomConteiner {
                     fill = 1;
                     type = SPLITH;
                     NEW leftPriceType {
                         type = CONTAINERH;
                         fill = 0.2;
                         MOVE pt.box {
                            caption = 'Виды цен';
                         }
                     }
                     NEW priceContainer{
                         fill = 1;
                         caption = 'Текущие цены';
                         NEW currentPriceContainer {
                             fill = 1;
                             type = TABBED;
                             NEW skuContainer {
                                 fill = 1;
                                 caption = 'Товары';
                                 type = SPLITH;
                                 MOVE skuTree.tree.box;
                                 MOVE sk.box {
                                     fill = 3;
                                     MOVE pp.box;
                                 }
                             }
                             NEW batchContainer {
                                fill = 1;
                                caption = 'Партии';
                                type = SPLITH;
                                MOVE skuTree2.tree.box;
                                MOVE b.box {
                                   fill = 3;
                                   MOVE pb.box;
                                }
                             }
                         }
                     }
                 }   
             }
             
         
     }
     MOVE functions.box;
}

NAVIGATOR {
    priceListDocuments {
        ADD currentPrices;
    }
}