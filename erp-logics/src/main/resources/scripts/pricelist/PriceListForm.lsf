MODULE PriceListForm;

REQUIRE PriceListLedger, StockMovement;

NAMESPACE PriceList;
// -------------------------- Текущие цены -------------------- //

FORM currentPrices 'Текущие цены'
    
    OBJECTS pt = PriceListType
    PROPERTIES(pt) READONLY namePriceListType
     
    OBJECTS s = Stock FIXED PANEL
    PROPERTIES(s) nameStock SELECTOR
    FILTERS isCompanyStock(s)
        
    OBJECTS dt = DATETIME FIXED PANEL
    PROPERTIES(dt) OBJVALUE

    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY orderSkuGroup(sg), skuTreeName = nameSkuGroup(sg)
    ORDER BY orderSkuGroup(sg), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' activeSkuGroup(sg) 'F5' DEFAULT
     
    OBJECTS sk = Sku 
    PROPERTIES(sk) READONLY nameSku ON SHORTCUT showMovementSkuSkuStock(sk, s) ON SHORTCUT showMovementPriceSkuSkuStock(sk, s), idBarcodeSku, idSku SHOWIF showIDs()
    PROPERTIES(sk, s, dt) READONLY balanceASkuStockDateTime
    PROPERTIES(pt, sk, s, dt) READONLY priceAPriceListTypeSkuStockDateTime, nameCompanyALedgerPriceListTypeSkuStockDateTime, descriptionALedgerPriceListTypeSkuStockDateTime
    ORDER BY nameSku(sk)
    FILTERS isParentSkuGroupSku(sg, sk),
            priceAPriceListTypeSkuStockDateTime(pt, sk, s, dt)
           
    OBJECTS pp = PriceListType
    PROPERTIES(pp) READONLY namePriceListType, nameCurrencyPriceListType 
    PROPERTIES(pp, sk, s, dt) READONLY priceAPriceListTypeSkuStockDateTime
    FILTERS  priceAPriceListTypeSkuStockDateTime(pp, sk, s, dt)
    
    TREE skuTree2 sg2 = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY orderSkuGroup(sg2), skuTreeName2 = nameSkuGroup(sg2)
    ORDER BY orderSkuGroup(sg2), skuTreeName2
    FILTERGROUP inactive1 FILTER 'Активные' activeSkuGroup(sg2) 'F5' DEFAULT

    OBJECTS b = Batch
    PROPERTIES(b) READONLY isPostedBatch, dateBatch, timeBatch, idBatch SHOWIF showIDs(), nameSkuBatch, nameStockBatch, nameBatch, costBatch
    PROPERTIES(b, s, dt) READONLY balanceABatchStockDateTime
    ORDER BY dateBatch(b), timeBatch(b)
    PROPERTIES(pt, b, s, dt) READONLY priceAPriceListTypeBatchStockDateTime, nameCompanyALedgerPriceListTypeBatchStockDateTime, descriptionALedgerPriceListTypeBatchStockDateTime
    FILTERGROUP filters
    FILTER 'Проведенные партии' isPostedBatch(b) 'F10' DEFAULT
    FILTERS isParentSkuGroupBatch(sg2, b),
            priceAPriceListTypeBatchStockDateTime(pt, b, s, dt)
    
    OBJECTS pb = PriceListType
    PROPERTIES(pb) READONLY namePriceListType, nameCurrencyPriceListType
    PROPERTIES(pb, b, s, dt) READONLY priceAPriceListTypeBatchStockDateTime
    FILTERS  priceAPriceListTypeBatchStockDateTime(pb, b, s, dt)
;
@extendFormFilterStockAccess(s, currentPrices);

DESIGN currentPrices {
     NEW pane {
         fill = 1;
         type = CONTAINERV;
             NEW firstContainer{
                type = CONTAINERV;
                NEW topContainer{
                    type = CONTAINERH;
                    MOVE s.box;
                    MOVE dt.box{
                        caption = 'Дата/время';
                    }
                }
             }
             NEW rightContainer {
                 fill = 1;
                 type = CONTAINERV;
                 NEW bottomConteiner {
                     fill = 1;
                     type = SPLITH;
                     NEW leftPriceType {
                         type = CONTAINERH;
                         fill = 0.2;
                         MOVE pt.box {
                            caption = 'Виды цен';
                         }
                     }
                     NEW priceContainer{
                         fill = 1;
                         caption = 'Текущие цены';
                         NEW currentPriceContainer {
                             fill = 1;
                             type = TABBED;
                             NEW skuContainer {
                                 fill = 1;
                                 caption = 'Товары';
                                 type = SPLITH;
                                 MOVE skuTree.tree.box;
                                 MOVE sk.box {
                                     fill = 3;
                                     MOVE pp.box;
                                 }
                             }
                             NEW batchContainer {
                                fill = 1;
                                caption = 'Партии';
                                type = SPLITH;
                                MOVE skuTree2.tree.box;
                                MOVE b.box {
                                   fill = 3;
                                   MOVE pb.box;
                                }
                             }
                         }
                     }
                 }   
             }
             
         
     }
     MOVE functions.box;
}

NAVIGATOR {
    priceListDocuments {
        ADD currentPrices;
    }
}