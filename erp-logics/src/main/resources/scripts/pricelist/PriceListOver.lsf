MODULE PriceListOver;

REQUIRE PriceListLedger;

NAMESPACE PriceList;

CLASS  OverPriceListType 'Перегруженный вид цены' : PriceList.PriceListType;
TABLE overPriceListType(OverPriceListType);

name 'Наименование' = DATA VARISTRING[100](OverPriceListType) IN recognize;

baseAPriceListLedger = DATA LedgerPriceListType (OverPriceListType);
nameBaseAPriceListLedger 'Вид цены 1'(OverPriceListType t) = name(baseAPriceListLedger(t));

baseBPriceListLedger = DATA LedgerPriceListType (OverPriceListType);
nameBaseBPriceListLedger 'Вид цены 2' (OverPriceListType t)= name(baseBPriceListLedger(t));

ledgerPriceListType(OverPriceListType type) += baseAPriceListLedger(type); 

priceB = OVERRIDE priceB(baseBPriceListLedger(OverPriceListType type), Sku sku, Stock stock, DATETIME dateTime), 
    priceB(baseAPriceListLedger(type), sku, stock, dateTime);
priceA = OVERRIDE priceA(baseBPriceListLedger(OverPriceListType type), Sku sku, Stock stock, DATETIME dateTime), 
    priceA(baseAPriceListLedger(type), sku, stock, dateTime);

priceB = OVERRIDE priceB(baseBPriceListLedger(OverPriceListType type), Batch batch, Stock stock, DATETIME dateTime), 
    priceB(baseAPriceListLedger(type), batch, stock, dateTime);
priceA = OVERRIDE priceA(baseBPriceListLedger(OverPriceListType type), Batch batch, Stock stock, DATETIME dateTime), 
    priceA(baseAPriceListLedger(type), batch, stock, dateTime);
    
countIncludeVATTypes(OverPriceListType type) = (1 IF includeVAT(baseAPriceListLedger(type))) (+) 
                                                         (1 IF includeVAT(baseBPriceListLedger(type)));    

CONSTRAINT countIncludeVATTypes(OverPriceListType type) == 1 IF type IS OverPriceListType
   MESSAGE 'Вид цены 1 и вид цены 2 должны быть одновременно или с НДС или без НДС' ;  
        
name(OverPriceListType type) += name(type);
includeVAT (OverPriceListType type) += countIncludeVATTypes(type) ==2;

priceB(OverPriceListType type, Sku sku, Stock stock, DATETIME dateTime) += priceB(type, sku, stock, dateTime);
priceA(OverPriceListType type, Sku sku, Stock stock, DATETIME dateTime) += priceA(type, sku, stock, dateTime);

priceB(OverPriceListType type, Batch batch, Stock stock, DATETIME dateTime) += priceB(type, batch, stock, dateTime); 
priceA(OverPriceListType type, Batch batch, Stock stock, DATETIME dateTime) += priceA(type, batch, stock, dateTime);
    
FORM overPriceListType 'Перегруженный вид цены'
    OBJECTS p = OverPriceListType FIXED PANEL
    PROPERTIES(p) name, nameBaseAPriceListLedger, nameBaseBPriceListLedger 

    EDIT OverPriceListType OBJECT p
;
    
FORM overPriceListTypeDialog 'Перегруженный вид цены'
    OBJECTS p = OverPriceListType
    PROPERTIES(p) READONLY name, nameBaseAPriceListLedger, nameBaseBPriceListLedger 

    DIALOG OverPriceListType OBJECT p
;

edit 'Редактировать' = ACTION EDITFORM OverPriceListType;
edit(OverPriceListType priceListType) += ACTION edit(priceListType);

EXTEND FORM priceListTypes
    PROPERTIES(pt) addOverPriceListType = ADDFORM[OverPriceListType]       
;

DESIGN priceListTypes {
    PROPERTY (addOverPriceListType) {
        caption = 'Добавить перегруженную цену';
    }
}

overCopy (OverPriceListType priceListType) += ACTION NEWSESSION {
    IF priceListType IS OverPriceListType THEN {
        FOR ADDOBJ p = OverPriceListType DO {
            baseAPriceListLedger(p) <- baseAPriceListLedger(priceListType);
            baseBPriceListLedger(p) <- baseBPriceListLedger(priceListType);

                                    
            FORM overPriceListType OBJECTS p=p MANAGESESSION DOCKED; 
        }
    }
}


