MODULE PriceListOver;

REQUIRE PriceListLedger;

NAMESPACE PriceList;

CLASS  OverPriceListType 'Перегруженный вид цены' : PriceList.PriceListType;
TABLE overPriceListType(OverPriceListType);

name 'Наименование' = DATA VARISTRING[100](OverPriceListType) IN recognize;

baseAPriceListLedger = DATA LedgerPriceListType (OverPriceListType);
nameBaseAPriceListLedger 'Вид цены 1'(OverPriceListType t) = name(baseAPriceListLedger(t));

baseBPriceListLedger = DATA LedgerPriceListType (OverPriceListType);
nameBaseBPriceListLedger 'Вид цены 2' (OverPriceListType t)= name(baseBPriceListLedger(t));

ledgerPriceListType(OverPriceListType type) += baseAPriceListLedger(type); 

priceB = OVERRIDE priceB(baseAPriceListLedger(type), sku, stock, dateTime), 
    priceB(baseBPriceListLedger(OverPriceListType type), Sku sku, Stock stock, DATETIME dateTime);
priceA = OVERRIDE priceA(baseAPriceListLedger(type), sku, stock, dateTime), 
    priceA(baseBPriceListLedger(OverPriceListType type), Sku sku, Stock stock, DATETIME dateTime);

priceB = OVERRIDE priceB(baseAPriceListLedger(type), batch, stock, dateTime), 
    priceB(baseBPriceListLedger(OverPriceListType type), Batch batch, Stock stock, DATETIME dateTime);
priceA = OVERRIDE priceA(baseAPriceListLedger(type), batch, stock, dateTime), 
    priceA(baseBPriceListLedger(OverPriceListType type), Batch batch, Stock stock, DATETIME dateTime);
    
countIncludeVATTypes(OverPriceListType type) = (1 IF includeVAT(baseAPriceListLedger(type))) (+) 
                                                         (1 IF includeVAT(baseBPriceListLedger(type)));    

CONSTRAINT countIncludeVATTypes(OverPriceListType type) == 1 IF type IS OverPriceListType
   MESSAGE 'Вид цены 1 и вид цены 2 должны быть одновременно или с НДС или без НДС' ;  
        
name(OverPriceListType type) += name(type);
includeVAT (OverPriceListType type) += countIncludeVATTypes(type) ==2;

priceB(OverPriceListType type, Sku sku, Stock stock, DATETIME dateTime) += priceB(type, sku, stock, dateTime);
priceA(OverPriceListType type, Sku sku, Stock stock, DATETIME dateTime) += priceA(type, sku, stock, dateTime);

priceB(OverPriceListType type, Batch batch, Stock stock, DATETIME dateTime) += priceB(type, batch, stock, dateTime); 
priceA(OverPriceListType type, Batch batch, Stock stock, DATETIME dateTime) += priceA(type, batch, stock, dateTime);
    
FORM overPriceListType 'Перегруженный вид цены'
    OBJECTS p = OverPriceListType PANEL
    PROPERTIES(p) name, nameBaseAPriceListLedger, nameBaseBPriceListLedger 

    EDIT OverPriceListType OBJECT p
;
    
FORM overPriceListTypeDialog 'Перегруженный вид цены'
    OBJECTS p = OverPriceListType
    PROPERTIES(p) READONLY name, nameBaseAPriceListLedger, nameBaseBPriceListLedger 

    LIST OverPriceListType OBJECT p
;

edit 'Редактировать' = ACTION EDITFORM OverPriceListType;
edit(OverPriceListType priceListType) += { edit(priceListType); }

EXTEND FORM priceListTypes
    PROPERTIES(pt) NEWSESSION addOverPriceListType = NEW[OverPriceListType]       
;

DESIGN priceListTypes {
    PROPERTY (addOverPriceListType) {
        caption = 'Добавить перегруженную цену';
    }
}

overCopy (OverPriceListType priceListType) += {
	NEWSESSION {
	    IF priceListType IS OverPriceListType THEN {
	        NEW p = OverPriceListType {
	            baseAPriceListLedger(p) <- baseAPriceListLedger(priceListType);
	            baseBPriceListLedger(p) <- baseBPriceListLedger(priceListType);
	
	                                    
	            SHOW overPriceListType OBJECTS p=p MANAGESESSION DOCKED; 
	        }
	    }
	}
}


