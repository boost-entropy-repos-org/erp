MODULE PriceListPromotion;

REQUIRE PriceListOperation, PriceListBefore;

NAMESPACE PriceList;

promotionPriceListOperation = DATA Operation();
namePromotionPriceListOperation 'Операция акции' = name(promotionPriceListOperation());

promotionPriceListType = DATA DataPriceListType();
namePromotionPriceListTypeOperation 'Вид цены акции' = name(promotionPriceListType());


EXTEND FORM options
    PROPERTIES() namePromotionPriceListOperation, namePromotionPriceListTypeOperation 
;
DESIGN options {
    price {
        MOVE PROPERTY(namePromotionPriceListOperation());
        MOVE PROPERTY(namePromotionPriceListTypeOperation());
    }
}   

promotionPriceListDetailA (Sku sku, Stock stock, DATETIME dateTime) =
    lastPriceListDetailA(promotionPriceListType(), sku, stock, dateTime, promotionPriceListOperation());
    
promotionPriceListDetailA (Sku sku, Stock stock) = 
    promotionPriceListDetailA(sku, stock, currentDateTime());
    
inPromotionPriceListDetailA 'В акции' (Sku sku, Stock stock) =
    TRUE IF promotionPriceListDetailA(sku, stock);

promotionPriceBeforeA 'Цена до акции' (Sku sku, Stock stock, DATETIME dateTime) =
    priceBefore(promotionPriceListDetailA(sku, stock, dateTime));

// Добавляем фильтр и подсветку на форму текущих остатков
    
EXTEND FORM currentBalanceSkuStock
    FILTERGROUP promotion
        FILTER 'В акции' promotionPriceListDetailA(s, st)
;

backgroundBalance(s,st) += WHEN promotionPriceListDetailA(s, st) THEN RGB(216,224,255);

//------
CLASS Promotion 'Акция';
TABLE promotion(Promotion);

name 'Наименование акции' = DATA VARISTRING[50](Promotion) IN recognize;
promotionName = GROUP AGGR Promotion promotion BY name(promotion);

inactive 'Неактивная' = DATA BOOLEAN (Promotion);
active 'Активная' (Promotion p) = p IS Promotion AND NOT inactive(p);

FORM dialogPromotions 'Акции' 
    OBJECTS p = Promotion
    PROPERTIES(p) READONLY name
    FILTERGROUP inactivePromotion FILTER 'Активная' active(p) 'shift F10' DEFAULT    
    
    LIST Promotion OBJECT p
;

FORM promotion 'Акция'
    OBJECTS p=Promotion PANEL
    PROPERTIES(p) name
    PROPERTIES inactive(p) AFTER name(p)   
    EDIT Promotion OBJECT p
;

FORM promotions 'Акции'
    OBJECTS p = Promotion
    PROPERTIES(p) READONLY name
    PROPERTIES (p) NEWSESSION NEW, EDIT, del = DELETE GRID 
    FILTERGROUP inactivePromotion FILTER 'Активная' active(p) 'shift F10' DEFAULT    
;

NAVIGATOR {
    priceListMasterData {
        ADD promotions;
    }
}
    
promotion = ABSTRACT Promotion (PriceList);
promotion = DATA Promotion (UserPriceList);

promotion(UserPriceList priceList) += promotion(priceList);

namePromotion 'Акция' = ABSTRACT VARISTRING[50](PriceList);
namePromotion 'Акция' = name(promotion(UserPriceList priceList)); 
namePromotion(UserPriceList priceList) += namePromotion(priceList);

namePromotion 'Акция' = namePromotion(priceList(PriceListDetail detail));

showPromotionPrice 'Акция' = DATA BOOLEAN (PriceList.Operation);
showPromotionPrice 'Акция' (UserPriceList p) = showPromotionPrice(operation(p)) MATERIALIZED;

EXTEND FORM operation PROPERTIES(o) showPromotionPrice;

DESIGN operation {
    showContainer {
        MOVE PROPERTY(showPromotionPrice(o));
    }
}

EXTEND FORM priceLists 
    PROPERTIES (p) READONLYIF isReadonly() namePromotion AFTER nameOperation(p)
;

EXTEND FORM userPriceList 
    PROPERTIES (p) namePromotion SHOWIF showPromotionPrice(p)
;

DESIGN userPriceList{
    first {
        MOVE PROPERTY(namePromotion(p))
        {minimumCharWidth = 30; preferredCharWidth = 40;} 
    }
}