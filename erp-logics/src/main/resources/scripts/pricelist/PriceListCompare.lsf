MODULE PriceListCompare;

REQUIRE PriceList, PriceListDashboard;

//Подсветка строк сравнения прайсов
is (sku, priceList) = GROUP SUM 1 BY sku(PriceListDetail detail), priceList(detail);

price 'Цена' (sku, type, priceList) = GROUP MAX price(PriceListDetail detail, DataPriceListType type)
    BY sku(detail), type, priceList(detail);

backgroundPricelist (Sku sku, PriceList p1, PriceList p2, DataPriceListType type) = 
    CASE WHEN is(sku, p1) AND NOT is(sku, p2) THEN RGB(192,255,192)  
         WHEN is(sku, p2) AND NOT is(sku, p1) THEN RGB(255,192,192)  
         WHEN price(sku, type, p1) != price(sku, type, p2) THEN RGB(255,255,192);  

headerName (PriceList priceList, DataPriceListType dataPriceListType) =
    (name[PriceListType](dataPriceListType) + ' (№' + number(priceList) + ')') IF in(priceList, dataPriceListType) CHARWIDTH 30;    

EXTEND FORM userPriceList
    OBJECTS p2 = UserPriceList PANEL
    PROPERTIES (p2) SELECTOR seriesNumber, date, nameCurrency, nameCompany
    FILTERS p != p2
    
    OBJECTS s3=Sku
    PROPERTIES BACKGROUND backgroundPricelist(s3, p, p2, t) READONLY inputName3 = name(s3), idBarcode(s3), shortNameUOM(s3)
    PROPERTIES(s3, t, p) READONLY price COLUMNS (t) HEADER headerName(p, t) SHOWIF headerName(p, t) BACKGROUND backgroundPricelist(s3, p, p2, t) TODRAW s3     
    PROPERTIES(s3, t, p2) READONLY price COLUMNS (t) HEADER headerName(p2, t) SHOWIF headerName(p2, t) BACKGROUND backgroundPricelist(s3, p, p2, t) TODRAW s3     
    ORDER inputName3
    FILTERS is(s3, p) OR is(s3, p2),
            company(p) == company(p2)
    FILTERGROUP compareFilter
        FILTER 'Удаленные' is(s3, p2) AND NOT is(s3, p) 'F9' 
        FILTER 'Добавленные' is(s3, p) AND NOT is(s3, p2) 'F10' 
    FILTERGROUP differentPricesFilter
        FILTER 'Различные цены' price(s3, t, p) != price(s3, t, p2) 'F11' 
;

DESIGN userPriceList{
    detailContainer{
        NEW fifthContainer{
            fill = 1;
            caption = 'Сравнение';
            MOVE BOX(p2){
                type = CONTAINERH;
                MOVE PROPERTY (seriesNumber(p2));
                MOVE PROPERTY (date(p2));
                MOVE PROPERTY (nameCurrency(p2));
                MOVE PROPERTY (nameCompany(p2));
            }
            MOVE BOX(s3);
        }
    }
}


percent 'Отклонение цены, %' (PriceListType pa,PriceListType pb,Sku sk,Stock sa,Stock sb,DATETIME da,DATETIME db)  = NUMERIC[20,2]((priceB(pb,sk,sb,db) - priceB(pa,sk,sa,da))*100.00/
                                                                           priceB(pb,sk,sb,db));

backgroundPercent  = IF percent(PriceListType pa,PriceListType pb,Sku sk,Stock sa,Stock sb,DATETIME da,DATETIME db) < 0 THEN RGB(255,100,100) 
                                   ELSE IF percent(pa,pb,sk,sa,sb,da,db)> 0 THEN RGB(100,255,100);
backgroundCompareA = RGB(250,225,225);
backgroundCompareB = RGB(225,225,250);
isDifferent(a,b) = IF a != b THEN TRUE;

FORM comparePriceList 'Сравнение цен '
    OBJECTS pa = PriceListType PANEL, pb = PriceListType PANEL,sa=Stock PANEL, sb=Stock PANEL  
    PROPERTIES SELECTOR BACKGROUND backgroundCompareA() name(pa), name(sa)
    PROPERTIES SELECTOR BACKGROUND backgroundCompareB() name(pb), name(sb)
    FILTERS active(sa), isCompany(sa), active(sb), isCompany(sb)    
    
    OBJECTS da = DATETIME 'Дата' PANEL, db = DATETIME 'Дата' PANEL 
    PROPERTIES VALUE (da) BACKGROUND backgroundCompareA(), VALUE (db) BACKGROUND backgroundCompareB()     
    
    OBJECTS           t=GroupType PANEL 
    PROPERTIES(t)     SELECTOR name        
        
    TREE skuTree g = Group PARENT parent
    PROPERTIES READONLY order(g), name(g)
    ORDER order(g), name(g)
    FILTERS groupType(g) == t
    FILTERGROUP inactive FILTER 'Активные' active(g) 'F5' DEFAULT 

    OBJECTS sk = Sku GRID 
    PROPERTIES READONLY idBarcode(sk),name(sk)
    PROPERTIES READONLY BACKGROUND backgroundCompareA() currentBalance(sk,sa), dateTimeLastOrderBatch(sk,sa), priceB(pa,sk,sa,da)
    PROPERTIES READONLY BACKGROUND backgroundCompareB() currentBalance(sk,sb) SHOWIF isDifferent(sa,sb),dateTimeLastOrderBatch(sk,sb) SHOWIF isDifferent(sa,sb),
                                                        priceB(pb,sk,sb,db)
    PROPERTIES READONLY percent(pa,pb,sk,sa,sb,da,db) BACKGROUND backgroundPercent(pa,pb,sk,sa,sb,da,db)
    FILTERS priceB(pa,sk,sa,da) OR priceB(pb,sk,sb,db), isParent(g,sk)
    FILTERGROUP ostFilter
        FILTER 'Есть остаток' currentBalance(sk,sa) OR currentBalance(sk,sb) 'F8' 
    FILTERGROUP compareFilter
        FILTER 'Первая цена выше' percent(pa,pb,sk,sa,sb,da,db) < 0 'F9' 
        FILTER 'Вторая цена выше' percent(pa,pb,sk,sa,sb,da,db) > 0 'F10'
;

DESIGN comparePriceList {
    NEW top FIRST{
        NEW param  {
            type = COLUMNS ;
            columns = 2;
            MOVE BOX(pa);
            MOVE BOX(pb);
            MOVE BOX(sa);
            MOVE BOX(sb);
            MOVE BOX(da);
            MOVE BOX(db);
        } 
    }
    NEW bottom AFTER top {
        fill = 1;
        type = SPLITH;
        NEW leftBottom {
            fill=2;
            MOVE BOX(t);
            MOVE BOX(TREE skuTree);
        }
        NEW rightBottom {
            fill = 5;
            MOVE BOX(sk);
        }
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    priceListDashboardNavigator {
        NEW comparePriceList;
    }
}