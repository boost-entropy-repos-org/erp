MODULE PriceListBefore;

REQUIRE PriceList, PriceListOperation;

NAMESPACE PriceList;


inBefore 'Изменять цены (до)' = ABSTRACT BOOLEAN (PriceList, DataPriceListType) MATERIALIZED;
inBefore 'Изменять цены (до)' = DATA BOOLEAN (UserPriceList, DataPriceListType);
inBefore (UserPriceList priceList, DataPriceListType dataPriceListType) += inBefore(priceList, dataPriceListType);

countBeforeDataPriceListTypes = GROUP SUM 1 IF inBefore(PriceList p,DataPriceListType t) BY p;
countBeforeDataPriceListTypes = GROUP SUM 1 IF inBefore(UserPriceList p,DataPriceListType t) BY p;

changeBefore 'Изменять цены (до)' = DATA BOOLEAN (DataPriceListType, Operation);

inBefore(UserPriceList priceList, DataPriceListType dataPriceListType) <- changeBefore(dataPriceListType, operation(priceList))
    WHEN CHANGED(operation(priceList));
    
EXTEND FORM operation
    PROPERTIES(pt, o) changeBefore TODRAW pt AFTER show(pt, o)
    
;

priceBefore 'Цена до' = ABSTRACT NUMERIC[16,4] (PriceListDetail);
priceBefore 'Цена до' = DATA NUMERIC[16,4] (UserPriceListDetail);
priceBefore(UserPriceListDetail d) += priceBefore(d);

priceListTypeBefore 'Вид цены для цены до' = DATA DataPriceListType (UserPriceList);

showPriceBefore 'Цена до' =  ABSTRACT BOOLEAN (PriceList);   
showPriceBefore 'Цена до' =  DATA BOOLEAN (UserPriceList);  
showPriceBefore (UserPriceListDetail d) = showPriceBefore(userPriceList(d));
showPriceBefore(UserPriceList p) += showPriceBefore(p);

EXTEND FORM userPriceList 
    PROPERTIES(p) showPriceBefore
    PROPERTIES(p,t)  inBefore[UserPriceList,DataPriceListType] SHOWIF showPriceBefore(p)        
    PROPERTIES(d) priceBefore SHOWIF showPriceBefore(p) BEFORE viewPrice(d,to)
    ;
    
DESIGN userPriceList {
    param{
        MOVE PROPERTY (showPriceBefore(p));
    }
}

priceListTypeBefore 'Вид цены для цены до' = DATA DataPriceListType (PriceList.Operation);
   
showPriceBefore 'Цена до' = DATA BOOLEAN (PriceList.Operation);
showPriceBefore(UserPriceList priceList) <- showPriceBefore(operation(priceList))
    WHEN CHANGED(operation(priceList));
skipConstraintPriceBefore 'Может быть не задана Цена до' = DATA BOOLEAN (PriceList.Operation);
    
EXTEND FORM operation PROPERTIES(o) showPriceBefore, skipConstraintPriceBefore;
DESIGN operation {
    showContainer {
        MOVE PROPERTY(showPriceBefore(o));
        MOVE PROPERTY(skipConstraintPriceBefore(o));
    }
}

@defineDocumentInterfaceHeaderTimePrefix(PriceList, fromPriceBefore, ' c (Цена до)');
@deriveDocumentHeaderTimePrefix(UserPriceList, fromPriceBefore);

@defineDocumentInterfaceHeaderTimePrefix(PriceList, toPriceBefore, ' по (Цена до)');
@deriveDocumentHeaderTimeBeforePrefix(UserPriceList, toPriceBefore);

CONSTRAINT showPriceBefore(UserPriceList p) AND isPosted(p) AND
            ((fromPriceBeforeDateTime(p) >= fromDateTime(p) AND fromPriceBeforeDateTime(p) <= toDateTime(p)) OR
            (toPriceBeforeDateTime(p) >= fromDateTime(p) AND toPriceBeforeDateTime(p) <= toDateTime(p))) 
    MESSAGE 'Дата с (Цена до) и Дата по (Цена до)не могут быть в интервале самого прайса.';
    
CONSTRAINT showPriceBefore(UserPriceList p) AND isPosted(p) AND NOT skipConstraintPriceBefore(operation(p)) AND 
            (NOT fromPriceBeforeDateTime(p) OR NOT toPriceBeforeDateTime(p)) AND [= GROUP SUM 1 IF priceBefore(UserPriceListDetail d) BY userPriceList(d)](p)
    MESSAGE 'Дата с (Цена до) и Дата по (Цена до) должны быть заданы для прайса';
    
CONSTRAINT showPriceBefore(userPriceList(UserPriceListDetail d)) AND isPosted(userPriceList(d))
            AND NOT skipConstraintPriceBefore(operation(d)) AND NOT priceBefore(d)
    MESSAGE 'Цена до должна быть задана для строки прайса.';
    
EXTEND FORM userPriceList
    PROPERTIES (p) SHOWIF showPriceBefore(p) fromPriceBeforeDate, fromPriceBeforeTime, toPriceBeforeDate, toPriceBeforeTime
;

DESIGN userPriceList{
    midContainer {
        type = CONTAINERV;
        NEW timePriceBeforeContainer{
            caption = 'Период действия (Цена до)';
            type = CONTAINERH;
            MOVE PROPERTY (fromPriceBeforeDate(p));
            MOVE PROPERTY (fromPriceBeforeTime(p));
            MOVE PROPERTY (toPriceBeforeDate(p));
            MOVE PROPERTY (toPriceBeforeTime(p));
        }
    }
}

CLASS PriceListBeforeLedger : PriceListLedger;
TABLE priceListBeforeLedger(PriceListBeforeLedger);

needPriceBefore(PriceListDetail d) = priceBefore(d) AND countBeforeDataPriceListTypes(priceList(d))
    AND fromPriceBeforeDateTime(priceList(d)) AND toPriceBeforeDateTime(priceList(d));

@defineAggregation(priceListDetail, priceListBeforeLedger, needPriceBefore);

in(PriceListBeforeLedger l, DataPriceListType t) += inBefore(priceList(priceListDetail(l)), t);

description(PriceListBeforeLedger l) += description[UserPriceList](priceList(priceListDetail(l))) + ' (цена до)' ;

price(PriceListBeforeLedger l, DataPriceListType t) += priceBefore(priceListDetail(l)) IF inBefore(priceList(priceListDetail(l)), t);

fromDateTime(PriceListBeforeLedger l) += fromPriceBeforeDateTime(priceList(priceListDetail(l)));
toDateTime(PriceListBeforeLedger l) += toPriceBeforeDateTime(priceList(priceListDetail(l)));

isPosted(PriceListBeforeLedger l) += isPosted(priceList(priceListDetail(l)));

sku(PriceListBeforeLedger l) += sku(priceListDetail(l));

company(PriceListBeforeLedger l) += company(priceList(priceListDetail(l)));

in(PriceListBeforeLedger l,Stock st) += in(priceList(priceListDetail(l)),st);

// Расчет цены до для склада и sku
lastBeforePriceListLedger 'Цена' (type, sku, stock, dateTime) =
    GROUP LAST PriceListBeforeLedger ledger
          BY LedgerPriceListType type, sku(ledger, type, Stock stock), stock, DATETIME dateTime
          ORDER fromDateTime(ledger, type, stock), ledger
          WHERE active(ledger, type, stock) AND NOT toDateTime(ledger, type, stock) < dateTime AND ledger IS PriceListBeforeLedger;

priceBefore 'Цена до' (LedgerPriceListType type, Sku sku, Stock stock, DATETIME dt) = priceBefore(priceListDetail(lastBeforePriceListLedger(type, sku, stock, dt)));