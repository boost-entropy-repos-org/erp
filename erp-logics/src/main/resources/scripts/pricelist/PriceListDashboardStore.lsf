MODULE PriceListDashboardStore;

REQUIRE PriceListDashboard, StoreSkuLedger, PriceListStore;

// -------------------- В прайсе ----------------- //

// по организациям
countDepartmentStoreA 'В прайсе' (type, sku, store, company, dateTime) =
    GROUP SUM 1 IF priceA(LedgerPriceListType type, Sku sku, DepartmentStore stock, LegalEntity company, DATETIME dateTime)
    BY type, sku, store(stock), company, dateTime;

countStoreA 'В прайсе' (type, sku, chainStores, company, dateTime) =
    GROUP SUM 1 IF countDepartmentStoreA(LedgerPriceListType type, Sku sku, Store store, LegalEntity company, DATETIME dateTime)
    BY type, sku, chainStores(store), company, dateTime;

countStoreA 'В прайсе' (type, sku, storeType, company, dateTime) =
    GROUP SUM 1 IF countDepartmentStoreA(LedgerPriceListType type, Sku sku, Store store, LegalEntity company, DATETIME dateTime)
    BY type, sku, storeType(store), company, dateTime;

countStoreA 'В прайсе' (type, sku, company, dateTime) =
    GROUP SUM 1 IF countDepartmentStoreA(LedgerPriceListType type, Sku sku, Store store, LegalEntity company, DATETIME dateTime)
    BY type, sku, company, dateTime;

// по складам
countDepartmentStoreA 'В прайсе' (type, sku, store, dateTime) =
    GROUP SUM 1 IF priceA(LedgerPriceListType type, Sku sku, DepartmentStore stock, DATETIME dateTime)
    BY type, sku, store(stock), dateTime;

countStoreA 'В прайсе' (type, sku, chainStores, dateTime) =
    GROUP SUM 1 IF countDepartmentStoreA(LedgerPriceListType type, Sku sku, Store store, DATETIME dateTime)
    BY type, sku, chainStores(store), dateTime;

countStoreA 'В прайсе' (type, sku, storeType, dateTime) =
    GROUP SUM 1 IF countDepartmentStoreA(LedgerPriceListType type, Sku sku, Store store, DATETIME dateTime)
    BY type, sku, storeType(store), dateTime;

countStoreA 'В прайсе' (type, sku, dateTime) =
    GROUP SUM 1 IF countDepartmentStoreA(LedgerPriceListType type, Sku sku, Store store, DATETIME dateTime)
    BY type, sku, dateTime;

// --------------------- В наличии ---------------- //

countStoreA 'В наличии' (sku, storeType, dateTime) =
    GROUP SUM 1 IF balanceA(Sku sku, Store store, DATETIME dateTime)
    BY sku, storeType(store), dateTime;

countStoreA 'В наличии' (sku, chainStores, dateTime) =
    GROUP SUM 1 IF balanceA(Sku sku, Store store, DATETIME dateTime)
    BY sku, chainStores(store), dateTime;

EXTEND FORM priceListDashboard
    // --- По организациям
    PROPERTIES READONLY countStoreA(pt, csk, c, dt) AFTER idBarcode(csk)

    OBJECTS ccs = ChainStores
    PROPERTIES READONLY name(ccs), countStore(ccs), countStoreA(pt, csk, ccs, c, dt), countStoreA(csk, ccs, dt)
    FILTERS countStoreA(pt, csk, ccs, c, dt)

    OBJECTS ctp=StoreType
    PROPERTIES READONLY name(ctp), countStore(ctp), countStoreA(pt, csk, ctp, c, dt), countStoreA(csk, ctp, dt)
    FILTERS countStoreA(pt, csk, ctp, c, dt)

    // --- По складам
    PROPERTIES READONLY countStoreA(pt, ssk, dt) AFTER idBarcode(ssk)

    OBJECTS scs = ChainStores
    PROPERTIES READONLY name(scs), countStore(scs), countStoreA(pt, ssk, scs, dt), countStoreA(ssk, scs, dt)
    FILTERS countStoreA(pt, ssk, scs, dt)

    OBJECTS stp=StoreType
    PROPERTIES READONLY name(stp), countStore(stp), countStoreA(pt, ssk, stp, dt), countStoreA(ssk, stp, dt)
    FILTERS countStoreA(pt, ssk, stp, dt)
;

DESIGN priceListDashboard {
    c.stocks {
        MOVE ccs.box BEFORE cst.box;
        MOVE ctp.box BEFORE cst.box;
    }
    s.stocks {
        MOVE scs.box BEFORE sst.box;
        MOVE stp.box BEFORE sst.box;
    }
}

// ------------------------------- Добавляем розничный вид цены ----------------------------------- //

activeMarkupACompany 'Надбавка, % (управленческая)' (LedgerPriceListType pt, Sku s, DepartmentStore d, LegalEntity c, DATETIME dt) =
    retailPriceA (s, d, dt) * 10000.0 /
    ((valueRate(VAT(s, d), DATE(dt)) + 100.0) *
     activePriceA (pt, s, c, dt) IF activePriceA (pt, s, c, dt) != 0)
    - 100.0;

markupAStock 'Надбавка, % (управленческая)' (LedgerPriceListType pt, Sku s, DepartmentStore d, DATETIME dt) =
    retailPriceA (s, d, dt) * 10000.0 /
    ((valueRate(VAT(s, d), DATE(dt)) + 100.0) *
     priceA (pt, s, d, dt) IF priceA (pt, s, d, dt) != 0)
    - 100.0;


EXTEND FORM priceListDashboard
    PROPERTIES TODRAW ssk READONLY AFTER sPrice
            markupAStock(pt, ssk, s, dt),
            sRetailPrice = retailPriceA(ssk, s, dt),
            markup(ssk, s),            
            markupPriceA(pt, ssk, s, dt)
;

DESIGN priceListDashboard {
    PROPERTY(sRetailPrice) {
        caption = 'Розничная цена (управленческая)';
    }
}

// -------------------- Изменение согласованных и розничных цен одновременно --------------------------- //
createRetailCompany(LegalEntity company, DATETIME dateTime, PriceListType dataPriceListType, Stock departmentStore) = ACTION {
    NEWSESSION NESTED (in[Sku]) {
        FOR NEW u = UserPriceList DO {
            fillCompanyPriceList(u, company, dateTime, dataPriceListType, departmentStore);
    
            in(u, DataPriceListType t) <- TRUE WHERE t == retailPriceListType(departmentStore);
            show(u, PriceListType t) <- TRUE WHERE t == retailPriceListType(departmentStore);
    
            SHOW userPriceList OBJECTS p = u MANAGESESSION DOCKED;
            in(Sku s) <- NULL;
        }
    }
}

createRetailPricesCompany(LegalEntity company, DATETIME dateTime, PriceListType dataPriceListType, Stock departmentStore) = ACTION {
    NEWSESSION NESTED (in[Sku]) {
        FOR NEW u = UserPriceList DO {
            fillCompanyPriceList(u, company, dateTime, dataPriceListType, departmentStore);
            price[UserPriceListDetail,DataPriceListType](UserPriceListDetail d, dataPriceListType) <-
                priceA[LedgerPriceListType,Sku,Stock,DATETIME](dataPriceListType, sku(d), departmentStore, dateTime)
                WHERE userPriceList(d) == u;
    
            in(u, DataPriceListType t) <- TRUE WHERE t == retailPriceListType(departmentStore);
            show(u, PriceListType t) <- TRUE WHERE t == retailPriceListType(departmentStore);
            price(UserPriceListDetail d, DataPriceListType t) <-
                priceA(t, sku(d), departmentStore, dateTime)
                WHERE userPriceList(d) == u AND t == retailPriceListType(departmentStore);
    
            SHOW userPriceList OBJECTS p = u MANAGESESSION DOCKED;
            in(Sku s) <- NULL;
        }
    }
}

// Изменение цен
modifyRetailPricesData 'Изменить'(DATETIME dateTime, LedgerPriceListType dataPriceListType, Stock stock) = ACTION {
    FOR [= GROUP SUM 1 IF in(Sku s) BY companyA(LedgerPriceListType t, s, Stock st, DATETIME d), t, st, d] (LegalEntity company, dataPriceListType, stock, dateTime) DO {
        createRetailPricesCompany(company, dateTime, dataPriceListType, stock);
    }
}

// Исключение из ассортимента
excludeRetailData 'Исключить'(DATETIME dateTime, LedgerPriceListType dataPriceListType, Stock stock) = ACTION {
    FOR [= GROUP SUM 1 IF in(Sku s) BY companyA(LedgerPriceListType t, s, Stock st, DATETIME d), t, st, d] (LegalEntity company, dataPriceListType, stock, dateTime) DO {
        createRetailCompany(company, dateTime, dataPriceListType, stock);
    }
}

EXTEND FORM priceListDashboard
    PROPERTIES TODRAW ssk FORCE PANEL modifyRetailPricesData(dt, pt, s),
                                      excludeRetailData(dt, pt, s)
;

DESIGN priceListDashboard {
    stockActionsContainer {
        NEW primaryRetailStockActions {
            type = CONTAINERH;
            caption = 'Согласованные и розничные цены';
            MOVE PROPERTY(modifyRetailPricesData(dt, pt, s));
            MOVE PROPERTY(excludeRetailData(dt, pt, s));
        }
    }
}

// ------------------------------- Изменение розничных цен по складу -------------------------- //

modifyRetailData 'Изменить'(DATETIME dateTime, PriceListType dataPriceListType, Stock departmentStore) = ACTION {

    NEWSESSION NESTED (in[Sku]) {
        FOR NEW u = UserPriceList DO {
            fromDate(u) <- toDate(dateTime);
            fromTime(u) <- toTime(dateTime);
            company(u) <- legalEntity(departmentStore);
            currency(u) <- currency(retailPriceListType(departmentStore));
            in(u, DataPriceListType t) <- TRUE WHERE t == retailPriceListType(departmentStore);
            show(u, PriceListType t) <- TRUE WHERE t == retailPriceListType(departmentStore);
            show(u, dataPriceListType) <- TRUE;
            dataIn(u, departmentStore) <- TRUE;
            FOR in(Sku sku)
                NEW d = UserPriceListDetail DO {
                sku(d) <- sku;
                userPriceList(d) <- u;
                price(d, DataPriceListType t) <-
                            priceA(t, sku(d), departmentStore, dateTime) WHERE t == retailPriceListType(departmentStore);
            }
    
            SHOW userPriceList OBJECTS p = u MANAGESESSION DOCKED;
            in(Sku s) <- NULL;
        }
    }
}

EXTEND FORM priceListDashboard
    PROPERTIES TODRAW csk FORCE PANEL
                                      modifyRetailData(dt, pt, s)
;

DESIGN priceListDashboard {
    stockActionsContainer {
        NEW retailStockActions {
            caption = 'Розничные цены';
            MOVE PROPERTY(modifyRetailData(dt, pt, s));
        }
    }
}
