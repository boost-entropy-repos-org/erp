MODULE PriceListManageStore;

REQUIRE PriceListManage, Store;

// -------------------- В ассортименте ----------------- //

countDepartmentStorePriceListTypeSkuStoreDateTime 'В ассортименте' (type, sku, store, dateTime) =
    GROUP SUM 1 IF pricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime)
    BY type, sku, storeDepartmentStore(stock), dateTime;

countStorePriceListTypeSkuChainStoresDateTime 'В ассортименте' (type, sku, chainStores, dateTime) =
    GROUP SUM 1 IF countDepartmentStorePriceListTypeSkuStoreDateTime(type, sku, store, dateTime)
    BY type, sku, chainStoresStore(store), dateTime;

countStorePriceListTypeSkuStoreTypeDateTime 'В ассортименте' (type, sku, storeType, dateTime) =
    GROUP SUM 1 IF countDepartmentStorePriceListTypeSkuStoreDateTime(type, sku, store, dateTime)
    BY type, sku, storeTypeStore(store), dateTime;

countStorePriceListTypeSkuDateTime 'В ассортименте' (type, sku, dateTime) =
    GROUP SUM 1 IF countDepartmentStorePriceListTypeSkuStoreDateTime(type, sku, store, dateTime)
    BY type, sku, dateTime;

// --------------------- В наличии ---------------- //

countStoresSkuStoreTypeDateTime 'В наличии' (sku, storeType, dateTime) =
    GROUP SUM 1 IF balanceASkuStoreDateTime(sku, store, dateTime)
    BY sku, storeTypeStore(store), dateTime;

countStoreSkuChainStoresDateTime 'В наличии' (sku, chainStores, dateTime) =
    GROUP SUM 1 IF balanceASkuStoreDateTime(sku, store, dateTime)
    BY sku, chainStoresStore(store), dateTime;

EXTEND FORM priceListManage
    // --- По организациям
    PROPERTIES READONLY countStorePriceListTypeSkuDateTime(pt, csk, dt) AFTER idBarcodeSku(csk)

    OBJECTS ccs = ChainStores
    PROPERTIES READONLY nameChainStores(ccs), countStoreChainStores(ccs), countStorePriceListTypeSkuChainStoresDateTime(pt, csk, ccs, dt), countStoreSkuChainStoresDateTime(csk, ccs, dt)
    FILTERS countStorePriceListTypeSkuChainStoresDateTime(pt, csk, ccs, dt)

    OBJECTS ctp=StoreType
    PROPERTIES READONLY nameStoreType(ctp), countStoreStoreType(ctp), countStorePriceListTypeSkuStoreTypeDateTime(pt, csk, ctp, dt), countStoresSkuStoreTypeDateTime(csk, ctp, dt)
    FILTERS countStorePriceListTypeSkuStoreTypeDateTime(pt, csk, ctp, dt)

    // --- По складам
    PROPERTIES READONLY countStorePriceListTypeSkuDateTime(pt, ssk, dt) AFTER idBarcodeSku(ssk)

    OBJECTS scs = ChainStores
    PROPERTIES READONLY nameChainStores(scs), countStoreChainStores(scs), countStorePriceListTypeSkuChainStoresDateTime(pt, ssk, scs, dt), countStoreSkuChainStoresDateTime(ssk, scs, dt)
    FILTERS countStorePriceListTypeSkuChainStoresDateTime(pt, ssk, scs, dt)

    OBJECTS stp=StoreType
    PROPERTIES READONLY nameStoreType(stp), countStoreStoreType(stp), countStorePriceListTypeSkuStoreTypeDateTime(pt, ssk, stp, dt), countStoresSkuStoreTypeDateTime(ssk, stp, dt)
    FILTERS countStorePriceListTypeSkuStoreTypeDateTime(pt, ssk, stp, dt)
;

EXTEND DESIGN priceListManage {
    c.stocks {
        ADD ccs.box BEFORE cst.box;
        ADD ctp.box BEFORE cst.box;
    }
    s.stocks {
        ADD scs.box BEFORE sst.box;
        ADD stp.box BEFORE sst.box;
    }
}

// ------------------------------- Добавляем розничный вид цены ----------------------------------- //

showRetailPrice (p, r) = p != r;

activeMarkupLedgerPriceListTypeLedgerPriceListTypeSkuCompanyDateTime 'Надбавка, %' (rt, pt, s, c, dt) =
    activePriceLedgerPriceListTypeSkuCompanyDateTime (rt, s, c, dt) * 100.0 /
    (activePriceLedgerPriceListTypeSkuCompanyDateTime (pt, s, c, dt) IF activePriceLedgerPriceListTypeSkuCompanyDateTime (pt, s, c, dt) != 0) - 100.0;

markupPriceListTypePriceListTypeSkuStockDateTime 'Надбавка, %' (rt, pt, sk, st, dt) =
    pricePriceListTypeSkuStockDateTime (rt, sk, st, dt) * 100.0 /
    (pricePriceListTypeSkuStockDateTime (pt, sk, st, dt) IF pricePriceListTypeSkuStockDateTime (pt, sk, st, dt) != 0) - 100.0;


EXTEND FORM priceListManage
    OBJECTS rt = DataPriceListType FIXED PANEL
    PROPERTIES(rt) SELECTOR namePriceListType

    PROPERTIES TODRAW csk READONLY SHOWIF showRetailPrice(pt, rt) AFTER cPrice
            cRetailPrice = activePriceLedgerPriceListTypeSkuCompanyDateTime(rt, csk, c, dt),
            activeMarkupLedgerPriceListTypeLedgerPriceListTypeSkuCompanyDateTime(rt, pt, csk, c, dt)
    PROPERTIES(rt, csk, cst, dt) TODRAW cst READONLY cRetailPriceStock = pricePriceListTypeSkuStockDateTime AFTER cPriceStock SHOWIF showRetailPrice(pt, rt)

    PROPERTIES TODRAW ssk READONLY SHOWIF showRetailPrice(pt, rt) AFTER sPrice
            sRetailPrice = pricePriceListTypeSkuStockDateTime(rt, ssk, s, dt),
            markupPriceListTypePriceListTypeSkuStockDateTime(rt, pt, ssk, s, dt)
    PROPERTIES(rt, ssk, sst, dt) TODRAW sst READONLY sRetailPriceStock = pricePriceListTypeSkuStockDateTime AFTER sPriceStock SHOWIF showRetailPrice(pt, rt)
;

EXTEND DESIGN priceListManage {
    params {
        ADD rt.box {
            caption = 'Розничный вид цены';
        }
    }
    PROPERTY(cRetailPrice) {
        caption = 'Розничная цена';
    }
    PROPERTY(cRetailPriceStock) {
        caption = 'Розничная цена';
    }
    PROPERTY(sRetailPrice) {
        caption = 'Розничная цена';
    }
    PROPERTY(sRetailPriceStock) {
        caption = 'Розничная цена';
    }
}

createCompanyRetailPriceList 'Создать розничный прайс' = ACTION (dateTime, dataPriceListType, retailPriceListType, company) {
    FOR ADDOBJ u = UserPriceList DO {
        fillCompanyPriceList(u, dateTime, dataPriceListType, company);
        SET inUserPriceListDataPriceListType(u, retailPriceListType) <- TRUE;
        SET showUserPriceListPriceListType(u, retailPriceListType) <- TRUE;
        FORM userPriceList OBJECTS p = u MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

createNewCompanyRetailPriceList 'Создать розничный прайс' = ACTION (dateTime, dataPriceListType, retailPriceListType, company) {
    inSkuCustomUser(s, u) <- inSku(s) WHERE u == currentUser();
    apply();
    createCompanyRetailPriceList(dateTime, dataPriceListType, retailPriceListType, company);
} TOOLBAR;

EXTEND FORM priceListManage
    PROPERTIES createNewCompanyRetailPriceList(dt, pt, rt, c) TODRAW csk FORCE PANEL SHOWIF showRetailPrice(pt, rt)
;
