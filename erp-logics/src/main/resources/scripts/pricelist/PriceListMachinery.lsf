MODULE PriceListMachinery;

REQUIRE PriceList, PriceListOperation, MachineryPriceTransaction;

NAMESPACE PriceList;

@Machinery.implementPriceTransactionDocument(PriceList, company);
createPriceListMachinery 'Загрузить в оборудование'(PriceTransactionDocument priceList) = ACTION  NEWSESSION {
    ASSIGN createMachineryPriceTransaction(Sku sku) <- TRUE IF countPriceListDetail(sku, priceList);
    ASSIGN createMachineryPriceTransactionDocument() <- priceList;
    FOR in(priceList, Stock stock) DO {
        EXEC createMachineryPriceTransaction(stock);   
    }            
    ASSIGN sent(priceList) <- TRUE;
    
    apply();
} TOOLBAR CONFIRM;

createSnapshotPriceListMachinery 'Перегрузить все позиции'(PriceTransactionDocument priceList) = ACTION  NEWSESSION {
    createMachineryPriceTransactionSnapshot() <- TRUE;
    createMachineryPriceTransaction(Sku sku) <- TRUE IF countPriceListDetail(sku, priceList);
    createMachineryPriceTransactionDocument() <- priceList;
    FOR in(priceList, Stock stock) DO {
        createMachineryPriceTransaction(stock);   
    }  
    sent(priceList) <- TRUE;
    apply();
} CONFIRM;

showCreateMachineryPriceTransaction 'Показывать' (PriceList priceList) = isPosted(priceList) AND NOT skip(priceList)
                                                                                                       AND NOT sent(priceList);
backgroundCreateMachineryPriceTransaction 'Цвет' (PriceTransactionDocument priceList) = IF countProcess(priceList) THEN
                                                                            RGB(255,255,128)
                                                                         ELSE
                                                                            RGB(212,255,212) IF showCreateMachineryPriceTransaction[PriceList](priceList);

EXTEND FORM priceLists
    PROPERTIES(p) statusMachinery ON SHORTCUT createSnapshotPriceListMachinery(p) BACKGROUND backgroundCreateMachineryPriceTransaction(p) READONLY AFTER stocks(p),
                  createPriceListMachinery FORCE PANEL SHOWIF showCreateMachineryPriceTransaction(p)
;
DESIGN priceLists {
    actionContainer {
        NEW machineryContainer {
            type = CONTAINERV;               
            caption = 'Загрузки' ;
            MOVE PROPERTY (createPriceListMachinery(p));
            
        }
    }
}