MODULE PriceListMachinery;

REQUIRE PriceList, MachineryPriceTransaction;

NAMESPACE PriceList;

@Machinery.implementPriceTransactionDocument(PriceList);
createPriceListMachineryPriceTransaction 'Загрузить в оборудование' = ACTION (priceList) NEWSESSION {
    ASSIGN createMachineryPriceTransactionSku(sku) <- TRUE IF countPriceListDetailSkuPriceList(sku, priceList);
    ASSIGN createMachineryPriceTransactionDocument() <- priceList;
    FOR inPriceListStock(priceList, stock) DO {
        EXEC createMachineryPriceTransaction(stock);   
    }            
    ASSIGN sentPriceTransactionDocument(priceList) <- TRUE;
    
    apply();
} TOOLBAR CONFIRM;

createSnapshotPriceListMachineryPriceTransaction 'Перегрузить все позиции' = ACTION (priceList) NEWSESSION {
    createMachineryPriceTransactionSnapshot() <- TRUE;
    createMachineryPriceTransactionSku(sku) <- TRUE IF countPriceListDetailSkuPriceList(sku, priceList);
    createMachineryPriceTransactionDocument() <- priceList;
    FOR inPriceListStock(priceList, stock) DO {
        createMachineryPriceTransaction(stock);   
    }  
    sentPriceTransactionDocument(priceList) <- TRUE;
    apply();
} CONFIRM;

showCreateMachineryPriceTransactionPriceList 'Показывать' (priceList) = isPostedPriceList(priceList) AND NOT Machinery.skipPriceTransactionDocument(priceList)
                                                                                                       AND NOT sentPriceTransactionDocument(priceList);
backgroundCreateMachineryPriceTransactionPriceList 'Цвет' (priceList) = IF countProcessPriceTransactionDocument(priceList) THEN
                                                                            RGB(255,255,128)
                                                                         ELSE
                                                                            RGB(212,255,212) IF showCreateMachineryPriceTransactionPriceList(priceList);

EXTEND FORM priceLists
    PROPERTIES(p) statusMachineryPriceTransactionDocument ON SHORTCUT createSnapshotPriceListMachineryPriceTransaction(p) BACKGROUND backgroundCreateMachineryPriceTransactionPriceList(p) READONLY AFTER stocksPriceList(p),
                  createPriceListMachineryPriceTransaction FORCE PANEL SHOWIF showCreateMachineryPriceTransactionPriceList(p)
;
EXTEND DESIGN priceLists {     
    detailContainer {    
        NEW actionContainer {
            caption = 'Действия';
            type = CONTAINERV;
            NEW createdContainer {
                caption = 'Создание на основе';
            }
            NEW machineryContainer {
                caption = 'Загрузка';                     
                NEW machinery {                 
                    caption = 'Загрузки' ;
                    type = CONTAINERV;    
                            
                    ADD PROPERTY (createPriceListMachineryPriceTransaction(p));
                }
            }
        }   
    } 
}