MODULE PromotionDashboard;

REQUIRE PriceListDashboard, PriceListOperation, Store, PriceListBefore, PriceListPromotion;

FORM promotionDashboard 'Управление акциями'
    OBJECTS dt = DATETIME FIXED PANEL
    PROPERTIES(dt) objValue = OBJVALUE

    OBJECTS o = PriceList.Operation FIXED PANEL
    PROPERTIES(o) SELECTOR PriceList.nameOperation

    OBJECTS pt = DataPriceListType FIXED PANEL
    PROPERTIES(pt) SELECTOR namePriceListType
    FILTERS changeDataPriceListTypeOperation(pt,o)
;
DESIGN promotionDashboard {
    NEW params {
        type = CONTAINERH;
        MOVE dt.box { 
            PROPERTY (objValue) { caption = 'Дата/время';}
        }
        MOVE o.box {
            caption = 'Операция';
        }        
        MOVE pt.box {
            caption = 'Вид цены';
        }
    }
    NEW detail {
        fill = 1;
        type = TABBED;
    }
    MOVE functions.box;
}

seekDefaultPriceListOperation = ACTION SEEK promotionDashboard.o promotionPriceListOperation();

EXTEND FORM promotionDashboard
     EVENTS ON INIT seekDefaultPriceListOperation()
;
NAVIGATOR {
    priceListDashboardNavigator {
        ADD promotionDashboard;
    }   
} 

// ---------------------------- По организациям ----------------------- //
// c


filterPromotionStock  = DATA LOCAL NESTED DepartmentStore ();
nameFilterPromotionStock 'Склад' = nameStock(filterPromotionStock()) MINCHARWIDTH 15 PREFCHARWIDTH 20;                                                     
notFilterPromotionStock = TRUE AND NOT filterPromotionStock();

changeCompanyFilterPromotionStock = ACTION () {
    REQUEST OBJECT s
    IF filterPromotionStock() THEN {            
        FORM companysStock OBJECTS s = filterPromotionStock() DIALOG SHOWDROP;        
    } ELSE {
        FORM companysStock DIALOG SHOWDROP;        
    }
    IF formResult() == FormResult.ok THEN {
        filterPromotionStock() <- requestedObject();

    } ELSE IF formResult() == FormResult.drop THEN {
        filterPromotionStock() <- NULL;
    }
}

//overPriceListDetailAPriceListTypeSkuDateTimeOperation  (type, sku, dateTime, operation) = IF filterPromotionStock() 
//    THEN activePriceListDetailAPriceListTypeSkuStockDateTimeOperation(type, sku, filterPromotionStock(), dateTime, operation)
//    ELSE activePriceListDetailAPriceListTypeSkuDateTimeOperation (type, sku, dateTime, operation);
//
//notePriceListTypeSkuDateTimeOperation 'Примечание' (type, sku, dateTime, operation) = notePriceList(priceListPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation)));                                            
//stocksPriceListTypeSkuDateTimeOperation 'Склады' (type, sku, dateTime, operation) = stocksPriceList(priceListPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation)));                                            
//fromDatePriceListTypeSkuDateTimeOperation 'Дата с' (type, sku, dateTime, operation) = fromDatePriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));                                            
//toDatePriceListTypeSkuDateTimeOperation 'Дата по' (type, sku, dateTime, operation) = toDatePriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));                                            
//priceBeforePriceListTypeSkuDateTimeOperation 'Цена (до)' (type, sku, dateTime, operation) = priceBeforePriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));
//activePricePriceListTypeSkuDateTimeOperation 'Цена' (type, sku, dateTime, operation) = pricePriceListDetailDataPriceListType(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation),type);
//descriptionPriceListTypeSkuDateTimeOperation 'Описание' (type, sku, dateTime, operation) = descriptionPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));                                            
//editActiveLedgerALedgerPriceListTypeSkuDateTimeOperation'Редактировать' (type, sku, dateTime, operation) = ACTION editPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation)) IMAGE 'edit.png';     

balanceBPriceListDetail 'Остаток (до акции)' (detail) = balanceBSkuStockDateTime(skuPriceListDetail(detail), filterPromotionStock(), fromDateTimePriceListDetail(detail));
notePriceListDetail 'Примечание' = notePriceList(priceListPriceListDetail(d));

EXTEND FORM promotionDashboard   
    OBJECTS c = LegalEntity FIXED PANEL
    PROPERTIES(c) nameLegalEntity SELECTOR
    FILTERS isCompanyLegalEntity(c)
    
    PROPERTIES nameFilterPromotionStock() ON CHANGE changeCompanyFilterPromotionStock()

    TREE cskg cskg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY orderSkuGroup(cskg), cskgName = nameSkuGroup(cskg)
    ORDER BY orderSkuGroup(cskg), cskgName
    FILTERGROUP inactive FILTER 'Активные' activeSkuGroup(cskg) 'F5' DEFAULT

    OBJECTS csk = PriceListDetail
    PROPERTIES(csk) READONLY nameSkuPriceListDetail, idBarcodeSkuPriceListDetail, fromDateTimePriceListDetail, toDateTimePriceListDetail
    ORDER BY nameSkuPriceListDetail(csk), fromDateTimePriceListDetail(csk)
    FILTERS isParentSkuGroupSku(cskg, skuPriceListDetail(csk)),
            fromDatePriceListDetail(csk) <= toDate(dt) AND NOT (toDatePriceListDetail(csk) < toDate(dt)),
            inPriceListDetailDataPriceListType(csk,pt) AND pricePriceListDetailDataPriceListType(csk,pt),
            companyPriceListDetail(csk) == c,
            operationPriceListDetail(csk) == o,
            inPriceListStock(priceListPriceListDetail(csk),filterPromotionStock()) OR (csk IS PriceListDetail AND NOT filterPromotionStock())
            
    PROPERTIES READONLY  balanceBPriceListDetail(csk) SHOWIF filterPromotionStock(), 
                         priceBeforePriceListDetail(csk), 
                         pricePriceListDetailDataPriceListType(csk,pt), 
                         fromDatePriceListDetail(csk), 
                         toDatePriceListDetail(csk)
    PROPERTIES READONLY  stocksPriceListDetail(csk), priceListTypesPriceListDetail(csk), notePriceListDetail(csk), 
                         descriptionPriceListDetail(csk)
    PROPERTIES editPriceListDetail(csk)           
            
;

DESIGN promotionDashboard {
    detail {
        NEW c {
            caption = 'Акционные товары';
            type = SPLITV;
            NEW c.skus {
                fill = 2;
                type = SPLITH;
                NEW c.skuFilters {
                    fill = 1;
                    MOVE PROPERTY(nameLegalEntity(c));
                    MOVE cskg.tree.box {
                        caption = 'Товарные группы';
                    }
                }
                NEW box {
                    fill = 3;
                    NEW fiter {
                        caption = 'Фильтр';
                        MOVE PROPERTY (nameFilterPromotionStock());
                    }
                    MOVE csk.box {caption = 'Товары';}                                                                    
                }
            }
        }
    }
}
//@extendFormFilterStockAccess(cst, promotionDashboard, company){
//    EXTEND FORM promotionDashboard FILTERS accessCompanyEmployeeStock(currentUser(), cst);
//};

// ----------------------------------- Список прайсов ---------------------------- //
// p

addUserPriceListLegalEntityPriceListTypeDateTimeOperation 'Добавить' = ACTION (legalEntity, type, dateTime, operation) NEWSESSION {
    FOR ADDOBJ p = UserPriceList DO {
        companyUserPriceList(p) <- legalEntity;
        fromDateUserPriceList(p) <- toDate(dateTime);
        fromTimeUserPriceList(p) <- toTime(dateTime);
        operationUserPriceList(p) <- operation;
        inUserPriceListDataPriceListType(p, type) <- TRUE;
        showPriceListPriceListType(p, type) <- TRUE;
        EXEC overAddUserPriceList(p); 
        FORM userPriceList OBJECTS p=p MANAGESESSION DOCKEDMODAL;
        IF formResult() == FormResult.ok THEN
            EXEC apply();
    }
} TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

EXTEND FORM promotionDashboard
    OBJECTS p = UserPriceList
    PROPERTIES(p) READONLY BACKGROUND backgroundPriceList(p) isPostedUserPriceList, numberUserPriceList, seriesUserPriceList, dateUserPriceList, timeUserPriceList,
                           fromDateUserPriceList, fromTimeUserPriceList, toDateUserPriceList, toTimeUserPriceList,
                           nameCurrencyUserPriceList, nameCompanyUserPriceList, priceListTypesPriceList, stocksPriceList,
                           noteUserPriceList
    PROPERTIES(p) EDITFORM, DELETE FORCE PANEL TOOLBAR, updatePriceList FORCE PANEL TOOLBAR
    ORDER BY fromDateUserPriceList(p), fromTimeUserPriceList(p)
    FILTERS inUserPriceListDataPriceListType(p, pt),
            companyUserPriceList(p) == c,
            operationUserPriceList(p) ==o
    PROPERTIES(c, pt, dt, o) addUserPriceListLegalEntityPriceListTypeDateTimeOperation FORCE PANEL TODRAW p       
    FILTERGROUP inactivePriceList
        FILTER 'Текущие' isActivePriceListDateTime(p,dt) 'F11'            
        FILTER 'Активные' isActivePriceListDateTime(p,dt) OR (fromDateTimePriceList(p) > dt) 'F10' DEFAULT   
        
    OBJECTS t = DataPriceListType FIXED GRID
    FILTERS t == pt
    
    OBJECTS d = PriceListDetail
    PROPERTIES(d) READONLY BACKGROUND backgroundPriceListDetail(d) indexPriceListDetail, idBarcodeSkuPriceListDetail, nameSkuPriceListDetail,
                           shortNameUOMSkuPriceListDetail, nameBatchPriceListDetail, nameCompanyPriceListDetail
    PROPERTIES(d) READONLY AFTER nameSkuPriceListDetail(d) originalIdBarcodeSkuPriceListDetail SHOWIF showOriginalIdBarcodeSkuPriceList(p), originalNameSkuPriceListDetail SHOWIF showOriginalNameSkuPriceList(p)
    
    ORDER BY indexPriceListDetail(d)
    
    OBJECTS ts = Stock
    PROPERTIES(ts) READONLY nameStock
    PROPERTIES(p, ts) READONLY inPriceListStock

    PROPERTIES(d, t) READONLY BACKGROUND backgroundPriceListDetail(d) pricePriceListDetailDataPriceListType COLUMNS (t) HEADER nameDataPriceListType(t)
    FILTERS priceListPriceListDetail(d) == p,
            inPriceListStock(p, ts),
            isCompanyStock(ts)
;
DESIGN promotionDashboard {
    detail {
        NEW p {
            caption = 'Акционные прайсы';
            type = SPLITV;
            MOVE p.box;
            NEW detailContainer{
                fill = 1;
                type = TABBED;
                NEW firstContainer{
                    caption = 'Спецификация';
                    type = SPLITH;
                    MOVE d.box {
                        fill = 4;
                    }
                    MOVE ts.box;
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';                       
                    }
                }
                NEW actionContainer {
                    caption = 'Действия';
                    type = CONTAINERH;
                    NEW createdContainer {
                        caption = 'Создание на основе';
                    }
                }
            }
        }
    }
}
//@extendFormFilterStockAccess(ts, promotionDashboard, company){
//    EXTEND FORM promotionDashboard FILTERS accessCompanyEmployeeStock(currentUser(), ts);
//};