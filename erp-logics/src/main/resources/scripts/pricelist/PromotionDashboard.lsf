MODULE PromotionDashboard;

REQUIRE PriceListDashboard, PriceListOperation, Store, PriceListBefore, PriceListPromotion;

CLASS Promotion 'Акция';
TABLE promotion(Promotion);

name 'Наименование акции' = DATA VARISTRING[50](Promotion) IN recognize;
promotionName = GROUP AGGR Promotion promotion BY name(promotion);

inactive 'Неактивная' = DATA BOOLEAN (Promotion);
active 'Активная' (Promotion p) = p IS Promotion AND NOT inactive(p);

FORM dialogPromotions 'Акции' 
    OBJECTS p = Promotion
    PROPERTIES(p) READONLY name
    FILTERGROUP inactivePromotion FILTER 'Активная' active(p) 'shift F10' DEFAULT    
    
    LIST Promotion OBJECT p
;

FORM promotion 'Акция'
    OBJECTS p=Promotion PANEL
    PROPERTIES(p) name
    PROPERTIES inactive(p) AFTER name(p)   
    EDIT Promotion OBJECT p
;

FORM promotions 'Акции'
    OBJECTS p = Promotion
    PROPERTIES(p) READONLY name
    PROPERTIES (p) NEWSESSION NEW, EDIT, del = DELETE GRID 
    FILTERGROUP inactivePromotion FILTER 'Активная' active(p) 'shift F10' DEFAULT    
;

NAVIGATOR {
    priceListMasterData {
        ADD promotions;
    }
}
    
promotion = ABSTRACT Promotion (PriceList);
promotion = DATA Promotion (UserPriceList);

promotion(UserPriceList priceList) += promotion(priceList);

namePromotion 'Акция' = ABSTRACT VARISTRING[50](PriceList);
namePromotion 'Акция' = name(promotion(UserPriceList priceList)); 
namePromotion(UserPriceList priceList) += namePromotion(priceList);

namePromotion 'Акция' = namePromotion(priceList(PriceListDetail detail));

showPromotionPrice 'Акция' = DATA BOOLEAN (PriceList.Operation);

showPromotionPrice 'Акция' = ABSTRACT BOOLEAN (PriceList);
showPromotionPrice 'Акция' = DATA BOOLEAN (UserPriceList);
showPromotionPrice(UserPriceList p) += showPromotionPrice(p);


EXTEND FORM userPriceList
    PROPERTIES (p) showPromotionPrice GRID 
;

DESIGN userPriceList {
    param{
        MOVE PROPERTY (showPromotionPrice(p));
    }
}

@deriveDocumentOperationProperty(UserPriceList, showPromotionPrice);

 
EXTEND FORM operation PROPERTIES(o) showPromotionPrice;

DESIGN operation {
    showContainer {
        MOVE PROPERTY(showPromotionPrice(o));
    }
}

EXTEND FORM priceLists 
    PROPERTIES (p) READONLY namePromotion AFTER nameOperation(p)
;

EXTEND FORM userPriceList 
    PROPERTIES (p) namePromotion SHOWIF showPromotionPrice(p)
;

DESIGN userPriceList{
    first {
        MOVE PROPERTY(namePromotion(p))
        {minimumCharWidth = 30; preferredCharWidth = 40;} 
    }
}

FORM promotionDashboard 'Управление акциями'
    OBJECTS dt = DATETIME PANEL
    PROPERTIES(dt) objValue = VALUE

    OBJECTS o = PriceList.Operation PANEL
    PROPERTIES(o) SELECTOR name

    OBJECTS pt = DataPriceListType PANEL
    PROPERTIES(pt) SELECTOR name[PriceListType]
    FILTERS change(pt,o)
;
DESIGN promotionDashboard {
    NEW params {
        type = CONTAINERH;
        MOVE dt.box { 
            PROPERTY (objValue) { caption = 'Дата/время';}
        }
        MOVE o.box {
            caption = 'Операция';
        }        
        MOVE pt.box {
            caption = 'Вид цены';
        }
    }
    NEW detail {
        fill = 1;
        type = TABBED;
    }
    MOVE functions.box;
}

seekDefaultPriceListOperation = { SEEK promotionDashboard.o = promotionPriceListOperation(); }

EXTEND FORM promotionDashboard
     EVENTS ON INIT seekDefaultPriceListOperation()
;
NAVIGATOR {
    priceListDashboardNavigator {
        ADD promotionDashboard;
    }   
} 

// ---------------------------- По организациям ----------------------- //
// c


filterPromotionStock  = DATA LOCAL NESTED DepartmentStore ();
nameFilterPromotionStock 'Склад' = name[Stock](filterPromotionStock()) MINCHARWIDTH 15 PREFCHARWIDTH 20;                                                     
notFilterPromotionStock = TRUE AND NOT filterPromotionStock();

changeCompanyFilterPromotionStock() = {
    DIALOG companysStock OBJECTS s = filterPromotionStock() INPUT NULL DO 
        filterPromotionStock() <- s;
}

//overPriceListDetailAPriceListTypeSkuDateTimeOperation  (type, sku, dateTime, operation) = IF filterPromotionStock() 
//    THEN activePriceListDetailAPriceListTypeSkuStockDateTimeOperation(type, sku, filterPromotionStock(), dateTime, operation)
//    ELSE activePriceListDetailAPriceListTypeSkuDateTimeOperation (type, sku, dateTime, operation);
//
//notePriceListTypeSkuDateTimeOperation 'Примечание' (type, sku, dateTime, operation) = notePriceList(priceListPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation)));                                            
//stocksPriceListTypeSkuDateTimeOperation 'Склады' (type, sku, dateTime, operation) = stocksPriceList(priceListPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation)));                                            
//fromDatePriceListTypeSkuDateTimeOperation 'Дата с' (type, sku, dateTime, operation) = fromDatePriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));                                            
//toDatePriceListTypeSkuDateTimeOperation 'Дата по' (type, sku, dateTime, operation) = toDatePriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));                                            
//priceBeforePriceListTypeSkuDateTimeOperation 'Цена (до)' (type, sku, dateTime, operation) = priceBeforePriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));
//activePricePriceListTypeSkuDateTimeOperation 'Цена' (type, sku, dateTime, operation) = pricePriceListDetailDataPriceListType(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation),type);
//descriptionPriceListTypeSkuDateTimeOperation 'Описание' (type, sku, dateTime, operation) = descriptionPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation));                                            
//editActiveLedgerALedgerPriceListTypeSkuDateTimeOperation'Редактировать' (type, sku, dateTime, operation) = ACTION editPriceListDetail(overPriceListDetailAPriceListTypeSkuDateTimeOperation(type, sku, dateTime, operation)) IMAGE 'edit.png';     

balanceB 'Остаток (до акции)' (PriceListDetail detail) = balanceB(sku(detail), filterPromotionStock(), fromDateTime(detail));
note 'Примечание' = note(priceList(PriceListDetail d));

EXTEND FORM promotionDashboard   
    OBJECTS c = LegalEntity PANEL
    PROPERTIES(c) name SELECTOR
    FILTERS isCompany(c)
    
    PROPERTIES nameFilterPromotionStock() ON CHANGE changeCompanyFilterPromotionStock()

    TREE cskg cskg = SkuGroup PARENT parent
    PROPERTIES READONLY order(cskg), cskgName = name(cskg)
    ORDER BY order(cskg), cskgName
    FILTERGROUP inactive FILTER 'Активные' active(cskg) 'F5' DEFAULT

    OBJECTS csk = PriceListDetail
    PROPERTIES(csk) READONLY nameSku, idBarcodeSku, fromDateTime, toDateTime, namePromotion
    ORDER BY nameSku(csk), fromDateTime(csk)
    FILTERS isParent(cskg, sku(csk)),
            fromDate(csk) <= toDate(dt) AND NOT (toDate(csk) < toDate(dt)),
            in(csk,pt) AND price(csk,pt),
            company(csk) == c,
            operation(csk) == o,
            in(priceList(csk),filterPromotionStock()) OR (csk IS PriceListDetail AND NOT filterPromotionStock())
            
    PROPERTIES READONLY  balanceB(csk) SHOWIF filterPromotionStock(), 
                         priceBefore(csk), 
                         price(csk,pt), 
                         fromDate(csk), 
                         toDate(csk)
    PROPERTIES READONLY  stocks(csk), priceListTypes(csk), note(csk), 
                         description(csk)
    PROPERTIES edit(csk)           
            
;

DESIGN promotionDashboard {
    detail {
        NEW c {
            caption = 'Акционные товары';
            type = SPLITV;
            NEW c.skus {
                fill = 2;
                type = SPLITH;
                NEW c.skuFilters {
                    fill = 1;
                    MOVE PROPERTY(name(c));
                    MOVE cskg.tree.box {
                        caption = 'Товарные группы';
                    }
                }
                NEW box {
                    fill = 3;
                    NEW fiter {
                        caption = 'Фильтр';
                        MOVE PROPERTY (nameFilterPromotionStock());
                    }
                    MOVE csk.box {caption = 'Товары';}                                                                    
                }
            }
        }
    }
}
//@extendFormFilterStockAccess(cst, promotionDashboard, company){
//    EXTEND FORM promotionDashboard FILTERS accessCompanyEmployeeStock(currentUser(), cst);
//};

// ----------------------------------- Список прайсов ---------------------------- //
// p

addUserPriceList 'Добавить'(LegalEntity legalEntity, PriceListType type, DATETIME dateTime, PriceList.Operation operation) = {
	NEWSESSION {
	    FOR NEW p = UserPriceList DO {
	        company(p) <- legalEntity;
	        fromDate(p) <- toDate(dateTime);
	        fromTime(p) <- toTime(dateTime);
	        operation(p) <- operation;
	        in[UserPriceList,DataPriceListType](p, type) <- TRUE;
	        show[PriceList,PriceListType](p, type) <- TRUE;
	        overAdd(p); 
	        SHOW userPriceList OBJECTS p = p MANAGESESSION DOCKED NOCANCEL;
	    }
	}
} TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

EXTEND FORM promotionDashboard
    OBJECTS p = UserPriceList
    PROPERTIES(p) READONLY BACKGROUND background(p) isPosted, namePromotion[PriceList], number, series, date, time,
                           fromDate, fromTime, toDate, toTime,
                           nameCurrency, nameCompany, priceListTypes, stocks,
                           note
    PROPERTIES(p) NEWSESSION EDIT, DELETE , update TOOLBAR
    ORDER BY fromDate(p), fromTime(p)
    FILTERS in(p, pt),
            company(p) == c,
            operation(p) ==o
    PROPERTIES(c, pt, dt, o) addUserPriceList TODRAW p       
    FILTERGROUP inactivePriceList
        FILTER 'Текущие' isActive(p,dt) 'F11'            
        FILTER 'Активные' isActive(p,dt) OR (fromDateTime[PriceList](p) > dt) 'F10' DEFAULT   
        
    OBJECTS t = DataPriceListType GRID
    FILTERS t == pt
    
    OBJECTS d = PriceListDetail
    PROPERTIES(d) READONLY BACKGROUND background(d) index, idBarcodeSku, nameSku,
                           shortNameUOMSku, nameBatch, nameCompany
    PROPERTIES(d) READONLY AFTER nameSku(d) originalIdBarcodeSku SHOWIF showOriginalIdBarcodeSku[PriceList](p), originalNameSku SHOWIF showOriginalNameSku[PriceList](p)
    
    ORDER BY index(d)
    
    OBJECTS ts = Stock
    PROPERTIES(ts) READONLY name
    PROPERTIES(p, ts) READONLY in

    PROPERTIES(d, t) READONLY BACKGROUND background(d) price COLUMNS (t) HEADER name(t)
    FILTERS priceList(d) == p,
            in(p, ts),
            isCompany(ts)
;
DESIGN promotionDashboard {
    detail {
        NEW p {
            caption = 'Акционные прайсы';
            type = SPLITV;
            MOVE p.box;
            NEW detailContainer{
                fill = 1;
                type = TABBED;
                NEW firstContainer{
                    caption = 'Спецификация';
                    type = SPLITH;
                    MOVE d.box {
                        fill = 4;
                    }
                    MOVE ts.box;
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';                       
                    }
                }
                NEW actionContainer {
                    caption = 'Действия';
                    type = CONTAINERH;
                    NEW createdContainer {
                        caption = 'Создание на основе';
                    }
                }
            }
        }
    }
}
//@extendFormFilterStockAccess(ts, promotionDashboard, company){
//    EXTEND FORM promotionDashboard FILTERS accessCompanyEmployeeStock(currentUser(), ts);
//};