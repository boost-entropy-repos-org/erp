MODULE PriceRound;

REQUIRE System, Utils, DefaultData, Stock, Sku;


//--  По группам
CLASS RoundCondition 'Условие округления';
TABLE roundCondition (RoundCondition);

nameRoundCondition 'Наименование' = DATA VARISTRING[50](RoundCondition);

currencyRoundCondition = DATA Currency(RoundCondition) NOT NULL DELETE;
nameCurrencyRoundCondition 'Валюта' (roundCondition)= nameCurrency(currencyRoundCondition(roundCondition));

denominatorPriceRoundCondition 'Делитель' =  DATA NUMERIC[14,2](RoundCondition);
minPriceRoundCondition 'Нижний порог' =  DATA NUMERIC[14,2](RoundCondition);
priceRoundRoundCondition 'Количество знаков после запятой' (roundCondition)=  DATA INTEGER (RoundCondition);

defaultRoundConditionCurrency = DATA RoundCondition(Currency);
defaultRoundCurrency 'Округление' (currency) = priceRoundRoundCondition(defaultRoundConditionCurrency(currency)) PERSISTENT;
isDefaultRoundConditionCurrency 'По умолчанию' (roundCondition, currency) = defaultRoundConditionCurrency(currency) == roundCondition;

priceRoundCurrency 'Округление по умолчанию' (currency) = priceRoundRoundCondition(defaultRoundConditionCurrency(currency)) PERSISTENT;

CLASS PriceInterval 'Диапазон цен';
TABLE priceInterval (PriceInterval);

roundConditionPriceInterval = DATA RoundCondition(PriceInterval) NOT NULL DELETE;

fromPriceInterval 'от' = DATA NUMERIC[14,3](PriceInterval) IN base;
toPriceInterval 'до' = DATA NUMERIC[14,3](PriceInterval) IN base;
modifierPriceInterval 'Округлённая цена интервала' = DATA NUMERIC[14,2](PriceInterval) IN base;

integerPriceRoundCondition 'Целое' (price, roundCondition) = divideInteger(price, denominatorPriceRoundCondition(roundCondition));

fractionPriceRoundCondition 'Дробное' (price, roundCondition) = price -
    integerPriceRoundCondition(price, roundCondition) * denominatorPriceRoundCondition(roundCondition);

modifierPriceIntervalPriceRoundCondition (price, roundCondition) =
    GROUP LAST modifierPriceInterval(interval)
          BY price AS NUMERIC[14,3], roundConditionPriceInterval(interval)
          ORDER interval
          WHERE fractionPriceRoundCondition(price, roundConditionPriceInterval(interval)) >= fromPriceInterval(interval) AND
                fractionPriceRoundCondition(price, roundConditionPriceInterval(interval)) < toPriceInterval(interval);


roundPriceRoundCondition (price, roundCondition) = NUMERIC[14,2](
    IF (price AS NUMERIC[14,2]) > minPriceRoundCondition(roundCondition) AND modifierPriceIntervalPriceRoundCondition(price, roundCondition)
        THEN
            integerPriceRoundCondition(price, roundCondition) * denominatorPriceRoundCondition(roundCondition) +
            modifierPriceIntervalPriceRoundCondition(price, roundCondition)
        ELSE
            round(price AS NUMERIC[14,2], priceRoundRoundCondition(roundCondition))
    ) COMPLEX; // для COMPLEX

roundPriceCurrency(price, currency) = round(price, defaultRoundCurrency(currency));

FORM priceIntervals 'Округления цен'

    OBJECTS c=Currency FIXED PANEL
    PROPERTIES (c)  SELECTOR nameCurrency

    OBJECTS rc=RoundCondition
    PROPERTIES(rc) nameRoundCondition, denominatorPriceRoundCondition, minPriceRoundCondition, priceRoundRoundCondition, ADDOBJ, DELETESESSION FORCE PANEL TOOLBAR
    PROPERTIES(rc, c) isDefaultRoundConditionCurrency
    
    OBJECTS p=PriceInterval
    PROPERTIES(p) fromPriceInterval, toPriceInterval, modifierPriceInterval, ADDOBJ, DELETESESSION FORCE PANEL TOOLBAR

    OBJECTS nu=NUMERIC[14,3] FIXED PANEL
    PROPERTIES val = OBJVALUE(nu)
    PROPERTIES(nu, rc) READONLY roundPriceRoundCondition

    FILTERS currencyRoundCondition(rc) == c,
            roundConditionPriceInterval(p) == rc
;

DESIGN priceIntervals {
    ADD c.box {
        PROPERTY (nameCurrency(c)) {
            preferredCharWidth = 30;
            panelLabelAbove = TRUE;
            font = 'bold 24';
        }
    }

    NEW specification {
        fill = 1;

        ADD rc.box;
        ADD p.box;
        ADD nu.box {
            caption = 'Тестовая форма';
            type = CONTAINERH;
            ADD PROPERTY (val) {
                caption = 'Введите число для примера';
                panelLabelAbove = TRUE;
                font = 'bold 36';
            }
            ADD PROPERTY (roundPriceRoundCondition(nu, rc)) {
                caption = 'Результат';
                panelLabelAbove = TRUE;
                font = 'bold 36';
            }
        }
    }
    ADD functions.box;
}

FORM dialogPriceIntervals 'Округления цен'

    OBJECTS rc=RoundCondition
    PROPERTIES(rc) READONLY nameRoundCondition, denominatorPriceRoundCondition, minPriceRoundCondition, priceRoundRoundCondition, nameCurrencyRoundCondition
    OBJECTS p=PriceInterval
    PROPERTIES(p) READONLY fromPriceInterval, toPriceInterval, modifierPriceInterval

    FILTERS roundConditionPriceInterval(p) == rc

    DIALOG RoundCondition OBJECT rc
;
DESIGN dialogPriceIntervals {
    main {
        preferredSize = (1024, 768);
    }
}

// --------------------------------------------------- Стандартные значения ------------------------------------ //

loadDefaultPriceInterval  = ACTION (value, shortName, name) {
    LOCAL currency = Currency();
    ASSIGN currency() <- currencyShortName(shortName);
    FOR ADDOBJ p = RoundCondition DO {
        ASSIGN currencyRoundCondition(p) <- currency();
        ASSIGN priceRoundRoundCondition(p)  <- value;
        ASSIGN defaultRoundConditionCurrency(c) <- p WHERE c == currency();
        ASSIGN nameRoundCondition(p) <- name;
    }
}

loadDefaultPriceIntervals 'Загрузить стандартные округления цен'  = ACTION () {
    EXEC loadDefaultPriceInterval(0, 'BLR', 'До рублей');
    EXEC loadDefaultPriceInterval(2, 'BLR', 'До копеек');

    LOCAL currency = Currency();
    ASSIGN currency() <- currencyShortName('BLR');
    FOR ADDOBJ p = RoundCondition DO {
        ASSIGN currencyRoundCondition(p) <- currency();
        ASSIGN minPriceRoundCondition(p) <- 25.0;
        ASSIGN priceRoundRoundCondition(p) <- -1;
        ASSIGN nameRoundCondition(p) <- 'До 50 рублей';
        denominatorPriceRoundCondition(p) <- 50.0;
        FOR ADDOBJ i1 = PriceInterval DO {
            roundConditionPriceInterval(i1) <- p;
            modifierPriceInterval(i1) <- 0.0;
            fromPriceInterval(i1) <- 0.0;
            toPriceInterval(i1) <- 25.0;
        }
        FOR ADDOBJ i2 = PriceInterval DO {
            roundConditionPriceInterval(i1) <- p;
            modifierPriceInterval(i2) <- 50.0;
            fromPriceInterval(i2) <- 25.0;
            toPriceInterval(i2) <- 50.0;
        }
    }

    EXEC loadDefaultPriceInterval(2, 'RUB', 'До копеек');
    EXEC loadDefaultPriceInterval(2, 'AUD', 'До центов');
    EXEC loadDefaultPriceInterval(2, 'BGN', 'До стотинок');
    EXEC loadDefaultPriceInterval(2, 'UAH', 'До копеек');
    EXEC loadDefaultPriceInterval(2, 'DKK', 'До эре');
    EXEC loadDefaultPriceInterval(2, 'USD', 'До центов');
    EXEC loadDefaultPriceInterval(2, 'EUR', 'До евроцентов');
    EXEC loadDefaultPriceInterval(2, 'PLN', 'До грошей');
    EXEC loadDefaultPriceInterval(2, 'ISK', 'До эйре');
    EXEC loadDefaultPriceInterval(2, 'CAD', 'До центов');
    EXEC loadDefaultPriceInterval(2, 'CNY', 'До фэней');
    EXEC loadDefaultPriceInterval(3, 'KWD', 'До филсов');
    EXEC loadDefaultPriceInterval(2, 'LVL', 'До сантимов');
    EXEC loadDefaultPriceInterval(2, 'LTL', 'До центов');
    EXEC loadDefaultPriceInterval(2, 'MDL', 'До баней');
    EXEC loadDefaultPriceInterval(2, 'NOK', 'До эре');
    EXEC loadDefaultPriceInterval(2, 'XDR', 'До центов');
    EXEC loadDefaultPriceInterval(2, 'SGD', 'До центов');
    EXEC loadDefaultPriceInterval(2, 'KGS', 'До тыйынов');
    EXEC loadDefaultPriceInterval(2, 'KZT', 'До тиынов');
    EXEC loadDefaultPriceInterval(2, 'TRY', 'До курушей');
    EXEC loadDefaultPriceInterval(2, 'GBP', 'До пенсов');
    EXEC loadDefaultPriceInterval(2, 'CZK', 'До геллеров');
    EXEC loadDefaultPriceInterval(2, 'SEK', 'До эре');
    EXEC loadDefaultPriceInterval(2, 'CHF', 'До рапенов');
    EXEC loadDefaultPriceInterval(2, 'JPY', 'До сэнов');
    EXEC loadDefaultPriceInterval(2, 'IRR', 'До динаров');
} IN loadDefault;


@implementLoadDefaultData(loadDefaultPriceIntervals);

