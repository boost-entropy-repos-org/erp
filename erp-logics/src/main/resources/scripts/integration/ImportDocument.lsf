MODULE ImportDocument;

REQUIRE System, Operation, SkuLedger;

CLASS FileExtension 'Тип файла' {
    xls 'XLS',
    xlsx 'XLSX',
    csv 'CSV',
    dbf 'DBF',
    txt 'TXT'
}

CLASS KeyType 'Ключ товара' {
    barcode 'Штрихкод',
    batch   'Код партии',
    item    'Код товара'
}

CLASS CustomKeyType 'Ключ свойства' {
    item 'Товар',
    article   'Артикул',
    document    'Документ',
    documentDetail 'Строка документа'
}

skuBatchId (idBatch) = skuBatch(batchId(idBatch));

CLASS ImportTypeDetail 'Свойство' {
    idDocument 'Идентификатор документа',
    numberDocument 'Номер документа',
    dateDocument 'Дата документа',
    timeDocument 'Время документа',
    idSupplier 'Код поставщика',
    idSupplierStock 'Код склада поставщика',
    currencyDocument 'Валюта документа',
    dataIndex 'Порядковый номер',
    idItem 'Код товара',
    idItemGroup 'Код группы товара',
    barcodeItem 'Штрихкод товара',
    originalCustomsGroupItem 'ТНВЭД (ориг.)',
    idBatch 'Код партии',
    idBox 'Код короба',
    nameBox 'Наименование короба',
    captionItem 'Наименование',
    originalCaptionItem 'Наименование (ориг.)',
    UOMItem 'Единица измерения',
    idManufacturer 'Код производителя',
    nameManufacturer 'Наименование производителя',
    sidOrigin2Country 'Код страны 2 знака',
    nameCountry 'Наименование страны',
    nameOriginCountry 'Наименование страны (ориг.)',
    importCountryBatch 'Страна ввоза',
    idCustomerStock 'Код склада покупателя',
    pharmacyPriceGroupItem 'Тип лекарственного средства',
    quantity 'Количество',
    price 'Цена',
    manufacturingPrice 'Цена производителя',
    contractPrice 'Контрактная цена',
    shipmentPrice 'Учётная цена',
    shipmentSum 'Учётная сумма',
    rateExchange 'Курс',
    sum 'Сумма',
    valueVAT 'НДС, %',
    sumVAT 'Сумма НДС',
    invoiceSum 'Сумма с НДС',
    seriesPharmacy 'Серия лекарственного средства',
    numberCompliance 'Номер сертификата',
    dateCompliance 'Дата сертификата',
    declaration 'Декларация',
    expiryDate 'Срок годности',
    manufactureDate 'Дата изготовления',
    idArticle 'Код артикула',
    captionArticle 'Наименование артикула',
    originalCaptionArticle 'Наименование артикула (ориг.)',
    idColor 'Код цвета',
    nameColor 'Цвет',
    idTheme 'Код темы',
    nameTheme 'Тема',
    netWeight 'Масса нетто (ед.)',
    netWeightSum 'Масса нетто (сумма)',
    grossWeight 'Масса брутто (ед.)',
    grossWeightSum 'Масса брутто (сумма)',
    composition 'Состав',
    originalComposition 'Состав (ориг.)',
    idSize 'Код размера',
    nameSize 'Размер',
    nameOriginalSize 'Размер (ориг.)',
    idCollection 'Код коллекции',
    nameCollection 'Коллекция',
    idSeasonYear 'Год',
    idSeason 'Код сезона',
    nameSeason 'Сезон',
    idBrand 'Код бренда',
    nameBrand 'Наименование бренда'
}
TABLE ImportTypeDetail(ImportTypeDetail);

// todo : delete in future releases 
propertyImportTypeDetail 'Свойство (устар.)' (ImportTypeDetail) = DATA VARSTRING[100] (ImportTypeDetail);

propImportTypeDetail 'Свойство' (ImportTypeDetail) = DATA Property (ImportTypeDetail);
canonicalNamePropImportTypeDetail 'Свойство' (d) = canonicalNameProperty(propImportTypeDetail(d));
migratePropImportTypeDetail = ACTION CUSTOM 'lsfusion.erp.integration.universal.MigrateImportDocumentPropertyActionProperty' (ImportTypeDetail);
propsMigrated() = DATA BOOLEAN ();
migrateProps() = ACTION {
    IF NOT propsMigrated() THEN {
        FOR propertyImportTypeDetail(ImportTypeDetail d) DO
            migratePropImportTypeDetail(d);
        propsMigrated() <- TRUE;
    }
}
onStarted() += ACTION migrateProps();

keyImportTypeDetail 'Ключ' (ImportTypeDetail) = DATA CustomKeyType (ImportTypeDetail);
nameKeyImportTypeDetail (ImportTypeDetail) = staticName(keyImportTypeDetail(ImportTypeDetail));
captionKeyImportTypeDetail 'Ключ' (ImportTypeDetail) = staticCaption(keyImportTypeDetail(ImportTypeDetail));

CLASS ImportType 'Тип импорта';
TABLE importType(ImportType);

nameImportType 'Имя' =  DATA VARSTRING[100](ImportType);
startRowImportType 'Начинать со строки' = DATA INTEGER(ImportType);
isPostedImportType 'Импортировать проведёнными' = DATA BOOLEAN(ImportType);
separatorImportType 'Разделитель' = DATA VARSTRING[2] (ImportType);

primaryKeyTypeImportType 'Тип первичного ключа' = DATA KeyType(ImportType);
captionPrimaryKeyTypeImportType 'Тип первичного ключа' (importType) = staticCaption(primaryKeyTypeImportType(importType));
namePrimaryKeyTypeImportType 'Тип первичного ключа' (importType) = staticName(primaryKeyTypeImportType(importType));
checkExistencePrimaryKeyImportType 'Проверять существование по первому ключу' (importType) = DATA BOOLEAN (ImportType);

secondaryKeyTypeImportType 'Тип вторичного ключа' = DATA KeyType(ImportType);
captionSecondaryKeyTypeImportType 'Тип вторичного ключа' (importType) = staticCaption(secondaryKeyTypeImportType(importType));
nameSecondaryKeyTypeImportType 'Тип вторичного ключа' (importType) = staticName(secondaryKeyTypeImportType(importType));

keyIsDigitImportType 'Ключ содержит только цифры' (importType) = DATA BOOLEAN(ImportType);
checkArticlesImportType 'Проверять артикулы на изменение' (importType) = DATA BOOLEAN(ImportType); 
multipleDocumentsImportType 'Несколько документов в одном файле' (importType) = DATA BOOLEAN(ImportType); 
importTypeDetailImportType (ImportType) = DATA ImportTypeDetail(ImportType);
propertyImportTypeDetailImportType 'Свойство артикула' (importType) = canonicalNamePropImportTypeDetail(importTypeDetailImportType(importType)); 
staticNameImportTypeDetailImportType  (importType) = staticName(importTypeDetailImportType(importType)); 
staticCaptionImportTypeDetailImportType  (importType) = staticCaption(importTypeDetailImportType(importType)); 

TABLE importTypeImportTypeDetail (ImportType, ImportTypeDetail);
indexImportTypeImportTypeDetail 'Поле' = DATA VARSTRING[50] (ImportType, ImportTypeDetail);

replaceOnlyNullImportTypeImportTypeDetail 'Не замещать' = DATA BOOLEAN (ImportType, ImportTypeDetail);

fileExtensionImportType  = DATA FileExtension(ImportType);
captionFileExtensionImportType 'Тип файла' (importType) = staticCaption(fileExtensionImportType(importType));

autoImportImportType 'Автоматический импорт' = DATA BOOLEAN(ImportType);
autoImportDirectoryImportType 'Папка' =  DATA VARSTRING[100](ImportType);

autoImportOperationImportType =  DATA Operation.Operation(ImportType);
nameAutoImportOperationImportType 'Операция'(importType) = Operation.nameOperation(autoImportOperationImportType(importType));

autoImportSupplierImportType =  DATA LegalEntity(ImportType);
nameAutoImportSupplierImportType 'Поставщик' (importType) =  nameLegalEntity(autoImportSupplierImportType(importType));
autoImportSupplierStockImportType = DATA Stock(ImportType);
nameAutoImportSupplierStockImportType 'Склад поставщика' (importType) =  nameStock(autoImportSupplierStockImportType(importType));
autoImportCustomerImportType = DATA LegalEntity(ImportType);
nameAutoImportCustomerImportType 'Покупатель' (importType) =  nameLegalEntity(autoImportCustomerImportType(importType));
autoImportCustomerStockImportType = DATA Stock(ImportType);
nameAutoImportCustomerStockImportType 'Склад покупателя' (importType) =  nameStock(autoImportCustomerStockImportType(importType));

useStockMappingImportType 'Настраиваемые коды складов' = DATA BOOLEAN(ImportType);

CLASS StockMappingEntry 'Настраиваемый код склада';
TABLE stockMappingEntry(StockMappingEntry);

idStockMappingEntry 'Код' = DATA VARSTRING[100] (StockMappingEntry);
importTypeStockMappingEntry = DATA ImportType(StockMappingEntry);
stockMappingEntryIdImportType (idEntry, importType) = GROUP AGGR stockMappingEntry BY idStockMappingEntry (stockMappingEntry), importTypeStockMappingEntry(stockMappingEntry); 
stockStockMappingEntry 'Склад' = DATA Stock (StockMappingEntry);
nameStockStockMappingEntry 'Склад' (stockMappingEntry) = nameStock(stockStockMappingEntry(stockMappingEntry)); 
idStockStockMappingEntry (stockMappingEntry) = idStock(stockStockMappingEntry(stockMappingEntry)); 

FORM importTypeDetails
    OBJECTS itd = ImportTypeDetail
    PROPERTIES(itd) staticCaption READONLY, propertyImportTypeDetail, canonicalNamePropImportTypeDetail, captionKeyImportTypeDetail
    DIALOG ImportTypeDetail OBJECT itd
;

FORM importTypeDetail 'Свойство импорта'
    OBJECTS itd = ImportTypeDetail FIXED PANEL
    PROPERTIES(itd) staticCaption, propertyImportTypeDetail, canonicalNamePropImportTypeDetail, captionKeyImportTypeDetail
    EDIT ImportTypeDetail OBJECT itd
;

FORM fileExtensions 'Типы файлов'
    OBJECTS f=FileExtension
    PROPERTIES(f) READONLY staticCaption
    DIALOG FileExtension OBJECT f
;

FORM keyTypes 'Ключи товара'
    OBJECTS k=KeyType
    PROPERTIES(k) READONLY staticCaption
    DIALOG KeyType OBJECT k
;

FORM customKeyTypes 'Ключи свойства'
    OBJECTS k=CustomKeyType
    PROPERTIES(k) READONLY staticCaption
    DIALOG CustomKeyType OBJECT k
;

showSeparator (importType) = fileExtensionImportType(importType)==FileExtension.csv OR fileExtensionImportType(importType)==FileExtension.txt;

FORM importType 'Тип импорта'
    OBJECTS i=ImportType FIXED PANEL
    PROPERTIES(i) nameImportType, captionFileExtensionImportType, startRowImportType, isPostedImportType,
                  separatorImportType SHOWIF showSeparator(i), captionPrimaryKeyTypeImportType, checkExistencePrimaryKeyImportType,
                  captionSecondaryKeyTypeImportType, keyIsDigitImportType, 
                  checkArticlesImportType, propertyImportTypeDetailImportType SHOWIF checkArticlesImportType(i), 
                  multipleDocumentsImportType,
                  useStockMappingImportType, autoImportImportType, 
                  autoImportDirectoryImportType FORCE PANEL SHOWIF autoImportImportType(i),
                  nameAutoImportOperationImportType FORCE PANEL, 
                  nameAutoImportSupplierImportType FORCE PANEL,
                  nameAutoImportSupplierStockImportType FORCE PANEL,
                  nameAutoImportCustomerImportType FORCE PANEL,
                  nameAutoImportCustomerStockImportType FORCE PANEL 
                                   
    OBJECTS  s=StockMappingEntry FIXED GRID              
    PROPERTIES(s) SHOWIF useStockMappingImportType(i) idStockMappingEntry, nameStockStockMappingEntry, DELETE, ADDOBJ                
    FILTERS importTypeStockMappingEntry(s) == i
              
    OBJECTS d = ImportTypeDetail
    PROPERTIES(d) staticCaption READONLY, propertyImportTypeDetail, canonicalNamePropImportTypeDetail, captionKeyImportTypeDetail
    PROPERTIES(i,d) indexImportTypeImportTypeDetail, replaceOnlyNullImportTypeImportTypeDetail                   
    PROPERTIES(d) FORCE PANEL ADDFORM, EDITFORM, DELETE TOOLBAR
    EDIT ImportType OBJECT i
;

DESIGN importType {
    MOVE i.box;
    NEW tabbedContainer {
        fill = 1;
        type = TABBED;
        NEW columnsContainer {
            caption = 'Колонки';
            MOVE d.box;
        }
        NEW documentParamsContainer {
            caption = 'Параметры документа';
            MOVE PROPERTY(nameAutoImportOperationImportType(i));
            MOVE PROPERTY(nameAutoImportSupplierImportType(i));
            MOVE PROPERTY(nameAutoImportSupplierStockImportType(i));
            MOVE PROPERTY(nameAutoImportCustomerImportType(i));
            MOVE PROPERTY(nameAutoImportCustomerStockImportType(i));
            
            NEW autoImportContainer {  
                caption = 'Автоматический импорт';      
                MOVE PROPERTY(autoImportImportType(i));
                MOVE PROPERTY(autoImportDirectoryImportType(i)); 
            }
            
            NEW stockMappingContainer {
                fill = 1;
                caption = 'Настраиваемые коды складов'; 
                MOVE PROPERTY(useStockMappingImportType(i));
                MOVE s.box;
            }            
        }
    }
    MOVE functions.box;
}

FORM importTypes 'Типы импорта'
    OBJECTS i=ImportType
    PROPERTIES(i) READONLY nameImportType, captionFileExtensionImportType, startRowImportType, isPostedImportType,
                   separatorImportType SHOWIF showSeparator(i), captionPrimaryKeyTypeImportType, checkExistencePrimaryKeyImportType,
                   captionSecondaryKeyTypeImportType, keyIsDigitImportType, 
                   checkArticlesImportType, multipleDocumentsImportType, autoImportImportType, autoImportDirectoryImportType SHOWIF autoImportImportType(i)
    PROPERTIES(i) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    DIALOG ImportType OBJECT i
;

EXTEND FORM integrationData
    OBJECTS i=ImportType
    PROPERTIES(i) READONLY nameImportType, captionFileExtensionImportType, startRowImportType, isPostedImportType,
                   separatorImportType SHOWIF showSeparator(i), captionPrimaryKeyTypeImportType, checkExistencePrimaryKeyImportType, 
                   captionSecondaryKeyTypeImportType, keyIsDigitImportType, 
                   checkArticlesImportType, multipleDocumentsImportType
    PROPERTIES(i) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
;

DESIGN integrationData {
    pane {
        NEW importType {
            MOVE i.box;
            caption = 'Универсальный импорт';
            NEW actionsContainer {
                caption = '';
                type = CONTAINERH;
            }
        }
    }
}

