MODULE ImportDocument;

REQUIRE System, Import;

CLASS ImportTypeFileExtension 'Тип файла' {
    xls 'XLS',
    xlsx 'XLSX',
    csv 'CSV',
    dbf 'DBF'
}

CLASS ImportKeyType 'Ключ товара' {
    barcode 'Штрихкод',
    batch   'Код партии',
    item    'Код товара'
}

skuBatchId (idBatch) = skuBatch(batchId(idBatch));

CLASS ImportTypeDetail 'Свойство' {
    numberDocument 'Номер инвойса',
    idItem 'Код товара',
    barcodeItem 'Штрихкод товара',
    idBatch 'Код партии',
    captionItem 'Наименование',
    UOMItem 'Единица измерения',
    manufacturerItem 'Производитель',
    countryItem 'Страна',
    pharmacyPriceGroupItem 'Тип лекарственного средства',
    quantity 'Количество',
    price 'Цена',
    manufacturingPrice 'Цена производителя',
    sum 'Сумма',
    valueVAT 'НДС, %',
    sumVAT 'Сумма НДС',
    invoiceSum 'Сумма с НДС',
    seriesPharmacy 'Серия лекарственного средства',
    compliance 'Сертификат',
    declaration 'Декларация',
    expiryDate 'Срок годности'
}

CLASS ImportType 'Тип импорта';

nameImportType 'Имя' =  DATA VARSTRING[100](ImportType);
startRowImportType 'Начинать со строки' = DATA INTEGER(ImportType);
separatorImportType 'Разделитель' = DATA INTEGER(ImportType);
importKeyTypeImportType 'Тип ключа' = DATA ImportKeyType(ImportType);
captionImportKeyTypeImportType 'Тип ключа' (importType) = staticCaption(importKeyTypeImportType(importType));
nameImportKeyTypeImportType 'Тип ключа' (importType) = staticName(importKeyTypeImportType(importType));

indexImportTypeImportTypeDetail 'Поле' = DATA VARSTRING[10] (ImportType, ImportTypeDetail);

importTypeFileExtensionImportType  = DATA ImportTypeFileExtension(ImportType);
captionImportTypeFileExtensionImportType 'Тип файла' (importType) = staticCaption(importTypeFileExtensionImportType(importType));

FORM importTypeDetails
    OBJECTS itd = ImportTypeDetail
    PROPERTIES(itd) staticCaption
    DIALOG ImportTypeDetail OBJECT itd
;

FORM importTypeFileExtensions 'Типы файлов'
    OBJECTS f=ImportTypeFileExtension
    PROPERTIES(f) READONLY staticCaption
    DIALOG ImportTypeFileExtension OBJECT f
;

FORM importKeyTypes 'Типы ключа'
    OBJECTS k=ImportKeyType
    PROPERTIES(k) READONLY staticCaption
    DIALOG ImportKeyType OBJECT k
;

showSeparator (importType) = importType==ImportTypeFileExtension.csv;

FORM importType 'Тип импорта'
    OBJECTS i=ImportType FIXED PANEL
    PROPERTIES(i) nameImportType, captionImportTypeFileExtensionImportType, startRowImportType,
                  separatorImportType SHOWIF showSeparator(i), captionImportKeyTypeImportType
    OBJECTS d = ImportTypeDetail
    PROPERTIES(d) staticCaption
    PROPERTIES(i,d) indexImportTypeImportTypeDetail                   
    EDIT ImportType OBJECT i
;

FORM importTypes 'Типы импорта'
    OBJECTS i=ImportType
    PROPERTIES(i) READONLY nameImportType, captionImportTypeFileExtensionImportType, startRowImportType,
                   separatorImportType SHOWIF showSeparator(i), captionImportKeyTypeImportType
    PROPERTIES(i) ADDFORM, EDITFORM, DELETE
    DIALOG ImportType OBJECT i
;

EXTEND DESIGN importType {
    ADD i.box;
    NEW tabbedContainer {
        fill = 1;
        type = TABBED;
        NEW columnsContainer {
            caption = 'Колонки';
            ADD d.box;
        }
    }
    ADD functions.box;
}

EXTEND FORM integrationData
    OBJECTS i=ImportType
    PROPERTIES(i) READONLY nameImportType, captionImportTypeFileExtensionImportType, startRowImportType,
                   separatorImportType SHOWIF showSeparator(i), captionImportKeyTypeImportType
    PROPERTIES(i) ADDFORM, EDITFORM, DELETE
    DIALOG ImportType OBJECT i
;
EXTEND DESIGN integrationData {
    pane {
        NEW importType {
            ADD i.box;
            caption = 'Универсальный импорт';
        }
    }
}

