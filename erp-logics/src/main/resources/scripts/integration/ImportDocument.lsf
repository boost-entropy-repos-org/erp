MODULE ImportDocument;

REQUIRE System;

CLASS ImportTypeFileExtension 'Тип файла' {
    xls 'XLS',
    xlsx 'XLSX',
    csv 'CSV',
    dbf 'DBF'
}

CLASS ImportTypeDetail 'Свойство' {
    numberDocument 'Номер инвойса',
    idItem 'Код товара',
    quantity 'Количество',
    price 'Цена',
    sum 'Сумма',
    VAT	'НДС, %',
    VATSum 'Сумма НДС',
    invoiceSum 'Сумма с НДС'
}

CLASS ImportType 'Тип импорта';

nameImportType 'Имя' =  DATA VARSTRING[100](ImportType);
startRowImportType 'Начинать со строки' = DATA INTEGER(ImportType);
separatorImportType 'Разделитель' = DATA INTEGER(ImportType);
byBarcodeImportType 'Импортировать по штрихкоду' = DATA BOOLEAN(ImportType);

indexImportTypeImportTypeDetail 'Поле' = DATA VARSTRING[10] (ImportType, ImportTypeDetail);

importTypeFileExtensionImportType  = DATA ImportTypeFileExtension(ImportType);
captionImportTypeFileExtensionImportType 'Тип файла' (importType) = staticCaption(importTypeFileExtensionImportType(importType));

FORM importTypeDetails
    OBJECTS itd = ImportTypeDetail
    PROPERTIES(itd) staticCaption
    DIALOG ImportTypeDetail OBJECT itd
;

FORM importTypeFileExtensions 'Типы файлов'
    OBJECTS f=ImportTypeFileExtension
    PROPERTIES(f) READONLY staticCaption
    DIALOG ImportTypeFileExtension OBJECT f
;

showSeparator (importType) = importType==ImportTypeFileExtension.csv;

FORM importType 'Тип импорта'
    OBJECTS i=ImportType FIXED PANEL
    PROPERTIES(i) nameImportType, captionImportTypeFileExtensionImportType, startRowImportType,
                   separatorImportType SHOWIF showSeparator(i), byBarcodeImportType
    EDIT ImportType OBJECT i
;

FORM importTypes 'Типы импорта'
    OBJECTS i=ImportType
    PROPERTIES(i) READONLY nameImportType, captionImportTypeFileExtensionImportType, startRowImportType,
                   separatorImportType SHOWIF showSeparator(i), byBarcodeImportType
    PROPERTIES(i) ADDFORM, EDITFORM, DELETE
    DIALOG ImportType OBJECT i

     OBJECTS d = ImportTypeDetail
     PROPERTIES(d) staticCaption
     PROPERTIES(i,d) indexImportTypeImportTypeDetail
;

EXTEND DESIGN importTypes {
    ADD i.box;
    NEW tabbedContainer {
        fill = 1;
        type = TABBED;
        NEW columnsContainer {
            caption = 'Колонки';
            ADD d.box;
        }
    }
    ADD functions.box;
}




