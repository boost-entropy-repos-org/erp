MODULE StockReports;

REQUIRE Stock, Barcode, PriceListLedger;

dataInSessionGroup 'Отм.' = DATA SESSION BOOLEAN (Group);

levelParentGroup (group) = GROUP MIN levelGroupGroup(group, parent) IF dataInSessionGroup(parent)
    BY group PERSISTENT;

inParentGroup (group) = TRUE IF levelParentGroup(group) PERSISTENT;

inSessionGroup 'Отм.' (group) = OVERRIDE
    inParentGroup(group),
    dataInSessionGroup(group);

sessionConcatGroups 'Склады' (groupType) =
    GROUP CONCAT nameGroup(group) IF inSessionGroup(group) AND NOT inSessionGroup(parentGroup(group)),','
    BY groupTypeGroup(group);

sumAGroupTypeStockDate 'Итоговая сумма товаров отчета' (groupType, stock, date) =
    GROUP SUM sumASkuStockDate(sku, stock, date) IF inSessionGroup(groupGroupTypeSku(groupType, sku))
    BY groupType, stock, date PREFCHARWIDTH 15;

sumInGroupTypeStockDateDate 'Итоговая сумма пришедшего товара отчета' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM sumInSkuStockDateDate(sku, stock, dateFrom, dateTo) IF inSessionGroup(groupGroupTypeSku(groupType, sku))
    BY groupType, stock, dateFrom, dateTo PREFCHARWIDTH 15;

sumOutGroupTypeStockDateDate 'Итоговая сумма ушедшегоо товара отчета' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM sumOutSkuStockDateDate(sku, stock, dateFrom, dateTo) IF inSessionGroup(groupGroupTypeSku(groupType, sku))
    BY groupType, stock, dateFrom, dateTo PREFCHARWIDTH 15;

//остатки по товарам
FORM printListBalanceSkusStock 'Остатки по товарам' PRINT
    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) OBJVALUE

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumAGroupTypeStockDate(gt, st ,d)

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    FILTERS inSessionGroup(groupGroupTypeSku(gt, s))
    ORDER BY nameSku

    PROPERTIES(s, st, d) READONLY balanceASkuStockDate, sumASkuStockDate
    FILTERS balanceASkuStockDate(s, st, d)
;

printListBalanceSkusStockDate 'Списком' (stock, date) =
    ACTION FORM printListBalanceSkusStock OBJECTS st = stock, d = date  IMAGE 'print.png' IN printGroup;

FORM printBalanceSkusStock 'Остатки по товарам' PRINT
    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) OBJVALUE

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumAGroupTypeStockDate(gt, st ,d)

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt)
    FILTERS groupTypeGroup(sk) == gt
    PROPERTIES(sk, st, d) READONLY sumASkuGroupStockDate

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    ORDER BY nameSku
    FILTERS isParentGroupSku(sk, s)

    PROPERTIES(s, st, d) READONLY balanceASkuStockDate, sumASkuStockDate
    FILTERS balanceASkuStockDate(s, st, d)
;

printBalanceSkusStockDate 'По группам' (stock, date) =
    ACTION FORM printBalanceSkusStock OBJECTS st = stock, d = date  IMAGE 'print.png' IN printGroup;

//остатки по партиям
FORM printListBalanceBatchesStock 'Остатки по партиям' PRINT
    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) OBJVALUE

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumAGroupTypeStockDate(gt, st ,d)

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY dateBatch, nameBatch, idBarcodeSkuBatch, nameSkuBatch, shortNameUOMBatch//, quantityBatch
    FILTERS inSessionGroup(groupGroupTypeSku(gt, skuBatch(bt)))
    ORDER BY          dateBatch, nameBatch, nameSkuBatch

    PROPERTIES(bt, st, d) READONLY balanceABatchStockDate, accountSumABatchStockDate
    FILTERS balanceABatchStockDate(bt, st, d)
;

printListBalanceBatchesStockDate 'Списком' (stock, date) =
    ACTION FORM printListBalanceBatchesStock OBJECTS st = stock, d = date  IMAGE 'print.png' IN printGroup;

FORM printBalanceBatchesStock 'Остатки по партиям' PRINT
    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) OBJVALUE

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumAGroupTypeStockDate(gt, st ,d)

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt)
    FILTERS groupTypeGroup(sk) == gt
    PROPERTIES(sk, st, d) READONLY accountSumASkuGroupBatchStockDate

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY dateBatch, nameBatch, idBarcodeSkuBatch, nameSkuBatch, shortNameUOMBatch//, quantityBatch
    FILTERS           isParentGroupBatch(sk, bt)
    ORDER BY          dateBatch, nameBatch, nameSkuBatch

    PROPERTIES(bt, st, d) READONLY balanceABatchStockDate, accountSumABatchStockDate
    FILTERS balanceABatchStockDate(bt, st, d)
;

printBalanceBatchesStockDate 'По группам' (stock, date) =
    ACTION FORM printBalanceBatchesStock OBJECTS st = stock, d = date  IMAGE 'print.png' IN printGroup;

FORM reportBalanceStock 'Отчет по остаткам'
    OBJECTS d=DATE FIXED PANEL
    PROPERTIES(d) OBJVALUE

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES sumAGroupTypeStockDate(gt, st ,d)
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES READONLY skuTreeName = nameGroup(sk)
    PROPERTIES(sk, st, d) READONLY sumASkuGroupStockDate, accountSumASkuGroupBatchStockDate
    PROPERTIES inSessionGroup(sk)
    ORDER BY skuTreeName
    FILTERS groupTypeGroup(sk) == gt

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    ORDER BY nameSku
    FILTERS isParentGroupSku(sk, s)

    PROPERTIES(s, st, d) READONLY balanceASkuStockDate, sumASkuStockDate
    PROPERTIES(st, d) printListBalanceSkusStockDate, printBalanceSkusStockDate
    FILTERS balanceASkuStockDate(s, st, d)

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY dateBatch, nameBatch, idBarcodeSkuBatch, nameSkuBatch, shortNameUOMBatch//, quantityBatch
    FILTERS           isParentGroupBatch(sk, bt)
    ORDER BY          dateBatch, nameBatch, nameSkuBatch

    PROPERTIES(bt, st, d) READONLY balanceABatchStockDate, accountSumABatchStockDate
    PROPERTIES(st, d) printListBalanceBatchesStockDate, printBalanceBatchesStockDate
    FILTERS balanceABatchStockDate(bt, st, d)
;

DESIGN reportBalanceStock FROM DEFAULT{
    NEW topContainer{
        childConstraints = TO THE RIGHT;
        ADD d.box;
        ADD st.box;
        ADD gt.box{ADD PROPERTY(inSessionGroup);}
        NEW sumContainer{
            caption = 'Итоговые суммы';
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY(sumAGroupTypeStockDate(gt, st ,d)){caption = 'Товаров отчета';}
        }
    }
    NEW bottomContainer{
        childConstraints = TO THE RIGHT;
        type = SPLITH;
        ADD skuTree.tree.box;
        NEW tabContainer{
            type = TABBED;
            fillHorizontal = 2;
            NEW skuContainer{
                caption = 'Товары';
                childConstraints = TO THE BOTTOM;
                NEW printSkuContainer{
                    childConstraints = TO THE RIGHT;
                    caption = 'Печать';
                    ADD PROPERTY(printListBalanceSkusStockDate);
                    ADD PROPERTY(printBalanceSkusStockDate);
                }
                ADD s.box;
            }
            NEW batchContainer{
                caption = 'Партии';
                childConstraints = TO THE BOTTOM;
                NEW printBatchContainer{
                    childConstraints = TO THE RIGHT;
                    caption = 'Печать';
                    ADD PROPERTY(printListBalanceBatchesStockDate);
                    ADD PROPERTY(printBalanceBatchesStockDate);
                }
                ADD bt.box;
            }
        }
    }
    ADD functions.box;
}

//оборотная ведомость
FORM printListBackSheet 'Оборотная ведомость' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumAGroupTypeStockDate(gt, st, df)
    PROPERTIES(gt, st, df, dt) sumInGroupTypeStockDateDate, sumOutGroupTypeStockDateDate
    PROPERTIES sumAGroupTypeStockDate(gt, st, dt)

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    FILTERS inSessionGroup(groupGroupTypeSku(gt, s))
    ORDER BY nameSku

    PROPERTIES(s, st, df) READONLY balanceASkuStockDate, sumASkuStockDate
    PROPERTIES(s, st, df, dt) READONLY quantitySumInSkuStockDateDate, sumInSkuStockDateDate, quantitySumOutSkuStockDateDate, sumOutSkuStockDateDate
    PROPERTIES(st, df) printListBalanceSkusStockDate, printBalanceSkusStockDate
    PROPERTIES(s, st, dt) READONLY balanceASkuStockDate, sumASkuStockDate
    FILTERS balanceASkuStockDate(s, st, df) OR balanceASkuStockDate(s, st, dt)
;

printListBackSheet 'Списком' (stock, dateFrom, dateTo) =
    ACTION FORM printListBackSheet OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printBackSheet 'Оборотная ведомость' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumAGroupTypeStockDate(gt, st, df)
    PROPERTIES(gt, st, df, dt) sumInGroupTypeStockDateDate, sumOutGroupTypeStockDateDate
    PROPERTIES sumAGroupTypeStockDate(gt, st, dt)

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt)
    FILTERS groupTypeGroup(sk) == gt
    PROPERTIES(sk, st, df) READONLY sumASkuGroupStockDate
    PROPERTIES(sk, st, df, dt) READONLY sumInGroupSkuStockDateDate, sumOutGroupSkuStockDateDate
    PROPERTIES(sk, st, dt) READONLY sumASkuGroupStockDate

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    ORDER BY nameSku
    FILTERS isParentGroupSku(sk, s)

    PROPERTIES(s, st, df) READONLY balanceASkuStockDate, sumASkuStockDate
    PROPERTIES(s, st, df, dt) READONLY quantitySumInSkuStockDateDate, sumInSkuStockDateDate, quantitySumOutSkuStockDateDate, sumOutSkuStockDateDate
    PROPERTIES(st, df) printListBalanceSkusStockDate, printBalanceSkusStockDate
    PROPERTIES(s, st, dt) READONLY balanceASkuStockDate, sumASkuStockDate
    FILTERS balanceASkuStockDate(s, st, df) OR balanceASkuStockDate(s, st, dt)
;

printBackSheet 'По группам' (stock, dateFrom, dateTo) =
    ACTION FORM printBackSheet OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM reportBackSheet 'Оборотная ведомость'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES sumAGroupTypeStockDate(gt, st, df)
    PROPERTIES(gt, st, df, dt) sumInGroupTypeStockDateDate, sumOutGroupTypeStockDateDate
    PROPERTIES sumAGroupTypeStockDate(gt, st, dt)
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES READONLY skuTreeName = nameGroup(sk)
    PROPERTIES(sk, st, df) READONLY sumASkuGroupStockDate
    PROPERTIES(sk, st, df, dt) READONLY sumInGroupSkuStockDateDate, sumOutGroupSkuStockDateDate
    PROPERTIES(sk, st, dt) READONLY sumASkuGroupStockDate
    PROPERTIES inSessionGroup(sk)
    ORDER BY skuTreeName
    FILTERS groupTypeGroup(sk) == gt

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    ORDER BY nameSku
    FILTERS isParentGroupSku(sk, s)

    PROPERTIES(s, st, df) READONLY balanceASkuStockDate, sumASkuStockDate
    PROPERTIES(s, st, df, dt) READONLY quantitySumInSkuStockDateDate, sumInSkuStockDateDate, quantitySumOutSkuStockDateDate, sumOutSkuStockDateDate
    PROPERTIES(st, df, dt) printListBackSheet, printBackSheet
    PROPERTIES(s, st, dt) READONLY balanceASkuStockDate, sumASkuStockDate
    FILTERS balanceASkuStockDate(s, st, df) OR balanceASkuStockDate(s, st, dt)
;

DESIGN reportBackSheet FROM DEFAULT{
    NEW topContainer{
        childConstraints = TO THE RIGHT;
        NEW dateContainer{
            caption = 'Период';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(dateFrom){caption = 'Дата (с)';}
            ADD PROPERTY(dateTo){caption = 'Дата (по)';}
        }
        ADD st.box;
        ADD gt.box{ADD PROPERTY(inSessionGroup);}
        NEW sumContainer{
            caption = 'Итоговые суммы';
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY(sumAGroupTypeStockDate(gt, st, df)){caption = 'Начала периода';}
            ADD PROPERTY(sumInGroupTypeStockDateDate){caption = 'Товара пришедшего за период';}
            ADD PROPERTY(sumOutGroupTypeStockDateDate){caption = 'Товара ушедшего за период';}
            ADD PROPERTY(sumAGroupTypeStockDate(gt, st, dt)){caption = 'Конца периода';}
        }
    }
    NEW bottomContainer{
        childConstraints = TO THE RIGHT;
        type = SPLITH;
        ADD skuTree.tree.box;
        PROPERTY(sumASkuGroupStockDate(sk, st, df)){caption = 'Остаток на начало периода';}
        PROPERTY(sumASkuGroupStockDate(sk, st, dt)){caption = 'Остаток на конец периода';}
        NEW tabContainer{
            type = TABBED;
            fillHorizontal = 2;
            NEW skuContainer{
                caption = 'Товары';
                childConstraints = TO THE BOTTOM;
                NEW printSkuContainer{
                    childConstraints = TO THE RIGHT;
                    caption = 'Печать';
                    ADD PROPERTY(printListBackSheet);
                    ADD PROPERTY(printBackSheet);
                }
                ADD s.box;
                PROPERTY(balanceASkuStockDate(s, st, df)){caption = 'Остаток на начало периода';}
                PROPERTY(sumASkuStockDate(s, st, df)){caption = 'Сумма на начало периода';}
                PROPERTY(balanceASkuStockDate(s, st, dt)){caption = 'Остаток на конец периода';}
                PROPERTY(sumASkuStockDate(s, st, dt)){caption = 'Сумма на конец периода';}
            }
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    stockReports{
        ADD reportBalanceStock;
        ADD reportBackSheet;
    }
}