MODULE StockReports;

REQUIRE EmployeeStock, Barcode, PriceListLedger, StockSkuDocument, OrderLedger;

quantityChildWithSession (group) = GROUP SUM 1 IF dataInSession(Group childGroup) AND isParent(childGroup, Group group) BY group;
quantityParentWithSession (group) = GROUP SUM 1 IF dataInSession(Group parentGroup) AND isParent(Group group, parentGroup) BY group;                                                                        
                                                                           
backgroundInSession 'Цвет' (Group group) = CASE 
    WHEN dataInSession(group) THEN RGB(0,0,0)
    WHEN inParent(group) THEN RGB(230,248,250) 
    WHEN quantityChildWithSession (group) != descendantNumber(group) AND NOT quantityParentWithSession (group) THEN RGB(203,203,203);

backgroundInclude 'Цвет' (GroupType groupType, Sku sku) = IF include(sku) 
    THEN RGB(0,0,0) 
    ELSE RGB(230,248,250) IF inSession(group(groupType, sku));

countIncludeSku 'Кол-во непосредственных товаров в группе' (group, groupType) =
    GROUP SUM 1 IF group(GroupType groupType, Sku sku) == Group group AND include(groupType,sku)
    BY group, groupType;
    
sessionConcatGroups 'Группы' (groupType) =
    GROUP CONCAT name(Group group) IF countIncludeSku(group, GroupType groupType),','
    BY groupType;  

// --------------------------------- Оптимизация с хранимыми свойствами ------------------------------------- //
 
// ---- Оборотная ведомость

balanceB 'Остаток на начало' = DATA LOCAL NUMERIC[16,5] (Sku);
sumB 'Сумма на начало' = DATA LOCAL NUMERIC[18,4] (Sku);

quantityIn 'Кол-во пришедшего' = DATA LOCAL NUMERIC[16,5] (Sku);
sumIn 'Сумма пришедшего' = DATA LOCAL NUMERIC[18,4] (Sku);

quantityOut 'Кол-во ушедшего' = DATA LOCAL NUMERIC[16,5] (Sku);
sumOut 'Сумма ушедшего' = DATA LOCAL NUMERIC[18,4] (Sku);

balanceA 'Остаток на конец' = DATA LOCAL NUMERIC[16,5] (Sku);
sumA 'Сумма на конец' = DATA LOCAL NUMERIC[18,4] (Sku);

balanceB 'Остаток на начало' = DATA LOCAL NUMERIC[16,5] (Group);
sumB 'Сумма на начало' = DATA LOCAL NUMERIC[18,4] (Group);

quantityIn 'Кол-во пришедшего' = DATA LOCAL NUMERIC[16,5] (Group);
sumIn 'Сумма пришедшего' = DATA LOCAL NUMERIC[18,4] (Group);

quantityOut 'Кол-во ушедшего' = DATA LOCAL NUMERIC[16,5] (Group);
sumOut 'Сумма ушедшего' = DATA LOCAL NUMERIC[18,4] (Group);

balanceA 'Остаток на конец' = DATA LOCAL NUMERIC[16,5] (Group);
sumA 'Сумма на конец' = DATA LOCAL NUMERIC[18,4] (Group);

inBackSheet 'Вкл.' = DATA LOCAL BOOLEAN (Stock);

inBackSheetStocks 'Склады' () =
    GROUP CONCAT name(Stock st) IF inBackSheet(st) ,', ' PREFCHARWIDTH 50;

overFillReportBackSheetFromTo = ABSTRACT (GroupType, DATE, DATE);

fillReportBackSheetFromTo 'Сформировать'(GroupType gt, DATE df, DATE dt) = {

    balanceB(Sku sk) <- NULL;           
    quantityIn(Sku sk) <- NULL;  
    quantityOut(Sku sk) <- NULL;  
    balanceA(Sku sk) <- NULL;  
    sumB(Sku sk) <- NULL;           
    sumIn(Sku sk) <- NULL;  
    sumOut(Sku sk) <- NULL;  
    sumA(Sku sk) <- NULL;  

    balanceB(Group g) <- NULL;           
    quantityIn(Group g) <- NULL;  
    quantityOut(Group g) <- NULL;  
    balanceA(Group g) <- NULL;  
    sumB(Group g) <- NULL;           
    sumIn(Group g) <- NULL;  
    sumOut(Group g) <- NULL;  
    sumA(Group g) <- NULL;  

    balanceA(Sku sk) <- NUMERIC[16,5]([ = GROUP SUM balanceA(Sku sk, Stock st, DATE dt) IF inBackSheet(st) BY sk, dt](sk, dt));           
    sumA(Sku sk) <- NUMERIC[18,4]([ = GROUP SUM sumA(Sku sk, Stock st, DATE dt) IF inBackSheet(st) BY sk, dt](sk, dt));  

    quantityIn(Sku sk) <- NUMERIC[16,5]([ = GROUP SUM quantitySumIn(Sku sk, Stock st, DATE df, DATE dt) IF inBackSheet(st) BY sk, df, dt](sk, df, dt));           
    sumIn(Sku sk) <- NUMERIC[18,4]([ = GROUP SUM sumIn(Sku sk, Stock st, DATE df, DATE dt) IF inBackSheet(st) BY sk, df, dt](sk, df, dt));  

    quantityOut(Sku sk) <- NUMERIC[16,5]([ = GROUP SUM quantitySumOut(Sku sk, Stock st, DATE df, DATE dt) IF inBackSheet(st) BY sk, df, dt](sk, df, dt));           
    sumOut(Sku sk) <- NUMERIC[18,4]([ = GROUP SUM sumOut(Sku sk, Stock st, DATE df, DATE dt) IF inBackSheet(st) BY sk, df, dt](sk, df, dt));  

    balanceB(Sku sk) <- NUMERIC[16,5]([ = GROUP SUM balanceB(Sku sk, Stock st, DATE df) IF inBackSheet(st) BY sk, df](sk, df));           
    sumB(Sku sk) <- NUMERIC[18,4]([ = GROUP SUM sumB(Sku sk, Stock st, DATE df) IF inBackSheet(st) BY sk, df](sk, df));  

    balanceA(Group g) <- [ = GROUP SUM balanceA(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;           
    sumA(Group g) <- [ = GROUP SUM sumA(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;   

    quantityIn(Group g) <- [ = GROUP SUM quantityIn(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;          
    sumIn(Group g) <- [ = GROUP SUM sumIn(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt; 

    quantityOut(Group g) <- [ = GROUP SUM quantityOut(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;          
    sumOut(Group g) <- [ = GROUP SUM sumOut(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt; 

    balanceB(Group g) <- [ = GROUP SUM balanceB(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;           
    sumB(Group g) <- [ = GROUP SUM sumB(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;   

    overFillReportBackSheetFromTo(gt, df, dt);      
}

recBalanceB 'Остаток на начало периода' = GROUP SUM balanceB (Group child) IF isParent(child, Group parent) BY parent;
recSumB 'Сумма на начало периода' = GROUP SUM sumB (Group child) IF isParent(child, Group parent) BY parent;
recSumIn 'Сумма пришедшего' = GROUP SUM sumIn (Group child) IF isParent(child, Group parent) BY parent;
recSumOut 'Сумма ушедшего' = GROUP SUM sumOut (Group child) IF isParent(child, Group parent) BY parent;
recSumA 'Сумма на конец периода' = GROUP SUM sumA (Group child) IF isParent(child, Group parent) BY parent;

sumB 'Итоговая сумма отчета' = GROUP SUM sumB(Sku sku) IF include(GroupType groupType, sku) BY groupType ;
sumIn 'Итоговая сумма пришедшего товара отчета' = GROUP SUM sumIn(Sku sku) IF include(GroupType groupType, sku) BY groupType;
sumOut 'Итоговая сумма ушедшего товара отчета'  = GROUP SUM sumOut(Sku sku) IF include(GroupType groupType, sku) BY groupType;
sumA 'Итоговая сумма отчета' = GROUP SUM sumA(Sku sku) IF include(GroupType groupType, sku) BY groupType;

// ---- Отчет по остаткам

overFillReportBalanceData = ABSTRACT (Stock, GroupType, DATETIME);

fillReportBalanceData 'Рассчитать'(Stock st, GroupType gt, DATETIME dt) = {

    balanceB(Sku sk) <- balanceB(sk, st, dt);
    sumB(Sku sk) <- sumB(sk, st, dt);
    
    balanceB(Group g) <- [ = GROUP SUM balanceB(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;           
    sumB(Group g) <- [ = GROUP SUM sumB(Sku sk) BY group(GroupType gt, sk)](g) IF  groupType(g) == gt;   

    overFillReportBalanceData(st,gt,dt);
}
// --------------------------------- Конец оптимизации с хранимыми свойствами ------------------------------------- //

filterBalanceBAInOut (Sku s) = (balanceB(s) OR
    balanceA(s) OR
    sumIn(s) OR
    sumOut(s));

//остатки по товарам
FORM printListBalanceSkusStock 'Остатки по товарам'
    OBJECTS d = DATETIME PANEL
    PROPERTIES(d) dateTime = VALUE
    PROPERTIES currentDateTime()

    OBJECTS st = Stock PANEL
    PROPERTIES(st) SELECTOR fullName

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumB(gt)

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcode, name, shortNameUOM
    FILTERS inSession(group(gt, s))
    ORDER BY name(s)

    PROPERTIES(s) READONLY balanceB, sumB
    FILTERS balanceB(s)
    FILTERS s IS Sku AND st IS Stock AND d IS DATETIME // фильтр, чтобы сохранилась старая иерархия
;

printListBalanceSkus 'Списком' (Stock stock, DATETIME dateTime, GroupType gtype) =
    { PRINT printListBalanceSkusStock OBJECTS st = stock, d = dateTime, gt = gtype; } IMAGE 'print.png' IN print;

accountPriceB 'Цена учетная' (Sku sku, Stock st, DATETIME dateTime) = prevPriceB(SystemLedgerPriceListType.accountPriceListType, sku, st, dateTime);   

FORM printBalanceSkusStock 'Остатки по товарам'
    OBJECTS d = DATETIME PANEL
    PROPERTIES(d) dateTime = VALUE
    PROPERTIES currentDateTime()

    OBJECTS st = Stock PANEL
    PROPERTIES(st) SELECTOR fullName

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumB(gt)

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDER BY canonicalName(sk)
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    FILTERS inSession(sk) AND countSku(sk, gt)
    FILTERS groupType(sk) == gt
    PROPERTIES(sk) READONLY sumB

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcode, name, shortNameUOM
    ORDER BY name(s)
    FILTERS group(gt, s) == sk

    PROPERTIES(s) READONLY balanceB, sumB
    PROPERTIES(s, st, d) READONLY accountPriceB
    FILTERS balanceB(s)
;

printBalanceSkus 'По группам' (Stock stock, DATETIME dateTime, GroupType gtype) =
    { PRINT printBalanceSkusStock OBJECTS st = stock, d = dateTime, gt = gtype; } IMAGE 'print.png' IN print;

//остатки по партиям
FORM printListBalanceBatchesStock 'Остатки по партиям'
    OBJECTS d = DATETIME PANEL
    PROPERTIES(d) dateTime = VALUE

    OBJECTS st = Stock PANEL
    PROPERTIES(st) SELECTOR fullName

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumB(gt)

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY date, name, idBarcodeSku, nameSku, shortNameUOM//, shippedQuantityBatch
    FILTERS inSession(group(gt, sku(bt)))
    ORDER BY          date(bt), name(bt), nameSku(bt)

    PROPERTIES(bt, st, d) READONLY balanceB, accountSumB
    FILTERS balanceB(bt, st, d)
;

printListBalanceBatches 'Списком' (Stock stock, DATETIME dateTime, GroupType gtype) =
    { PRINT printListBalanceBatchesStock OBJECTS st = stock, d = dateTime, gt = gtype; }  IMAGE 'print.png' IN print;

FORM printBalanceBatchesStock 'Остатки по партиям'
    OBJECTS d = DATETIME PANEL
    PROPERTIES(d) dateTime = VALUE

    OBJECTS st = Stock PANEL
    PROPERTIES(st) SELECTOR fullName

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumB(gt)

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDER BY canonicalName(sk)
    FILTERS inSession(sk) AND countSku(sk, gt)
    FILTERS groupType(sk) == gt
    PROPERTIES(sk, st, d) READONLY accountSumBSkuBatch
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY date, name, idBarcodeSku, nameSku, shortNameUOM//, shippedQuantityBatch
    FILTERS           group(gt, sku(bt)) == sk
    ORDER BY          date(bt), name(bt), nameSku(bt)

    PROPERTIES(bt, st, d) READONLY balanceB, accountSumB
    FILTERS balanceB(bt, st, d)
;

printBalanceBatches 'По группам' (Stock stock, DATETIME dateTime, GroupType gtype) =
    { PRINT printBalanceBatchesStock OBJECTS st = stock, d = dateTime, gt = gtype; }  IMAGE 'print.png' IN print;

showAccountSumBatch 'Показывать учетные суммы по партиям в отчете по остаткам' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() showAccountSumBatch
;
DESIGN options {
    stock1 {
        MOVE PROPERTY(showAccountSumBatch());
    }
}

FORM reportBalanceStock 'Отчет по остаткам'
    OBJECTS d=DATETIME PANEL
    PROPERTIES(d) VALUE

    OBJECTS st = Stock PANEL
    PROPERTIES(st) SELECTOR name
    FILTERS isCompany(st)

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
//    PROPERTIES sumBGroupTypeStockDate(gt, st ,d)
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parent
    PROPERTIES inSession(sk) BACKGROUND backgroundInSession(sk)
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    PROPERTIES(sk) READONLY recBalanceB, recSumB
    PROPERTIES(sk, st, d) READONLY accountSumRecBSkuBatch SHOWIF showAccountSumBatch()
    ORDER BY order(sk), skuTreeName
    FILTERS groupType(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS s = Sku
    PROPERTIES(s) READONLY id SHOWIF showIDs(), idBarcode, name ON SHORTCUT showMovementSku(s,st) ON SHORTCUT showMovementPriceSku(s,st), shortNameUOM
    ORDER BY name(s)
    FILTERS isParent(sk, s)

    PROPERTIES(s) READONLY balanceB, sumB
    PROPERTIES(s, st, d) READONLY accountSumBatchB SHOWIF showAccountSumBatch()
    PROPERTIES(st, d, gt) printListBalanceSkus, printBalanceSkus
    FILTERGROUP balance 
        FILTER 'С остатком (кол-во)' balanceB(s) 'F7' DEFAULT
        FILTER 'С остатком (кол-во/сумма)' balanceB(s) OR sumB(s) 'F8'   
    FILTERGROUP incorrectSum FILTER 'Неправильная сумма по партиям' abs(sumB(s) (-) accountSumBatchB(s, st, d)) > 1 'F9'   

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY date, name ON SHORTCUT showMovementBatch(bt,st), idBarcodeSku, 
                      nameSku ON SHORTCUT showMovementSku(bt,st), shortNameUOM//, shippedQuantityBatch
    FILTERS           isParent(sk, bt)
    ORDER BY          date(bt), name(bt), nameSku(bt)

    PROPERTIES(bt, st, d) READONLY balanceB, accountSumB
    PROPERTIES(st, d, gt) printListBalanceBatches, printBalanceBatches
    FILTERS balanceB(bt, st, d)

    EVENTS 
        ON CHANGE gt fillReportBalanceData(st, gt, d),
        ON CHANGE d fillReportBalanceData(st, gt, d),
        ON CHANGE st fillReportBalanceData(st, gt, d)
;
@extendFormFilterStockAccess(st, reportBalanceStock);

DESIGN reportBalanceStock {
    NEW topContainer{
        type = CONTAINERH;
        MOVE gt.box;        
        MOVE d.box;
        MOVE st.box;
    }
    NEW bottomContainer {
        fill = 1;
        type = SPLITH;
        MOVE skuTree.tree.box;
        NEW tabContainer {
            fill = 3;
            type = TABBED;
            NEW skuContainer{
                caption = 'Товары';
                NEW printSkuContainer{
                    type = CONTAINERH;
                    caption = 'Печать';
                    MOVE PROPERTY(printListBalanceSkus(st,d,gt));
                    MOVE PROPERTY(printBalanceSkus(st,d,gt));
                }
                MOVE s.box;
            }
            NEW batchContainer{
                caption = 'Партии';
                NEW printBatchContainer {
                    type = CONTAINERV;
                    caption = 'Печать';
                    NEW print1{
                        type = CONTAINERH;
                        MOVE PROPERTY(printListBalanceBatches(st,d,gt));
                        MOVE PROPERTY(printBalanceBatches(st,d,gt));
                    }

                }
                MOVE bt.box;
            }
        }
    }
    MOVE functions.box;
}

//-- Печатные формы для оборотной ведомости списком и по группам--//

FORM printListBackSheet 'Оборотная ведомость'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES inBackSheetStocks()

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES(gt) READONLY sumB, sumIn, sumOut, sumA

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcode, name, shortNameUOM
    ORDER BY name(s)

    PROPERTIES(s) READONLY balanceB, sumB
    PROPERTIES(s) READONLY quantityIn, sumIn, quantityOut, sumOut
    PROPERTIES(s) READONLY balanceA, sumA
    FILTERS include(gt, s)
    FILTERS filterBalanceBAInOut (s)
    FILTERS s IS Sku AND df IS DATE AND dt IS DATE // фильтр, чтобы сохранилась старая иерархия
;

printListBackSheet 'Списком' (DATE dateFrom, DATE dateTo, GroupType gt) =
    { PRINT printListBackSheet OBJECTS df = dateFrom, dt = dateTo, gt = gt; }  IMAGE 'print.png' IN print;
xlsListBackSheet 'Списком (XLS)' (DATE dateFrom, DATE dateTo, GroupType gt) =
    { PRINT printListBackSheet OBJECTS df = dateFrom, dt = dateTo, gt = gt XLS; } IMAGE 'print.png' IN print;

FORM printBackSheet 'Оборотная ведомость'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES inBackSheetStocks()

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES(gt) READONLY sumB, sumIn, sumOut, sumA

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDER BY canonicalName(sk)
    FILTERS countIncludeSku(sk, gt)
    FILTERS groupType(sk) == gt
    PROPERTIES(sk) READONLY sumB, sumIn, sumOut, sumA
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT
    FILTERS sk IS Group AND df IS DATE AND dt IS DATE // фильтр, чтобы сохранилась старая иерархия

    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcode, name, shortNameUOM
    ORDER BY name(s)
    FILTERS group(gt, s) == sk

    PROPERTIES(s) READONLY balanceB, sumB
    PROPERTIES(s) READONLY quantityIn, sumIn, quantityOut, sumOut
    PROPERTIES(s) READONLY balanceA, sumA
    FILTERS include(gt, s)
    FILTERS filterBalanceBAInOut (s)
    FILTERS s IS Sku AND df IS DATE AND dt IS DATE // фильтр, чтобы сохранилась старая иерархия
;

printBackSheet 'По группам' (DATE dateFrom, DATE dateTo, GroupType gt) =
    { PRINT printBackSheet OBJECTS df = dateFrom, dt = dateTo, gt = gt; }  IMAGE 'print.png' IN print;

xlsBackSheet 'По группам (XLS)' (DATE dateFrom, DATE dateTo, GroupType gt) =
    { PRINT printBackSheet OBJECTS df = dateFrom, dt = dateTo,  gt=gt XLS; } IMAGE 'print.png' IN print;

fillDocumentReport 'Подбор документа'(GroupType gt, DATE df, DATE dt) = {
    DIALOG documents DO {
        include(Sku sku) <- TRUE WHERE [ = GROUP SUM 1 BY document(DocumentDetail d), sku(d)](selectedDocument(), sku);
        formRefresh();
        fillReportBackSheetFromTo(gt, df, dt);
    }
} TOOLBAR;

deleteInDataSessionReport 'Очистить выборку'(GroupType gt, DATE df, DATE dt) = {
    include(Sku sku) <- NULL;
    formRefresh();
    fillReportBackSheetFromTo(gt, df, dt);
} TOOLBAR;

FORM dialogStocksBackSheet 'Выбор складов'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY VALUE(a), name(sg)
    ORDER BY name(sg)
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, name
    PROPERTIES (s) inBackSheet
    ORDER BY name(s)
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isCompany(s),
            countCompanyStock(sg)

    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT    
    FILTERGROUP select FILTER 'Отм.' inBackSheet(s) 'F9' 
;

DESIGN dialogStocksBackSheet {
    main{
        preferredSize = (1024, 768);
        NEW topContainer {
            type = SPLITH;
            fill = 1;
            MOVE stockTree.tree.box {
                caption = 'Склады';
            }
            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(name(s)){
                    minimumCharWidth = 35;
                    preferredCharWidth = 35;
                }
            }
        }
        MOVE functions.box;
    }
}

@extendFormFilterStockAccess(s, dialogStocksBackSheet, company);    
changeStocksBackSheet(GroupType gt, DATE df, DATE dt) = {
    DIALOG dialogStocksBackSheet DO 
        fillReportBackSheetFromTo(gt, df, dt);
}

showMovementSkuBackSheet 'Показать движение товара по складу' (Sku sku) = { 
    NEWSESSION NESTED(inBackSheet) 
        SHOW movementSkuStock OBJECTS s = sku, st = [ = GROUP MIN Stock st IF inBackSheet(st)]() ; 
}

showMovementPriceSkuBackSheet 'Показать изменение цены товара по складу' (Sku sku) = { 
    NEWSESSION NESTED(inBackSheet) 
        SHOW movementPriceSkuStock OBJECTS s = sku, st = [ = GROUP MIN Stock st IF inBackSheet(st)]() ; 
}

showReserveSkuBackSheet 'Показать резерв товара по складу' (Sku sku) = { 
    NEWSESSION NESTED(inBackSheet) 
        SHOW orderSkuLedgerNotDate OBJECTS sk = sku, st = [ = GROUP MIN Stock st IF inBackSheet(st)]() ;
}

FORM reportBackSheet 'Оборотная ведомость' 
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES (gt) READONLY sumB, sumIn, sumOut, sumA

    PROPERTIES inBackSheetStocks() ON CHANGE changeStocksBackSheet(gt, df, dt)

    TREE skuTree sk = Group PARENT parent
    PROPERTIES inSession(sk) BACKGROUND backgroundInSession(sk)
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    PROPERTIES(sk) READONLY recSumB, recSumIn, recSumOut, recSumA
    ORDER BY order(sk), skuTreeName
    FILTERS groupType(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS s = Sku
    PROPERTIES(gt,s) include BACKGROUND backgroundInclude(gt,s)
    PROPERTIES(s) READONLY idBarcode, name  ON SHORTCUT showMovementSkuBackSheet(s) ON SHORTCUT showMovementPriceSkuBackSheet(s) ON SHORTCUT showReserveSkuBackSheet(s), shortNameUOM
    ORDER BY name(s)
    
    PROPERTIES(s) READONLY balanceB, sumB
    PROPERTIES(s) READONLY quantityIn, sumIn, quantityOut, sumOut
    PROPERTIES(s) READONLY balanceA, sumA
    PROPERTIES(df, dt, gt) printListBackSheet, xlsListBackSheet, printBackSheet, xlsBackSheet

    FILTERGROUP include FILTER 'Отмеченные' include(gt, s) DEFAULT 
    
    FILTERS filterBalanceBAInOut (s)    
    PROPERTIES(gt, df, dt) TODRAW s fillDocumentReport, deleteInDataSessionReport
    
    EVENTS 
        ON CHANGE gt fillReportBackSheetFromTo(gt, df, dt), 
        ON CHANGE df fillReportBackSheetFromTo(gt, df, dt), 
        ON CHANGE dt fillReportBackSheetFromTo(gt, df, dt)
;

onInitReportBackSheet() = {
    IF countAccessCompanyStock(currentUser()) ==1 THEN {
        inBackSheet(Stock st)  <- TRUE WHERE  defaultCompanyStock(currentUser())== st; 
        fillReportBackSheetFromTo(SystemGroupType.skuGroupType,currentDate(),currentDate());
    }    
}

EXTEND FORM reportBackSheet 
    EVENTS 
        ON INIT onInitReportBackSheet()
;

DESIGN reportBackSheet {
    NEW topContainer {
        type = CONTAINERH;
        NEW dates {
            type = CONTAINERH;
            caption = 'Период';
            MOVE PROPERTY(dateFrom){caption = 'Дата (с)';}
            MOVE PROPERTY(dateTo){caption = 'Дата (по)';}
        }
        MOVE gt.box;
        NEW stocks {
            caption = 'Склады';
            type = CONTAINERH;
            MOVE PROPERTY(inBackSheetStocks());        
        }
    }
    NEW sumContainer{
        type = CONTAINERH;
        caption = 'Итоговые суммы';
        MOVE PROPERTY(sumB(gt)){caption = 'Начала периода';}
        MOVE PROPERTY(sumIn(gt)){caption = 'Товара пришедшего за период';}
        MOVE PROPERTY(sumOut(gt)){caption = 'Товара ушедшего за период';}
        MOVE PROPERTY(sumA(gt)){caption = 'Конца периода';}
    }
    NEW bottomContainer {
        fill = 1;
        type = SPLITH;

        MOVE skuTree.tree.box;

        NEW tabContainer {
            fill = 2;
            type = TABBED;
            NEW skuContainer{
                caption = 'Товары';
                NEW printSkuContainer {
                    type = CONTAINERH;
                    caption = 'Печать';
                    MOVE PROPERTY(printListBackSheet(df,dt,gt));
                    MOVE PROPERTY(xlsListBackSheet(df,dt,gt));
                    MOVE PROPERTY(printBackSheet(df,dt,gt));
                    MOVE PROPERTY(xlsBackSheet(df,dt,gt));
                }
                MOVE s.box;
                PROPERTY(balanceB(s)){caption = 'Остаток на начало периода';}
                PROPERTY(sumB(s)){caption = 'Сумма на начало периода';}
                PROPERTY(balanceA(s)){caption = 'Остаток на конец периода';}
                PROPERTY(sumA(s)){caption = 'Сумма на конец периода';}
            }
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    stockReports{
        ADD reportBalanceStock;
        ADD reportBackSheet;
    }
}