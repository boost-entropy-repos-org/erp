MODULE InventoryTerminal;

REQUIRE Inventory, Terminal;

NAMESPACE Terminal;

addDetailDialogTerminalListInventory 'Заполнить из документа ТСД' (listInventory) = ACTION (listInventory) {
    FORM terminalDocuments MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL document = TerminalDocument ();
        document() <- chosenObject('td');
        usedTerminalDocument(terminalDocument) <- TRUE WHERE terminalDocument == document();

        LOCAL page = PageInventory ();
        LOCAL detailCount = INTEGER();
        detailCount() <- 0;

        FOR terminalDocumentTerminalDocumentDetail(tdd) == document() DO {
            IF detailCount() == 0 THEN {
                FOR ADDOBJ pi = PageInventory DO {
                    listInventoryPageInventory(pi) <- listInventory;
                    page() <- pi;
                }
            }

            FOR ADDOBJ pid = PageInventoryDetail DO {
                pageInventoryPageInventoryDetail(pid) <- page();
                skuPageInventoryDetail(pid) <- OVERRIDE skuId(barcodeTerminalDocumentDetail(tdd)), skuBarcodeIdDate(barcodeTerminalDocumentDetail(tdd), dateListInventory(listInventory));
                quantityPageInventoryDetail(pid) <- quantityTerminalDocumentDetail(tdd);

                detailCount() <- detailCount() + 1;

                IF detailCount() >= 20 THEN {
                    ASSIGN detailCount() <- 0;
                }
            }
        }

    }
} TOOLBAR;

EXTEND FORM listInventory
    PROPERTIES SHOWIF isSkuListInventory(li) addDetailDialogTerminalListInventory(li) TODRAW dp
;