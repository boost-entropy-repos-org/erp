MODULE InventoryTerminal;

REQUIRE Inventory, Terminal;

NAMESPACE Terminal;

detailCountInventory 'Количество строк на страницу'  = DATA INTEGER (Inventory) IN documentPrm;

EXTEND FORM inventory
    PROPERTIES detailCountInventory(in);

detailCountOperation 'Количество строк на страницу' = DATA INTEGER (Inventory.Operation);

EXTEND FORM Inventory.operation
     PROPERTIES(o) detailCountOperation
 ;
DESIGN Inventory.operation {
    showContainer {
        MOVE PROPERTY(detailCountOperation(o));
    }
}

@deriveDocumentOperationProperty(Inventory, detailCount);

addInventoryDetailsTerminalDocumentListInventory = ACTION (td,li){

    LOCAL page = PageInventory ();
    LOCAL detailCount = INTEGER();
    detailCount() <- 0;

    FOR terminalDocumentTerminalDocumentDetail(tdd) == td DO {
        IF detailCount() == 0 THEN {
            FOR ADDOBJ pi = PageInventory DO {
                listInventoryPageInventory(pi) <- li;
                page() <- pi;
            }
        }

        FOR ADDOBJ pid = PageInventoryDetail DO {
            pageInventoryPageInventoryDetail(pid) <- page();
            skuPageInventoryDetail(pid) <- OVERRIDE skuId(barcodeTerminalDocumentDetail(tdd)), skuBarcodeIdDate(barcodeTerminalDocumentDetail(tdd), dateListInventory(li));
            quantityPageInventoryDetail(pid) <- quantityTerminalDocumentDetail(tdd);

            detailCount() <- detailCount() + 1;

            IF detailCount() >= (OVERRIDE 20, detailCountInventory(inventoryListInventory(li))) THEN {
                ASSIGN detailCount() <- 0;
            }
        }
    }

}

addDetailDialogTerminalListInventory 'Заполнить из документа ТСД' (listInventory) = ACTION (listInventory) {
    FORM terminalDocuments MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL document = TerminalDocument ();
        document() <- chosenObject('td');
        usedTerminalDocument(terminalDocument) <- TRUE WHERE terminalDocument == document();

        addInventoryDetailsTerminalDocumentListInventory(document(),listInventory);
    }
} TOOLBAR;

includeTerminalDocument 'Отм.' = DATA LOCAL BOOLEAN (TerminalDocument);
EXTEND FORM terminalDocuments PROPERTIES (td) includeTerminalDocument BEFORE idTerminalDocument(td);
 
addListInventoryDialogTerminalStockInventory 'Заполнить из документов ТСД' (stock, inventory) = ACTION (stock, inventory) {
    FORM terminalDocuments MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR  includeTerminalDocument(td) ADDOBJ  li = ListInventory DO {
            usedTerminalDocument(td) <- TRUE;
            stockListInventory(li) <- stock;
            inventoryListInventory(li) <- inventory;
            includeTerminalDocument(td) <- NULL;
            
            addInventoryDetailsTerminalDocumentListInventory(td,li);
        }
    }
} TOOLBAR;

EXTEND FORM listInventory
    PROPERTIES SHOWIF isSkuListInventory(li) addDetailDialogTerminalListInventory(li) TODRAW dp
;

EXTEND FORM inventory
    PROPERTIES(ds, in) addListInventoryDialogTerminalStockInventory
;