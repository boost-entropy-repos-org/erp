MODULE InventoryTerminal;

REQUIRE Inventory, Terminal;

NAMESPACE Terminal;

detailCount 'Количество строк на страницу'  = DATA INTEGER (Inventory) IN documentPrm;

EXTEND FORM inventory
    PROPERTIES detailCount(in);

detailCount 'Количество строк на страницу' = DATA INTEGER (Inventory.Operation);

EXTEND FORM Inventory.operation
     PROPERTIES(o) detailCount
 ;
DESIGN Inventory.operation {
    showContainer {
        MOVE PROPERTY(detailCount(o));
    }
}

@deriveDocumentOperationProperty(Inventory, detailCount);

addInventoryDetails(TerminalDocument td,ListInventory li) = ACTION {

    LOCAL page = PageInventory ();
    LOCAL detailCount = INTEGER();
    detailCount() <- 0;

    LOCAL countDetail = INTEGER();
    countDetail() <- (OVERRIDE 20, detailCount(inventory(li)));
    
    LOCAL countPages = INTEGER();
    countPages() <- (countTerminalDocumentDetail(td) + countDetail() - 1) / countDetail();
    
    FOR iterate(INTEGER i, 1, countPages()) NEW p = PageInventory DO {
        listInventory(p) <- li;
    }
    
    FOR terminalDocument(TerminalDocumentDetail tdd) == td INLINE NEW pid = PageInventoryDetail DO {
        pageInventory(pid) <- pageInventoryIndex(li, ((index(tdd) - 1)/countDetail()) + 1);
        sku(pid) <- sku(tdd);
        quantity(pid) <- quantity(tdd);
    }
    DELETE PageInventory p WHERE  listInventory(p) == li AND NOT countPageInventoryDetail(p);
}

addDetailDialogTerminal 'Заполнить из документа ТСД' (ListInventory listInventory) = ACTION {
    SHOW terminalDocuments ;
    IF formResult() == FormResult.ok THEN {
        LOCAL document = TerminalDocument ();
        document() <- chosenObject('td');
        used(TerminalDocument terminalDocument) <- TRUE WHERE terminalDocument == document();

        addInventoryDetails(document(),listInventory);
    }
} TOOLBAR;
 
addListInventoryDialogTerminal 'Заполнить из документов ТСД' (Stock stock, Inventory inventory) = ACTION {
    SHOW terminalDocuments ;
    IF formResult() == FormResult.ok THEN {
        FOR  include(TerminalDocument td) NEW  li = ListInventory DO {
            used(td) <- TRUE;
            stock(li) <- stock;
            inventory(li) <- inventory;
            include(td) <- NULL;
            
            addInventoryDetails(td,li);
        }
    }
} TOOLBAR;

EXTEND FORM listInventory
    PROPERTIES SHOWIF isSku(li) addDetailDialogTerminal(li) TODRAW dp
;

EXTEND FORM inventory
    PROPERTIES(ds, in) addListInventoryDialogTerminal
;