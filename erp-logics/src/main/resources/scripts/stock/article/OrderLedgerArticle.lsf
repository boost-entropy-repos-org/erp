MODULE OrderLedgerArticle;

REQUIRE StockArticleDocument, OrderLedger;

NAMESPACE OrderLedger;

FORM orderArticleLedgerNotDate 'Резерв по артикулам'

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES  SELECTOR nameStock(st)

    OBJECTS ar = Article FIXED PANEL
    PROPERTIES SELECTOR idArticle(ar)

    OBJECTS s = OrderLedger
    PROPERTIES(s) READONLY BACKGROUND backgroundReserveOrderLedger(s) statusReserveOrderLedger     
    PROPERTIES(s) READONLY  dateOrderLedger, 
                  dateTimeOrderLedger, nameContactOrderLedger, nameContactStockOrderLedger, descriptionOrderLedger,
                  toShipQuantityOrderLedger, quantityOrderLedger
    ORDER BY statusReserveOrderLedger(s), dateTimeOrderLedger(s)
                  
    FILTERS stockOrderLedger(s)==st,
            articleItem(skuOrderLedger(s))==ar

    FILTERS isPostedOrderLedger(s) AND NOT isClosedOrderLedger(s)
    FILTERGROUP toShipQuantityFilters
        FILTER 'Не выполнено' toShipQuantityOrderLedger(s) > 0 'F7' DEFAULT   

    FILTERGROUP stockTimesFilters
        FILTER 'Резерв (закупки) всего' isPurchaseOrderLedger(s) 'F11'
        FILTER 'Резерв (продажи) всего' TRUE AND NOT isPurchaseOrderLedger(s) 'F10'
;

DESIGN orderArticleLedgerNotDate {
    main {
        type = CONTAINERV;
        NEW row{
            type = CONTAINERH;
            MOVE st.box;
            MOVE ar.box;
        }
        MOVE s.box;
    }
    MOVE functions.box;
}

FORM orderArticleLedger 'Резерв по артикулам'

    OBJECTS t = DATETIME FIXED PANEL
    PROPERTIES  dVal=OBJVALUE(t)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES  SELECTOR nameStock(st)

    OBJECTS ar = Article FIXED PANEL
    PROPERTIES SELECTOR idArticle(ar)

    OBJECTS s = OrderLedger
    PROPERTIES(s) READONLY BACKGROUND backgroundReserveOrderLedger(s) statusReserveOrderLedger     
    PROPERTIES(s) READONLY BACKGROUND backgroundOrderLedgerDateTime(s,t)  dateOrderLedger, 
                  dateTimeOrderLedger, nameContactOrderLedger, nameContactStockOrderLedger, descriptionOrderLedger,
                  toShipQuantityOrderLedger, quantityOrderLedger
    ORDER BY statusReserveOrderLedger(s)
                  
    FILTERS stockOrderLedger(s)==st,
            articleItem(skuOrderLedger(s))==ar

    FILTERS isPostedOrderLedger(s) AND NOT isClosedOrderLedger(s)
    FILTERGROUP toShipQuantityFilters
        FILTER 'Не выполнено' toShipQuantityOrderLedger(s) > 0 'F7' DEFAULT   

    FILTERGROUP stockTimesFilters
        FILTER 'Резерв (закупки) всего' isPurchaseOrderLedger(s) 'F11'
        FILTER 'Резерв (продажи) всего' TRUE AND NOT isPurchaseOrderLedger(s) 'F10'
        FILTER 'Резерв (закупки) на дату' dateTimeOrderLedger(s) < t AND isPurchaseOrderLedger(s) 'F9'
        FILTER 'Резерв (продажи) на дату' dateTimeOrderLedger(s)< t AND NOT isPurchaseOrderLedger(s) 'F8'
        FILTER 'На дату' dateTimeOrderLedger(s) < t 'F7'


;

DESIGN orderArticleLedger {
    main {
        type = CONTAINERV;
        NEW row{
            type = CONTAINERH;
            MOVE t.box;
            MOVE st.box;
            MOVE ar.box;
        }
        MOVE s.box;
    }
    MOVE functions.box;
}
//-- Для просмотра резерва несколько складов
META extendFormDocumentArticleStockOrderLedger(object)
    reviewReserveArticleStock###object 'Резерв' (article, stock, object) = ACTION {
        IF shipmentDateTime###object(object) THEN {
            FORM orderArticleLedger OBJECTS t=shipmentDateTime###object(object), st=stock, ar = article MODAL;
        } ELSE {
            FORM orderArticleLedgerNotDate OBJECTS st=stock, ar = article MODAL;    
        }
    }
    
END
//-- Для просмотра резерва один склад
META extendFormDocumentArticleOrderLedger(object, stockProp)
    reviewReserveArticle###object 'Резерв' (article, object) = ACTION {
        IF dateTime###object(object) THEN {
            FORM orderArticleLedger OBJECTS t=dateTime###object(object), st=stockProp###object(object), ar = article MODAL;
        } ELSE {
            FORM orderArticleLedgerNotDate OBJECTS st=stockProp###object(object), ar = article MODAL;
        }
    }
    
END