MODULE StockArticle;

REQUIRE Stock, ItemArticle;

NAMESPACE Stock;

balanceB 'Остаток на начало дня' (article, stock, date) = GROUP BY article(Item sku), Stock stock, DATE date SUM balanceB(sku,stock,date);
balanceA 'Остаток на конец дня' (article, stock, date) = GROUP BY article(Item sku), Stock stock, DATE date SUM balanceA(sku,stock,date);

defaultSku 'Товар по умолчанию' (article, stock)= GROUP
                                                                BY article(Item sku), Stock stock MIN sku IF currentBalance(sku, stock);
                                                                
defaultSku 'Товар по умолчанию' (article)= GROUP
                                                                BY article(Item sku) MIN sku IF currentBalance(sku, Stock stock);                                                                

currentBalance 'Текущий остаток' (article, stock) = GROUP BY article(Item sku), Stock stock SUM currentBalance(sku,stock);
prevCurrentBalance 'Текущий остаток' (Article article, Stock stock) = PREV(currentBalance(article, stock));
prevCurrentBalance 'Текущий остаток' (article) = GROUP BY Article article SUM prevCurrentBalance(article, Stock stock);

currentBalance 'Текущий остаток' (article, color, size, stock)= GROUP
    BY article(Item sku), calcColor(sku), calcSize(sku), Stock stock SUM currentBalance(sku, stock);
prevCurrentBalance 'Текущий остаток' (Article article, Color color, Size size, Stock stock)= PREV(currentBalance(article, color, size, stock));

currentBalance 'Текущий остаток' (skuGroup, stock) = GROUP BY skuGroup(Sku sku), Stock stock SUM currentBalance(sku,stock);
recCurrentBalance 'Текущий остаток' (skuGroup, stock) = GROUP 
    BY SkuGroup parent, Stock stock SUM currentBalance(SkuGroup group, stock) IF isParent(group, parent);

quantity (article, stock, date) = GROUP BY article(Item sku), Stock stock, DATE date SUM quantity(sku,stock,date); 
signedQuantity (article, stock, date) = GROUP BY article(Item sku), Stock stock, DATE date SUM signedQuantity(sku,stock,date); 
