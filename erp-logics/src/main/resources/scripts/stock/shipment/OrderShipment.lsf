MODULE OrderShipment;

REQUIRE Order, Shipment;

NAMESPACE Shipment;

quantityShipmentDetailOrderShipment = ABSTRACT NUMERIC[14,3] (Order, Shipment);

shippedOrderDetail 'Кол-во (поставлено)' (orderDetail) = ABSTRACT NUMERIC[14,3] (OrderDetail);

META defineOrderShipment(sign, stockProp)

    orderShipmentDetail = ABSTRACT Order (ShipmentDetail) PERSISTENT;
    orderUserShipmentDetail = DATA Order (UserShipmentDetail);
    orderShipmentDetail(shipmentDetail) += orderUserShipmentDetail(shipmentDetail);
    
    overCopyUserShipmentDetail(d, detail) += ACTION (d, detail) {      
        orderUserShipmentDetail(d) <- orderUserShipmentDetail(detail);             
    } 

    CONSTRAINT supplierUserShipmentDetail(shipmentDetail) != supplierOrder(orderUserShipmentDetail(shipmentDetail)) OR
               customerUserShipmentDetail(shipmentDetail) != customerOrder(orderUserShipmentDetail(shipmentDetail))
        CHECKED BY orderUserShipmentDetail
            MESSAGE 'Поставщик и покупатель в заказе и поставке должны соответствовать друг другу';

    descriptionOrderShipmentDetail 'Заказ' (shipmentDetail) = descriptionOrder(orderShipmentDetail(shipmentDetail)) IN order;
    descriptionOrderUserShipmentDetail 'Заказ' (userShipmentDetail) = descriptionOrder(orderUserShipmentDetail(userShipmentDetail)) IN order;

    quantityShipmentDetailOrderShipment (order, shipment) = GROUP SUM quantityShipmentDetail(shipmentDetail) BY orderShipmentDetail(shipmentDetail), shipmentShipmentDetail(shipmentDetail);
    Shipment.quantityShipmentDetailOrderShipment (order, shipment) += quantityShipmentDetailOrderShipment (order, shipment);

    ordersShipment 'Заказы' (shipment) = GROUP CONCAT VARSTRING[255](descriptionOrder(order)) IF quantityShipmentDetailOrderShipment(order, shipment) , ', '
                                               BY shipment
                                               ORDER order IN order MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;

//-- Подсчет поставленного к-ва по заказу

    shippedShipmentDetailOrderSku 'Кол-во (поставлено)' (order, sku) = GROUP SUM quantityShipmentDetail(shipmentDetail) IF isPostedShipmentDetail(shipmentDetail)
                                                      BY orderShipmentDetail(shipmentDetail), skuShipmentDetail(shipmentDetail);
    shippedOrderDetail 'Кол-во (поставлено)' (orderDetail) = PARTITION UNGROUP shippedShipmentDetailOrderSku
                                                              LIMIT STRICT quantityOrderDetail (orderDetail)
                                                              BY orderOrderDetail(orderDetail), skuOrderDetail(orderDetail)
                                                              ORDER orderDetail PERSISTENT;
                                                              
    Shipment.shippedOrderDetail(d) += shippedOrderDetail(d);
    toShipOrderDetail 'Не поставлено' (orderDetail) = quantityOrderDetail (orderDetail) (-) shippedOrderDetail(orderDetail);

    toShipOrderDetailStockOrder 'Не поставлено по складу' (stock, order) = GROUP SUM toShipOrderDetail(orderDetail) IF toShipOrderDetail(orderDetail) > 0
                                                                                     BY stockProp###orderDetail(orderDetail), orderOrderDetail(orderDetail);

    shippedOrderDetailOrder 'Кол-во (поставлено)' (order) = GROUP SUM shippedOrderDetail(orderDetail) BY orderOrderDetail(orderDetail) IN documentSum PERSISTENT;

    // Создание поставки на основе заказа //
    FORM shipmentOrders 'Заказы'###sign
        OBJECTS s = LegalEntity FIXED PANEL
        PROPERTIES (s) READONLY nameLegalEntity
        OBJECTS ss = Stock FIXED PANEL
        PROPERTIES (ss) READONLY nameStock

        OBJECTS c = LegalEntity FIXED PANEL
        PROPERTIES (c) READONLY nameLegalEntity
        OBJECTS cs = Stock FIXED PANEL
        PROPERTIES (cs) READONLY nameStock

        OBJECTS o = Order
        PROPERTIES (o) READONLY isPostedOrder, numberOrder, seriesOrder, dateOrder, timeOrder, nameSupplierOrder, nameSupplierStockOrder,
                                nameCustomerOrder, nameCustomerStockOrder, nameCurrencyOrder, countOrderDetailOrder, quantityOrderDetailOrder,
                                shippedOrderDetailOrder, sumOrderDetailOrder, noteOrder
        FILTERS isPostedOrder(o),
                inOrderSupplier(o, s),
                customerOrder(o) == c,
                customerStockOrder(o) == cs
//        FILTERGROUP order
//            FILTER 'Заказы с непоставленными товарами со склада' 'F10' toShipOrderDetailStockOrder(s, o) DEFAULT
//            FILTER 'Заказы со склада' 'F9' countOrderDetailStockOrder(s, o)

        OBJECTS d = OrderDetail
        PROPERTIES (d) READONLY indexOrderDetail, idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail
        PROPERTIES (d) READONLY quantityOrderDetail, shippedOrderDetail, priceOrderDetail, sumOrderDetail, name###stockProp###orderDetail,
                       toShipOrderDetail
        FILTERS orderOrderDetail(d) == o
//        FILTERGROUP orderDetail
//            FILTER 'Строки с непоставленными товарами со склада' 'F10' stockOrderDetail(d) == s AND toShipOrderDetail(d) > 0 DEFAULT
//            FILTER 'Строки со склада' 'F9' stockOrderDetail(d) == s
    ;

    DESIGN shipmentOrders FROM DEFAULT {
        main {
            preferredSize = (1024, 768);
            NEW header {
                type = CONTAINERH;
                ADD s.box;
                ADD ss.box;
                ADD c.box;
                ADD cs.box;
            }
            ADD o.box;
            ADD d.box;
            ADD functions.box;
            PROPERTY(toShipOrderDetail(d)) { background = #FFFFCC; }
        }
    }

    inOrderDetailUserShipment (orderDetail, userShipment) = IF differentStocksInDetailsStock(customerStockOrderDetail(orderDetail))
        THEN orderDetail IS OrderDetail AND userShipment IS UserShipment
        ELSE supplierStockOrderDetail(orderDetail) == supplierStockUserShipment(userShipment);

    overFillOrderUserShipmentDetailOrderDetail = ABSTRACT ACTION LIST (UserShipmentDetail, OrderDetail);

    fillOrderUserShipment 'Заполнить на основе заказа' =  ACTION (userShipment) {
        FORM shipmentOrders OBJECTS s = supplierUserShipment(userShipment), ss = supplierStockUserShipment(userShipment),
                                    c = customerUserShipment(userShipment), cs = customerStockUserShipment(userShipment) MODAL;
        IF formResult() == FormResult.ok THEN {
            LOCAL saleOrder = Order();
            ASSIGN saleOrder() <- chosenObject('o');

            FOR orderOrderDetail(orderDetail) == saleOrder() AND
//                stockProp###OrderDetail(orderDetail) == stockProp###UserShipment(userShipment) AND
                toShipOrderDetail(orderDetail) > 0
                AND inOrderDetailUserShipment(orderDetail, userShipment)
                ADDOBJ d = UserShipmentDetail DO {
                    ASSIGN userShipmentUserShipmentDetail(d) <- userShipment;
                    ASSIGN orderUserShipmentDetail(d) <- saleOrder();
                    ASSIGN skuUserShipmentDetail(d) <- skuOrderDetail(orderDetail);
                    ASSIGN quantityUserShipmentDetail (d) <- toShipOrderDetail(orderDetail);

                    EXEC overFillOrderUserShipmentDetailOrderDetail(d, orderDetail);
            }
        }
    } IN order;

    EXTEND FORM userOrder
        PROPERTIES(o) READONLY shippedOrderDetailOrder
        PROPERTIES(d) READONLY shippedOrderDetail AFTER quantityUserOrderDetail(d)
    ;

    EXTEND FORM orders
        PROPERTIES(o) READONLY BACKGROUND backgroundSkuOrder(o) shippedOrderDetailOrder AFTER quantityOrderDetailOrder(o)
        PROPERTIES(d) READONLY shippedOrderDetail AFTER quantityOrderDetail(d)
        FILTERGROUP  filter10
            FILTER   'Непоставленные товары' 'F10' NOT shippedOrderDetail (d) ==  quantityOrderDetail (d)            
    ;

    EXTEND FORM userShipment
        PROPERTIES(s) fillOrderUserShipment, ordersShipment READONLY
        PROPERTIES(d) descriptionOrderUserShipmentDetail BEFORE deletesd
    ;
    EXTEND DESIGN userShipment { headerCreateDetail { ADD s.order { type = CONTAINERV; }}}


    EXTEND FORM shipments
        PROPERTIES(s) READONLY ordersShipment
        PROPERTIES(d) READONLY descriptionOrderShipmentDetail
    ;

//--  Резервирование
    toShipQuantityOrderLedger (ledger) += toShipOrderDetail(ledger);

    // цены в подборе
    @extendFormDocumentPriceSku(userOrder, accountPriceListType, stockProp, ' учетная', userOrder, o, is);
    @extendFormDocumentPriceBatch(userOrder, accountPriceListType, stockProp, ' учетная', userOrder, o, is);
END

META defineOrderShipmentPack(showPackage)
    EXTEND FORM shipmentOrders
        PROPERTIES (d) READONLY SHOWIF showPack###order(o) BEFORE quantityOrderDetail(d)
                       idBarcodePackOrderDetail, shortNameUOMPackOrderDetail,
                       amountPackOrderDetail, packQuantityOrderDetail
    ;

    overFillOrderUserShipmentDetailOrderDetail(s, o) += ACTION (s, o) {
        ASSIGN barcodePackUserShipmentDetail(s) <- barcodePackOrderDetail(o);
        ASSIGN amountPackUserShipmentDetail(s) <- amountPackOrderDetail(o);
        ASSIGN packQuantityUserShipmentDetail (s) <- toShipOrderDetail(o)/amountPackOrderDetail(o);
    }
END

META defineOrderShipmentExecution (dumb)

    shippedShipmentDetailOrder 'Кол-во (поставлено)' (order) = GROUP SUM shippedOrderDetail(d)
        IF isClosedOrderDetail(d) AND isPostedOrderDetail(d) BY orderOrderDetail(d);
                                                               
    percentQuantityExecutionOrder '% исп. заявки (кол-во)' (order)= shippedShipmentDetailOrder(order)*100.00 /
        (quantityOrderDetailOrder(order) IF quantityOrderDetailOrder(order)!= 0);                                                              
    
    countShippedOrderDetailOrder (order) = GROUP SUM 1 IF shippedOrderDetail(d) >= quantityOrderDetail(d) 
        AND isClosedOrderDetail(d) AND isPostedOrderDetail(d) BY orderOrderDetail(d);   
    percentCountExecutionOrder '% исп. заявки (позиции)' (order) = countShippedOrderDetailOrder (order)*100.00/
        (countOrderDetailOrder(order) IF countOrderDetailOrder(order)!=0);
    
    sumShippedOrderDetailOrder (order) = GROUP SUM shippedOrderDetail(d)*priceOrderDetail(d) 
        IF isClosedOrderDetail(d) AND isPostedOrderDetail(d) BY orderOrderDetail(d);   
    percentSumExecutionOrder '% исп. заявки (сумма)' (order) = sumShippedOrderDetailOrder(order)*100.00/(sumOrderDetailOrder(order) IF sumOrderDetailOrder(order)!=0);    
    
    EXTEND FORM orders
        PROPERTIES(o) READONLY percentQuantityExecutionOrder, percentCountExecutionOrder, percentSumExecutionOrder     
    ;       
END
META defineOrderInvoiceSupplierExecution (NS, sign)
                                                      
    shippedShipmentDetailSupplierDateFromTo (supplier, dateFrom, dateTo)= GROUP SUM shippedShipmentDetailOrder(order) 
        IF dateOrder(order) >= dateFrom AND dateOrder(order) <= dateTo AND isPostedOrder(order) AND isClosedOrder(order)
            BY supplierOrder(order), dateFrom, dateTo;                                                   
    quantityShipmentDetailSupplierDateFromTo (supplier, dateFrom, dateTo)= GROUP SUM quantityOrderDetailOrder(order) 
        IF dateOrder(order) >= dateFrom AND dateOrder(order) <= dateTo AND isPostedOrder(order) AND isClosedOrder(order)
            BY supplierOrder(order), dateFrom, dateTo;                                                                         
    percentQuantityExecutionSupplierDateFromTo '% исп. заявки (кол-во)' (supplier, dateFrom, dateTo)= shippedShipmentDetailSupplierDateFromTo (supplier, dateFrom, dateTo)*100.00 /
        (quantityShipmentDetailSupplierDateFromTo(supplier, dateFrom, dateTo) IF quantityShipmentDetailSupplierDateFromTo(supplier, dateFrom, dateTo)!= 0);                                                              
//--    
    countShippedSupplierDateFromTo (supplier, dateFrom, dateTo)= GROUP SUM countShippedOrderDetailOrder(order) 
        IF dateOrder(order) >= dateFrom AND dateOrder(order) <= dateTo AND isPostedOrder(order) AND isClosedOrder(order)
            BY supplierOrder(order), dateFrom, dateTo;                                                   
    countSupplierDateFromTo (supplier, dateFrom, dateTo)= GROUP SUM countOrderDetailOrder(order) 
        IF dateOrder(order) >= dateFrom AND dateOrder(order) <= dateTo AND isPostedOrder(order) AND isClosedOrder(order)
            BY supplierOrder(order), dateFrom, dateTo;      
    percentCountExecutionSupplierDateFromTo '% исп. заявки (позиции)' (supplier, dateFrom, dateTo) = countShippedSupplierDateFromTo (supplier, dateFrom, dateTo)*100.00 /
        (countSupplierDateFromTo(supplier, dateFrom, dateTo) IF countSupplierDateFromTo(supplier, dateFrom, dateTo)!=0);
//--    
    sumShippedSupplierDateFromTo (supplier, dateFrom, dateTo)= GROUP SUM sumShippedOrderDetailOrder(order) 
        IF dateOrder(order) >= dateFrom AND dateOrder(order) <= dateTo AND isPostedOrder(order) AND isClosedOrder(order)
            BY supplierOrder(order), dateFrom, dateTo;                                                   
    sumSupplierDateFromTo (supplier, dateFrom, dateTo)= GROUP SUM sumOrderDetailOrder(order) 
        IF dateOrder(order) >= dateFrom AND dateOrder(order) <= dateTo AND isPostedOrder(order) AND isClosedOrder(order)
            BY supplierOrder(order), dateFrom, dateTo;      
    percentSumExecutionSupplierDateFromTo '% исп. заявки (сумма)' (supplier, dateFrom, dateTo) = sumShippedSupplierDateFromTo(supplier, dateFrom, dateTo)*100.00 /
        (sumSupplierDateFromTo(supplier, dateFrom, dateTo) IF sumSupplierDateFromTo(supplier, dateFrom, dateTo)!=0);    
    

    FORM executionSupplierOrder 'Исполнение заявок' TITLE 'Исполнение заявок'###sign
    
        OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
        PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)
       
        OBJECTS s = LegalEntity
        PROPERTIES READONLY nameLegalEntity(s), percentQuantityExecutionSupplierDateFromTo(s,dFrom,dTo), percentCountExecutionSupplierDateFromTo(s,dFrom,dTo),
                   percentSumExecutionSupplierDateFromTo(s,dFrom,dTo)       
        FILTERS    quantityShipmentDetailSupplierDateFromTo(s,dFrom,dTo)  
        OBJECTS o = Order
        PROPERTIES (o) READONLY isPostedOrder, isClosedOrder, numberOrder, seriesOrder, dateOrder, timeOrder,
                                nameSupplierOrder, nameSupplierStockOrder, nameCustomerOrder, nameCustomerStockOrder, 
                                countOrderDetailOrder, quantityOrderDetailOrder, sumOrderDetailOrder, VATSumOrderDetailOrder, invoiceSumOrderDetailOrder,
                                percentQuantityExecutionOrder, percentCountExecutionOrder, percentSumExecutionOrder 
        FILTERS isPostedOrder(o),
                isClosedOrder(o),
                supplierOrder(o) == s,
                dateOrder(o) >= dFrom,
                dateOrder(o) <= dTo

    ; 
    
    @extendFormFilterAccessStock(Order, o, executionSupplierOrder, customerStock, company);
    
    DESIGN executionSupplierOrder FROM DEFAULT{
        main{

            NEW dates{
                type = CONTAINERH;
                caption = 'Период';
                ADD PROPERTY(valFrom){
                    caption = 'Дата с';
                }
                ADD PROPERTY(valTo){
                    caption = 'Дата по';
                }
            }

            ADD s.box;
            ADD o.box;
            ADD functions.box;
        }
    }        
    NAVIGATOR {
        NS##sReports {
            ADD executionSupplierOrder;
        }
    }                     
END