MODULE InvoiceShipment;

REQUIRE Invoice, Shipment;

NAMESPACE Shipment;

GROUP invoicesGroup 'Приемка по накладным' : base;

META defineInvoiceShipment(sign, stockProp, filter)

    invoiceDetailShipmentDetail = ABSTRACT InvoiceDetail (ShipmentDetail) PERSISTENT;
    invoiceDetailUserShipmentDetail = DATA InvoiceDetail (UserShipmentDetail);
    invoiceDetailShipmentDetail(shipmentDetail) += invoiceDetailUserShipmentDetail(shipmentDetail);

    CONSTRAINT supplierShipmentDetail(detail) != supplierInvoiceDetail(invoiceDetailUserShipmentDetail(detail)) OR
               customerShipmentDetail(detail) != customerInvoiceDetail(invoiceDetailUserShipmentDetail(detail)) OR
               skuShipmentDetail(detail) != skuInvoiceDetail(invoiceDetailUserShipmentDetail(detail))
        CHECKED BY invoiceDetailUserShipmentDetail
            MESSAGE 'Поставщик, покупатель и товар в накладной и поставке должны соответствовать друг другу';


    descriptionIndexInvoiceDetailShipmentDetail 'Строка накладной' (detail) = descriptionIndexInvoiceDetail(invoiceDetailShipmentDetail(detail));
    descriptionIndexInvoiceDetailUserShipmentDetail 'Строка накладной' (detail) = descriptionIndexInvoiceDetail(invoiceDetailUserShipmentDetail(detail));

    quantityShipmentDetailInvoiceShipment (invoice, shipment) = GROUP SUM quantityShipmentDetail(shipmentDetail) BY invoiceInvoiceDetail(invoiceDetailShipmentDetail(shipmentDetail)), shipmentShipmentDetail(shipmentDetail);
    relationInvoiceShipment 'Связь' (shipment) = GROUP SUM 1 IF quantityShipmentDetailInvoiceShipment (invoice, shipment) BY shipment;

    invoicesShipment 'Накладные' (shipment) = GROUP CONCAT VARSTRING[255](descriptionInvoice(invoice)) IF quantityShipmentDetailInvoiceShipment(invoice, shipment) , ', '
                                                    BY shipment
                                                    ORDER invoice IN invoiceGroup MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;

    shippedInvoiceDetail 'Кол-во (поставлено)' (invoiceDetail) = GROUP SUM quantityShipmentDetail(shipmentDetail) IF isPostedShipmentDetail(shipmentDetail)
                                                                       BY invoiceDetailShipmentDetail(shipmentDetail) PERSISTENT;

    TABLE shipmentDetailBatch (ShipmentDetail, Batch);
    costShipmentDetailBatch = ABSTRACT NUMERIC[14,3] (ShipmentDetail, Batch) PERSISTENT;

    TABLE invoiceDetailBatch (InvoiceDetail, Batch);
    costInvoiceDetailBatch (invoiceDetail, batch) = GROUP SUM costShipmentDetailBatch(shipmentDetail, batch) BY invoiceDetailShipmentDetail(shipmentDetail), batch PERSISTENT;

    countBatchInvoiceDetail 'Кол-во партий' (invoiceDetail) = GROUP SUM 1 IF costInvoiceDetailBatch(invoiceDetail, batch) BY invoiceDetail PERSISTENT;
    maxBatchInvoiceDetail (invoiceDetail) = GROUP MAX batch IF costInvoiceDetailBatch(invoiceDetail, batch) BY invoiceDetail;

    toShipInvoiceDetail 'Не поставлено' (invoiceDetail) = quantityInvoiceDetail (invoiceDetail) (-) shippedInvoiceDetail(invoiceDetail);

    toShipInvoiceDetailStockInvoice 'Не поставлено по складу' (stock, invoice) =
        GROUP SUM toShipInvoiceDetail(invoiceDetail) IF toShipInvoiceDetail(invoiceDetail) > 0
              BY stockProp###InvoiceDetail(invoiceDetail), invoiceInvoiceDetail(invoiceDetail);

    toShipInvoiceDetailInvoice 'Не поставлено по накладной' (invoice) =
        GROUP SUM toShipInvoiceDetail(invoiceDetail) IF toShipInvoiceDetail(invoiceDetail) > 0
              BY invoiceInvoiceDetail(invoiceDetail);

    // Создание поставки на основе инвойса //

    backgroundShippedInvoice 'Цвет' (invoice) = RGB(255, 224, 255) IF invoice IS Invoice;
    backgroundShippedInvoiceDetail (detail) = backgroundShippedInvoice(invoiceInvoiceDetail(detail));

    FORM shipmentInvoices 'Накладные'###sign
        OBJECTS s = LegalEntity FIXED PANEL
        PROPERTIES (s) READONLY nameLegalEntity
        OBJECTS c = LegalEntity FIXED PANEL
        PROPERTIES (c) READONLY nameLegalEntity

        OBJECTS i = Invoice
        PROPERTIES (i) READONLY isPostedInvoice FORCE GRID, numberInvoice, seriesInvoice, dateInvoice, timeInvoice,
                                nameSupplierInvoice, nameSupplierStockInvoice, nameCustomerInvoice, nameCustomerStockInvoice,
                                nameCurrencyInvoice, seriesNumberContractSkuInvoice, isCommissionInvoice,
                                countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                                VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice, ordersInvoice,
                                noteInvoice, objectClassName
        FILTERS supplierInvoice(i) == s,
                customerInvoice(i) == c,
                isPostedInvoice(i)

        OBJECTS d = InvoiceDetail
        PROPERTIES (d) READONLY indexInvoiceDetail, idBarcodeSkuInvoiceDetail, nameSkuInvoiceDetail, shortNameUOMSkuInvoiceDetail
        PROPERTIES (d) READONLY quantityInvoiceDetail, shippedInvoiceDetail, priceInvoiceDetail, sumInvoiceDetail,
                       numberVATInvoiceDetail, valueVATInvoiceDetail, VATSumInvoiceDetail, invoiceSumInvoiceDetail,
                       name###stockProp###InvoiceDetail, descriptionOrderInvoiceDetail
        FILTERS invoiceInvoiceDetail(d) == i

        DIALOG Invoice OBJECT i
    ;

    DESIGN shipmentInvoices FROM DEFAULT {
        main {
            preferredSize = (1024, 768);
            NEW documentContainer BEFORE functions.box {

                childConstraints = TO THE BOTTOM;

                NEW headerBox {
                    childConstraints = TO THE RIGHT;
                    ADD s.box {caption = 'Поставщик';};
                    ADD c.box {caption = 'Покупатель';};
                }
                ADD i.box;
                NEW documentDetail {
                    type = TABBED;

                    ADD d.box {
                        caption = 'Спецификация';
                    }
                    NEW printTab {
                        caption = 'Печатные формы';
                        NEW printContainer {
                            caption = 'Печать';
                            childConstraints = TO THE BOTTOM;
                            fillVertical = 1.0;
                        }
                    }
                }
            }
        }
    }
    overFillInvoiceUserShipmentInvoice = ABSTRACT ACTION LIST (UserShipment, Invoice);                          // документ
    overFillInvoiceUserShipmentDetailInvoiceDetail = ABSTRACT ACTION LIST (UserShipmentDetail, InvoiceDetail);  // детайл

    fillInvoiceUserShipment 'Заполнить на основе накладной' =  ACTION (userShipment) {
        FORM shipmentInvoices OBJECTS s = supplierUserShipment(userShipment), c = customerUserShipment(userShipment) MODAL;
        IF formResult() == FormResult.ok THEN {
            LOCAL saleInvoice = Invoice();
            ASSIGN saleInvoice() <- chosenObject('i');
            EXEC overFillInvoiceUserShipmentInvoice(userShipment, saleInvoice());

            FOR invoiceInvoiceDetail(invoiceDetail) == saleInvoice() AND
                toShipInvoiceDetail(invoiceDetail) > 0
                ADDOBJ d = UserShipmentDetail DO {
                    ASSIGN userShipmentUserShipmentDetail(d) <- userShipment;
                    ASSIGN invoiceDetailUserShipmentDetail(d) <- invoiceDetail;
                    ASSIGN orderUserShipmentDetail(d) <- orderInvoiceDetail(invoiceDetail);

                    ASSIGN skuUserShipmentDetail(d) <- skuInvoiceDetail(invoiceDetail);

                    ASSIGN quantityUserShipmentDetail (d) <- toShipInvoiceDetail(invoiceDetail);

                    EXEC overFillInvoiceUserShipmentDetailInvoiceDetail(d, invoiceDetail);
            }
        }
    } IN invoiceGroup;

// Создание поставки на основе нескольких инвойсов //

    TABLE invoiceUserShipment (Invoice, UserShipment);
    includeInvoiceUserShipment 'Вкл. в приемку' = DATA BOOLEAN (Invoice, UserShipment) MINCHARWIDTH 10 PREFCHARWIDTH 15;
    includeInvoicesUserShipment 'Накладные в приемке' (userShipment) = GROUP CONCAT VARSTRING[255](descriptionInvoice(invoice)) IF includeInvoiceUserShipment(invoice, userShipment) , ', '
                                                        BY userShipment
                                                        ORDER invoice IN invoicesGroup MINCHARWIDTH 30 PREFCHARWIDTH 30 PERSISTENT;
    FORM includeInvoicesUserShipment 'Накладные'
        OBJECTS s = UserShipment FIXED PANEL

        OBJECTS i = Invoice
        PROPERTIES (i) READONLY isPostedInvoice FORCE GRID, numberInvoice, seriesInvoice, dateInvoice, timeInvoice,
                                nameSupplierInvoice, nameSupplierStockInvoice, nameCustomerInvoice, nameCustomerStockInvoice,
                                nameCurrencyInvoice, seriesNumberContractSkuInvoice, isCommissionInvoice,
                                countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                                VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice, ordersInvoice,
                                noteInvoice, objectClassName

        FILTERS supplierInvoice(i) == supplierUserShipment(s),
                customerInvoice(i) == customerUserShipment(s),
                supplierStockInvoice(i) == supplierStockUserShipment(s),
                customerStockInvoice(i) == customerStockUserShipment(s),
                isPostedInvoice(i),
                toShipInvoiceDetailInvoice(i)
        PROPERTIES(i,s) BACKGROUND backgroundShippedInvoice(i) includeInvoiceUserShipment BEFORE isPostedInvoice(i)
    ;

    dialogIncludeInvoicesUserShipment 'Включить накладные в поставку' (userShipment) = ACTION FORM includeInvoicesUserShipment OBJECTS s = userShipment MODAL IN invoicesGroup;

    shipmentDimensionInvoiceDetail = ABSTRACT ShipmentDimension (InvoiceDetail);
    nameShipmentDimensionInvoiceDetail 'Регистр хранения' (detail) = nameShipmentDimension(shipmentDimensionInvoiceDetail(detail));
    shipmentDimensionUserInvoiceDetail = ABSTRACT ShipmentDimension (UserInvoiceDetail);
    nameShipmentDimensionUserInvoiceDetail 'Регистр хранения' (detail) = nameShipmentDimension(shipmentDimensionUserInvoiceDetail(detail));
    shipmentDimensionInvoiceDetail(detail) += shipmentDimensionUserInvoiceDetail(detail);
    defaultInvoiceDetailSkuUserShipment (sku, userShipment) = GROUP MAX detail IF includeInvoiceUserShipment(invoiceInvoiceDetail(detail), userShipment)
        BY skuInvoiceDetail(detail), userShipment;
    defaultInvoiceDetailSkuDimensionUserShipment (sku, dimension, userShipment) = GROUP MAX detail IF includeInvoiceUserShipment(invoiceInvoiceDetail(detail), userShipment)
        BY skuInvoiceDetail(detail), shipmentDimensionInvoiceDetail(detail), userShipment;

    fillInvoiceDetailUserShipmentDetailDimension 'Привязать поставку к накладным' = ACTION (detail, dimension) {

        invoiceDetailUserShipmentDetail(detail) <- IF dimension IS ShipmentDimension
                                    THEN defaultInvoiceDetailSkuDimensionUserShipment(skuUserShipmentDetail(detail), dimension, userShipmentUserShipmentDetail(detail))
                                    ELSE defaultInvoiceDetailSkuUserShipment(skuUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail));
    };

    fillInvoiceDetailUserShipmentDimension 'Привязать поставку к накладным' = ACTION (userShipment, dimension) {
        FOR userShipmentUserShipmentDetail(detail) == userShipment DO {
            EXEC fillInvoiceDetailUserShipmentDetailDimension(detail, dimension);
        }
    } IN invoicesGroup;
    fillInvoiceDetailUserShipment 'Привязать поставку к накладным' (userShipment) = fillInvoiceDetailUserShipmentDimension(userShipment, shipmentDimensionUserShipment(userShipment)) IN invoicesGroup;

    WHEN SESSION FORMS userShipment
        (CHANGED(shipmentDimensionUserShipmentDetail(detail)) OR CHANGED(skuUserShipmentDetail(detail))) AND NOT invoiceDetailUserShipmentDetail(detail) AND s==shipmentDimensionUserShipmentDetail(detail)
            DO EXEC fillInvoiceDetailUserShipmentDetailDimension(detail, s);

    EXTEND FORM userShipment
        PROPERTIES(s) fillInvoiceUserShipment, invoicesShipment READONLY
        PROPERTIES(s) dialogIncludeInvoicesUserShipment, includeInvoicesUserShipment READONLY, fillInvoiceDetailUserShipment
        PROPERTIES(d) descriptionIndexInvoiceDetailUserShipmentDetail BEFORE deletesd
    ;
    EXTEND DESIGN userShipment {
        headerCreateDetail {
            ADD s.invoiceGroup {
                type = CONTAINERV;
            }
            ADD s.invoicesGroup {
                type = CONTAINERV;
            }
        }
    }


    EXTEND FORM shipments
        PROPERTIES(s) READONLY invoicesShipment
        PROPERTIES(d) READONLY descriptionIndexInvoiceDetailShipmentDetail
    ;

//-- аггр.объект

    @defineDocumentInterfaceDetailPricePrefix(invoice, shipment, ' (учетная)');
    @defineDocumentInterfaceDetailQuantityPrefix (invoice, shipment, ' (поставлено)');
    shipmentQuantityUserInvoiceDetail(detail) <- quantityUserInvoiceDetail(detail) WHEN CHANGED(quantityUserInvoiceDetail(detail));

    @defineDocumentInterfaceDetailDataSumPrefix (invoice, shipment, ' (учетная)');
    @deriveDocumentDetailSumPrefix(userInvoice, shipment, currency, shipmentQuantity);

    @defineDocumentInterfaceHeaderCreateShipment (invoice);
    @defineDocumentInterfaceHeaderSumPrefix (invoice, shipment, ' (учетная)');

    @defineDocumentInterfaceHeaderItemSum (invoice, shipment, shipment);

    @defineDocumentInterfaceHeaderTimePrefix(Invoice, shipment, ' поставки');
    shipmentDateUserInvoice(i) <- dateUserInvoice(i) WHEN CHANGED(dateUserInvoice(i));
    shipmentTimeUserInvoice(i) <- timeUserInvoice(i) WHEN CHANGED(timeUserInvoice(i));

    @defineDocumentInterfaceDetailExpiryDate(invoice);

    overCopyInvoice(s, d) += ACTION (s, d) {
        ASSIGN createShipmentUserInvoice(d) <- createShipmentUserInvoice(s);
    }

    skipCreateShipmentInvoiceDetail = ABSTRACT BOOLEAN (InvoiceDetail) PERSISTENT;
    createShipmentInvoiceDetail 'Создать поставку' (invoiceDetail) = createShipmentInvoice(invoiceInvoiceDetail(invoiceDetail)) AND
                                                                     isStockSkuInvoiceDetail(invoiceDetail) AND
                                                                     NOT skipCreateShipmentInvoiceDetail(invoiceDetail) PERSISTENT;

    createShipmentUserInvoiceDetail 'Создать поставку' (invoiceDetail) = createShipmentUserInvoice(userInvoiceUserInvoiceDetail(invoiceDetail)) AND
                                                                         isStockSkuUserInvoiceDetail(invoiceDetail);

    showShipmentInvoice (invoice) =  invoice IS Invoice AND NOT createShipmentInvoice(invoice);

        EXTEND FORM userInvoice
        PROPERTIES(i) BACKGROUND backgroundShippedInvoice(i) createShipmentUserInvoice
        PROPERTIES(i) BACKGROUND backgroundShippedInvoice(i) SHOWIF createShipmentUserInvoice(i) shipmentSumUserInvoiceDetailUserInvoice,
                      shipmentDateUserInvoice, shipmentTimeUserInvoice
        PROPERTIES(d) BEFORE deleteid SHOWIF createShipmentInvoice(i) BACKGROUND backgroundShippedInvoice(i)
                      expiryDateUserInvoiceDetail, name###stockProp###userInvoiceDetail ON CHANGE changeData###stockProp###filter###userInvoiceDetail(d), shipmentSumUserInvoiceDetail
        PROPERTIES(d) BEFORE deleteid BACKGROUND backgroundShippedInvoice(i) shipmentPriceUserInvoiceDetail
        PROPERTIES(d) READONLY shippedInvoiceDetail AFTER quantityUserInvoiceDetail SHOWIF showShipmentInvoice(i) BACKGROUND backgroundShippedInvoice(i)
        PROPERTIES(d) shipmentQuantityUserInvoiceDetail AFTER quantityUserInvoiceDetail SHOWIF createShipmentInvoice(i) BACKGROUND backgroundShippedInvoice(i)
    ;
    EXTEND DESIGN userInvoice {
        headerCreateDocuments {
            NEW headerCreateShipment {
                caption = 'Поставка';
                childConstraints = TO THE RIGHTBOTTOM;
                ADD PROPERTY(createShipmentUserInvoice);
                ADD PROPERTY(shipmentDateUserInvoice);
                ADD PROPERTY(shipmentTimeUserInvoice);
            }
        }
    }

    EXTEND FORM invoices
        PROPERTIES(i) READONLYIF isReadonly() BACKGROUND backgroundShippedInvoice(i) createShipmentInvoice
        PROPERTIES(i) READONLY BACKGROUND backgroundShippedInvoice(i) shipmentSumInvoiceDetailInvoice BEFORE ordersInvoice
        PROPERTIES(d) READONLY SHOWIF createShipmentInvoice(i) BACKGROUND backgroundShippedInvoice(i)
                      expiryDateInvoiceDetail, name###stockProp###invoiceDetail, shipmentPriceInvoiceDetail, shipmentSumInvoiceDetail
        PROPERTIES(d) READONLY shippedInvoiceDetail AFTER quantityInvoiceDetail SHOWIF showShipmentInvoice(i) BACKGROUND backgroundShippedInvoice(i)
        PROPERTIES(d) READONLY shipmentQuantityInvoiceDetail AFTER quantityInvoiceDetail SHOWIF createShipmentInvoice(i) BACKGROUND backgroundShippedInvoice(i)
    ;
    EXTEND FORM shipmentInvoices
        PROPERTIES (d) READONLY BACKGROUND backgroundShippedInvoice(i)  toShipInvoiceDetail, shipmentPriceInvoiceDetail
    ;

    CLASS InvoiceShipment 'Поставка на основе инвойса'###sign : Shipment;
    CLASS InvoiceShipmentPosted 'Проведенная поставка на основе инвойса'###sign : InvoiceShipment, PostedObject;
    CLASS InvoiceShipmentDetail 'Строка поставки на основе инвойса'###sign : ShipmentDetail;

    @defineDocumentTables(invoiceShipment);

    @defineDocumentAggregation(invoice, invoiceShipment, createShipmentInvoice);
    shipmentShipmentDetail(detail) += invoiceShipmentInvoiceShipmentDetail(detail);

    @defineDocumentDetailIndex(invoiceShipment);

    dateShipment(shipment) += shipmentDateInvoice(invoiceInvoiceShipment(shipment));
    timeShipment(shipment) += shipmentTimeInvoice(invoiceInvoiceShipment(shipment));

    @defineDocumentAggregationStockPrefix(invoice, invoiceShipment, supplierStock, 'Склад поставщика', , );
    supplierStockShipment(shipment) += supplierStockInvoiceShipment(shipment);
    dataSupplierStockShipmentDetail(shipmentDetail) += dataSupplierStockInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));
    @defineDocumentAggregationStockPrefix(invoice, invoiceShipment, customerStock, 'Склад покупателя', , );
    customerStockShipment(shipment) += customerStockInvoiceShipment(shipment);
    dataCustomerStockShipmentDetail(shipmentDetail) += dataCustomerStockInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));

    @defineDocumentAggregationLegalEntityPrefix(invoice, invoiceShipment, supplier, 'Поставщик', , );
    supplierShipment(shipment) += supplierInvoiceShipment(shipment);
    @defineDocumentAggregationLegalEntityPrefix(invoice, invoiceShipment, customer, 'Покупатель', , );
    customerShipment(shipment) += customerInvoiceShipment(shipment);

    @defineDocumentAggregationPosted(invoice, invoiceShipment);
    isPostedShipment(shipment) += isPostedInvoiceShipment(shipment);

    numberInvoiceShipment 'Номер документа' (invoiceShipment) = numberInvoice(invoiceInvoiceShipment(invoiceShipment));
    numberShipment(shipment) += numberInvoiceShipment(shipment);

    seriesInvoiceShipment 'Серия документа' (invoiceShipment) = seriesInvoice(invoiceInvoiceShipment(invoiceShipment));
    seriesShipment(shipment) += seriesInvoiceShipment(shipment);

    seriesNumberInvoiceShipment 'Серия/номер документа' (invoiceShipment) = seriesNumberInvoice(invoiceInvoiceShipment(invoiceShipment));

    noteInvoiceInvoiceShipment 'Примечание' (invoiceShipment) = noteInvoice(invoiceInvoiceShipment(invoiceShipment));
    noteShipment(shipment) += noteInvoiceInvoiceShipment(shipment);

    currencyInvoiceShipment  (invoiceShipment) = currencyInvoice(invoiceInvoiceShipment(invoiceShipment));
    currencyShipment (shipment) += currencyInvoiceShipment(shipment);

    @defineDocumentDescription(invoiceShipment, InvoiceShipmentDetail, seriesNumberInvoiceShipment, 'Поставка на основе инвойса'###sign);
    descriptionShipment (shipment) += descriptionInvoiceShipment(shipment);

    @defineDocumentAggregationDetailProperty (invoice, invoiceShipment, order, 'Заказ');
    orderShipmentDetail (shipmentDetail) += orderInvoiceShipmentDetail(shipmentDetail);

    @defineDocumentAggregationDetailSku(invoice, invoiceShipment, sku);
    skuShipmentDetail(shipmentDetail) +=  skuInvoiceShipmentDetail(shipmentDetail);

    quantityShipmentDetail(shipmentDetail) += shipmentQuantityInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));
    expiryDateShipmentDetail(shipmentDetail) += expiryDateInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));

    priceInvoiceShipmentDetail(invoiceShipmentDetail) = shipmentPriceInvoiceDetail(invoiceDetailInvoiceShipmentDetail(invoiceShipmentDetail));
    priceShipmentDetail(shipmentDetail) += priceInvoiceShipmentDetail(shipmentDetail);

    sumInvoiceShipmentDetail(invoiceShipmentDetail) = shipmentSumInvoiceDetail(invoiceDetailInvoiceShipmentDetail(invoiceShipmentDetail));
    sumShipmentDetail(shipmentDetail) += sumInvoiceShipmentDetail(shipmentDetail);

    invoiceDetailShipmentDetail(shipmentDetail) += invoiceDetailInvoiceShipmentDetail(shipmentDetail);

    editShipment(shipment) += ACTION EXEC editInvoice(invoiceInvoiceShipment(shipment));

    // цены в подборе
    @extendFormDocumentPriceSku(userInvoice, accountPriceListType, stockProp, ' учетная', userInvoice, i, isUserInvoice);
    @extendFormDocumentPriceBatch(userInvoice, accountPriceListType, stockProp, ' учетная', userInvoice, i, isUserInvoice);
END

META defineInvoiceShipmentBatch(dumb)
    EXTEND FORM shipmentInvoices
        PROPERTIES (d) READONLY nameBatchInvoiceDetail AFTER shortNameUOMSkuInvoiceDetail
    ;

    overFillInvoiceUserShipmentDetailInvoiceDetail (sd, id) += ACTION ASSIGN batchUserShipmentDetail (sd) <- batchInvoiceDetail(id);

    @defineDocumentAggregationDetailBatch (invoice, invoiceShipment);
    batchShipmentDetail (shipmentDetail) += batchInvoiceShipmentDetail(shipmentDetail);

    expiryDateUserInvoiceDetail(detail) <- prevExpiryDateBatch(batchUserInvoiceDetail(detail)) WHEN CHANGED (batchUserInvoiceDetail(detail));
END

META defineInvoiceShipmentPack(dumb)
    EXTEND FORM shipmentInvoices
        PROPERTIES (d) READONLY SHOWIF showPack##Invoice(i) BEFORE quantityInvoiceDetail
                       idBarcodePackInvoiceDetail, shortNameUOMPackInvoiceDetail,
                       amountPackInvoiceDetail, packQuantityInvoiceDetail
    ;
    overFillInvoiceUserShipmentDetailInvoiceDetail(s, i) += ACTION (s, i) {
        ASSIGN barcodePackUserShipmentDetail(s) <- barcodePackInvoiceDetail(i);
        ASSIGN amountPackUserShipmentDetail(s) <- amountPackInvoiceDetail(i);
        ASSIGN packQuantityUserShipmentDetail(s) <- toShipInvoiceDetail(i)/amountPackInvoiceDetail(i);
    }

    showPackShipment(shipment) += showPackInvoice(invoiceInvoiceShipment(shipment));
    barcodePackShipmentDetail(shipmentDetail) +=  barcodePackInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));
    amountPackShipmentDetail(shipmentDetail) +=  amountPackInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));
    packQuantityShipmentDetail(shipmentDetail) +=  packQuantityInvoiceDetail(invoiceDetailInvoiceShipmentDetail(shipmentDetail));
END

//-- Действие

META defineInvoiceShipmentAction (sign)
    moveUserShipmentInvoice 'Поставка'###sign =  ACTION (invoice) NEWSESSION{

        FOR ADDOBJ s = UserShipment DO {

            ASSIGN supplierUserShipment(s) <- supplierInvoice(invoice);
            ASSIGN supplierStockUserShipment(s) <- supplierStockInvoice(invoice);
            ASSIGN customerUserShipment(s) <- customerInvoice(invoice);
            ASSIGN customerStockUserShipment(s) <- customerStockInvoice(invoice);
            ASSIGN currencyUserShipment(s) <- currencyInvoice(invoice);
            ASSIGN numberObject(s) <- numberInvoice(invoice);
            ASSIGN seriesObject(s) <- seriesInvoice(invoice);
            ASSIGN noteUserShipment(s) <- noteInvoice(invoice);
            EXEC overFillInvoiceUserShipmentInvoice(s, invoice);

            FOR invoiceInvoiceDetail(detail) == invoice  ADDOBJ d = UserShipmentDetail DO {
                ASSIGN userShipmentUserShipmentDetail(d) <- s;
                ASSIGN invoiceDetailUserShipmentDetail(d) <- detail;
                ASSIGN orderUserShipmentDetail(d) <- orderInvoiceDetail(detail);

                ASSIGN dataCustomerStockUserShipmentDetail(d) <- dataCustomerStockInvoiceDetail(detail);
                ASSIGN dataSupplierStockUserShipmentDetail(d) <- dataSupplierStockInvoiceDetail(detail);

                ASSIGN skuUserShipmentDetail(d) <- skuInvoiceDetail(detail);
                ASSIGN quantityUserShipmentDetail (d) <- toShipInvoiceDetail(detail);
                EXEC overFillInvoiceUserShipmentDetailInvoiceDetail(d, detail);
            }

            FORM userShipment OBJECTS s = s MANAGESESSION DOCKEDMODAL;
        }
    } TOOLBAR;

    EXTEND FORM invoices
        PROPERTIES(i) moveUserShipmentInvoice
    ;
    EXTEND DESIGN invoices {
        createdContainer{
            ADD PROPERTY(moveUserShipmentInvoice);
        }
    }
END

