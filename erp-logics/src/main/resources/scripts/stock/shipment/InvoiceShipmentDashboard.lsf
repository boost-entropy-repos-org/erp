MODULE InvoiceShipmentDashboard;

REQUIRE Dashboard, InvoiceShipment;

NAMESPACE Shipment;

notCreateShipmentInvoice (invoice) = invoice IS Invoice AND NOT createShipmentInvoice(invoice);

notUserShipment (shipment) = shipment IS Shipment AND NOT isUserShipment(shipment);
  
FORM invoiceShipmentDashboard 'Приемка по накладным' AUTOREFRESH 60

    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES(dt) date = OBJVALUE
    
    OBJECTS i = Invoice
    PROPERTIES (i) READONLY statusShippedInvoice BACKGROUND backgroundShipInvoice(i)
    PROPERTIES (i) READONLY numberInvoice, seriesInvoice, dateInvoice, nameSupplierInvoice, nameSupplierStockInvoice, 
                            nameCustomerInvoice, nameCustomerStockInvoice, 
                            nameCurrencyInvoice, countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                            VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice, noteInvoice
    PROPERTIES (i) FORCE PANEL TOOLBAR createMultiShipmentInvoice SHOWIF notShippedInvoice(i), addUserShipmentInvoice SHOWIF notCreateShipmentInvoice(i)
    
    FILTERS isPostedInvoice(i),
            toShipInvoiceDetailInvoice(i),
            inInvoice(i)
        
    FILTERGROUP shipped 
        FILTER 'Не оприходованы' i IS Invoice AND NOT isShippedInvoice(i) 'F11' DEFAULT     
        FILTER 'Оприходованы' isShippedInvoice(i) 'F10' 
        
    OBJECTS d = InvoiceDetail
    PROPERTIES (d) READONLY indexInvoiceDetail, idBarcodeSkuInvoiceDetail, nameSkuInvoiceDetail, shortNameUOMSkuInvoiceDetail
    PROPERTIES (d) READONLY quantityInvoiceDetail, priceInvoiceDetail, sumInvoiceDetail,
                   valueVATInvoiceDetail, VATSumInvoiceDetail, invoiceSumInvoiceDetail, toShipInvoiceDetail        
    FILTERS invoiceInvoiceDetail(d)==i
        
    OBJECTS s = Shipment
    PROPERTIES (s) READONLY isPostedShipment FORCE GRID
    PROPERTIES (s) READONLYIF isReadonly() numberShipment, seriesShipment, dateShipment, nameSupplierShipment,
                            nameSupplierStockShipment, nameCustomerShipment, nameCustomerStockShipment 
    PROPERTIES (s) READONLY countShipmentDetailShipment, quantityShipmentDetailShipment, sumShipmentDetailShipment, 
                   noteShipment, invoicesShipment 
    
    PROPERTIES (s) deleteMultiShipment SHOWIF notUserShipment(s) FORCE PANEL TOOLBAR
    PROPERTIES (s) editShipment 
    PROPERTIES (s) deletes=DELETE FORCE PANEL TOOLBAR SHOWIF isUserShipment(s)   
    
    FILTERS inShipment(s)
    
    FILTERGROUP filters0 FILTER 'Накладные на дату' dateInvoice(i)<=dt 'F5' DEFAULT
    FILTERGROUP filters1 FILTER 'Поставки на дату' dateShipment(s)==dt 'F5' DEFAULT
;    

DESIGN invoiceShipmentDashboard FROM DEFAULT {
    main{
        NEW dash BEFORE functions.box {
            type = SPLITV;
            fill = 1;
            
            ADD i.box {PROPERTY(addUserShipmentInvoice(i)){ caption = 'Принять частично';}}
            NEW details {
                fill = 1;
                type = TABBED;
                ADD s.box;
                ADD d.box {
                    caption = 'Детализация';
                }                    
            }
        }
        NEW date BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            ADD PROPERTY(date);
        }
        
    }
}

@extendFormFilterAccessStock(Invoice, i, invoiceShipmentDashboard, customerStock, company);
@extendFormFilterAccessStock(Invoice, i, invoiceShipmentDashboard, supplierStock, supplier);
@extendFormFilterAccessLegalEntity(Invoice, i, invoiceShipmentDashboard, customer, company);
@extendFormFilterAccessLegalEntity(Invoice, i, invoiceShipmentDashboard, supplier, supplier);

@extendFormFilterAccessStock(Shipment, s, invoiceShipmentDashboard, customerStock, company);
@extendFormFilterAccessStock(Shipment, s, invoiceShipmentDashboard, supplierStock, supplier);
@extendFormFilterAccessLegalEntity(Shipment, s, invoiceShipmentDashboard, customer, company);
@extendFormFilterAccessLegalEntity(Shipment, s, invoiceShipmentDashboard, supplier, supplier);
 
NAVIGATOR {
    purchaseDashboardNavigator {
        ADD invoiceShipmentDashboard;
    }
}    