MODULE OrderPrint;

REQUIRE LegalEntity, Bank, Order;

NAMESPACE Order;

@defineDocumentAccount(order, supplier, ' поставщика');
@defineDocumentPhone(order, supplier, ' поставщика');

@defineDocumentAccount(order, customer, ' покупателя');
@defineDocumentPhone(order, customer, ' покупателя');

//Рекламный блок

adImageOrder 'Рекламное изображение счет-фактуры' = DATA IMAGEFILE ();
loadImageOrder 'Загрузить рекламное изображение счет-фактуры' () = ACTION LOADFILE adImageOrder();
openImageOrder 'Просмотреть рекламное изображение счет-фактуры' () = ACTION OPENFILE adImageOrder();
deleteImageOrder 'Удалить рекламное изображение счет-фактуры' () = ACTION {adImageOrder() <- NULL;} SHORTCUT adImageOrder;

adTextOrder 'Рекламное изображение счет-фактуры' = DATA RICHTEXT ();

EXTEND FORM options
    PROPERTIES() adImageOrder ON CHANGE loadImageOrder(), adTextOrder
;

DESIGN options {
    orders {
        NEW adContainer{
            type = CONTAINERH;
            fill = 1;
            caption = 'Рекламный блок';
            MOVE PROPERTY(adImageOrder()){
                panelCaptionAbove = TRUE;
                alignment = STRETCH;
            }
            MOVE PROPERTY(adTextOrder()){
                panelCaptionAbove = TRUE;
                fill = 3;
            }
        }
    }
}

FORM printOrder 'Счет-фактура (печать)'
    OBJECTS o = Order FIXED PANEL
    PROPERTIES (o) READONLY nameSupplierOrder, nameSupplierStockOrder, addressSupplierOrder, taxNumberSupplierOrder, 
                   numberAccountSupplierOrder, nameBankSupplierOrder, addressBankSupplierOrder,
                   departmentBankSupplierOrder, CBUBankSupplierOrder, MFOBankSupplierOrder, phoneSupplierOrder,
                   nameCustomerOrder, nameCustomerStockOrder, addressCustomerOrder, 
                   numberAccountCustomerOrder, nameBankCustomerOrder, addressBankCustomerOrder,
                   departmentBankCustomerOrder, CBUBankCustomerOrder, MFOBankCustomerOrder, phoneCustomerOrder,
                   numberOrder, seriesOrder, seriesNumberOrder, dateOrder, timeOrder, closeDateOrder, currencyOrder,
                   nameCurrencyOrder, noteOrder, countOrderDetailOrder, quantityOrderDetailOrder, sumOrderDetailOrder,
                   VATSumOrderDetailOrder, invoiceSumOrderDetailOrder, nameAcquisitionTargetOrder
    PROPERTIES() adImage=adImageOrder, adText=adTextOrder

    OBJECTS d = OrderDetail
    PROPERTIES (d) READONLY indexOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail,
                   idBarcodeSkuOrderDetail, quantityOrderDetail,
                   priceOrderDetail, sumOrderDetail, numberVATOrderDetail,
                   valueVATOrderDetail,  VATSumOrderDetail, invoiceSumOrderDetail
    FILTERS orderOrderDetail(d) == o
;

printOrder 'Счет-фактура' (order) = ACTION FORM printOrder OBJECTS o = order PRINT  IMAGE 'print.png' IN print;

FORM printOrderXLS 'Заказ'
    OBJECTS o = Order FIXED PANEL
    PROPERTIES (o) READONLY nameSupplierOrder, fullNameSupplierOrder, nameSupplierStockOrder, addressSupplierOrder, 
                   numberAccountSupplierOrder, nameBankSupplierOrder, addressBankSupplierOrder,
                   departmentBankSupplierOrder, CBUBankSupplierOrder, MFOBankSupplierOrder, phoneSupplierOrder,
                   nameCustomerOrder, fullNameCustomerOrder, nameCustomerStockOrder, addressCustomerOrder, 
                   numberAccountCustomerOrder, nameBankCustomerOrder, addressBankCustomerOrder,
                   departmentBankCustomerOrder, CBUBankCustomerOrder, MFOBankCustomerOrder, phoneCustomerOrder,
                   numberOrder, seriesOrder, seriesNumberOrder, dateOrder, timeOrder, closeDateOrder, currencyOrder,
                   nameCurrencyOrder, noteOrder, countOrderDetailOrder, quantityOrderDetailOrder, sumOrderDetailOrder,
                   VATSumOrderDetailOrder, invoiceSumOrderDetailOrder, shipmentDateTimeOrder, shipmentDateOrder, shipmentTimeOrder, 
                   addressCustomerStockOrder, descriptionContractSkuOrder, createdNameUserOrder

    OBJECTS d = OrderDetail
    PROPERTIES (d) READONLY indexOrderDetail, nameSkuOrderDetail, idSkuOrderDetail, idBarcodeSkuOrderDetail, shortNameUOMSkuOrderDetail,
                   quantityOrderDetail,
                   priceOrderDetail, sumOrderDetail, numberVATOrderDetail,
                   valueVATOrderDetail,  VATSumOrderDetail, invoiceSumOrderDetail, allBarcodesSkuOrderDetail
    FILTERS orderOrderDetail(d) == o
;

excelOrder 'Заказ (xls)' (order) = ACTION FORM printOrderXLS OBJECTS o = order PRINT XLSX IN print;