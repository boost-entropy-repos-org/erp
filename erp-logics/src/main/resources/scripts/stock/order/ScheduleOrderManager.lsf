MODULE ScheduleOrderManager;

META defineScheduleOrderManager(managed, filter)

manager = DATA Employee (ScheduleOrder);
nameManager 'Менеджер' (ScheduleOrder scheduleOrder) = shortName(manager(scheduleOrder));

manager (ScheduleOrderDetail scheduleOrderDetail) = manager(scheduleOrder(scheduleOrderDetail));

countInManagers (e, s) = GROUP SUM 1 IF inManager(Employee m, Employee e) AND m == manager(ScheduleOrder s) BY e, s;
inManager (Employee e, ScheduleOrder s) = TRUE IF countInManagers (e, s) OR e==manager(s);

countInManagersOrder (e, o) = GROUP SUM 1 IF inManager(Employee m, Employee e) AND m == overManager(managed(Order o), filter##Stock(o)) BY e, o;
inManager (Employee e, Order o) = TRUE IF countInManagersOrder (e, o) OR e == overManager(managed(o), filter##Stock(o));

EXTEND FORM scheduleOrder PROPERTIES (s) nameManager;

DESIGN scheduleOrder {
    header {
        NEW managerContainer {
            caption = 'Менеджмент';
            MOVE PROPERTY (nameManager(s));
        }
    }
}

WHEN SETCHANGED (managed(ScheduleOrder s)) DO manager(s) <- manager(managed(s));

EXTEND FORM scheduleOrderDashboard
    FILTERS inManager(currentUser(), s) OR NOT manager(s)
    FILTERS inManager(currentUser(), ss) OR NOT manager(ss);

EXTEND FORM scheduleOrderDetails
    FILTERS inManager(currentUser(), scheduleOrder(sd)) OR NOT manager(sd)
    FILTERS inManager(currentUser(),o) OR NOT overManager(managed(o), filter##Stock(o));
    

END