MODULE WriteOffPurchaseItem;

REQUIRE WriteOffPurchase, Item;

NAMESPACE WriteOff;

writeOffRate 'Норма отходов' = DATA WriteOffRate (Country, Item);
nameWriteOffRate 'Норма отходов' = name(writeOffRate(Country country, Item item)) MINCHARWIDTH 20 PREFCHARWIDTH 30;
percentWriteOffRate 'Норма отходов,%' = percent(writeOffRate(Country country, Item item)) MINCHARWIDTH 6 PREFCHARWIDTH 10;

nameWriteOffRateDefaultCountry 'Норма отходов' = name(writeOffRate(defaultCountry(), Item item)) MINCHARWIDTH 20 PREFCHARWIDTH 30;
percentWriteOffRateDefaultCountry 'Норма отходов,%' = percent(writeOffRate(defaultCountry(), Item item)) MINCHARWIDTH 6 PREFCHARWIDTH 10;

FORM dialogWriteOffRate 'Нормы отходов'
    OBJECTS c=Country FIXED PANEL
//    PROPERTIES(c) READONLY nameCountry
    OBJECTS r=WriteOffRate
    PROPERTIES(r) READONLY name, percent, nameCountry
    PROPERTIES(r) NEWEDIT, EDIT, DELETE
    FILTERS country(r) == c
;

changeWriteOffRateDefaultCountry(Item item) = ACTION {
    REQUEST OBJECT r
    SHOW dialogWriteOffRate OBJECTS c = defaultCountry() DIALOG SHOWDROP;
    IF formResult() == FormResult.ok THEN {
        writeOffRate(Country country, item) <- requestedObject() WHERE country == defaultCountry();

    } ELSE IF formResult() == FormResult.drop THEN {
        writeOffRate(Country country, item) <- NULL WHERE country == defaultCountry();
    }
}

CONSTRAINT country(writeOffRate(Country country, Item item)) != country
    CHECKED BY writeOffRate[Country,Item] MESSAGE 'Страна нормы отходов не совпадает со страной';

writeOffRate (Country country, Item sku) += writeOffRate (country, sku);

overCopy(Item s, Item d) += ACTION {
    ASSIGN writeOffRate(Country country, d) <- writeOffRate(country, s);
}

EXTEND FORM item
    PROPERTIES(c,i) nameWriteOffRate, percentWriteOffRate
    PROPERTIES(i) nameWriteOffRateDefaultCountry ON CHANGE changeWriteOffRateDefaultCountry(i), percentWriteOffRateDefaultCountry READONLY
;
DESIGN item { regionPrm  { MOVE PROPERTY(nameWriteOffRateDefaultCountry(i)); MOVE PROPERTY(percentWriteOffRateDefaultCountry(i));} }


EXTEND FORM items 
    PROPERTIES(i) nameWriteOffRateDefaultCountry ON CHANGE changeWriteOffRateDefaultCountry(i) READONLYIF isReadonly(), percentWriteOffRateDefaultCountry READONLY
;