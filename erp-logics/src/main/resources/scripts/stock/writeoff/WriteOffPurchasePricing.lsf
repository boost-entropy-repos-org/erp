MODULE WriteOffPurchasePricing;

REQUIRE WriteOffPurchase, PurchaseManufacturingPrice, PricingPurchase;

PRIORITY Purchase; 
NAMESPACE WriteOff;


//------- Подсчет сумм для проводок при списании технологических отходов на приходе товаров ----------//

writeOffRetailMarkupSum= round(retailMarkup[InvoiceDetail](UserInvoiceDetail d) * writeOffSum[InvoiceDetail](d)/100.0, currency(d));
writeOffRetailVATSum= round(
    writeOffSum[InvoiceDetail](UserInvoiceDetail d) * ( 100.0 (+) retailMarkup[InvoiceDetail] (d))*valueRetailVAT[InvoiceDetail](d)/10000.0, 
    currency(d));
writeOffRetailSum= OVERRIDE round(retailPrice(InvoiceDetail d) * writeOffQuantity(d), priceRound(roundCondition(retailPriceListType(d), sku(d)))),
    round(retailPrice(d) * writeOffQuantity(d), currency(d));


roundWriteOff 'Округления' (InvoiceDetail d) = writeOffRetailSum(d) (-) 
                                              writeOffRetailVATSum(d) (-) 
                                              writeOffRetailMarkupSum(d) (-)
                                              writeOffSum(d);

sumWriteOffInvoiceDetailPercent 'Сумма поставщика' (i) = GROUP
    BY invoice(InvoiceDetail d) SUM sum(d) IF percentWriteOffRate(d);
writeOffQuantityInvoiceDetail 'Списано' (i) = GROUP
    BY invoice(InvoiceDetail d) SUM writeOffQuantity(d) IF percentWriteOffRate(d);
    
writeOffSumInvoiceDetailPercent 'Сумма списания' (i) = GROUP
    BY invoice(InvoiceDetail d) SUM writeOffSum(d) IF percentWriteOffRate(d);

writeOffRetailMarkupSumInvoiceDetailPercent 'Сумма надбавки' (i) = GROUP
    BY invoice(UserInvoiceDetail d) SUM writeOffRetailMarkupSum(d) IF percentWriteOffRate[InvoiceDetail](d);
    
writeOffRetailVATSumInvoiceDetailPercent 'Сумма НДС' (i) = GROUP
    BY invoice(UserInvoiceDetail d) SUM writeOffRetailVATSum(d) IF percentWriteOffRate[InvoiceDetail](d);
    
writeOffRetailSumInvoiceDetailPercent 'Сумма розничная' (i) = GROUP
    BY invoice(InvoiceDetail d) SUM writeOffRetailSum(d) IF percentWriteOffRate(d);

roundWriteOffInvoiceDetailPercent 'Сумма округления' (i) = GROUP
    BY invoice(InvoiceDetail d) SUM roundWriteOff(d) IF percentWriteOffRate(d);
    