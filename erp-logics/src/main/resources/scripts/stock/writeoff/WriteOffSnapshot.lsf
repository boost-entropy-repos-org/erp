MODULE WriteOffSnapshot;

REQUIRE PurchaseSnapshot, WriteOff;

NAMESPACE WriteOff;


//-- Sku
writeOffQuantity 'Кол-во списание' = DATA NUMERIC[14,3] (Sku, Stock, Snapshot);
writeOffSum 'Сумма списание' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot);

//-- Batch
writeOffQuantity 'Кол-во списание' = DATA NUMERIC[14,3] (Batch, Stock, Snapshot);
writeOffSum 'Сумма списание' = DATA NUMERIC[16,2] (Batch, Stock, Snapshot);


//-- группа/склад       
writeOffQuantity 'Кол-во списание' (group, stock, snapshot) =
    GROUP SUM writeOffQuantity (Sku sku, Stock stock, Snapshot snapshot) IF isParent(SkuGroup group, sku)
        BY group, stock, snapshot MATERIALIZED;
writeOffSum 'Сумма списание' (group, stock, snapshot) =
    GROUP SUM writeOffSum (Sku sku, Stock stock, Snapshot snapshot) IF isParent(SkuGroup group, sku)
        BY group, stock, snapshot MATERIALIZED;  
        
//-- группа          
writeOffQuantity 'Кол-во списание' (group, snapshot) =
    GROUP SUM writeOffQuantity (SkuGroup group, Stock stock, Snapshot snapshot)  BY group, snapshot;    
writeOffSum 'Сумма списание' (group, snapshot) =
    GROUP SUM writeOffSum (SkuGroup group, Stock stock, Snapshot snapshot)  BY group, snapshot;    
                
//--По складам
writeOffQuantity 'Кол-во списание' (stock, snapshot) =
    GROUP SUM writeOffQuantity (SkuGroup sku, Stock stock, Snapshot snapshot)  BY stock, snapshot;   
writeOffSum 'Сумма списание' (stock, snapshot) =
    GROUP SUM writeOffSum (SkuGroup sku, Stock stock, Snapshot snapshot)  BY stock, snapshot;  
                     
//-- Sku на дату
writeOffQuantity 'Кол-во списание' = DATA NUMERIC[14,3] (Sku, Stock, Snapshot, DATE);
writeOffSum 'Сумма списание' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot, DATE);        
             
//--По складам на дату     
writeOffQuantity 'Кол-во списание' (stock, snapshot, date) =
    GROUP SUM writeOffQuantity (Sku sku, Stock stock, Snapshot snapshot, DATE date)  BY stock, snapshot, date; 
writeOffSum 'Сумма списание' (stock, snapshot, date) =
    GROUP SUM writeOffSum (Sku sku, Stock stock, Snapshot snapshot, DATE date)  BY stock, snapshot, date;    
           
nameWriteOffQuantity (Stock st) = CONCAT ' ', name(st), ' (кол-во списание)';
nameWriteOffSum (Stock st) = CONCAT ' ', name(st), ' (сумма списание)';    
       
isWriteOff 'Списание' = DATA BOOLEAN (Snapshot) IN evidence;
hintWriteOffBackground  = RGB(0, 252, 233) IF TRUE;      
  
isWriteOff 'Списание' = DATA BOOLEAN (SnapshotType);

EXTEND FORM snapshotType
    PROPERTIES (t) isWriteOff
;
DESIGN snapshotType {
    row6 {
        MOVE PROPERTY (isWriteOff (t));
    }
}
EXTEND FORM snapshotTypes
    PROPERTIES (t) READONLY  isWriteOff
;
@deriveDocumentSnapshotProperty(isWriteOff);

       
//-- Расширяем ACTION    
writeOffSum 'Списано (сумма)' (sku, stock, date) =  GROUP SUM sum(WriteOffDetail ledger) IF isPosted(ledger) BY sku(ledger), stock(ledger), date(ledger);
writeOffQuantity 'Списано (кол-во)' (sku, stock, date) =  GROUP SUM quantity(WriteOffDetail ledger) IF isPosted(ledger) BY sku(ledger), stock(ledger), date(ledger);
  
writeOffSum 'Списано за интервал (сумма)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        writeOffSum(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY sku, stock, dateFrom, dateTo;  
writeOffQuantity 'списано за интервал (кол-во)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        writeOffQuantity(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY sku, stock, dateFrom, dateTo;    
                    
overTakeSkuFromTo(Snapshot snapshot, DATE dateFrom, DATE dateTo) += {
    IF isWriteOff(snapshot) THEN {
        IF isDate(snapshot) THEN {
            IF isQuantity(snapshot) THEN {
                writeOffQuantity(Sku sku, Stock stock, snapshot, DATE date) <- NULL;
                writeOffQuantity(Sku sku, Stock stock, snapshot, DATE date) <- NUMERIC[14,3](writeOffQuantity(sku, stock, date)) 
                    WHERE include(snapshot, stock) AND include(snapshot, sku) AND date AS DATE AND date >= (dateFrom AS DATE) 
                        AND date <= (dateTo AS DATE) AND writeOffQuantity(sku, stock, date);
            }
            IF isSum(snapshot) THEN {
                writeOffSum(Sku sku, Stock stock, snapshot, DATE date) <- NULL;
                writeOffSum(Sku sku, Stock stock, snapshot, DATE date) <- NUMERIC[16,2](writeOffSum(sku, stock, date)) 
                    WHERE include(snapshot, stock) AND include(snapshot, sku) AND date AS DATE AND date >= (dateFrom AS DATE) 
                        AND date <= (dateTo AS DATE) AND writeOffSum(sku, stock, date);   
                     
            }                               
        }
        
        IF isQuantity(snapshot) THEN {
            writeOffQuantity(Sku sku, Stock stock, snapshot) <- NULL;
            writeOffQuantity(Sku sku, Stock stock, snapshot) <- NUMERIC[14,3](writeOffQuantity(sku, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku) AND writeOffQuantity(sku, stock, dateFrom, dateTo);
        }    
        IF isSum(snapshot) THEN { 
            writeOffSum(Sku sku, Stock stock, snapshot) <- NULL;
            writeOffSum(Sku sku, Stock stock, snapshot) <- NUMERIC[16,2](writeOffSum(sku, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku) AND writeOffSum(sku, stock, dateFrom, dateTo);    
 
        }              
    }            
};                                                                                                
               
//-- Batch      
writeOffSum 'Списано (сумма)' (batch, stock, date) =  GROUP SUM sum(WriteOffDetail ledger) IF isPosted(ledger) BY batch(ledger), stock(ledger), date(ledger);
writeOffQuantity 'Списано (кол-во)' (batch, stock, date) =  GROUP SUM quantity(WriteOffDetail ledger) IF isPosted(ledger) BY batch(ledger), stock(ledger), date(ledger);
  
writeOffSum 'Списано за интервал (сумма)' (batch, stock, dateFrom, dateTo) = GROUP SUM
        writeOffSum(Batch batch, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY batch, stock, dateFrom, dateTo;  
writeOffQuantity 'списано за интервал (кол-во)' (batch, stock, dateFrom, dateTo) = GROUP SUM
        writeOffQuantity(Batch batch, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY batch, stock, dateFrom, dateTo;

overTakeBatchFromTo(Snapshot snapshot, DATE dateFrom, DATE dateTo) += {
    IF isWriteOff(snapshot) THEN {
        IF isQuantity(snapshot) THEN {
            writeOffQuantity(Batch batch, Stock stock, snapshot) <- NULL;
            writeOffQuantity(Batch batch, Stock stock, snapshot) <- NUMERIC[14,3](writeOffQuantity(batch, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku(batch)) AND writeOffQuantity(batch, stock, dateFrom, dateTo);
        }
        IF isSum(snapshot) THEN { 
            writeOffSum(Batch batch, Stock stock, snapshot) <- NULL;
            writeOffSum(Batch batch, Stock stock, snapshot) <- NUMERIC[16,2](writeOffSum(batch, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku(batch)) AND writeOffSum(batch, stock, dateFrom, dateTo);   
       
        }          
    }      
};  
       
//-- SHOWIF
isQuantityWriteOff= isQuantity(Snapshot snapshot) AND isWriteOff(snapshot);
isSumWriteOff= isSum(Snapshot snapshot) AND isWriteOff(snapshot);
//--
isQuantityWriteOffDate= isQuantity(Snapshot snapshot) AND isWriteOff(snapshot) AND isDate(snapshot);
isSumWriteOffDate= isSum(Snapshot snapshot) AND isWriteOff(snapshot) AND isDate(snapshot);
//--
isQuantityWriteOffBatch= isQuantity(Snapshot snapshot) AND isWriteOff(snapshot) AND isBatch(snapshot);
isSumWriteOffBatch= isSum(Snapshot snapshot) AND isWriteOff(snapshot) AND isBatch(snapshot);
                                                    
                                                         
EXTEND FORM snapshot
    PROPERTIES(r)      BACKGROUND hintWriteOffBackground() isWriteOff
//--sku  
    PROPERTIES(s,st,r) READONLY BACKGROUND hintWriteOffBackground() BEFORE inQuantity(s,st,r)
                       writeOffQuantity SHOWIF isQuantityWriteOff(r), 
                       writeOffSum SHOWIF isSumWriteOff(r)

//--sku на дату  
    PROPERTIES(s3,st3,r,d3) READONLY BACKGROUND hintWriteOffBackground() BEFORE inQuantity(s3,st3,r,d3)
                       writeOffQuantity SHOWIF isQuantityWriteOffDate(r), 
                       writeOffSum SHOWIF isSumWriteOffDate(r)

//--По складам на дату 
    PROPERTIES(st4,r,d4) READONLY BACKGROUND hintWriteOffBackground() BEFORE inQuantity(st4,r,d4)
                       writeOffQuantity COLUMNS 'g' (st4) HEADER nameWriteOffQuantity(st4) SHOWIF isQuantityWriteOffDate(r), 
                       writeOffSum COLUMNS 'g' (st4) HEADER nameWriteOffSum(st4) SHOWIF isSumWriteOffDate(r)
                                                                 
//-- batch
    PROPERTIES(bt,ss,r) READONLY BACKGROUND hintWriteOffBackground() BEFORE inQuantity(bt,ss,r) 
                       writeOffQuantity SHOWIF isQuantityWriteOffBatch(r), 
                       writeOffSum SHOWIF isSumWriteOffBatch(r)
        
//-- По группам                           
    PROPERTIES(sk1,r)  READONLY BACKGROUND hintWriteOffBackground() BEFORE balanceA(sk1,r)        
                       writeOffQuantity SHOWIF isQuantityWriteOff(r), 
                       writeOffSum SHOWIF isSumWriteOff(r)
    PROPERTIES(skg,r)  READONLY BACKGROUND hintWriteOffBackground() BEFORE balanceA(skg,r)        
                       writeOffQuantity SHOWIF isQuantityWriteOff(r), 
                       writeOffSum SHOWIF isSumWriteOff(r)

//-- По складам                           
    PROPERTIES(ts1,r)  READONLY BACKGROUND hintWriteOffBackground() BEFORE inQuantity(ts1,r)                         
                       writeOffQuantity SHOWIF isQuantityWriteOff(r), 
                       writeOffSum SHOWIF isSumWriteOff(r)

;  
DESIGN snapshot {
    row6 {
        MOVE PROPERTY (isWriteOff (r));
    }
}


EXTEND FORM snapshots
    PROPERTIES(r)      READONLY  BACKGROUND hintWriteOffBackground() isWriteOff     
;       