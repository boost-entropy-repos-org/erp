MODULE StockMovement;

REQUIRE SkuLedger, EmployeeStock, PriceListLedger, StockDocumentSkuLedger;

NAMESPACE Stock;

// -------------------------------- Движение товара -------------------------- //

currentAccountPriceASkuStock 'Цена учетная' (sku, stock) = prevPriceAPriceListTypeSkuStockDateTime(SystemLedgerPriceListType.accountPriceListType, sku, stock, currentDateTime());
accountPriceASkuStockSkuLedger 'Цена учетная' (sku, stock, ledger) = prevPriceAPriceListTypeSkuStockDateTime(SystemLedgerPriceListType.accountPriceListType, sku, stock, dateTimeSkuLedger(ledger));
accountPriceASkuSkuLedger 'Цена учетная' (sku, ledger)= prevPriceAPriceListTypeSkuStockDateTime(SystemLedgerPriceListType.accountPriceListType, sku, stockSkuLedger(ledger), dateTimeSkuLedger(ledger));

FORM movementSku 'Движение товара'
    OBJECTS s = Sku FIXED PANEL
    PROPERTIES(s) READONLY nameSku

    OBJECTS bil = SkuLedger
    PROPERTIES(bil) READONLY numberDocumentSkuLedger, seriesDocumentSkuLedger, dateTimeSkuLedger, descriptionSkuLedger, nameStockSkuLedger,
                             nameLegalEntityDocumentSkuLedger, nameLegalEntityStockDocumentSkuLedger, signedQuantitySkuLedger, signedPriceSkuLedger, 
                             signedSumSkuLedger, balanceASkuLedger
    PROPERTIES(s,bil) READONLY accountPriceASkuSkuLedger   
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)                    
    ORDER BY dateTimeSkuLedger(bil)
    FILTERS skuSkuLedger(bil) == s,
            isPostedSkuLedger(bil),
            isCompanyStockSkuLedger(bil)
;
@extendFormFilterAccessStock(SkuLedger, bil, movementSku, stock, company);

FORM movementSkuStock 'Движение товара по складу'
    OBJECTS s = Sku FIXED PANEL
    PROPERTIES(s) READONLY nameSku

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock

    OBJECTS bil = SkuLedger
    PROPERTIES(bil) READONLY numberDocumentSkuLedger, seriesDocumentSkuLedger, dateTimeSkuLedger, descriptionSkuLedger,
                             nameLegalEntityDocumentSkuLedger, nameLegalEntityStockDocumentSkuLedger, signedQuantitySkuLedger, signedPriceSkuLedger,
                             signedSumSkuLedger , balanceASkuLedger
    PROPERTIES(s,st,bil) READONLY accountPriceASkuStockSkuLedger                           
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)
    ORDER BY dateTimeSkuLedger(bil)
    FILTERS skuSkuLedger(bil) == s,
            isPostedSkuLedger(bil),
            stockSkuLedger(bil)==st,
            isCompanyStock(st)
;
DESIGN movementSkuStock {
    NEW topContainer{
        type = CONTAINERH;
        MOVE s.box;
        MOVE st.box;
    }
    MOVE bil.box;
    MOVE functions.box;
}
@extendFormFilterStockAccess(st, movementSkuStock);

META defineMovementSku(detail, stockProp)
    showMovementSkuStock###detail 'Показать движение товара по складу' (detail) = ACTION FORM movementSkuStock OBJECTS s = sku###detail(detail), st = stockProp###detail(detail) MODAL SHORTCUT nameSku###detail;
    showMovementSku###detail 'Показать движение товара' (detail) = ACTION FORM movementSku OBJECTS s = sku###detail(detail) MODAL SHORTCUT nameSku###detail;
END
META defineMovementSku(detail)
    showMovementSkuStock###detail 'Показать движение товара по складу' (detail) = ACTION FORM movementSkuStock OBJECTS s = sku###detail(detail) MODAL SHORTCUT nameSku###detail;
    showMovementSku###detail 'Показать движение товара' (detail) = ACTION FORM movementSku OBJECTS s = sku###detail(detail) MODAL SHORTCUT nameSku###detail;
END


FORM balancesSkuStock 'Остатки'
    OBJECTS s = Sku FIXED PANEL
    PROPERTIES(s) READONLY nameSku

    OBJECTS st = Stock
    PROPERTIES(st) READONLY idStock, nameStock
    PROPERTIES READONLY currentBalanceSkuStock(s,st), currentAccountPriceASkuStock(s,st)
    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(st) 'ctrl F10' DEFAULT
    ORDER BY idStock(st)
    FILTERS currentBalanceSkuStock(s,st),
            isCompanyStock(st)
;
@extendFormFilterStockAccess(st, balancesSkuStock);

META defineBalancesSku(detail)
    showBalancesSkuStock###detail 'Показать остатки товара' (detail) = ACTION FORM balancesSkuStock OBJECTS s = sku###detail(detail) MODAL SHORTCUT nameSku###detail;
END

showMovementSkuSku'Показать движение товара' (sku) = ACTION FORM movementSku OBJECTS s = sku MODAL SHORTCUT nameSku;
showBalancesSkuSku 'Показать остатки товара' (sku) = ACTION FORM balancesSkuStock OBJECTS s = sku MODAL SHORTCUT nameSku;

// История по правой кнопке (два объекта)
showMovementSkuSkuStock 'Показать движение товара по складу' (sku, stock) = ACTION FORM movementSkuStock OBJECTS s = sku, st = stock MODAL;
showMovementSkuSkuStocks 'Показать движение товара по складу' (sku) = ACTION FORM movementSkuStock OBJECTS s = sku MODAL SHORTCUT nameSku;

// -------------------------------- Движение по партии -------------------------- //
balanceABatchLedger 'Остаток (после)' (batch, ledger) = balanceABatchStockDateTime(batch, stockSkuLedger(ledger), dateTimeSkuLedger(ledger));
accountPriceABatchStockSkuLedger 'Цена учетная' (batch, stock, ledger)= prevPriceAPriceListTypeBatchStockDateTime(SystemLedgerPriceListType.accountPriceListType, batch, stock, dateTimeSkuLedger(ledger));
accountPriceABatchSkuLedger 'Цена учетная' (batch, ledger)= prevPriceAPriceListTypeBatchStockDateTime(SystemLedgerPriceListType.accountPriceListType, batch, stockSkuLedger(ledger), dateTimeSkuLedger(ledger));

FORM movementBatch 'Движение по партии'
    OBJECTS bt = Batch FIXED PANEL
    PROPERTIES(bt) READONLY nameBatch, nameSkuBatch

    OBJECTS bil = SkuLedger
    PROPERTIES(bil) READONLY numberDocumentSkuLedger, seriesDocumentSkuLedger, dateTimeSkuLedger, descriptionSkuLedger, nameStockSkuLedger,
                             nameLegalEntityDocumentSkuLedger, nameLegalEntityStockDocumentSkuLedger, signedQuantitySkuLedger, signedPriceSkuLedger, signedSumSkuLedger

    PROPERTIES      READONLY balanceABatchLedger(bt,bil), accountPriceABatchSkuLedger(bt,bil)
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)        
    ORDER BY dateTimeSkuLedger(bil)
    FILTERS costSkuLedgerBatch(bil, bt) OR batchSkuLedger(bil)==bt,
            isPostedSkuLedger(bil),
            isCompanyStockSkuLedger(bil)

;
FORM movementBatchStock 'Движение по партии'
    OBJECTS bt = Batch FIXED PANEL
    PROPERTIES(bt) READONLY nameBatch, nameSkuBatch

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock

    OBJECTS bil = SkuLedger
    PROPERTIES(bil) READONLY numberDocumentSkuLedger, seriesDocumentSkuLedger, dateTimeSkuLedger, descriptionSkuLedger,
                             nameLegalEntityDocumentSkuLedger, nameLegalEntityStockDocumentSkuLedger, signedQuantitySkuLedger,
                             signedPriceSkuLedger, signedSumSkuLedger
    PROPERTIES      READONLY balanceABatchLedger(bt,bil), accountPriceABatchStockSkuLedger(bt,st,bil)
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil) 
    ORDER BY dateTimeSkuLedger(bil)
    FILTERS costSkuLedgerBatch(bil, bt) OR batchSkuLedger(bil)==bt,
            isPostedSkuLedger(bil),
            stockSkuLedger(bil)==st,
            isCompanyStock(st)
;
DESIGN movementBatchStock {
    NEW topContainer{
        type = CONTAINERH;
        MOVE bt.box;
        MOVE st.box;
    }
    MOVE bil.box;
    MOVE functions.box;
}


META defineMovementBatch(detail, batchProp, stockProp)
    showMovementBatchStock###detail 'Показать движение по партии и складу' (detail) = ACTION FORM movementBatchStock OBJECTS bt = batchProp###detail(detail), st = stockProp###detail(detail) MODAL SHORTCUT nameBatch###detail;
    showMovementBatch###detail 'Показать движение по партии' (detail) = ACTION FORM movementBatch OBJECTS bt = batchProp###detail(detail) MODAL SHORTCUT nameBatch###detail;
END
META defineMovementBatch(detail, stockProp)
    @defineMovementBatch(detail, batch, stockProp);
END

META defineMovementIdBatch(detail, batchProp, stockProp)
    showMovementIdBatchStock###detail 'Показать движение по партии и складу' (detail) = ACTION FORM movementBatchStock OBJECTS bt = batchProp###detail(detail), st = stockProp###detail(detail) MODAL SHORTCUT idBatch###detail;
    showMovementIdBatch###detail 'Показать движение по партии' (detail) = ACTION FORM movementBatch OBJECTS bt = batchProp###detail(detail) MODAL SHORTCUT idBatch###detail;
END
META defineMovementIdBatch(detail, stockProp)
    @defineMovementIdBatch(detail, batch, stockProp);
END

FORM balancesBatchStock 'Остатки'
    OBJECTS bt = Batch FIXED PANEL
    PROPERTIES(bt) READONLY nameBatch, nameSkuBatch

    OBJECTS st = Stock
    PROPERTIES(st) READONLY idStock, nameStock
    PROPERTIES READONLY currentBalanceBatchStock(bt,st), currentAccountPriceBatchStock(bt,st)
    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(st) 'ctrl F10' DEFAULT
    ORDER BY idStock(st)
    FILTERS currentBalanceBatchStock(bt,st),
            isCompanyStock(st)
;
META defineBalancesBatch(detail, batchProp)
    showBalancesBatchStock###detail 'Показать остатки по партии' (detail) = ACTION FORM balancesBatchStock OBJECTS bt = batchProp###detail(detail) MODAL SHORTCUT nameBatch###detail;
END
META defineBalancesBatch(detail)
    @defineBalancesBatch(detail, batch);
END

META defineBalancesIdBatch(detail, batchProp)
    showBalancesIdBatchStock###detail 'Показать остатки по партии' (detail) = ACTION FORM balancesBatchStock OBJECTS bt = batchProp###detail(detail) MODAL SHORTCUT idBatch###detail;
END
META defineBalancesIdBatch(detail)
    @defineBalancesIdBatch(detail, batch);
END

showMovementBatchStockBatch 'Показать движение по партии и складу возникновения партии' (batch) = ACTION FORM movementBatchStock OBJECTS bt = batch, st = stockBatch(batch) MODAL SHORTCUT nameBatch;
showMovementBatchStocksBatch 'Показать движение по партии и складу' (batch) = ACTION FORM movementBatchStock OBJECTS bt = batch MODAL SHORTCUT nameBatch;
showMovementBatchBatch 'Показать движение по партии' (batch) = ACTION FORM movementBatch OBJECTS bt = batch MODAL SHORTCUT nameBatch;
showBalancesBatchStockBatch 'Показать остатки по партии' (batch) = ACTION FORM balancesBatchStock OBJECTS bt = batch MODAL SHORTCUT nameBatch;

showMovementBatchBatchStock 'Показать движение по партии и складу' (batch, stock) = ACTION FORM movementBatchStock OBJECTS bt = batch, st = stock MODAL;

showMovementBatchStockIdBatch 'Показать движение по партии и складу возникновения партии' (batch) = ACTION FORM movementBatchStock OBJECTS bt = batch, st = stockBatch(batch) MODAL SHORTCUT idBatch;
showMovementBatchStocksIdBatch 'Показать движение по партии и складу' (batch) = ACTION FORM movementBatchStock OBJECTS bt = batch MODAL SHORTCUT idBatch;
showMovementBatchIdBatch 'Показать движение по партии' (batch) = ACTION FORM movementBatch OBJECTS bt = batch MODAL SHORTCUT idBatch;
showBalancesBatchIdStockBatch 'Показать остатки по партии' (batch) = ACTION FORM balancesBatchStock OBJECTS bt = batch MODAL SHORTCUT idBatch;

showMovementSkuStockBatch 'Показать движение товара по складу возникновения партии' (batch) = ACTION FORM movementSkuStock OBJECTS s = skuBatch(batch), st = stockBatch(batch) MODAL SHORTCUT nameSkuBatch;
showMovementSkuStocksBatch 'Показать движение товара по складу' (batch) = ACTION FORM movementSkuStock OBJECTS s = skuBatch(batch) MODAL SHORTCUT nameSkuBatch;
showMovementSkuBatch 'Показать движение товара' (batch) = ACTION FORM movementSku OBJECTS s = skuBatch(batch) MODAL SHORTCUT nameSkuBatch;

showMovementSkuBatchStock 'Показать движение товара по складу' (batch, stock) = ACTION FORM movementSkuStock OBJECTS s = skuBatch(batch), st = stock MODAL;

//@defineMovementSku(batch, stock){
//    showMovementSkuStockBatch 'Показать движение товара по складу' (batch) = ACTION FORM movementSkuStock OBJECTS s = skuBatch(batch), st = stockBatch(batch) MODAL SHORTCUT nameSkuBatch;
//    showMovementSkuBatch 'Показать движение товара' (batch) = ACTION FORM movementSku OBJECTS s = skuBatch(batch) MODAL SHORTCUT nameSkuBatch;
//}; //-- показываем по нажатию правой клавиши движение товара
@defineBalancesSku(batch); //-- показываем по нажатию правой клавиши остатки товара

EXTEND FORM currentBalanceSkuStock 
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)  
;
EXTEND FORM balanceSkuStock 
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)
;
EXTEND FORM currentBalanceBatchStock 
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)
;
EXTEND FORM balanceBatchStock 
    PROPERTIES(bil) editSkuLedger SHOWIF allowedEditSkuLeadger(bil)
;




