MODULE Store;

REQUIRE System, Utils, Historizable, EmployeeStock, Tax, LegalEntity, Retail, Bank, Cash;

// -------------------------------------- Торговая сеть ------------------------------- //

CLASS ChainStores 'Торговая сеть' : StockGroup;
TABLE chainStores (ChainStores);

@defineExternalizable(chainStores, VARSTRING[100]);

nameChainStores 'Наименование' = DATA VARISTRING[100](ChainStores) IN recognize;

nameStockGroup(group) += nameChainStores(group) IF group IS ChainStores;

// -------------------------------------- Формат магазина ----------------------------- //

CLASS StoreType 'Формат магазина' : StockGroup;
TABLE storeType (StoreType);

@defineExternalizable(storeType, VARSTRING[100]);

nameStoreType 'Наименование' = DATA VARISTRING[100](StoreType);

nameStockGroup(group) += nameStoreType(group) IF group IS StoreType;

chainStoresStoreType = DATA ChainStores (StoreType) NOT NULL DELETE;
nameChainStoresStoreType 'Торговая сеть' (storeType) = nameChainStores(chainStoresStoreType(storeType)) IN base;

inChainStoresStoreType(chainStores, storeType) = chainStoresStoreType(storeType) == chainStores;

parentStockGroup (storeType) += chainStoresStoreType(storeType);

storeTypeNameChainStores 'Формат магазина по имени и торговой сети' (name, chainStores) = GROUP AGGR storeType BY nameStoreType(storeType), idStoreType(storeType) WHERE storeType IS StoreType;

FORM storeType 'Формат магазина'
    OBJECTS t=StoreType FIXED PANEL
    PROPERTIES(t) nameChainStoresStoreType, nameStoreType
    EDIT StoreType OBJECT t
;

FORM storeTypes 'Форматы магазинов'
    OBJECTS t=StoreType
    PROPERTIES(t) nameChainStoresStoreType READONLY, nameStoreType READONLY, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameStoreType(t)
    DIALOG StoreType OBJECT t
;
DESIGN storeTypes { main{ preferredSize = (600, 400); } }

FORM chainStores 'Торговая сеть'
    OBJECTS n=ChainStores FIXED PANEL
    PROPERTIES(n) nameChainStores, idChainStores SHOWIF showIDs()

    OBJECTS s=StoreType
    PROPERTIES(s) nameStoreType
    PROPERTIES(s) ADDOBJ, DELETESESSION
    FILTERS inChainStoresStoreType(n, s)

    EDIT ChainStores OBJECT n
;

// -------------------------------------- Магазин ------------------------------- //

CLASS Store 'Магазин' : StockGroup, TaxUnit, POI;
TABLE store (Store);

@defineExternalizable(store, VARSTRING[100]);

nameStore 'Наименование' = DATA VARISTRING[100](Store) INDEXED;

nameStockGroup(group) += nameStore(group) IF group IS Store;

legalEntityStore 'Компания' = DATA LegalEntity (Store) INDEXED NOT NULL;
nameLegalEntityStore 'Компания' (store) = nameLegalEntity(legalEntityStore(store)) IN base;
fullNameLegalEntityStore 'Компания' (store) = fullNameLegalEntity(legalEntityStore(store));

bankAccountStore = DATA Bank.Account (Store);
bankAccountStore(store) <- accountLegalEntity(legalEntityStore(store))
    WHEN CHANGED(legalEntityStore(store));
numberBankAccountStore 'Номер счета для инкасаций' = Bank.numberAccount(bankAccountStore(store));

CONSTRAINT Bank.legalEntityAccount(bankAccountStore(store)) != legalEntityStore(store)
    CHECKED BY bankAccountStore MESSAGE 'Организация магазина должна совпадать с организацией расчетного счета';

isCompanyStore(store) = isCompanyLegalEntity(legalEntityStore(store));

languageStore (store) = languageLegalEntity(legalEntityStore(store));

regionStore = DATA Region (Store);
nameRegionStore 'Регион' (store) = nameRegion(regionStore(store));

addressStore 'Адрес' = DATA VARSTRING[100] (Store);
addressPOI (store) += addressStore(store);

latitudeStore 'Координата X' = DATA NUMERIC[10,5](Store);
longitudeStore 'Координата Y' = DATA NUMERIC[10,5](Store);

chairmanStore = DATA Employee (Store);
nameChairmanStore 'Директор магазина' (store) = nameContact(chairmanStore(store));

openDateStore 'Дата открытия' = DATA DATE (Store);

latitudePOI (store) += latitudeStore(store);
longitudePOI (store) += longitudeStore(store);

storeTypeStore = DATA StoreType (Store) INDEXED AUTOSET;
nameStoreTypeStore 'Формат' (store) = nameStoreType(storeTypeStore(store)) IN base;

countStoreStoreType 'Кол-во магазинов' (storeType) =
    GROUP SUM 1 IF storeTypeStore(store) == storeType
    BY storeType;

inStoreTypeStore (storeType, store) = storeTypeStore (store) == storeType;

chainStoresStore (store) = chainStoresStoreType(storeTypeStore(store));
nameChainStoresStore 'Торговая сеть' (store) = nameChainStores(chainStoresStore(store)) IN base;

countStoreChainStores 'Кол-во магазинов' (chainStores) =
    GROUP SUM 1 IF chainStoresStore(store) == chainStores
    BY chainStores;

inChainStoresStore (chainStores, store) = chainStoresStore(store) == chainStores;

parentStockGroup (store) += storeTypeStore(store);

taxUnitGroupTaxUnit(store) += legalEntityStore(store);
descriptionTaxUnit(store) += nameStore(store) + ' ' + addressStore(store);

inChainStoresStoreTypeStore (chainStores, storeType, store) =
    (storeTypeStore(store) == storeType AND chainStores IS ChainStores) OR
    (chainStoresStore(store) == chainStores AND NOT storeType) OR
    (store IS Store AND NOT storeType AND NOT chainStores);
    
// Неактивный
inactiveStore 'Неактивный' = DATA BOOLEAN (Store);
activeStore 'Активный' (s) = s IS Store AND NOT inactiveStore(s);

// -------------------------------------- Отдел магазина ------------------------------- //

CLASS DepartmentStore 'Отдел магазина' : Stock;
TABLE departmentStore (DepartmentStore);
TABLE departmentStoreDate (DepartmentStore, DATE);

@defineExternalizable(departmentStore, VARSTRING[100]);
idStock (s) += idDepartmentStore(s);

nameDepartmentStore 'Наименование' = DATA VARISTRING[150](DepartmentStore) INDEXED MINCHARWIDTH 20 PREFCHARWIDTH 30 IN recognize;
fullNameDepartmentStore 'Наименование полное' = DATA VARISTRING[150](DepartmentStore) INDEXED MINCHARWIDTH 20 PREFCHARWIDTH 30;

nameStock(stock) += nameDepartmentStore(stock) IF stock IS DepartmentStore;
fullNameStock(stock) += (OVERRIDE nameDepartmentStore(stock), fullNameDepartmentStore(stock)) IF stock IS DepartmentStore;

storeDepartmentStore = DATA Store (DepartmentStore) INDEXED AUTOSET NOT NULL DELETE;
nameStoreDepartmentStore 'Магазин' (departmentStore) = nameStore(storeDepartmentStore(departmentStore)) IN base MINCHARWIDTH 20 PREFCHARWIDTH 30;
dataStockGroupDepartmentStore = DATA StockGroup (DepartmentStore);
nameDataStockGroupDepartmentStore = nameStockGroup(dataStockGroupDepartmentStore(d));
stockGroupDepartmentStore = OVERRIDE storeDepartmentStore(departmentStore), dataStockGroupDepartmentStore(departmentStore);
nameStockGroupDepartmentStore 'Группа складов' = nameStockGroup(stockGroupDepartmentStore(d)) MINCHARWIDTH 20 PREFCHARWIDTH 30;

stockGroupStock (departmentStore) += OVERRIDE storeDepartmentStore(departmentStore), dataStockGroupDepartmentStore(departmentStore);

inStoreDepartment(store, departmentStore) = storeDepartmentStore(departmentStore) == store;

storeTypeDepartmentStore(departmentStore) = storeTypeStore(storeDepartmentStore(departmentStore)) PERSISTENT INDEXED;
chainStoresDepartmentStore(departmentStore) = chainStoresStoreType(storeTypeDepartmentStore(departmentStore)) PERSISTENT INDEXED;

legalEntityDepartmentStore (departmentStore) = legalEntityStore(storeDepartmentStore(departmentStore)) PERSISTENT INDEXED;
legalEntityStock (stock) += legalEntityDepartmentStore(stock);

quantityDaysCloseDepartmentStore 'Срок автоматического закрытия заказов' = DATA INTEGER(DepartmentStore);
quantityDaysCloseOrdersStock(stock) += quantityDaysCloseDepartmentStore(stock);

userLegalEntityDepartmentStore 'Отм.' = DATA BOOLEAN (LegalEntity, DepartmentStore);
userLegalEntityStock(legalEntity, stock) += userLegalEntityDepartmentStore(legalEntity, stock);

nameLegalEntityDepartmentStore 'Компания' (departmentStore) = nameLegalEntity(legalEntityDepartmentStore(departmentStore));

primaryDepartmentStoreStore 'Основной отдел' (store) =
    GROUP MIN departmentStore BY storeDepartmentStore(departmentStore);

countDepartmentStoreStore 'Кол-во отделов' (store) = GROUP SUM 1 IF dep IS DepartmentStore BY storeDepartmentStore(dep);

addressDepartmentStore 'Адрес' (departmentStore) = addressStore(storeDepartmentStore(departmentStore));
addressStock (departmentStore) += addressDepartmentStore(departmentStore);

latitudeDepartmentStore 'Координата X' (departmentStore) = latitudeStore(storeDepartmentStore(departmentStore));
longitudeDepartmentStore 'Координата Y' (departmentStore) = longitudeStore(storeDepartmentStore(departmentStore));

latitudePOI (departmentStore) += latitudeDepartmentStore(departmentStore);
longitudePOI (departmentStore) += longitudeDepartmentStore(departmentStore);

regionStock(stock) += regionStore(storeDepartmentStore(stock));

@defineHistorizable(tradingSquare, , 'Торговая площадь', NUMERIC[10,2], departmentStore, nameDepartmentStore, public);

tradingSquareStoreDate (store, date) = GROUP SUM tradingSquareDepartmentStoreDate(departmentStore, date) BY storeDepartmentStore(departmentStore), date;
tradingSquareStore 'Торговая площадь' (store) = tradingSquareStoreDate(store, currentDate());

cashAccountDepartmentStore 'Счет' = DATA Cash.Account (DepartmentStore);
numberCashAccountDepartmentStore 'Номер счета' = Cash.numberAccount(cashAccountDepartmentStore(departmentStore));

CONSTRAINT legalEntityDepartmentStore(departmentStore) != Cash.legalEntityAccount(cashAccountDepartmentStore(departmentStore))
    CHECKED BY cashAccountDepartmentStore MESSAGE 'Организация отдела магазина должна совпадать с организацией счета';

inChainStoresStoreTypeStoreDepartmentStore (chainStores, storeType, store, department) =
    (storeDepartmentStore(department) == store AND storeType IS StoreType AND chainStores IS ChainStores) OR
    (storeTypeDepartmentStore(department) == storeType AND chainStores IS ChainStores AND NOT store) OR
    (chainStoresDepartmentStore(department) == chainStores AND NOT store AND NOT storeType) OR
    (department IS DepartmentStore AND NOT store AND NOT storeType AND NOT chainStores);

inChainStoresStoreTypeDepartmentStore (chainStores, storeType, departmentStore)= inChainStoresStoreTypeStore (chainStores, storeType, storeDepartmentStore(departmentStore));     

// Неактивный
inactiveDepartmentStore 'Неактивный' = DATA BOOLEAN (DepartmentStore);
inactiveStock(st) += inactiveDepartmentStore(st);

inStoreEmployee 'Сотрудник магазина' (store, employee) = GROUP SUM 1 IF inStockEmployee(d, employee)
    BY storeDepartmentStore(d), employee;
    
CONSTRAINT chairmanStore(store) AND NOT inStoreEmployee(store, chairmanStore(store))
    CHECKED BY chairmanStore MESSAGE 'Работник должен быть сотрудником магазина';

// -------------------------------------- Формы отдела магазинов ------------------------------------------ //

FORM departmentStore 'Отдел магазина'
    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES(d) nameDep=nameDepartmentStore, fullNameDepartmentStore, nameStoreDepartmentStore, nameStockGroupDepartmentStore, tradingSquareDepartmentStore,
                  numberCashAccountDepartmentStore SHOWIF isCompanyStock(d), quantityDaysCloseDepartmentStore SHOWIF isCompanyStock(d),
                  idDepartmentStore SHOWIF showIDs(), inactiveDepartmentStore

    OBJECTS e = Employee FIXED GRID
    PROPERTIES(e) SHOWIF isCompanyStock(d) READONLY firstNameContact, lastNameContact
    PROPERTIES(e) SHOWIF isCompanyStock(d) ADDSESSIONFORM, EDITSESSIONFORM, DELETESESSION
    FILTERS inStockEmployee(d, e)

    EDIT DepartmentStore OBJECT d
;

DESIGN departmentStore {
    main {
        preferredSize = (1024, 768);
        NEW header {
            type = CONTAINERH;
            NEW headerLeft {
                NEW headerMainParams {
                    caption = 'Основные параметры';
                    type = COLUMNS;
                    columns = 3;
                    MOVE PROPERTY(nameDep);
                    MOVE PROPERTY(fullNameDepartmentStore(d));
                    MOVE PROPERTY(idDepartmentStore(d));
                    MOVE PROPERTY(nameStoreDepartmentStore(d));
                    MOVE PROPERTY(nameStockGroupDepartmentStore(d));
                    MOVE PROPERTY(tradingSquareDepartmentStore(d));
                    MOVE PROPERTY(inactiveDepartmentStore(d)); 
                }
                NEW headerExtraParams {
                    type = COLUMNS;
                    columns = 2;
                    caption = 'Дополнительные параметры';
                    MOVE PROPERTY(numberCashAccountDepartmentStore(d));
                    MOVE PROPERTY(quantityDaysCloseDepartmentStore(d));
                }
            }
            NEW headerRight;
        }
        MOVE e.box;
        MOVE functions.box;
    }
}

editDepartmentStore 'Редактировать' = ACTION EDITFORM DepartmentStore;
editStock(stock) += ACTION editDepartmentStore(stock);

editSessionDepartmentStore 'Редактировать' = ACTION EDITFORM SESSION DepartmentStore;
editSessionStock(stock) += ACTION editSessionDepartmentStore(stock);

FORM departmentStores 'Отделы магазинов'
    TREE treeStore a=STRING[3], t=ChainStores, st=StoreType, s=Store
    PROPERTIES READONLY OBJVALUE(a), nameChainStores(t), nameStoreType(st), nameStore(s)
    FILTERS stringEqualsAll(a), inChainStoresStoreType (t, st), inStoreTypeStore(st, s)

    OBJECTS d=DepartmentStore
    PROPERTIES(d) READONLY idDepartmentStore SHOWIF showIDs(), depName = nameDepartmentStore, fullNameDepartmentStore
    FILTERS inChainStoresStoreTypeStoreDepartmentStore(t, st, s, d)
    ORDER BY depName

    DIALOG DepartmentStore OBJECT d
;

DESIGN departmentStores {
    main {
        preferredSize = (1024, 768);

        NEW topContainer {
            fill = 1;
            type = SPLITH;

            MOVE treeStore.tree.box {
                caption = 'Склады';
            }

            MOVE d.box {
                fill = 3;
                d.grid {
                    defaultComponent = TRUE;
                }
            }
        }

        MOVE functions.box;
    }
}

changeStore = ACTION (store) {
    REQUEST VARSTRING[110] INPUT;
    IF requestedString() THEN {
        FOR storeDepartmentStore(dep) == store DO {
            ASSIGN nameDepartmentStore(dep) <- replace(nameDepartmentStore(dep), nameStore(store), requestedString());
        }
    }
    ASSIGN nameStore(store) <- requestedString();
}


FORM store 'Магазин'
    OBJECTS s=Store FIXED PANEL
    PROPERTIES(s) nameStore ON CHANGE changeStore(s), addressStore, nameStoreTypeStore, tradingSquareStore READONLY, 
                  nameChairmanStore, openDateStore, nameLegalEntityStore, numberBankAccountStore SHOWIF isCompanyStore(s),
                  nameRegionStore, latitudeStore, longitudeStore, showOnMapPOI, idStore SHOWIF showIDs(), inactiveStore

    OBJECTS d=DepartmentStore
    PROPERTIES(d) READONLY nameDepartmentStore, idDepartmentStore SHOWIF showIDs(), nameStockGroupDepartmentStore, tradingSquareDepartmentStore
    PROPERTIES(d)          ADDNESTEDFORM, EDITNESTEDFORM, deleteD=DELETESESSION
    FILTERS inStoreDepartment(s, d)

    EDIT Store OBJECT s
;
DESIGN store {
    NEW header {
        NEW headerMainParams {
            caption = 'Основные параметры';
            type = CONTAINERH;
            MOVE PROPERTY(nameStore(s));
            MOVE PROPERTY(idStore(s));
            MOVE PROPERTY(nameStoreTypeStore(s));
            MOVE PROPERTY(nameLegalEntityStore(s));
            MOVE PROPERTY(inactiveStore(s));
        }
        NEW headerLocationParams {
            caption = 'Координаты';
            type = CONTAINERH;
            MOVE PROPERTY(addressStore(s));
            MOVE PROPERTY(latitudeStore(s));
            MOVE PROPERTY(longitudeStore(s));
            MOVE PROPERTY(showOnMapPOI(s));
        }
        NEW headerPriceParams {
            caption = 'Цены';
            type = COLUMNS;
            columns = 4;
        }
        NEW headerExtraParams {
            caption = 'Дополнительные параметры';
            type = COLUMNS;
            columns = 4;
            MOVE PROPERTY(nameChairmanStore(s));            
            MOVE PROPERTY(numberBankAccountStore(s));
            MOVE PROPERTY(nameRegionStore(s));
            MOVE PROPERTY(tradingSquareStore(s));
            MOVE PROPERTY(openDateStore(s));
        }
    }

    MOVE d.box;
    MOVE functions.box;
}

// -------------------------------------- Формы магазинов ------------------------------------------ //

FORM stores 'Магазины'
    TREE treeStore a=STRING[3], t=ChainStores, st=StoreType
    PROPERTIES READONLY OBJVALUE(a), nameChainStores(t), nameStoreType(st)

    FILTERS stringEqualsAll(a)
    FILTERS inChainStoresStoreType (t, st)

    PROPERTIES(t)          addT=ADDFORM FORCE PANEL, editT=EDITFORM FORCE PANEL, deletet=DELETE FORCE PANEL TOOLBAR

    OBJECTS s=Store
    PROPERTIES(s) READONLYIF isReadonly() nameStore, idStore SHOWIF showIDs(), addressStore, nameStoreTypeStore, nameLegalEntityStore, nameRegionStore
    PROPERTIES(s)          ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    FILTERS inChainStoresStoreTypeStore(t, st, s)

    OBJECTS d=DepartmentStore
    PROPERTIES(d) READONLY nameDepartmentStore, idDepartmentStore SHOWIF showIDs()
    FILTERS inStoreDepartment(s, d)
    DIALOG Store OBJECT s
;

DESIGN stores {

    NEW pane {
        fill = 1;
        type = SPLITH;

        MOVE treeStore.tree.box {caption = 'Группы магазинов';}

        NEW firstCase {
            fill = 3;
            type = SPLITV;

            MOVE s.box {
                fill = 2;
            }
            MOVE d.box;
        }
    }

    MOVE functions.box;
}
@extendFormEditable(stores);

NAVIGATOR {
    retailMasterData {
        ADD stores;
    }
}

// -------------------------------------- Макросы ----------------------------------------------- //

META defineDocumentHeaderDepartmentStore (object)
    @defineDocumentHeaderStock(object, departmentStore, 'Отдел магазина');
END

META defineDocumentDetailDepartmentStoreCustom (object, detail)
   @defineDocumentDetailStock (object, detail, departmentStore, departmentStore, 'Отдел магазина');
   store###detail (detail) = storeDepartmentStore(departmentStore###detail(detail));
   nameStore###detail 'Магазин' (detail) = nameStore(store###detail(detail));
END
META defineDocumentDetailDepartmentStore (object)
    @defineDocumentDetailDepartmentStoreCustom (object, object##Detail);
END

META defineDocumentDepartmentStore (object)
    @defineDocumentHeaderDepartmentStore(object);
    @defineDocumentDetailDepartmentStore(object);
END

addStoreLegalEntity 'Добавить магазин' = ACTION (legalEntity) NESTEDSESSION{
    FOR ADDOBJ st = Store DO {
        ASSIGN legalEntityStore(st) <- legalEntity;
        FORM store OBJECTS s = st MANAGESESSION DOCKEDMODAL;
    }
}

EXTEND FORM legalEntity
    PROPERTIES addStoreLegalEntity(l) TODRAW st FORCE PANEL TOOLBAR
;

