MODULE StoreWarehouse;

REQUIRE StoreSkuLedger, WarehouseSkuLedger, PriceStockGroupWarehouse, PriceListStore, WriteOff;

changeClassWarehouse 'Сделать магазином' = ACTION (Stock warehouse) NEWSESSION {

    LOCAL n = VARSTRING[110]();
    LOCAL a = VARSTRING[100]();
    LOCAL le = LegalEntity();
    LOCAL la = NUMERIC[10,5]();
    LOCAL lo = NUMERIC[10,5]();
    LOCAL i = INTEGER();
    LOCAL w = WriteOffCommittee();
    LOCAL e = Employee();
    LOCAL ee = Employee();
    LOCAL eee = Employee();
    LOCAL b = BOOLEAN();
    LOCAL r = BOOLEAN (Employee);
    LOCAL id = VARSTRING[100]();

    ASSIGN n() <- name[Warehouse](warehouse);
    ASSIGN a() <- address[Warehouse](warehouse);
    ASSIGN le() <- legalEntity[Warehouse](warehouse);
    ASSIGN la() <- latitude[Warehouse](warehouse);
    ASSIGN lo() <- longitude[Warehouse](warehouse);
    ASSIGN i() <- quantityDaysClose[Warehouse](warehouse);
    ASSIGN w() <- writeOffCommittee(warehouse);
    ASSIGN e() <- responsiblePerson(warehouse);
    ASSIGN ee() <- booker(warehouse);
    ASSIGN eee() <- controller(warehouse);
    ASSIGN b() <- explicitBatchLedger[Warehouse](warehouse);
    ASSIGN id() <- id[Warehouse](warehouse);

    FOR in(warehouse, Employee employee) DO {
        ASSIGN r(employee) <- in(warehouse, employee);
    }

    CHANGECLASS warehouse TO DepartmentStore;

    FOR ADDOBJ s = Store DO{
        ASSIGN name(s) <- n();
        ASSIGN address(s) <- a();
        ASSIGN legalEntity(s) <- le();
        ASSIGN latitude(s) <- la();
        ASSIGN longitude(s) <- lo();

        ASSIGN name[DepartmentStore](warehouse) <- n();
        ASSIGN store(warehouse) <- s;
        ASSIGN quantityDaysClose[DepartmentStore](warehouse) <- i();
        ASSIGN writeOffCommittee(warehouse) <- w();
        ASSIGN dataResponsiblePerson(warehouse, DATE date) <- e() WHERE date == currentDate();
        ASSIGN dataBooker(warehouse, DATE date) <- ee() WHERE date == currentDate();
        ASSIGN dataController(warehouse, DATE date) <- eee() WHERE date == currentDate();
        ASSIGN explicitBatchLedger[DepartmentStore](warehouse) <- b();
        ASSIGN id[Warehouse](warehouse) <- id();
        FOR r(Employee employee) DO {
            ASSIGN in(warehouse, employee) <- r(employee);
        }

        FORM store OBJECTS s=s MODAL;
        IF formResult() == FormResult.ok THEN
            EXEC apply();
    }
} CONFIRM TOOLBAR;

EXTEND FORM warehouses
    PROPERTIES(w) changeClassWarehouse FORCE PANEL
;