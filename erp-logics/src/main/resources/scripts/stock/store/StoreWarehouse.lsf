MODULE StoreWarehouse;

REQUIRE StoreSkuLedger, WarehouseSkuLedger, PriceStockGroupWarehouse, PriceListStore, WriteOff;

changeClassWarehouse 'Сделать магазином' = ACTION (warehouse) NEWSESSION {

    LOCAL n = VARSTRING[110]();
    LOCAL a = VARSTRING[100]();
    LOCAL le = LegalEntity();
    LOCAL la = NUMERIC[10,5]();
    LOCAL lo = NUMERIC[10,5]();
    LOCAL i = INTEGER();
    LOCAL w = WriteOffCommittee();
    LOCAL e = Employee();
    LOCAL ee = Employee();
    LOCAL eee = Employee();
    LOCAL b = BOOLEAN();
    LOCAL r = BOOLEAN (Employee);
    LOCAL id = VARSTRING[100]();

    ASSIGN n() <- nameWarehouse(warehouse);
    ASSIGN a() <- addressWarehouse(warehouse);
    ASSIGN le() <- legalEntityWarehouse(warehouse);
    ASSIGN la() <- latitudeWarehouse(warehouse);
    ASSIGN lo() <- longitudeWarehouse(warehouse);
    ASSIGN i() <- quantityDaysCloseWarehouse(warehouse);
    ASSIGN w() <- writeOffCommitteeStock(warehouse);
    ASSIGN e() <- responsiblePersonStock(warehouse);
    ASSIGN ee() <- bookerStock(warehouse);
    ASSIGN eee() <- controllerStock(warehouse);
    ASSIGN b() <- explicitBatchLedgerWarehouse(warehouse);
    ASSIGN id() <- idWarehouse(warehouse);

    FOR inStockEmployee(warehouse, employee) DO {
        ASSIGN r(employee) <- inStockEmployee(warehouse, employee);
    }

    CHANGECLASS warehouse TO DepartmentStore;

    FOR ADDOBJ s = Store DO{
        ASSIGN nameStore(s) <- n();
        ASSIGN addressStore(s) <- a();
        ASSIGN legalEntityStore(s) <- le();
        ASSIGN latitudeStore(s) <- la();
        ASSIGN longitudeStore(s) <- lo();

        ASSIGN nameDepartmentStore(warehouse) <- n();
        ASSIGN storeDepartmentStore(warehouse) <- s;
        ASSIGN quantityDaysCloseDepartmentStore(warehouse) <- i();
        ASSIGN writeOffCommitteeStock(warehouse) <- w();
        ASSIGN dataResponsiblePersonStockDate(warehouse, date) <- e() WHERE date == currentDate();
        ASSIGN dataBookerStockDate(warehouse, date) <- ee() WHERE date == currentDate();
        ASSIGN dataControllerStockDate(warehouse, date) <- eee() WHERE date == currentDate();
        ASSIGN explicitBatchLedgerDepartmentStore(warehouse) <- b();
        ASSIGN idWarehouse(warehouse) <- id();
        FOR r(employee) DO {
            ASSIGN inStockEmployee(warehouse, employee) <- r(employee);
        }

        FORM store OBJECTS s=s MODAL;
        IF formResult() == FormResult.ok THEN
            EXEC apply();
    }
} CONFIRM TOOLBAR;

EXTEND FORM warehouses
    PROPERTIES(w) changeClassWarehouse FORCE PANEL
;