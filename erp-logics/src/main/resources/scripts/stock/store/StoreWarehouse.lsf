MODULE StoreWarehouse;

REQUIRE Store, Warehouse, PriceListWarehouse, PriceListStore, WriteOff, PriceListStock;

changeClassWarehouse 'Сделать магазином' = ACTION (warehouse) NEWSESSION {

    LOCAL n = STRING[110]();
    LOCAL a = STRING[100]();
    LOCAL le = LegalEntity();
    LOCAL la = NUMERIC[10,5]();
    LOCAL lo = NUMERIC[10,5]();
    LOCAL p = PriceStockGroup();
    LOCAL i = INTEGER();
    LOCAL w = WriteOffCommittee();
    LOCAL e = Employee();
    LOCAL ee = Employee();
    LOCAL eee = Employee();
    LOCAL b = BOOLEAN();
    LOCAL r = BOOLEAN (Employee);

    ASSIGN n() <- nameWarehouse(warehouse);
    ASSIGN a() <- addressWarehouse(warehouse);
    ASSIGN le() <- legalEntityWarehouse(warehouse);
    ASSIGN la() <- latitudeWarehouse(warehouse);
    ASSIGN lo() <- longitudeWarehouse(warehouse);
    ASSIGN p() <- priceStockGroupWarehouse(warehouse);
    ASSIGN i() <- quantityDaysCloseWarehouse(warehouse);
    ASSIGN w() <- writeOffCommitteeStock(warehouse);
    ASSIGN e() <- MRPStock(warehouse);
    ASSIGN ee() <- accountantStock(warehouse);
    ASSIGN eee() <- auditorStock(warehouse);
    ASSIGN b() <- explicitBatchLedgerWarehouse(warehouse);

    FOR inEmployeeDivisionEmployee(warehouse, employee) DO {
        ASSIGN r(employee) <- inEmployeeDivisionEmployee(warehouse, employee);
    }

    CHANGECLASS warehouse TO DepartmentStore;

    FOR ADDOBJ s = Store DO{
        ASSIGN nameStore(s) <- n();
        ASSIGN addressStore(s) <- a();
        ASSIGN legalEntityStore(s) <- le();
        ASSIGN latitudeStore(s) <- la();
        ASSIGN longitudeStore(s) <- lo();
        ASSIGN priceStockGroupStore(s) <- p();

        ASSIGN nameDepartmentStore(warehouse) <- n();
        ASSIGN storeDepartmentStore(warehouse) <- s;
        ASSIGN quantityDaysCloseDepartmentStore(warehouse) <- i();
        ASSIGN writeOffCommitteeStock(warehouse) <- w();
        ASSIGN MRPStock(warehouse) <- e();
        ASSIGN accountantStock(warehouse) <- ee();
        ASSIGN auditorStock(warehouse) <- eee();
        ASSIGN explicitBatchLedgerDepartmentStore(warehouse) <- b();
        FOR r(employee) DO {
            ASSIGN inEmployeeDivisionEmployee(warehouse, employee) <- r(employee);
        }

        FORM store OBJECTS s=s MODAL;
        IF formResult() == FormResult.ok THEN
            EXEC apply();
    }
} CONFIRM TOOLBAR;

EXTEND FORM warehouses
    PROPERTIES(w) changeClassWarehouse FORCE PANEL
;