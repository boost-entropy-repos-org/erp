MODULE PriceListStore;

REQUIRE PriceListCalc, Store;

// ------------------------- Виды цен по умолчанию для формата -------------- //

retailPriceListType 'Розничная цена (управленческая)' = DATA PriceListType(StoreType);
nameRetailPriceListType 'Розничная цена (управленческая)' (StoreType t) = name(retailPriceListType(t)); 

dataRetailPriceListType 'Розничная цена (управленческая)' = DATA PriceListType(Store);
nameDataRetailPriceListType 'Розничная цена (управленческая)' (Store s) = name(dataRetailPriceListType(s));

retailPriceListType (Store s) = OVERRIDE dataRetailPriceListType(s), retailPriceListType(storeType(s)) PERSISTENT;
retailPriceListType (DepartmentStore d) = retailPriceListType(store(d)) PERSISTENT;

retailPriceA 'Розничная цена (управленческая)' (Sku s, DepartmentStore d, DATETIME t) =  
    priceA[LedgerPriceListType,Sku,Stock,DATETIME](retailPriceListType(d), s, d, t);

retailCalcPriceListType 'Розничная цена (расчетная)' = DATA CalcPriceListType(StoreType);
nameRetailCalcPriceListType 'Розничная цена (расчетная)' (StoreType t) = name(retailCalcPriceListType(t)); 

dataRetailCalcPriceListType 'Розничная цена (расчетная)' = DATA CalcPriceListType(Store);
nameDataRetailCalcPriceListType 'Розничная цена (расчетная)' (Store s) = name[PriceListType](dataRetailCalcPriceListType(s));

retailCalcPriceListType (Store s) = OVERRIDE dataRetailCalcPriceListType(s), retailCalcPriceListType(storeType(s)) PERSISTENT;
retailCalcPriceListType (DepartmentStore d) = retailCalcPriceListType(store(d)) PERSISTENT;

markup 'Надбавка, % (расчетная)' (Sku s, DepartmentStore st) =
    markup(retailCalcPriceListType(st), s);

markupPriceA 'Розничная цена (расчетная)' (Sku s, DepartmentStore d, DATETIME t) =  
    priceA(retailCalcPriceListType(d), s, d, t);

markupPriceA 'Розничная цена (расчетная) ' (BasePriceListType pt, Sku s, DepartmentStore d, DATETIME t) =
    priceA(pt, retailCalcPriceListType(d), s, d, t);

EXTEND FORM chainStores
    PROPERTIES(s) nameRetailPriceListType, nameRetailCalcPriceListType AFTER name(s) 
;

EXTEND FORM store
    PROPERTIES(s) nameDataRetailPriceListType, nameDataRetailCalcPriceListType
;

DESIGN store {
    headerPriceParams {
        MOVE PROPERTY(nameDataRetailPriceListType(s));
        MOVE PROPERTY(nameDataRetailCalcPriceListType(s));
    }
}