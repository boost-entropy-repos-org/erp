MODULE PriceListStore;

REQUIRE PriceListCalc, Store, PriceListStock;

//------------------------- Расширяем форму Магазина -------------------------//

priceStockGroupStore = DATA PriceStockGroup (Store);
namePriceStockGroupStore 'Ценовая группа' (store)= namePriceStockGroup(priceStockGroupStore(store));

priceStockGroupStock(stock) += priceStockGroupStore(storeDepartmentStore(stock));

EXTEND FORM store
    PROPERTIES(s)   SHOWIF isCompanyStore(s) namePriceStockGroupStore
;
EXTEND DESIGN store {
    headerExtraParams{
        ADD PROPERTY(namePriceStockGroupStore(s)) BEFORE PROPERTY(nameRegionStore(s));
    }
}

// ------------------------- Виды цен по умолчанию для формата -------------- //

retailPriceListTypeStoreType 'Розничная цена (управленческая)' = DATA PriceListType(StoreType);
nameRetailPriceListTypeStoreType 'Розничная цена (управленческая)' (t) = namePriceListType(retailPriceListTypeStoreType(t)); 

retailPriceListTypeStore (s) = retailPriceListTypeStoreType(storeTypeStore(s)) PERSISTENT;
retailPriceListTypeDepartmentStore (d) = retailPriceListTypeStore(storeDepartmentStore(d)) PERSISTENT;

retailPriceASkuDepartmentStoreDateTime 'Розничная цена (управленческая)' (s, d, t) =  
    priceALedgerPriceListTypeSkuStockDateTime(retailPriceListTypeDepartmentStore(d), s, d, t);

retailCalcPriceListTypeStoreType 'Розничная цена (расчетная)' = DATA CalcPriceListType(StoreType);
nameRetailCalcPriceListTypeStoreType 'Розничная цена (расчетная)' (t) = nameCalcPriceListType(retailCalcPriceListTypeStoreType(t)); 

retailCalcPriceListTypeStore (s) = retailCalcPriceListTypeStoreType(storeTypeStore(s)) PERSISTENT;
retailCalcPriceListTypeDepartmentStore (d) = retailCalcPriceListTypeStore(storeDepartmentStore(d)) PERSISTENT;

markupSkuDepartmentStore 'Надбавка, % (расчетная)' (s, st) =
    markupCalcPriceListTypeSku(retailCalcPriceListTypeDepartmentStore(st), s);

markupPriceASkuDepartmentStoreDateTime 'Розничная цена (расчетная)' (s, d, t) =  
    priceACalcPriceListTypeSkuStockDateTime(retailCalcPriceListTypeDepartmentStore(d), s, d, t);

markupPriceABasePriceListTypeSkuDepartmentStoreDateTime 'Розничная цена (расчетная) ' (pt, s, d, t) =
    priceABasePriceListTypeCalcPriceListTypeSkuStockDateTime(pt, retailCalcPriceListTypeDepartmentStore(d), s, d, t);

EXTEND FORM chainStores
    PROPERTIES(s) nameRetailPriceListTypeStoreType, nameRetailCalcPriceListTypeStoreType AFTER nameStoreType(s) 
;
