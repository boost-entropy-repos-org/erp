MODULE PriceListStore;

REQUIRE PriceListCalc, Store;

// ------------------------- Виды цен по умолчанию для формата -------------- //

retailPriceListTypeStoreType 'Розничная цена (управленческая)' = DATA PriceListType(StoreType);
nameRetailPriceListTypeStoreType 'Розничная цена (управленческая)' (t) = namePriceListType(retailPriceListTypeStoreType(t)); 

dataRetailPriceListTypeStore 'Розничная цена (управленческая)' = DATA PriceListType(Store);
nameDataRetailPriceListTypeStore 'Розничная цена (управленческая)' (s) = namePriceListType(dataRetailPriceListTypeStore(s));

retailPriceListTypeStore (s) = OVERRIDE retailPriceListTypeStoreType(storeTypeStore(s)), dataRetailPriceListTypeStore(s) PERSISTENT;
retailPriceListTypeDepartmentStore (d) = retailPriceListTypeStore(storeDepartmentStore(d)) PERSISTENT;

retailPriceASkuDepartmentStoreDateTime 'Розничная цена (управленческая)' (s, d, t) =  
    priceALedgerPriceListTypeSkuStockDateTime(retailPriceListTypeDepartmentStore(d), s, d, t);

retailCalcPriceListTypeStoreType 'Розничная цена (расчетная)' = DATA CalcPriceListType(StoreType);
nameRetailCalcPriceListTypeStoreType 'Розничная цена (расчетная)' (t) = nameCalcPriceListType(retailCalcPriceListTypeStoreType(t)); 

dataRetailCalcPriceListTypeStore 'Розничная цена (расчетная)' = DATA CalcPriceListType(Store);
nameDataRetailCalcPriceListTypeStore 'Розничная цена (расчетная)' (s) = namePriceListType(dataRetailCalcPriceListTypeStore(s));

retailCalcPriceListTypeStore (s) = OVERRIDE retailCalcPriceListTypeStoreType(storeTypeStore(s)), dataRetailCalcPriceListTypeStore(s) PERSISTENT;
retailCalcPriceListTypeDepartmentStore (d) = retailCalcPriceListTypeStore(storeDepartmentStore(d)) PERSISTENT;

markupSkuDepartmentStore 'Надбавка, % (расчетная)' (s, st) =
    markupCalcPriceListTypeSku(retailCalcPriceListTypeDepartmentStore(st), s);

markupPriceASkuDepartmentStoreDateTime 'Розничная цена (расчетная)' (s, d, t) =  
    priceACalcPriceListTypeSkuStockDateTime(retailCalcPriceListTypeDepartmentStore(d), s, d, t);

markupPriceABasePriceListTypeSkuDepartmentStoreDateTime 'Розничная цена (расчетная) ' (pt, s, d, t) =
    priceABasePriceListTypeCalcPriceListTypeSkuStockDateTime(pt, retailCalcPriceListTypeDepartmentStore(d), s, d, t);

EXTEND FORM chainStores
    PROPERTIES(s) nameRetailPriceListTypeStoreType, nameRetailCalcPriceListTypeStoreType AFTER nameStoreType(s) 
;

EXTEND FORM store
    PROPERTIES(s) nameDataRetailPriceListTypeStore, nameDataRetailCalcPriceListTypeStore
;

DESIGN store {
    headerPriceParams {
        MOVE PROPERTY(nameDataRetailPriceListTypeStore(s));
        MOVE PROPERTY(nameDataRetailCalcPriceListTypeStore(s));
    }
}