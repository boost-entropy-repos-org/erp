MODULE PriceRoundStore;

REQUIRE PriceRound, Store, Country;

//------------------------- Расширяем форму Отдела -------------------------//

dataDiscountRoundCondition = DATA RoundCondition (DepartmentStore);
nameDiscountDataRoundCondition 'Условие округления скидки' (DepartmentStore departmentStore)= name(dataDiscountRoundCondition(departmentStore)) IN bookkeeping;

dataRoundCondition = DATA RoundCondition (DepartmentStore);
nameDataRoundCondition 'Условие округления розничной цены' (DepartmentStore departmentStore)= name(dataRoundCondition(departmentStore)) IN bookkeeping;

roundCondition = DATA RoundCondition(Country);
nameRoundCondition 'Условие округления розничной цены (по умолчанию)' (Country country)= name(roundCondition(country));

roundConditionCountry (DepartmentStore departmentStore) = roundCondition(country(legalEntity(departmentStore))) MATERIALIZED;

roundCondition (DepartmentStore departmentStore) = OVERRIDE dataRoundCondition(departmentStore), roundConditionCountry(departmentStore) MATERIALIZED;
roundDiscountCondition (DepartmentStore departmentStore) = OVERRIDE dataDiscountRoundCondition(departmentStore), roundCondition(departmentStore) MATERIALIZED;

CONSTRAINT currency(DepartmentStore departmentStore) != currency(dataRoundCondition(departmentStore))
           CHECKED BY dataRoundCondition[DepartmentStore]
           MESSAGE 'Валюта округления должна совпадать с валютой склада';

CONSTRAINT currency(DepartmentStore departmentStore) != currency(dataDiscountRoundCondition(departmentStore))
           CHECKED BY dataDiscountRoundCondition[DepartmentStore]
           MESSAGE 'Валюта округления должна совпадать с валютой склада';

CONSTRAINT currency(Country country) != currency(roundCondition(country))
           CHECKED BY roundCondition[Country]
           MESSAGE 'Валюта округления должна совпадать с валютой страны';

EXTEND FORM departmentStore
    PROPERTIES(d) nameDataRoundCondition, nameDiscountDataRoundCondition
;

DESIGN departmentStore {
    headerExtraParams {
        MOVE PROPERTY(nameDataRoundCondition(d));
        MOVE PROPERTY(nameDiscountDataRoundCondition(d));
    }
}

EXTEND FORM country
    PROPERTIES (c) nameRoundCondition
;