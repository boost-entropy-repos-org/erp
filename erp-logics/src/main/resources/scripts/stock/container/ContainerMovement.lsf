MODULE ContainerMovement;

REQUIRE System,
        Utils,
        Stock,
        Store,
        SkuLedger,
        PriceListLedger;


//------------------------------------Отчет по таре-------------------------------------------//

overMovementContainerBatchStockDateFromTo (batch, stock, dateFrom, dateTo) =
    OVERRIDE balanceBBatchStockDate(batch, stock, dateFrom),
             balanceABatchStockDate(batch, stock, dateTo),
             costInBalanceBatchStockDateFromTo(batch, stock, dateFrom, dateTo),
             costOutBalanceBatchStockDateFromTo(batch, stock, dateFrom, dateTo);

accountSumInBalanceBatchStockDateFromTo 'Сумма прихода' (batch, stock, dateFrom, dateTo) = GROUP SUM costSkuLedgerBatch (ledger, batch)* accountPriceABatchStockDateTime(batch, stockSkuLedger(ledger), dateTimeSkuLedger(ledger))
    IF ledger IS InSkuLedger AND isPostedSkuLedger(ledger)
    AND dateSkuLedger(ledger) >= dateFrom AND dateSkuLedger(ledger)<= dateTo
    BY batch, stockSkuLedger(ledger), dateFrom, dateTo;
    
accountSumOutBalanceBatchStockDateFromTo 'Сумма расхода' (batch, stock, dateFrom, dateTo) = GROUP SUM costSkuLedgerBatch (ledger, batch)* accountPriceABatchStockDateTime(batch, stockSkuLedger(ledger), dateTimeSkuLedger(ledger))
    IF ledger IS OutSkuLedger AND isPostedSkuLedger(ledger)
    AND dateSkuLedger(ledger) >= dateFrom AND dateSkuLedger(ledger)<= dateTo
    BY batch, stockSkuLedger(ledger), dateFrom, dateTo;

inContainerMovementBatch = DATA LOCAL BOOLEAN (Batch);

balanceInBatch 'Остаток на начало' =  DATA LOCAL NUMERIC[14,3] (Batch);
sumInBatch 'Сумма на начало' =  DATA LOCAL NUMERIC[15,3] (Batch);

balanceOutBatch 'Остаток на конец' =  DATA LOCAL NUMERIC[14,3] (Batch);
sumOutBatch 'Сумма на конец' =  DATA LOCAL NUMERIC[15,3] (Batch);

balanceMoveInBatch 'Приход' =  DATA LOCAL NUMERIC[14,3] (Batch);
sumMoveInBatch 'Сумма прихода' =  DATA LOCAL NUMERIC[15,3] (Batch);

balanceMoveOutBatch 'Расход' =  DATA LOCAL NUMERIC[14,3] (Batch);
sumMoveOutBatch 'Сумма Расхода' =  DATA LOCAL NUMERIC[15,3] (Batch);

lastPriceBatch 'Цена'  = DATA LOCAL NUMERIC[15,3] (Batch);

accountSumContainerStockDateFrom 'Остаток на начало по складу'  = GROUP SUM sumInBatch (bt);

accountSumContainerStockDateTo 'Остаток на конец по складу' = GROUP SUM sumOutBatch(bt);

includeContainerMovementBatchStockDateFromTo 'Рассчитать' = ACTION (stock, dateFrom, dateTo){
    
    inContainerMovementBatch (bt) <- isPostedBatch(bt) AND isContainerBatch(bt);
    
    inContainerMovementBatch (bt) <- NULL WHERE inContainerMovementBatch (bt) AND NOT overMovementContainerBatchStockDateFromTo(bt, stock, dateFrom, dateTo); 
    
    balanceInBatch(bt) <- NULL;
    balanceInBatch(bt) <- balanceBBatchStockDate(bt, stock, dateFrom) WHERE inContainerMovementBatch (bt);
    
    sumInBatch (bt) <- NULL;
    sumInBatch (bt) <- balanceInBatch(bt) * accountPriceBBatchStockDate(bt, stock, dateFrom) WHERE inContainerMovementBatch (bt);
    
    balanceOutBatch (bt) <- NULL;
    balanceOutBatch (bt) <- balanceABatchStockDate(bt, stock, dateTo) WHERE inContainerMovementBatch (bt);
    
    lastPriceBatch(bt) <- NULL;
    lastPriceBatch(bt) <- accountPriceABatchStockDate(bt, stock, dateTo) WHERE inContainerMovementBatch (bt);
    
    lastPriceBatch(bt) <- accountPriceABatchStockDate(bt, stock, 2030_01_01) WHERE inContainerMovementBatch (bt) AND NOT lastPriceBatch(bt);
    
    sumOutBatch (bt) <- NULL;
    sumOutBatch (bt) <- balanceOutBatch(bt) * lastPriceBatch(bt) WHERE inContainerMovementBatch (bt);
    
    balanceMoveInBatch (bt) <- NULL;
    balanceMoveInBatch (bt) <- costInBalanceBatchStockDateFromTo(bt, stock, dateFrom, dateTo) WHERE inContainerMovementBatch (bt);
    
    sumMoveInBatch (bt) <- NULL;
    sumMoveInBatch (bt) <- [= GROUP SUM costSkuLedgerBatch (ledger, batch) * (OVERRIDE lastPriceBatch(batch), accountPriceABatchStockDateTime(batch, stockSkuLedger(ledger), dateTimeSkuLedger(ledger)))
                               IF ledger IS InSkuLedger AND isPostedSkuLedger(ledger)
                               AND dateSkuLedger(ledger) >= dateFrom AND dateSkuLedger(ledger)<= dateTo
                               BY batch, stockSkuLedger(ledger), dateFrom, dateTo](bt, stock, dateFrom, dateTo) WHERE inContainerMovementBatch (bt);
    
    balanceMoveOutBatch (bt) <- NULL;
    balanceMoveOutBatch (bt) <- costOutBalanceBatchStockDateFromTo(bt, stock, dateFrom, dateTo) WHERE inContainerMovementBatch (bt);

    sumMoveOutBatch (bt) <- NULL;
    sumMoveOutBatch (bt) <- [= GROUP SUM costSkuLedgerBatch (ledger, batch) * (OVERRIDE lastPriceBatch(batch), accountPriceABatchStockDateTime(batch, stockSkuLedger(ledger), dateTimeSkuLedger(ledger)))
                                IF ledger IS OutSkuLedger AND isPostedSkuLedger(ledger)
                                AND dateSkuLedger(ledger) >= dateFrom AND dateSkuLedger(ledger)<= dateTo
                                BY batch, stockSkuLedger(ledger), dateFrom, dateTo](bt, stock, dateFrom, dateTo) WHERE inContainerMovementBatch (bt);
 
    formRefresh();
}

balanceInBatches = GROUP SUM balanceInBatch(bt) IF inContainerMovementBatch(bt);
balanceOutBatches = GROUP SUM balanceOutBatch(bt) IF inContainerMovementBatch(bt);

balanceMoveInBatches = GROUP SUM balanceMoveInBatch(bt) IF inContainerMovementBatch(bt);
sumMoveInBatches = GROUP SUM sumMoveInBatch(bt) IF inContainerMovementBatch(bt);
balanceMoveOutBatches = GROUP SUM balanceMoveOutBatch(bt) IF inContainerMovementBatch(bt);
sumMoveOutBatches = GROUP SUM sumMoveOutBatch(bt) IF inContainerMovementBatch(bt);

//--

balanceInSkuPrice = GROUP SUM balanceInBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);
sumInSkuPrice = GROUP SUM sumInBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);

balanceOutSkuPrice = GROUP SUM balanceOutBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);
sumOutSkuPrice = GROUP SUM sumOutBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);

balanceMoveInSkuPrice = GROUP SUM balanceMoveInBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);
sumMoveInSkuPrice = GROUP SUM sumMoveInBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);

balanceMoveOutSkuPrice = GROUP SUM balanceMoveOutBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);
sumMoveOutSkuPrice = GROUP SUM sumMoveOutBatch(bt) IF inContainerMovementBatch(bt) BY skuBatch(bt), lastPriceBatch(bt);


FORM containerMovement 'Отчет по таре'

    OBJECTS params = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)

    OBJECTS dep = DepartmentStore  FIXED PANEL
    PROPERTIES(dep) SELECTOR nameDepartmentStore, nameLegalEntityDepartmentStore  

    OBJECTS bt=Batch

    PROPERTIES READONLY accountSumContainerStockDateFrom(), accountSumContainerStockDateTo()
    PROPERTIES(bt) READONLY idBarcodeSkuBatch, nameSkuBatch, nameSupplierBatch, lastPriceBatch
    PROPERTIES(bt) READONLY balanceInBatch, sumInBatch, balanceOutBatch, sumOutBatch, balanceMoveInBatch, sumMoveInBatch, balanceMoveOutBatch, sumMoveOutBatch
               
    FILTERS inContainerMovementBatch(bt)
    
    ORDER BY nameSkuBatch(bt)

    PROPERTIES includeContainerMovementBatchStockDateFromTo(dep, dFrom, dTo) TOOLBAR TODRAW bt
    
    EVENTS 
        ON CHANGE dFrom includeContainerMovementBatchStockDateFromTo(dep, dFrom, dTo), 
        ON CHANGE dTo includeContainerMovementBatchStockDateFromTo(dep, dFrom, dTo),
        ON CHANGE dep includeContainerMovementBatchStockDateFromTo(dep, dFrom, dTo)
;

DESIGN containerMovement {
    NEW header {
        type = CONTAINERH;
        MOVE params.box {
            type = CONTAINERH;
        }
        MOVE dep.box {
            type = CONTAINERH;
        }
    }

    NEW header2 {
        type = CONTAINERH;
        NEW sums {
            caption = 'Суммы';
            type = CONTAINERH;
            MOVE PROPERTY(accountSumContainerStockDateFrom());
            MOVE PROPERTY(accountSumContainerStockDateTo());
        }
        NEW print {
            type = CONTAINERH;
            caption = 'Печать';
        }
    }

    MOVE bt.box;
    MOVE functions.box;
}

@extendFormFilterStockAccess(dep, containerMovement);

NAVIGATOR {
    stockReports {
        ADD containerMovement;
    }
}