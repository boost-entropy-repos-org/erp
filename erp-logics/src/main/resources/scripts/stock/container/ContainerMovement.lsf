MODULE ContainerMovement;

REQUIRE System,
        Utils,
        Stock,
        Store,
        SkuLedger,
        PriceListLedger;


//------------------------------------Отчет по таре-------------------------------------------//


accountSumContainerStockDateFrom 'Остаток на начало по складу' (stock, dateFrom) = GROUP SUM accountSumBBatchStockDate(batch, stock, dateFrom)
    IF isContainerBatch(batch) BY stock, dateFrom;

accountSumContainerStockDateTo 'Остаток на конец по складу' (stock, dateTo) = GROUP SUM accountSumABatchStockDate(batch, stock, dateTo)
    IF isContainerBatch(batch) BY stock, dateTo;

overMovementContainerBatchStockDateFromTo (batch, stock, dateFrom, dateTo) =
    OVERRIDE balanceBBatchStockDate(batch, stock, dateFrom),
             balanceABatchStockDate(batch, stock, dateTo),
             costInBalanceBatchStockDateFromTo(batch, stock, dateFrom, dateTo),
             costOutBalanceBatchStockDateFromTo(batch, stock, dateFrom, dateTo);

FORM containerMovement 'Отчет по таре'

    OBJECTS params = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)

    OBJECTS dep = DepartmentStore  FIXED PANEL
    PROPERTIES(dep) SELECTOR nameDepartmentStore, nameLegalEntityDepartmentStore

    OBJECTS bt=Batch

    PROPERTIES READONLY accountSumContainerStockDateFrom(dep, dFrom), accountSumContainerStockDateTo(dep, dTo)
    PROPERTIES(bt) READONLY idBarcodeSkuBatch, nameSkuBatch, costBatch
    PROPERTIES(bt, dep, dFrom) READONLY balanceBBatchStockDate, accountSumBBatchStockDate
    PROPERTIES(bt, dep, dTo) READONLY balanceABatchStockDate, accountSumABatchStockDate
    PROPERTIES(bt, dep, dFrom, dTo) READONLY costInBalanceBatchStockDateFromTo, costSumInBalanceBatchStockDateFromTo,
               costOutBalanceBatchStockDateFromTo, costSumOutBalanceBatchStockDateFromTo

    FILTERS isPostedBatch(bt),
            isContainerBatch(bt),
            overMovementContainerBatchStockDateFromTo(bt, dep, dFrom, dTo)!=0
;

DESIGN containerMovement FROM DEFAULT{
    NEW header {
        type = CONTAINERH;
        ADD params.box {
            type = CONTAINERH;
        }
        ADD dep.box {
            type = CONTAINERH;
        }
    }

    NEW header2 {
        type = CONTAINERH;
        NEW sums {
            caption = 'Суммы';
            type = CONTAINERH;
            ADD PROPERTY(accountSumContainerStockDateFrom);
            ADD PROPERTY(accountSumContainerStockDateTo);
        }
        NEW print {
            type = CONTAINERH;
            caption = 'Печать';
        }
    }

    ADD bt.box;
    ADD functions.box;
}

@extendFormFilterStockAccess(DepartmentStore, dep, containerMovement);

NAVIGATOR {
    stockReports {
        ADD containerMovement;
    }
}