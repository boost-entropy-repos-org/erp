MODULE Pack;

REQUIRE System,
        Utils,
        Stock,
        Barcode;

PRIORITY Utils, Stock;

GROUP itemPack 'Упаковка товара' : public;

META definePackSku (caption, NS)
    packBarcodeSku 'Штрихкод для '##caption = DATA Barcode (Sku);
    packBarcode 'Штрихкод для '##caption (barcode)=  packBarcodeSku(skuBarcode(barcode)) == barcode;
    
    CONSTRAINT skuBarcode(packBarcodeSku(item)) != item CHECKED BY packBarcodeSku MESSAGE 'Выбран неверный sku для штрихкода упаковки ('##caption##')';
    
    changePackBarcodeSku = ACTION (barcode, sku) {
        REQUEST BOOLEAN INPUT;
        IF requestedLogical() THEN {
            ASSIGN packBarcodeSku(sku) <- barcode;
        } ELSE
            ASSIGN packBarcodeSku(sku) <- NULL;
    }
    
    EXTEND FORM barcodeDialog
        PROPERTIES (b) READONLY NS.packBarcode
    ;
    EXTEND FORM barcodeSku
        PROPERTIES (b) READONLY NS.packBarcode
    ;
    
    idBarcodePackSku 'Штрихкод упаковки' (sku)= idBarcode(packBarcodeSku(sku));
    shortNameUOMPackSku 'Ед. изм. упаковки' (sku)= shortNameUOMBarcode(packBarcodeSku(sku));
    amountPackSku 'Кол-во в упаковке'(sku) =amountBarcode(packBarcodeSku(sku)) IN itemPack;
    
    changeValuePackSku = ACTION (sku) {
        IF requestedNumeric() THEN {
            IF NOT packBarcodeSku(sku) THEN {
                FOR ADDOBJ b = Barcode DO {
                   ASSIGN skuBarcode(b) <- sku;
                   ASSIGN dataAmountBarcode (b) <- requestedNumeric();
    
                   ASSIGN packBarcodeSku(sku) <- b;
                }
            } ELSE {
                ASSIGN dataAmountBarcode (bb) <- requestedNumeric() WHERE bb == packBarcodeSku(sku);
            }
    
        } ELSE {
            IF packBarcodeSku(sku) THEN {
                DELETE p WHERE p==packBarcodeSku(sku);
            }
        }
    }

    changePackSku = ACTION (sku) {
        REQUEST NUMERIC[14,3] INPUT;
        EXEC changeValuePackSku(sku);
    }
END

// --------------------------- Макрос по добавлению упаковки в документы ---------------------- //

META defineDocumentPack(object, detail, f)

    // Объявляем галочку по, которой будут показываться колонки с упаковкой
    @defineDocumentInterfaceProperty (object, showPack, 'Упаковка');

    // Добавляем в строки штрихкод
    @defineDocumentInterfaceBarcodePrefix (object, sku, , );
    @deriveDocumentDetailBarcodeCustom (user###detail, sku, showPack);

    // Объявляем первичное свойство количество упаковок
    @defineDocumentInterfaceDetailQuantityPrefix(object, pack, ' упаковок');

    // Автоматически проставляем кол-во упаковок из количества
    packQuantityUser###detail(d) <- quantityUser###detail(d) / (amountPackUser###detail(d) IF amountPackUser###detail(d) != 0)
        WHEN CHANGED(quantityUser###detail(d)) OR CHANGED(amountPackUser###detail(d));

    // Объявляем действия по изменению количества в упаковке и количества упаковок
    overChangeAmountPackUser###detail = ABSTRACT ACTION LIST (###detail); 
    changeAmountPackUser###detail = ACTION (detail) {
        REQUEST NUMERIC[14,3] INPUT;
        ASSIGN amountPackUser###detail(detail) <- requestedNumeric();
        ASSIGN quantityUser###detail(detail) <- packQuantityUser###detail(detail)*(OVERRIDE 1.0, amountPackUser###detail(detail));
        EXEC overChangeAmountPackUser###detail(detail);
    }

    changePackQuantityUser###detail = ACTION (detail) {
        REQUEST NUMERIC[14,3] INPUT;
        ASSIGN packQuantityUser###detail(detail) <- requestedNumeric();
        ASSIGN quantityUser###detail(detail) <- packQuantityUser###detail(detail)*(OVERRIDE 1.0, amountPackUser###detail(detail));
    }

    // Расширяем формы
    EXTEND FORM user###object
        PROPERTIES (f) showPackUser###object
        PROPERTIES (d) SHOWIF showPackUser###object(f) BEFORE quantityUser###detail(d)
                       idBarcodePackUser###detail, shortNameUOMPackUser###detail,
                       amountPackUser###detail ON CHANGE changeAmountPackUser###detail(d), packQuantityUser###detail ON CHANGE changePackQuantityUser###detail(d)
    ;

    EXTEND FORM object##s
        PROPERTIES(d) READONLY SHOWIF showPack###object(f) BEFORE quantity###detail(d)
                      idBarcodePack###detail, shortNameUOMPack###detail,
                      amountPack###detail, packQuantity###detail
    ;

    // Добавляем в копирование инвойсов
    overCopy###object(s, d) += ACTION(s, d) {
        ASSIGN showPackUser###object(d) <- showPack###object(s);
    }
END

META defineDocumentPack(object, f)
    @defineDocumentPack(object, object##Detail, f);
END

// --------------------------- Макрос по добавлению упаковки в документы (подбор по нескольким складам) ---------------------- //

META defineDocumentPackSkuStock(object, detail, skuProp, stockProp)
    packQuantitySku###object###stockProp###stock 'Кол-во упаковок в документе' (sku, object, stock) = GROUP SUM packQuantity###detail(detail)
        BY skuProp###detail(detail), object###detail(detail), stockProp###stock###detail(detail);

    changePackQuantitySku###object###Stock = ACTION (sku, object, stock) {
        REQUEST NUMERIC[14,3] INPUT;
        IF detail###Sku###object###stockProp###stock(sku, object, stock) THEN {
            IF requestedNumeric() THEN {
                ASSIGN packQuantity###detail(detail) <- requestedNumeric() WHERE detail == detail###Sku###object###stockProp###stock(sku, object, stock);
                ASSIGN quantity###detail(detail) <- packQuantity###detail(detail)*(OVERRIDE 1.0, amountPack###detail(detail)) WHERE detail == detail###Sku###object###stockProp###stock(sku, object, stock);
            } ELSE {
                FOR detail###Sku###object###stockProp###stock(sku, object, stock) == d DO
                    DELETE d;
            }
        } ELSE {
            IF requestedNumeric() THEN  {
                FOR ADDOBJ d = ###detail DO {
                   object###detail(d) <- object;
//                 Пока отключим работу с несколькими складами
//                 data###stockProp###stock###detail (d) <- stock;
                   skuProp###detail(d) <- sku;
                   packQuantity###detail (d) <- requestedNumeric();
                   quantity###detail(d) <- packQuantity###detail(d) * (OVERRIDE 1.0, amountPack###detail(d));
                   shipmentDataDate###detail (d) <- shipmentDate###object(object);
                   shipmentDataTime###detail (d) <- shipmentTime###object(object);
                }
            }
        }
    }

    changeAmountPackSku###object###Stock = ACTION (sku, object, stock) {
        REQUEST NUMERIC[14,3] INPUT;
        IF detail###Sku###object###stockProp###stock(sku, object, stock) THEN {
            IF requestedNumeric() THEN {
                ASSIGN amountPack###detail(detail) <- requestedNumeric() WHERE detail == detail###Sku###object###stockProp###stock(sku, object, stock);
                ASSIGN quantity###detail(detail) <- packQuantity###detail(detail)*(OVERRIDE 1.0, amountPack###detail(detail)) WHERE detail == detail###Sku###object###stockProp###stock(sku, object, stock) AND packQuantity###detail(detail);
            } ELSE {
                ASSIGN amountPack###detail(detail) <- NULL WHERE detail == detail###Sku###object###stockProp###stock(sku, object, stock);
            }

        }
        EXEC changeValuePackSku(sku);
    }

END
META defineDocumentPackSkuStock(object, skuProp, stockProp)
    @defineDocumentPackSkuStock(object, object##Detail, skuProp, stockProp);
END


META extendFormDocumentPackSkuStock(object, form, concrete, stockProp)
    EXTEND FORM form
        PROPERTIES SHOWIF showPack###object(concrete) BEFORE quantitySku###object###stockProp###stock(ks, concrete, st)
//                   idBarcodePackSku(ks) READONLY, shortNameUOMPackSku(ks) READONLY,
                   amountPackSku(ks) ON CHANGE changeAmountPackSku###object###Stock(ks, concrete, st),
                   packQuantitySku###object###stockProp###stock(ks, concrete, st) ON CHANGE changePackQuantitySku###object###stock(ks, concrete, st)
    ;
    DESIGN form {
        PROPERTY(packQuantitySku###object###stockProp###stock(ks, concrete, st)) { background = #FFBC02;}  //9AFC9A
    }
        
    
END

// --------------------------- Макрос по добавлению упаковки в документы (подбор по одному складу ) ---------------------- //

META defineDocumentPackSku(object, detail, skuProp, stockProp)
    packQuantitySku###object 'Кол-во упаковок в документе' (sku, object) = GROUP SUM packQuantity###detail(detail)
        BY skuProp###detail(detail), object###detail(detail);

    changePackQuantitySku###object = ACTION (sku, object) {
        REQUEST NUMERIC[14,3] INPUT;
        IF detail###Sku###object(sku, object) THEN {
            IF requestedNumeric() THEN {
                packQuantity###detail(detail) <- requestedNumeric() WHERE detail == detail###Sku###object(sku, object);
                quantity###detail(detail) <- packQuantity###detail(detail)*(OVERRIDE 1.0, amountPack###detail(detail)) WHERE detail == detail###Sku###object(sku, object);
            } ELSE {
                FOR detail###Sku###object(sku, object) == d DO 
                    DELETE d;
            }
        } ELSE {
            IF requestedNumeric() THEN
                FOR ADDOBJ d = ###detail DO {
                    object###detail(d) <- object;
                    skuProp###detail(d) <- sku;
                    packQuantity###detail (d) <- requestedNumeric();
                    quantity###detail(d) <- packQuantity###detail(d) * (OVERRIDE 1.0, amountPack###detail(d));
                }
        }
    }
    changeAmountPackSku###object = ACTION (sku, object) {
        REQUEST NUMERIC[14,3] INPUT;
        IF detail###Sku###object(sku, object) THEN {
            IF requestedNumeric() THEN {
                amountPack###detail(detail) <- requestedNumeric() WHERE detail == detail###Sku###object(sku, object);
                quantity###detail(detail) <- packQuantity###detail(detail)*(OVERRIDE 1.0, amountPack###detail(detail)) WHERE detail == detail###Sku###object(sku, object) AND packQuantity###detail(detail);
            } ELSE {
                amountPack###detail(detail) <- NULL WHERE detail == detail###Sku###object(sku, object);
            }

        }
        EXEC changeValuePackSku(sku);
    }

END
META defineDocumentPackSku(object, skuProp, stockProp)
    @defineDocumentPackSku(object, object##Detail, skuProp, stockProp);
END


META extendFormDocumentPackSku(object, form, concrete)
    EXTEND FORM form
        PROPERTIES SHOWIF showPack###object(concrete) BEFORE quantity###object##Detail###sku###object(ks, concrete)
//                   idBarcodePackSku(ks) READONLY, shortNameUOMPackSku(ks) READONLY,
                   amountPackSku(ks) ON CHANGE changeAmountPackSku###object(ks, concrete),
                   packQuantitySku###object(ks, concrete) ON CHANGE changePackQuantitySku###object(ks, concrete)
    ;
    DESIGN form {
        PROPERTY(packQuantitySku###object(ks, concrete)) { background = #FFBC02;}
    }
END
