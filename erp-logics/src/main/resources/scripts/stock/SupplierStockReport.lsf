MODULE SupplierStockReports;

REQUIRE StockReports, SaleLedger, PurchaseLedger, PurchaseShipment;

NAMESPACE StockReports;

inReportStock 'Вкл.' = DATA LOCAL BOOLEAN (Stock);

//-- закупка
quantityPurchaseSkuSupplierStockDateFromTo 'Закуплено за интервал (кол-во)' = GROUP SUM quantityPurchaseSkuStockSupplierStockDateFromTo (sku, stock, supplierStock, dateFrom, dateTo) IF inReportStock(stock)
    BY sku, supplierStock, dateFrom, dateTo;
sumPurchaseSkuSupplierStockDateFromTo 'Закуплено за интервал (сумма с НДС)' = GROUP SUM sumPurchaseSkuStockSupplierStockDateFromTo (sku, stock, supplierStock, dateFrom, dateTo) IF inReportStock(stock)
    BY sku, supplierStock, dateFrom, dateTo;    
supplierSumPurchaseSkuSupplierStockDateFromTo 'Закуплено за интервал (сумма без НДС)' = GROUP SUM 
    (sumPurchaseSkuStockSupplierStockDateFromTo (sku, stock, supplierStock, dateFrom, dateTo) (-)
    sumVATPurchaseSkuStockSupplierStockDateFromTo (sku, stock, supplierStock, dateFrom, dateTo)) IF inReportStock(stock)
        BY sku, supplierStock, dateFrom, dateTo;       
averagePricePurchaseSkuSupplierStockDateFromTo 'Средняя цена закупки с НДС' =  sumPurchaseSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) /
    (quantityPurchaseSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) IF quantityPurchaseSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) !=0);         

quantityPurchaseSupplierStockDateFromTo 'Закуплено за интервал (кол-во)' = GROUP SUM quantityPurchaseSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;
sumPurchaseSupplierStockDateFromTo 'Закуплено за интервал (сумма с НДС)' = GROUP SUM sumPurchaseSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;
supplierSumPurchaseSupplierStockDateFromTo 'Закуплено за интервал (сумма без НДС)' = GROUP SUM supplierSumPurchaseSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;

//-- реализация         
quantitySoldSkuSupplierStockDateFromTo 'Реализация (кол-во)' (sku, supplierStock, dateFrom, dateTo)= GROUP SUM 
    quantitySoldBatchStockDateFromTo(batch, stock, dateFrom, dateTo) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom, dateTo; 
sumSoldSkuSupplierStockDateFromTo 'Реализация (сумма)' (sku, supplierStock, dateFrom, dateTo)= GROUP SUM 
    sumSoldBatchStockDateFromTo(batch, stock, dateFrom, dateTo) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom, dateTo;  
averagePriceSkuSupplierStockDateFromTo 'Средняя цена реализации' =  sumSoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) /
    (quantitySoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) IF quantitySoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) !=0);

quantitySoldSupplierStockDateFromTo 'Реализация (кол-во)' = GROUP SUM quantitySoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;
sumSoldSupplierStockDateFromTo 'Реализация (сумма)' = GROUP SUM sumSoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;

//-- р-ия в ценах поставщика  
sumSaleSkuSupplierStockDateFromTo 'Продано за интервал (сумма с НДС)' (batch, stock, dateFrom, dateTo)= GROUP SUM 
    quantitySoldBatchStockDate(batch, stock, date)* invoicePriceInvoiceDetailBatch(batch)
    IF date >= dateFrom AND date <= dateTo AND inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom, dateTo;   
supplierSumSaleSkuSupplierStockDateFromTo 'Продано за интервал (сумма без НДС)' (batch, stock, dateFrom, dateTo)= GROUP SUM 
    quantitySoldBatchStockDate(batch, stock, date)* priceInvoiceDetailBatch(batch)
    IF date >= dateFrom AND date <= dateTo AND inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom, dateTo;    
averageSalePriceSkuSupplierStockDateFromTo 'Средняя цена реализации с НДС' =  sumSaleSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) /
    (quantitySoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) IF quantitySoldSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) !=0);        

sumSaleSupplierStockDateFromTo 'Продано за интервал (сумма с НДС)' = GROUP SUM sumSaleSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;
supplierSumSaleSupplierStockDateFromTo 'Продано за интервал (сумма без НДС)' = GROUP SUM supplierSumSaleSkuSupplierStockDateFromTo(sku, supplierStock, dateFrom, dateTo) BY supplierStock, dateFrom, dateTo;

// остаток на начало
balanceBSkuSupplierStockDateFrom 'Остаток на начало'  =  GROUP SUM balanceBBatchStockDate(batch, stock, dateFrom) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom;           
sumBSkuSupplierStockDateFrom 'Сумма с НДС начало'  =  GROUP SUM balanceBBatchStockDate(batch, stock, dateFrom)*invoicePriceInvoiceDetailBatch(batch) IF inReportStock(stock)    
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom;   
supplierSumBSkuSupplierStockDateFrom 'Сумма без НДС начало'  =  GROUP SUM balanceBBatchStockDate(batch, stock, dateFrom)*priceInvoiceDetailBatch(batch) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateFrom;    
                       
balanceBSupplierStockDateFrom 'Остаток на начало' = GROUP SUM balanceBSkuSupplierStockDateFrom(sku, supplierStock, dateFrom) BY supplierStock, dateFrom;
sumBSupplierStockDateFrom 'Сумма с НДС начало' = GROUP SUM sumBSkuSupplierStockDateFrom(sku, supplierStock, dateFrom) BY supplierStock, dateFrom;                       
supplierSumBSupplierStockDateFrom 'Сумма без НДС начало' = GROUP SUM supplierSumBSkuSupplierStockDateFrom(sku, supplierStock, dateFrom) BY supplierStock, dateFrom;   

// остаток на конец
balanceASkuSupplierStockDateTo'Остаток на конец'  =  GROUP SUM balanceABatchStockDate(batch, stock, dateTo) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateTo;           
sumASkuSupplierStockDateTo 'Сумма с НДС конец'  =  GROUP SUM balanceABatchStockDate(batch, stock, dateTo)*invoicePriceInvoiceDetailBatch(batch) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateTo;   
supplierSumASkuSupplierStockDateTo 'Сумма без НДС конец'  =  GROUP SUM balanceABatchStockDate(batch, stock, dateTo)*priceInvoiceDetailBatch(batch) IF inReportStock(stock)
        BY skuBatch(batch), supplierStockBatch(batch), dateTo; 

balanceASupplierStockDateTo 'Остаток на конец' = GROUP SUM balanceASkuSupplierStockDateTo(sku, supplierStock, dateTo) BY supplierStock, dateTo;
sumASupplierStockDateTo 'Сумма с НДС конец' = GROUP SUM sumASkuSupplierStockDateTo(sku, supplierStock, dateTo) BY supplierStock, dateTo;                       
supplierSumASupplierStockDateTo 'Сумма без НДС конец' = GROUP SUM supplierSumASkuSupplierStockDateTo(sku, supplierStock, dateTo) BY supplierStock, dateTo;   
       
stocksReport 'Склады' () = GROUP CONCAT nameStock(stock) IF inReportStock(stock) , ', '                                                 
                                                 ORDER stock MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;
        
FORM supplierStockReportPrint 'По поставщикам'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)    
    
    OBJECTS st = Stock FIXED PANEL
    PROPERTIES (st) SELECTOR nameStock, fullNameStock, nameLegalEntityStock, fullNameLegalEntityStock
    FILTERS isSupplierStock(st)
    PROPERTIES  TODRAW st stocksReport(), currentDate()
    
    OBJECTS sk = Sku  
    PROPERTIES(sk) idSku, idBarcodeSku, nameSku, shortNameUOMSku

    PROPERTIES (sk,st,dFrom,dTo) quantityPurchaseSkuSupplierStockDateFromTo, sumPurchaseSkuSupplierStockDateFromTo,
                supplierSumPurchaseSkuSupplierStockDateFromTo,averagePricePurchaseSkuSupplierStockDateFromTo, 
                quantitySoldSkuSupplierStockDateFromTo, sumSoldSkuSupplierStockDateFromTo, averagePriceSkuSupplierStockDateFromTo,
                sumSaleSkuSupplierStockDateFromTo, supplierSumSaleSkuSupplierStockDateFromTo, averageSalePriceSkuSupplierStockDateFromTo
    PROPERTIES (sk,st,dFrom) balanceBSkuSupplierStockDateFrom, sumBSkuSupplierStockDateFrom, supplierSumBSkuSupplierStockDateFrom 
    PROPERTIES (sk,st,dTo) balanceASkuSupplierStockDateTo, sumASkuSupplierStockDateTo, supplierSumASkuSupplierStockDateTo          
    
    PROPERTIES(st,dFrom,dTo) quantityPurchaseSupplierStockDateFromTo,sumPurchaseSupplierStockDateFromTo, supplierSumPurchaseSupplierStockDateFromTo,
                quantitySoldSupplierStockDateFromTo,sumSoldSupplierStockDateFromTo,
                sumSaleSupplierStockDateFromTo, supplierSumSaleSupplierStockDateFromTo
    PROPERTIES(st,dFrom) balanceBSupplierStockDateFrom,  sumBSupplierStockDateFrom,  supplierSumBSupplierStockDateFrom        
    PROPERTIES(st,dTo) balanceASupplierStockDateTo,  sumASupplierStockDateTo,  supplierSumASupplierStockDateTo              
    
    FILTERS quantityPurchaseSkuSupplierStockDateFromTo(sk,st,dFrom,dTo) OR 
            quantitySoldSkuSupplierStockDateFromTo(sk,st,dFrom,dTo) OR 
            balanceBSkuSupplierStockDateFrom(sk,st,dFrom) OR
            balanceASkuSupplierStockDateTo(sk,st,dTo)
;
supplierStockReportPrintXLS 'Отчет по поставщику (xls)' (st,dFrom,dTo) = ACTION (st,dFrom,dTo) {
    FORM supplierStockReportPrint OBJECTS st=st, dFrom = dFrom, dTo=dTo PRINT XLS;
    inReportStock(stock) <- NULL;
}IMAGE 'print.png' IN print;    
supplierStockReportPrintXLSX 'Отчет по поставщику (xlsx)' (st,dFrom,dTo) = ACTION (st,dFrom,dTo) {
    FORM supplierStockReportPrint OBJECTS st=st, dFrom = dFrom, dTo=dTo PRINT XLSX;
    inReportStock(stock) <- NULL;
}IMAGE 'print.png' IN print;    

FORM supplierStockReport 'По поставщикам'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)    
    
    OBJECTS sup=Stock FIXED PANEL // склад поставщика
    PROPERTIES (sup) SELECTOR nameStock
    FILTERS isSupplierStock(sup)

    
    TREE stockGroupTree sg = StockGroup PARENT parentStockGroup
    PROPERTIES READONLY nameStockGroup(sg)   

    
    OBJECTS st = Stock FIXED GRID
    PROPERTIES (st) READONLY nameStock, idStock SHOWIF showIDs()
    PROPERTIES (st) inReportStock
    FILTERS isParentStockGroupStockGroup(stockGroupStock(st), sg) OR
            st IS Stock AND NOT sg IS StockGroup OR
            st IS Stock AND sg IS StockGroup AND NOT stockGroupStock(st)   
    FILTERS isCompanyStock(st)
    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(st) 'F10' DEFAULT

    PROPERTIES(sup,dFrom,dTo) supplierStockReportPrintXLS, supplierStockReportPrintXLSX
;
DESIGN supplierStockReport {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box;
        MOVE sup.box {caption = 'Поставщик';}
        MOVE sup.print;
    }
    NEW header {
        fill = 1;
        type = SPLITH;
        MOVE stockGroupTree.tree.box;
        MOVE st.box;
    }
    MOVE functions.box;
}
NAVIGATOR {
    stockReports{
        ADD supplierStockReport;
    }
}