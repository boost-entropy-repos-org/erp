MODULE Warehouse;

REQUIRE System,
        Stock,
        Hierarchy,
        WriteOff,
        LegalEntity;

// ----------------------------------------- Группы складов ------------------------------ //
CLASS WarehouseGroup 'Группа складов' : StockGroup;
TABLE warehouseGroup (WarehouseGroup);

@defineExternalizable(warehouseGroup, STRING[100]);

nameWarehouseGroup 'Наименование' = DATA ISTRING[100](WarehouseGroup);

nameStockGroup(group) += nameWarehouseGroup(group) IF group IS WarehouseGroup;

TABLE warehouseGroupWarehouseGroup(WarehouseGroup, WarehouseGroup);
@defineHierarchy(warehouseGroup);
parentStockGroup (stockGroup) += parentWarehouseGroup (stockGroup);

FORM warehouseGroup 'Группа складов'

    OBJECTS w = WarehouseGroup FIXED PANEL
    PROPERTIES(w) nameWarehouseGroup, nameParentWarehouseGroup

    EDIT WarehouseGroup OBJECT w
;

FORM warehouseGroups 'Группы складов'

    TREE warehouseGroupTree wg = WarehouseGroup PARENT parentWarehouseGroup
    PROPERTIES READONLY wgTreeName = nameWarehouseGroup(wg)
    PROPERTIES(wg) ADDFORM, EDITFORM, DELETE
    ORDER BY nameWarehouseGroup(wg)

    DIALOG WarehouseGroup OBJECT wg
;

// ----------------------------------------- Склады ------------------------------ //

CLASS Warehouse 'Склад' : Stock;
TABLE warehouse(Warehouse);

@defineExternalizable(warehouse, STRING[100]);
idStock (s) += idWarehouse(s);

nameWarehouse 'Наименование' = DATA ISTRING[150](Warehouse) INDEXED;

nameStock(stock) += nameWarehouse(stock) IF stock IS Warehouse;

warehouseGroupWarehouse (warehouse) = DATA WarehouseGroup (Warehouse) INDEXED AUTOSET;
nameWarehouseGroupWarehouse 'Группа складов' (warehouse) = nameWarehouseGroup(warehouseGroupWarehouse(warehouse)) IN base MINCHARWIDTH 20 PREFCHARWIDTH 30;
isParentWarehouseGroupWarehouse (warehouseGroup, warehouse) =
    isParentWarehouseGroupWarehouseGroup(warehouseGroupWarehouse(warehouse), warehouseGroup) PERSISTENT;
stockGroupStock (stock) += warehouseGroupWarehouse (stock);

legalEntityWarehouse (warehouse) = DATA LegalEntity (Warehouse) INDEXED NOT NULL;
nameLegalEntityWarehouse 'Организация' (warehouse) = nameLegalEntity(legalEntityWarehouse(warehouse)) IN base MINCHARWIDTH 20 PREFCHARWIDTH 30;
legalEntityStock (stock) += legalEntityWarehouse(stock);

userLegalEntityWarehouse 'Отм.' = DATA BOOLEAN (LegalEntity, Warehouse);
userLegalEntityStock(legalEntity, stock) += userLegalEntityWarehouse(legalEntity, stock);

addressWarehouse 'Адрес' (warehouse) = DATA STRING[100] (Warehouse);
addressStock(stock) += addressWarehouse(stock);

latitudeWarehouse 'Координата X' = DATA NUMERIC[10,5](Warehouse);
longitudeWarehouse 'Координата Y' = DATA NUMERIC[10,5](Warehouse);

latitudePOI (warehouse) += latitudeWarehouse(warehouse);
longitudePOI (warehouse) += longitudeWarehouse(warehouse);

explicitBatchLedgerWarehouse 'Партионный учет' = DATA BOOLEAN (Warehouse);
explicitBatchLedgerStock(stock) += explicitBatchLedgerWarehouse(stock);

quantityDaysCloseWarehouse 'Срок автоматического закрытия заказов' = DATA INTEGER(Warehouse);
quantityDaysCloseOrdersStock(stock) += quantityDaysCloseWarehouse(stock);

regionWarehouse = DATA Region (Warehouse);
regionStock(warehouse) += regionWarehouse(warehouse);

// -------------------------------------------------- Формы ----------------------------------------- //

FORM warehouse 'Склад'

    OBJECTS           w=Warehouse FIXED PANEL
    PROPERTIES(w)     nameWarehouse, idWarehouse SHOWIF showIDs(), nameWarehouseGroupWarehouse, addressWarehouse, nameLegalEntityWarehouse, nameWriteOffCommitteeStock,
                      explicitBatchLedgerWarehouse, latitudeWarehouse, longitudeWarehouse, showOnMapPOI, quantityDaysCloseWarehouse, nameRegionStock
    ORDER BY          nameWarehouse

    OBJECTS           e = Employee
    PROPERTIES(e)     READONLY firstNameContact, lastNameContact
    PROPERTIES(e)     ADDSESSIONFORM, EDITSESSIONFORM, DELETESESSION
    FILTERS           inStockEmployee(w, e)

    EDIT Warehouse OBJECT w
;
DESIGN warehouse FROM DEFAULT{
    main {
        NEW headerParams {
            childConstraints = TO THE RIGHT;
            NEW headerParamsLeft {
                childConstraints = TO THE BOTTOM;
                NEW headerMainParams {
                    caption = 'Основные параметры';
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY(nameWarehouse);
                    ADD PROPERTY(idWarehouse);
                    ADD PROPERTY(nameWarehouseGroupWarehouse);
                    ADD PROPERTY(nameLegalEntityWarehouse);
                }
                NEW headerLocationParams {
                    caption = 'Координаты';
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY(addressWarehouse);
                    ADD PROPERTY(latitudeWarehouse);
                    ADD PROPERTY(longitudeWarehouse);
                    ADD PROPERTY(showOnMapPOI);
                }
                NEW headerExtraParams {
                    caption = 'Дополнительные параметры';
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY(explicitBatchLedgerWarehouse);
                    ADD PROPERTY(nameRegionStock);
                    ADD PROPERTY(quantityDaysCloseWarehouse);
                }
            }
            NEW headerParamsRight {
                childConstraints = TO THE BOTTOM;
                ADD w.committeeGroup;
            }
        }
        ADD e.box;
        ADD functions.box;
    }
}

editWarehouse 'Редактировать' = ACTION EDITFORM Warehouse;
editStock(stock) += editWarehouse(stock);

editSessionWarehouse 'Редактировать' = ACTION EDITFORM SESSION Warehouse;
editSessionStock(stock) += editSessionWarehouse(stock);

FORM warehouses 'Склады'

    TREE warehouseTree b=STRING[3], wg = WarehouseGroup PARENT parentWarehouseGroup
    PROPERTIES READONLY OBJVALUE(b), wgTreeName = nameWarehouseGroup(wg)
    PROPERTIES(wg) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    ORDER BY wgTreeName
    FILTERS stringEqualsAll(b)

    OBJECTS w=Warehouse
    PROPERTIES(w) READONLYIF isReadonly() nameWarehouse, idWarehouse SHOWIF showIDs(), nameWarehouseGroupWarehouse, addressWarehouse, nameLegalEntityWarehouse, nameRegionStock, latitudePOI, longitudePOI
    PROPERTIES(w) ADDFORM, EDITFORM, deletew=DELETE
    ORDER BY nameWarehouse
    FILTERS isParentWarehouseGroupWarehouse(wg, w) OR (w IS Warehouse AND wg IS WarehouseGroup AND NOT warehouseGroupWarehouse(w)) OR (w IS Warehouse AND NOT wg)

    DIALOG Warehouse OBJECT w
;
//@extendFormFilterStockAccess(Warehouse, w, warehouses);
//@extendFormFilterStockGroupAccess(WarehouseGroup, wg, warehouses);

DESIGN warehouses FROM DEFAULT{

    NEW mainContainer{
        type = SPLITH;
        childConstraints = TO THE RIGHT;
        ADD warehouseTree.tree.box;
        ADD w.box {
            fillHorizontal = 2.0;
            w.grid {
                defaultComponent = TRUE;
            }
        }
    }
    ADD functions.box;
}
@extendFormEditable(warehouses);

addWarehouseLegalEntity 'Добавить склад' = ACTION (legalEntity) {

    FOR ADDOBJ wr = Warehouse DO {
        ASSIGN legalEntityWarehouse(wr) <- legalEntity;
        FORM warehouse OBJECTS w = wr DOCKEDMODAL;
    }
}

EXTEND FORM legalEntity

    PROPERTIES addWarehouseLegalEntity(l) TODRAW st FORCE PANEL TOOLBAR
;

NAVIGATOR {
    stockMasterData {
        ADD warehouses;
        ADD warehouseGroups;
    }
}