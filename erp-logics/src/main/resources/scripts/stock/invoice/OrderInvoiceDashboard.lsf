MODULE OrderInvoiceDashboard;

REQUIRE Dashboard, OrderInvoice;

NAMESPACE Invoice;

excludeFilterInvoicedOrder = ABSTRACT BOOLEAN (Order);
filterInvoiced = TRUE IF toInvoiceOrder(o) AND NOT excludeFilterInvoicedOrder(o);  
backgroundOrder = ABSTRACT CASE COLOR (Order);

FORM orderInvoiceDashboard 'Приемка по заказам' AUTOREFRESH 60

    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) date = OBJVALUE
    
    OBJECTS o=Order FIXED GRID
    PROPERTIES (o) selectedOrder
    PROPERTIES (o) READONLY BACKGROUND backgroundOrder(o) isPostedOrder, numberOrder, seriesOrder, dateOrder, nameFromOrder, nameFromStockOrder, nameToOrder, nameToStockOrder, 
                   nameOperationOrder, quantityOrderDetailOrder, sumOrderDetailOrder, VATSumOrderDetailOrder, invoiceSumOrderDetailOrder, noteOrder,
                   shipmentDateOrder, shipmentTimeOrder, objectClassName    
    FILTERS inOrder(o), isPostedOrder(o)
    FILTERGROUP filters FILTER 'На дату' shipmentDateOrder(o) <= d 'F5' DEFAULT
    FILTERGROUP filterInvoiced 
        FILTER 'Не оприходованы' filterInvoiced(o) 'F7' DEFAULT
        FILTER 'Оприходованы' NOT filterInvoiced(o) 'F6'
    
    OBJECTS od=OrderDetail FIXED GRID
    PROPERTIES (od) READONLY indexOrderDetail, idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail,
                    packQuantityOrderDetail
    PROPERTIES (od) READONLY quantityOrderDetail, priceOrderDetail, sumOrderDetail,
                   valueVATOrderDetail, VATSumOrderDetail, invoiceSumOrderDetail,
                   nameSupplierStockOrderDetail, shipmentDateOrderDetail, shipmentTimeOrderDetail, toInvoiceOrderDetail  
    FILTERS orderOrderDetail(od)==o
    ORDER BY indexOrderDetail(od)
    
    OBJECTS i=Invoice FIXED GRID
    PROPERTIES (i) READONLY isPostedInvoice, numberInvoice, seriesInvoice, dateInvoice, timeInvoice,                                 
                   nameFromInvoice, nameFromStockInvoice, nameToInvoice, nameToStockInvoice,
                   nameCurrencyInvoice, countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                   VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice

    PROPERTIES (i) editInvoice SHOWIF overShowEditInvoice(i)
    PROPERTIES     deletei=DELETE(i) FORCE PANEL TOOLBAR SHOWIF overShowDeleteInvoice(i)  
    FILTERS inInvoice(i)
    FILTERGROUP filters1 FILTER 'На дату' dateInvoice(i) == d 'F5' DEFAULT
    
    PROPERTIES(o) createMultiUserInvoice FORCE PANEL TOOLBAR 
;

DESIGN orderInvoiceDashboard {
    main{

        NEW dash BEFORE functions.box{
            fill = 1;
            type = SPLITV;
            NEW header {
                fill = 1;                         
                MOVE o.box;

            }
            NEW pickings {
                fill = 1;
                type = TABBED;
                MOVE i.box {
                    i.panel {
                        type = CONTAINERH;
                    }
                }
                MOVE od.box {
                    caption = 'Детализация заказа';
                }
            }
        }          
        NEW secondContainer BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            MOVE PROPERTY(date);
        }
        PROPERTY(packQuantityOrderDetail(od)) { background = #D4FFD4; }
        PROPERTY(quantityOrderDetail(od)) { background = #D4FFD4; }
        PROPERTY(priceOrderDetail(od)) { background = #D4FFD4; }
    }
}
 
@defineFilterIsOpened (order, orderInvoiceDashboard, o);
@defineFilterIsOpened (invoice, orderInvoiceDashboard, i);
@extendFormFilterAccessStock(Order, o, orderInvoiceDashboard, Order.toStock, company);
@extendFormFilterAccessStock(Invoice, i, orderInvoiceDashboard, Invoice.toStock, company);
@extendFormFilterRoleAccessNS(order, o, orderInvoiceDashboard, Operation); 
@extendFormFilterRoleAccessNS(invoice, i, orderInvoiceDashboard, Operation); 
NAVIGATOR {
    purchaseDashboardNavigator {
        ADD orderInvoiceDashboard;
    }
}


