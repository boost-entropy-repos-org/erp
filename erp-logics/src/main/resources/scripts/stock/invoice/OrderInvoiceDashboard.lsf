MODULE OrderInvoiceDashboard;

REQUIRE Dashboard, OrderInvoice;

NAMESPACE Invoice;

FORM orderInvoiceDashboard 'Приемка по заказам' AUTOREFRESH 60

    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) date = OBJVALUE
    
    OBJECTS o=Order FIXED GRID
    PROPERTIES (o) selectedOrder
    PROPERTIES (o) READONLY isPostedOrder, numberOrder, seriesOrder, dateOrder, nameFromOrder, nameFromStockOrder, nameToOrder, nameToStockOrder, 
                   nameOperationOrder, quantityOrderDetailOrder, sumOrderDetailOrder, VATSumOrderDetailOrder, invoiceSumOrderDetailOrder, noteOrder,
                   shipmentDateOrder, shipmentTimeOrder, objectClassName    
    FILTERS inOrder(o), isPostedOrder(o)
    FILTERGROUP filters FILTER 'Сегодня' 'F5' shipmentDateOrder(o) <= d DEFAULT
    FILTERGROUP filterInvoiced FILTER 'Не оприходованы' 'F5' toInvoiceOrder(o) DEFAULT
    
    OBJECTS od=OrderDetail FIXED GRID
    PROPERTIES (od) READONLY indexOrderDetail, idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail,
                    packQuantityOrderDetail
    PROPERTIES (od) READONLY quantityOrderDetail, priceOrderDetail, sumOrderDetail,
                   valueVATOrderDetail, VATSumOrderDetail, invoiceSumOrderDetail,
                   nameSupplierStockOrderDetail, shipmentDateOrderDetail, shipmentTimeOrderDetail, toInvoiceOrderDetail  
    FILTERS orderOrderDetail(od)==o
    
    OBJECTS i=Invoice FIXED GRID
    PROPERTIES (i) READONLY isPostedInvoice, numberInvoice, seriesInvoice, dateInvoice, timeInvoice,                                 
                   nameFromInvoice, nameFromStockInvoice, nameToInvoice, nameToStockInvoice,
                   nameCurrencyInvoice, countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                   VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice

    PROPERTIES (i) editInvoice
    PROPERTIES     deletei=DELETE(i) FORCE PANEL TOOLBAR  
    FILTERS inInvoice(i)
    FILTERGROUP filters1 FILTER 'Сегодня' 'F5' dateInvoice(i) == d DEFAULT
    
    PROPERTIES(o) createMultiUserInvoice FORCE PANEL TOOLBAR 
;

DESIGN orderInvoiceDashboard FROM DEFAULT {
    main{

        NEW dash BEFORE functions.box{
            fill = 1;
            type = SPLITV;
            NEW header {
                fill = 1;                         
                ADD o.box;

            }
            NEW pickings {
                fill = 1;
                type = TABBED;
                ADD i.box;
                ADD od.box {
                    caption = 'Детализация заказа';
                }
            }
        }          
        NEW secondContainer BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            ADD PROPERTY(date);
        }
        PROPERTY(packQuantityOrderDetail(od)) { background = #D4FFD4; }
        PROPERTY(quantityOrderDetail(od)) { background = #D4FFD4; }
        PROPERTY(priceOrderDetail(od)) { background = #D4FFD4; }
    }
}
 
@defineFilterIsOpened (order, orderInvoiceDashboard, o);
@defineFilterIsOpened (invoice, orderInvoiceDashboard, i);
@extendFormFilterAccessStock(Order, o, orderInvoiceDashboard, toStock, company);
@extendFormFilterAccessStock(Invoice, i, orderInvoiceDashboard, toStock, company);

NAVIGATOR {
    purchaseDashboardNavigator {
        ADD orderInvoiceDashboard;
    }
}


