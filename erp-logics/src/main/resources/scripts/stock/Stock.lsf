MODULE Stock;

REQUIRE System, MasterData, Utils, Hierarchy, LegalEntity, I18n, Geo, Employee, Finance, Dashboard;

// ----------------------------------- Склады ------------------------------------------ //
CLASS ABSTRACT StockGroup 'Группа складов';
TABLE stockGroup (StockGroup);

name 'Наименование' = ABSTRACT VARISTRING[100](StockGroup);

TABLE stockGroupStockGroup(StockGroup, StockGroup);
@defineHierarchyAbstract(stockGroup);

@defineHierarchyPlain(stockGroup);

FORM stockGroups 'Группы складов'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS g = StockGroup
    PROPERTIES(g) READONLY name
    ORDER BY name(g)
    FILTERS isParent(g, sg) OR (g IS StockGroup AND NOT sg)

    LIST StockGroup OBJECT g
;

DESIGN stockGroups {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE g.box {
                fill = 2;
                g.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(name(g)){
                    minimumCharWidth = 35;
                    preferredCharWidth = 35;
                }
            }
        }

        MOVE functions.box;
    }
}

CLASS ABSTRACT Stock 'Склад';
TABLE stock(Stock);
TABLE stockDate(Stock, DATE);

@defineExternalizableAbstract(stock, VARSTRING[100]);

name 'Наименование' = ABSTRACT VARISTRING[150](Stock) PERSISTENT INDEXED MINCHARWIDTH 20 PREFCHARWIDTH 30;
phone 'Телефон' = ABSTRACT VARISTRING[100](Stock) PERSISTENT INDEXED MINCHARWIDTH 20 PREFCHARWIDTH 30;
fullName 'Наименование полное' = ABSTRACT VARISTRING[150](Stock) PERSISTENT INDEXED MINCHARWIDTH 20 PREFCHARWIDTH 30;

stockGroup (stock) = ABSTRACT StockGroup (Stock) PERSISTENT INDEXED;
nameStockGroup 'Группа' (Stock stock) = name(stockGroup (stock));

legalEntity (stock) = ABSTRACT LegalEntity (Stock) PERSISTENT INDEXED;
nameLegalEntity 'Компания' (Stock stock) = name(legalEntity(stock));
fullNameLegalEntity 'Компания' (Stock st)= fullName(legalEntity(st));

idLegalEntity 'Компания' (Stock stock) = id(legalEntity(stock));

countStock 'Кол-во складов компании' = GROUP SUM 1 BY legalEntity(Stock stock) PERSISTENT;

quantityDaysCloseOrders 'Срок автоматического закрытия заказов' = ABSTRACT INTEGER(Stock) PERSISTENT;
autoCloseOrders 'Автоматически закрывать заказы' = ABSTRACT BOOLEAN (Stock) PERSISTENT;

isCompany(Stock stock) = isCompany(legalEntity(stock)) PERSISTENT; // PERSISTENT так как у компании складов может быть значительно больше чем у других юрлиц, и так статистика будет правильнее  
isSupplier(Stock stock) = isSupplier(legalEntity(stock));
isCustomer(Stock stock) = isCustomer(legalEntity(stock));
isBuyer(Stock stock) = isBuyer(legalEntity(stock));
isSeller(Stock stock) = isSeller(legalEntity(stock));

notCompany(Stock stock) = NOT isCompany(stock);

legalEntityId (VARSTRING[100] stock)= legalEntity(stock(stock));

countCompanyStock 'Кол-во складов' (stockGroup) = GROUP SUM 1 IF isCompany(Stock stock)
    AND isParent(stockGroup(stock), StockGroup stockGroup)
    BY stockGroup PERSISTENT;

countSupplierStock 'Кол-во складов' (stockGroup) = GROUP SUM 1 IF isSupplier(Stock stock)
    AND isParent(stockGroup(stock), StockGroup stockGroup)
    BY stockGroup PERSISTENT;

countCustomerStock 'Кол-во складов' (stockGroup) = GROUP SUM 1 IF isCustomer(Stock stock)
    AND isParent(stockGroup(stock), StockGroup stockGroup)
    BY stockGroup PERSISTENT;

TABLE userLegalEntityStock (LegalEntity, Stock);
user 'Отм.' = ABSTRACT BOOLEAN (LegalEntity, Stock) PERSISTENT;
in 'Отм.' (LegalEntity legalEntity, Stock stock) = (legalEntity(stock) == legalEntity) OR user(legalEntity, stock);
isDefault 'Свой склад' (LegalEntity legalEntity, Stock stock) = legalEntity(stock) == legalEntity;

default 'По умолч.' = DATA BOOLEAN (Stock); 
dataDefaultStock = GROUP AGGR Stock stock BY legalEntity(stock) WHERE default(stock);

countStocks (legalEntity) = GROUP SUM 1 IF in(LegalEntity legalEntity, Stock stock) BY legalEntity;
minStock 'Первый склад' (legalEntity)= GROUP MIN Stock stock IF in(LegalEntity legalEntity, stock) BY legalEntity;

defaultStock 'Склад по умолчанию' (LegalEntity l) = OVERRIDE minStock(l) IF countStocks(l) == 1, dataDefaultStock(l);

META deriveDocumentLegalEntityDefaultStock(cls, property, form)
    WHEN SESSION FORMS form CHANGED(property(cls o)) AND NOT CHANGED(property##Stock(o)) AND NOT in(property(o), property##Stock(o)) DO {
        property##Stock(o) <- defaultStock(property(o));
    }
END


currency (Stock stock) = currency(legalEntity(stock)) PERSISTENT;
language (Stock stock) = language(legalEntity(stock)) PERSISTENT;

// ------------------------------------ объединение организаций -------------------------- //

replace(LegalEntity l, LegalEntity o) += ACTION {
    legalEntity(Stock stock) <- o WHERE legalEntity(stock) == l;
} 

// ------------------------------------ объединение складов -------------------------- //

in 'Отм.' = DATA LOCAL BOOLEAN (Stock);
countInStock = GROUP SUM 1 IF in(Stock Stock);

FORM confirmReplaceStock 'Объединяемые склады'
    OBJECTS stock = Stock FIXED PANEL 

    OBJECTS s = Stock  
    PROPERTIES(s) READONLY name   
    FILTERS s==stock

    OBJECTS s2 = Stock
    PROPERTIES(s2) in
    PROPERTIES(s2) READONLY name
    FILTERS in(s2)
;

DESIGN confirmReplaceStock {
    REMOVE stock.box;
    NEW splitContainer{
        type = SPLITV;
        fill = 1;
        MOVE s.box{
            caption = 'Основной склад';
        }
        MOVE s2.box{
            caption = 'Удаляемый склад';
        }           
    }
    MOVE functions.box;
}

replace = ACTION ABSTRACT LIST (Stock, Stock);

replace 'Объединить'(Stock stock) = ACTION {   
    DIALOG confirmReplaceStock OBJECTS s = stock DO {    
        FOR in(Stock s) AND stock IS Stock AND NOT isCompany(s) AND NOT isCompany(stock) DO {
            IF s != stock THEN {
                replace(s, stock);
                in(s) <- NULL;
                DELETE s;            
            } ELSE {
                MESSAGE 'Выделенный склад не может совпадать с объединяемым и не должен являться складом собственной компанией';
            }
        }
        apply();
    }
    in(Stock s) <- NULL;       
} CONFIRM;

dataStock = DATA Stock (LegalEntity);
stock(LegalEntity legalEntity) = OVERRIDE [= GROUP MIN Stock stock BY legalEntity(stock)](legalEntity), dataStock(legalEntity) IF active(legalEntity) PERSISTENT;

primaryLegalEntity 'Основной' (Stock stock) = stock(legalEntity(stock)) == stock;
changePrimary(LegalEntity legalEntity, Stock stock) = ACTION {
    REQUEST BOOLEAN INPUT;
    IF requestedLogical() THEN {
        dataStock(legalEntity) <- stock;
    }
}

FORM confirmReplaceStockLegalEntity 'Объединяемые склады'
    OBJECTS l = LegalEntity FIXED PANEL 
    OBJECTS l2 = LegalEntity FIXED PANEL 

    OBJECTS s = Stock  
    PROPERTIES(s) primaryLegalEntity ON CHANGE changePrimary(l, s)   
    PROPERTIES(s) READONLY name    
    FILTERS l==legalEntity(s)

    OBJECTS s2 = Stock
    PROPERTIES(s2) in
    PROPERTIES(s2) READONLY name
    FILTERS in(s2), 
            l2==legalEntity(s2)
;

DESIGN confirmReplaceStockLegalEntity {
    REMOVE l.box;
    REMOVE l2.box;
    NEW splitContainer{
        type = SPLITV;
        fill = 1;
        MOVE s.box{
            caption = 'Основной склад';
        }
        MOVE s2.box{
            caption = 'Удаляемый склад';
        }           
    }
    MOVE functions.box;
}

preReplace(LegalEntity l, LegalEntity o) += ACTION {
    in(Stock s) <- TRUE WHERE legalEntity(s) == l;
    DIALOG confirmReplaceStockLegalEntity OBJECTS l=o, l2=l DO {        
        FOR in(Stock s) AND l==legalEntity(s) AND legalEntity(Stock stock) == o AND primaryLegalEntity(stock) AND NOT isCompany(s) AND NOT isCompany(stock) DO {
            IF s != stock THEN {
                replace(s, stock);
                in(s) <- NULL;
                DELETE s;            
            } ELSE {
                MESSAGE 'Выделенный склад не может совпадать с объединяемым и не должен являться складом собственной компанией';
            }
        }   
    }
    in(Stock s) <- NULL;       
}

EXTEND CLASS Stock : POI;

name[POI](Stock poi) += name(poi) IF poi IS Stock;

country (Stock stock) = country(legalEntity(stock)) PERSISTENT;
nameCountry 'Страна' (Stock stock) = name(country(stock));
nameCountry (Stock poi) += nameCountry(poi);

address 'Адрес' (stock) = ABSTRACT VARISTRING[100] (Stock) PERSISTENT;
mainAddress (Stock poi) += address(poi);

email 'E-mail для заказов' (stock) = ABSTRACT VARSTRING[300] (Stock) PERSISTENT MINCHARWIDTH 30 PREFCHARWIDTH 50;

//latitudeStock 'Координата X' = DATA NUMERIC[10,5](Stock);
//longitudeStock 'Координата Y' = DATA NUMERIC[10,5](Stock);

//latitudePOI(stock) += latitudeStock(stock);
//longitudePOI(stock) += longitudeStock(stock);

TABLE stockGroupStock (StockGroup, Stock);
isParent (StockGroup stockGroup, Stock stock) = isParent(stockGroup(stock), stockGroup) PERSISTENT;

GROUP committee 'Комиссии' : public;
GROUP responsibility 'Ответственные лица' : public;
GROUP bookkeeping 'Учет и цены' : public;

// Неактивный
inactive 'Неактивный' = ABSTRACT BOOLEAN (Stock);
active 'Активный' (Stock st) = st IS Stock AND NOT inactive(st);

// -------------------------------------- Регион ------------------------------- //

CLASS Region 'Регион';
TABLE region (Region);

name 'Наименование' = DATA VARISTRING[100](Region);

region 'Регион' (stock) = ABSTRACT Region (Stock) PERSISTENT;
nameRegion 'Регион' (Stock stock)= name(region(stock)) MINCHARWIDTH 20 PREFCHARWIDTH 30;

region 'Регион' (stockGroup) = ABSTRACT Region (StockGroup) PERSISTENT;
nameRegion 'Регион' (StockGroup stockGroup)= name(region(stockGroup)) MINCHARWIDTH 20 PREFCHARWIDTH 30;

FORM region 'Регион'
    OBJECTS r=Region FIXED PANEL
    PROPERTIES(r) name
    EDIT Region OBJECT r
;

FORM regions 'Регионы'
    OBJECTS r=Region
    PROPERTIES(r) READONLY name
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE FORCE PANEL TOOLBAR
    LIST Region OBJECT r
;
DESIGN regions { main{ preferredSize = (600, 400); } }

// ---------------------------- Действия по редактированию складов ---------------------- //
edit 'Редактировать' = ACTION ABSTRACT LIST (Stock) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;
editSession 'Редактировать' = ACTION ABSTRACT LIST (Stock) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;

// ----------------------------------------- Формы --------------------------------- //

FORM stocks 'Склады'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address, email
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg)

    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT    

    LIST Stock OBJECT s
;

DESIGN stocks {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(stockName){
                    minimumCharWidth = 35;
                    preferredCharWidth = 35;
                }
            }
        }

        MOVE functions.box;
    }
}

// ---------------------------------------------- Макросы для документов ------------------------------------------------- //

// Склады
META defineDocumentHeaderStock (object, stockClass, stockCaption, prefix)
    @defineDocumentHeaderStockInner (object, stockClass, ###stockClass, stockCaption, prefix);
END

META defineDocumentHeaderStockInner (object, stockClass, stockClassUpper, stockCaption, prefix)
    prefix###stockClass = DATA stockClassUpper(###object);
    name###prefix###stockClass stockCaption (###object object) = name(prefix###stockClass(object)) IN documentHeader
            MINCHARWIDTH 20 PREFCHARWIDTH 20;
    fullName###prefix###stockClass stockCaption (###object object) = fullName(prefix###stockClass(object)) IN documentHeader
            MINCHARWIDTH 20 PREFCHARWIDTH 20;
    replace(###stockClass s, ###stockClass o) += ACTION {ASSIGN prefix###stockClass(###object object) <- o WHERE prefix###stockClass(object) == s;}             
            
    prefix###legalEntityStock (###object object) = legalEntity(prefix###stockClass(object));
    name###prefix##LegalEntityStock 'Компания ('###stockCaption###')' (###object object) = name(prefix###legalEntityStock (object));
    fullName###prefix##LegalEntityStock 'Компания ('###stockCaption###') полное наим-ие' (###object object) = fullName(prefix###legalEntityStock (object));       
    address###prefix##LegalEntityStock 'Адрес компании ('###stockCaption###')' (###object object) = address(prefix###legalEntityStock (object));

    address###prefix##Stock 'Адрес ('###stockCaption###')' (###object object) = address(prefix###stockClass(object));

    prefix###country (###object object) = country(prefix###stockClass(object));
END

META defineDocumentAbstractHeaderStock (object, stockClass, stockCaption, prefix)
    @defineDocumentAbstractHeaderStockInner(object, stockClass, ###stockClass, stockCaption, prefix);
END

META defineDocumentAbstractHeaderStockInner (object, stockClass, stockClassUpper, stockCaption, prefix)
    prefix###stockClass = ABSTRACT stockClassUpper(###object) PERSISTENT INDEXED;
    name###prefix###stockClass stockCaption (###object object) = name(prefix###stockClass(object)) IN documentHeader
            MINCHARWIDTH 20 PREFCHARWIDTH 20;
    fullName###prefix###stockClass stockCaption (###object object) = fullName(prefix###stockClass(object)) IN documentHeader
            MINCHARWIDTH 20 PREFCHARWIDTH 20;
            
    prefix###legalEntityStock (###object object) = legalEntity(prefix###stockClass(object));
    name###prefix##LegalEntityStock 'Компания ('###stockCaption###')' (###object object) = name(prefix###legalEntityStock (object));
    fullName###prefix##LegalEntityStock 'Компания ('###stockCaption###') полное наим-ие' (###object object) = fullName(prefix###legalEntityStock (object));
    address###prefix##LegalEntityStock 'Адрес компании ('###stockCaption###')' (###object object) = address(prefix###legalEntityStock (object));
    address###prefix##Stock 'Адрес ('###stockCaption###')' (###object object) = address(prefix###stockClass(object));

    prefix###country (###object object) = country(prefix###stockClass(object));
END
META defineDocumentInterfaceHeaderStock (object, stockClass, stockCaption, prefix)
    @defineDocumentAbstractHeaderStock (object, stockClass, stockCaption, prefix);
    @defineDocumentHeaderStock (user###object, stockClass, stockCaption, prefix);
    prefix###stockClass (User###object object) += prefix###stockClass(object);
END

META defineDocumentHeaderStock (object, stockClass, stockCaption)
    @defineDocumentHeaderStock(object, stockClass, stockCaption, );
END
META defineDocumentAbstractHeaderStock (object, stockClass, stockCaption)
    @defineDocumentAbstractHeaderStock(object, stockClass, stockCaption, );
END
META defineDocumentInterfaceHeaderStock (object, stockClass, stockCaption)
    @defineDocumentInterfaceHeaderStock(object, stockClass, stockCaption, );
END

META defineDocumentDetailStock (object, detail, stockClass, stockProp, stockCaption)
    stockProp (###detail idetail) = stockProp(object(idetail));
    name###stockProp stockCaption (###detail idetail) = name(stockProp(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;
    fullName###stockProp stockCaption (###detail idetail) = fullName(stockProp(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;    
    country###stockProp (###detail idetail) = country(stockProp(idetail));
    legalEntity###stockProp (###detail idetail) = legalEntity(stockProp(idetail));
    nameLegalEntity###stockProp 'Компания ('###stockCaption###')' (###detail idetail) = name(legalEntity###stockProp (idetail));
    fullNameLegalEntity###stockProp 'Компания ('###stockCaption###') полное наим-ие' (###detail idetail) = fullName(legalEntity###stockProp (idetail));
END
META defineDocumentDetailStock (object, stockProp, stockCaption)
    @defineDocumentDetailStock (object, object##Detail, stock, stockProp, stockCaption);
END

META defineDocumentDetailDataStock (object, detail, stockClass, stockProp, stockCaption)
    @defineDocumentDetailDataStockInner (object, detail, stockClass, ###stockClass, stockProp, stockCaption);
END

META defineDocumentDetailDataStockInner (object, detail, stockClass, stockClassUpper, stockProp, stockCaption)
    stockProp (###detail idetail) = stockProp(object(idetail)) PERSISTENT;
    name###stockProp stockCaption (###detail idetail) = name(stockProp(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;
    fullName###stockProp stockCaption (###detail idetail) = fullName(stockProp(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;         
    country###stockProp (###detail idetail) = country(stockProp(idetail));
    legalEntity###stockProp (###detail idetail) = legalEntity(stockProp(idetail));
    nameLegalEntity###stockProp 'Компания ('###stockCaption###')' (###detail idetail) = name(legalEntity###stockProp (idetail));
    fullNameLegalEntity###stockProp 'Компания ('###stockCaption###') полное наим-ие' (###detail idetail) = fullName(legalEntity###stockProp (idetail));
    
END

META defineDocumentAbstractDetailDataStock (object, detail, stockClass, stockProp, stockCaption)
    @defineDocumentAbstractDetailDataStockInner (object, detail, stockClass, ###stockClass, stockProp, stockCaption);
END

META defineDocumentAbstractDetailDataStockInner (object, detail, stockClass, stockClassUpper, stockProp, stockCaption)
    stockProp (###detail idetail) = stockProp(object(idetail)) PERSISTENT INDEXED;
    name###stockProp stockCaption (###detail idetail) = name(stockProp(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;
    fullName###stockProp stockCaption (###detail idetail) = fullName(stockProp(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;      
    country###stockProp (###detail idetail) = country(stockProp(idetail));
    legalEntity###stockProp (###detail idetail) = legalEntity(stockProp(idetail));
    nameLegalEntity###stockProp 'Компания ('###stockCaption###')' (###detail idetail) = name(legalEntity###stockProp (idetail));
    fullNameLegalEntity###stockProp 'Компания ('###stockCaption###')' (###detail idetail) = fullName(legalEntity###stockProp (idetail));
    
END
META defineDocumentInterfaceDetailDataStock (object, detail, stockClass, stockProp, stockCaption)
    @defineDocumentAbstractDetailDataStock(object, detail, stockClass, stockProp, stockCaption);
    @defineDocumentDetailDataStock(user###object, user###detail, stockClass, stockProp, stockCaption);
END

META defineDocumentDetailDataStock (object, stockClass, stockProp, stockCaption)
    @defineDocumentDetailDataStock (object, object##Detail, stockClass, stockProp, stockCaption);
END
META defineDocumentAbstractDetailDataStock (object, stockClass, stockProp, stockCaption)
    @defineDocumentAbstractDetailDataStock (object, object##Detail, stockClass, stockProp, stockCaption);
END
META defineDocumentInterfaceDetailDataStock (object, stockClass, stockProp, stockCaption)
    @defineDocumentInterfaceDetailDataStock (object, object##Detail, stockClass, stockProp, stockCaption);
END

META defineDocumentStock (object, stockClass, stockCaption, prefix)
    @defineDocumentHeaderStock(object, stockClass, stockCaption, prefix);
    @defineDocumentDetailStock(object, prefix###stockClass, stockCaption);
END
META defineDocumentAbstractStock (object, stockClass, stockCaption, prefix)
    @defineDocumentAbstractHeaderStock(object, stockClass, stockCaption, prefix);
    @defineDocumentDetailStock(object, prefix###stockClass, stockCaption);
END
META defineDocumentInterfaceStock (object, stockClass, stockCaption, prefix)
    @defineDocumentInterfaceHeaderStock(object, stockClass, stockCaption, prefix);
    @defineDocumentDetailStock(object, prefix###stockClass, stockCaption);
    @defineDocumentDetailStock(user###object, prefix###stockClass, stockCaption);
END

META defineDocumentDataStock (object, stockClass, stockCaption, prefix)
    @defineDocumentHeaderStock(object, stockClass, stockCaption, prefix);
    @defineDocumentDetailDataStock(object, stockClass, prefix###stockClass, stockCaption);
END
META defineDocumentAbstractDataStock (object, stockClass, stockCaption, prefix)
    @defineDocumentAbstractHeaderStock(object, stockClass, stockCaption, prefix);
    @defineDocumentAbstractDetailDataStock(object, stockClass, prefix###stockClass, stockCaption);
END
META defineDocumentInterfaceDataStock (object, stockClass, stockCaption, prefix)
    @defineDocumentInterfaceHeaderStock(object, stockClass, stockCaption, prefix);
    @defineDocumentInterfaceDetailDataStock(object, stockClass, prefix###stockClass, stockCaption);
END

META defineDocumentStock (object, stockClass, stockCaption)
    @defineDocumentStock(object, stockClass, stockCaption, );
END
META defineDocumentAbstractStock (object, stockClass, stockCaption)
    @defineDocumentAbstractStock(object, stockClass, stockCaption, );
END
META defineDocumentInterfaceStock (object, stockClass, stockCaption)
    @defineDocumentInterfaceStock(object, stockClass, stockCaption, );
END

META defineDocumentDataStock (object, stockClass, stockCaption)
    @defineDocumentDataStock(object, stockClass, stockCaption, );
END
META defineDocumentAbstractDataStock (object, stockClass, stockCaption)
    @defineDocumentAbstractDataStock(object, stockClass, stockCaption, );
END
META defineDocumentInterfaceDataStock (object, stockClass, stockCaption)
    @defineDocumentInterfaceDataStock(object, stockClass, stockCaption, );
END

// ---------------------------------------------- Агрегированные документы -------------------------------------------- //

// Склады
META defineDocumentAggregationHeaderStockPrefix (primObject, aggrObject, stockClass, stockProp, stockCaption, prefixP, prefixA)
    stockProp###stockClass (###aggrObject object) = stockProp###stockClass(primObject(object)) PERSISTENT;
    name###stockProp###stockClass stockCaption (###aggrObject object) = name(stockProp###stockClass(object)) MINCHARWIDTH 20 PREFCHARWIDTH 40;
    fullName###stockProp###stockClass stockCaption (###aggrObject object) = fullName(stockProp###stockClass(object)) MINCHARWIDTH 20 PREFCHARWIDTH 40;    
END
META defineDocumentAggregationHeaderStock (primObject, aggrObject, stockClass, stockProp, stockCaption)
    @defineDocumentAggregationHeaderStockPrefix (primObject, aggrObject, stockClass, stockProp, stockCaption, , );
END

META defineDocumentAggregationDetailStockPrefix (primObject, aggrObject, stockProp, stockCaption, prefixP, prefixA)
    stockProp (###aggrObject##Detail detail) = stockProp(primObject##Detail(detail)) PERSISTENT;
END
META defineDocumentAggregationDetailStock (primObject, aggrObject, stockProp, stockCaption)
    @defineDocumentAggregationDetailStockPrefix(primObject, aggrObject, stockProp, stockCaption, , );
END

META defineDocumentAggregationStockPrefix (primObject, aggrObject, stockProp, stockCaption, prefixP, prefixA)
    @defineDocumentAggregationHeaderStockPrefix(primObject, aggrObject, stock, stockProp, stockCaption, prefixP, prefixA);
    @defineDocumentAggregationDetailStockPrefix(primObject, aggrObject, stockProp###stock, stockCaption, prefixP, prefixA);
END
META defineDocumentAggregationStock (primObject, aggrObject, stockProp, stockCaption)
    @defineDocumentAggregationStockPrefix(primObject, aggrObject, stockProp, stockCaption, , );
END

// ---------------------------------- Диалоги по выбору складов поставщиков/компаний/покупателей когда есть организация --------------------------- //

FORM stocksCompany 'Собственные склады'
    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isCompany(s)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN stocksCompany {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
            }
        }

        MOVE functions.box;
    }
}

FORM companyStocks 'Склады'
    OBJECTS l = LegalEntity FIXED PANEL

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            in(l,s),
            isCompany(s)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN companyStocks {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
            }
        }

        MOVE functions.box;
    }
}

FORM supplierStocks 'Склады'
    OBJECTS l = LegalEntity FIXED PANEL

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            in(l,s),
            isSupplier(s)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN supplierStocks {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
            }
        }

        MOVE functions.box;
    }
}

FORM customerStocks 'Склады'
    OBJECTS l = LegalEntity FIXED PANEL

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            in(l,s),
            isCustomer(s)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN customerStocks {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(stockName){
                    minimumCharWidth = 40;
                    preferredCharWidth = 40;
                }
            }
        }

        MOVE functions.box;
    }
}

META defineDocumentDialogStock(objectClass, filter, prefix, dataProp)
    change###dataProp###prefix##Stock###filter(###objectClass o) = ACTION {
        DIALOG filter###stocks OBJECTS l = prefix(o), s = dataProp###prefix##Stock(o) INPUT NULL CONSTRAINTFILTER DO {
            dataProp###prefix##Stock(o) <- s;
        }
    }
END
META defineDocumentDialogStock(objectClass, filter, prefixA, prefixB, dataProp)
    change###prefixA##Stock###filter(###objectClass o) = ACTION {
        DIALOG filter###stocks OBJECTS l = filter(o), s = prefixA##Stock(o) INPUT NULL CONSTRAINTFILTER DO {
            prefixA##Stock(o) <- s;
        }
    }
END

META defineDocumentDialogSupplierCustomerStock(objectClass, supplierFilter, customerFilter)
    @defineDocumentDialogStock(objectClass, supplierFilter, supplier, );
    @defineDocumentDialogStock(objectClass, customerFilter, customer, );
END

// -------- Диалоги по выбору складов поставщиков/компаний/покупателей когда нет организация ----------- //

FORM companysStock 'Склады'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isCompany(s)

    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN companysStock {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
            }
        }

        MOVE functions.box;
    }
}

FORM suppliersStock 'Склады'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isSupplier(s)
            
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN suppliersStock {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
            }
        }

        MOVE functions.box;
    }
}

FORM customersStock 'Склады'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, stockName = name, nameLegalEntity, nameCountry, address
    PROPERTIES(s)          edit
    ORDER BY stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isCustomer(s)
      
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT        
;

DESIGN customersStock {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(stockName){
                    minimumCharWidth = 40;
                    preferredCharWidth = 40;
                }
            }
        }

        MOVE functions.box;
    }
}

META defineDocumentDialogStocks(objectClass, stockClass, filter, prefix, dataProp)
    change###stockClass###filter(###objectClass o) = ACTION {
        DIALOG filter##s###stock OBJECTS s = stockClass(o) INPUT NULL CONSTRAINTFILTER DO {
            stockClass(o) <- s;
        }
    }
END

// ------------------------------------------ Расширение формы организаций ---------------------------- //

FORM chooseStocks 'Выбор складов'

    OBJECTS l = LegalEntity FIXED PANEL
    
    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)

    OBJECTS s=Stock
    PROPERTIES(s) READONLY name, address, nameLegalEntity
    PROPERTIES user(l, s)
    ORDER BY name(s)
    FILTERS isParent(sg, s) OR (s IS Stock AND sg IS StockGroup AND NOT stockGroup(s)) OR (s IS Stock AND NOT sg)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT
;

DESIGN chooseStocks {
    MOVE l.box;
    NEW mainContainer{
        fill = 1;
        type = SPLITH;
        MOVE stockTree.tree.box;
        MOVE s.box { fill = 3; }
    }
    MOVE functions.box;
}

chooseStocks 'Выбрать склады'(LegalEntity legalEntity) = ACTION {

    SHOW chooseStocks OBJECTS l = legalEntity ;
}

EXTEND FORM legalEntity
    TREE stockTree b=STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(b), sgTreeName = name(sg)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(b)

    OBJECTS st=Stock
    PROPERTIES(st) in
    PROPERTIES(st) READONLY name, id SHOWIF showIDs(), nameLegalEntity, nameCountry, address
    PROPERTIES(st) FORCE PANEL TOOLBAR SHOWIF isDefault(l,st) editSession, DELETE, replace SHOWIF notCompany(st) 
    PROPERTIES chooseStocks(l) TODRAW st FORCE PANEL TOOLBAR
    FILTERS isParent(sg, st) OR (st IS Stock AND NOT sg)
    FILTERS in(l, st)
    FILTERGROUP inactiveStock FILTER 'Активный' active(st) 'ctrl F10' DEFAULT
    
    OBJECTS s=LegalEntity FIXED GRID
    PROPERTIES(s) READONLY name  SHOWIF isCustomer(l)
    FILTERGROUP sinactiveLegalEntity FILTER 'Активная' active(s) 'shift F10' DEFAULT
    OBJECTS c=LegalEntity FIXED GRID
    PROPERTIES(c) READONLY name SHOWIF isSupplier(l)
    FILTERGROUP cinactiveLegalEntity FILTER 'Активная' active(c) 'shift F10' DEFAULT
;

DESIGN legalEntity{
    extendContainer {
        NEW stockContainer {
            caption = 'Склады';
            type = CONTAINERH;
            type = SPLITH;
            MOVE stockTree.tree.box {
                fill = 1;
                caption = 'Группа складов';
            }
            MOVE st.box {
                fill = 3;
            }
        }

        NEW agreementPurchaseContainer {
            caption = 'Закупка';
            type = SPLITH;
            MOVE s.box { caption = 'Поставщики'; }
            NEW docPurchaseContainer {
                fill = 5;
                type = TABBED;
            }
        }

        NEW agreementSaleContainer {
            caption = 'Продажа';
            type = SPLITH;
            MOVE c.box { caption = 'Покупатели';}
            NEW docSaleContainer {
                fill = 5;
                type = TABBED;
            }
        }
    }
}

EXTEND FORM legalEntities
    PROPERTIES(l) READONLY countStock
;


//----------------------Отображать на формах списков документы, которые не закрыты---------------//
META defineFilterIsOpened (object, form, obj)   
    EXTEND FORM form
        FILTERGROUP object##filters6 FILTER 'Открыт' isOpened(obj) 'F6' DEFAULT
    ;   
END 



NAVIGATOR {
    NEW stockNavigator 'Склад' BEFORE financeNavigator WINDOW toolbar IMAGE '/images/warehouse.png' {
        NEW stockReports 'Отчеты';
        NEW stockMasterData 'Справочники';
    }
}

NAVIGATOR {
    dashboardNavigator {
        NEW stockDashboardNavigator 'Склад' {
        }
    }    
}


EXTEND FORM options

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(a), name(sg)
    ORDER BY name(sg)
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, name, nameLegalEntity
    ORDER BY name(s)
    
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isCompany(s)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT     
    
;

DESIGN options {
    pane {
        NEW stock {
            caption = 'Склад';
            fill = 1;
            NEW stock1 {
                type = COLUMNS;
                columns = 2;
            }
            NEW topContainer {
                type = SPLITH;
                fill = 1;            
                
                MOVE stockTree.tree.box {
                    caption = 'Группы складов';
                }
    
                MOVE s.box {
                    fill = 3;
                    s.grid {
                        defaultComponent = TRUE;
                    }
                }
            }
        }
    }
}

// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultStockGroups 'Загрузить стандартные группы складов' () = ACTION ABSTRACT LIST () IN loadDefault;

@implementLoadDefaultData(loadDefaultStockGroups);

//---------------- Подсветка отмеченных групп ---------------//
//-- С фильтром
META defineBackgroundNearestBoolean(allProp, property, object1, object2, object2Class, nearestProp, filterProp)

    background###allProp 'Цвет' (###object1 o) = RGB(0,0,0) IF allProp(o);
    
    background###property 'Цвет' (###object1 o, ###object2##Group sg) = IF data###property(o, sg) THEN RGB(0,0,0)
        ELSE RGB(230,248,250) 
        IF (OVERRIDE allProp(o) IF sg IS object2Class###group, nearestProp (o, sg));    
        
    background###property 'Цвет' (###object1 o, ###object2 st) = IF data###property(o, st) AND filterProp(st) THEN RGB(0,0,0)
        ELSE RGB(230,248,250) 
        IF property(o, object2##Group(st)) AND filterProp(st);    
END
META defineBackgroundNearestBoolean(allProp, property, object1, object2, nearestProp, filterProp)

    @defineBackgroundNearestBoolean(allProp, property, object1, object2, ###object2, nearestProp, filterProp); 
END
