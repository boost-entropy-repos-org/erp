MODULE SaleReturnOrder;

REQUIRE Order, SaleShipment, SaleOperation, SalePack;

PRIORITY Sale, Operation;

NAMESPACE SaleReturn;

@defineOrder(' (продажа-возврат)', supplier, supplier, company, customer, company, SaleReturn);
@extendFormFilterAccessStock(Order, o, orders, SaleReturn.supplierStock, company);
@extendFormFilterAccessStock(Order, o, orders, SaleReturn.customerStock, customer);
@extendFormFilterAccessLegalEntity(Order, o, orders, supplier, company);
@extendFormFilterAccessLegalEntity(Order, o, orders, customer, customer);

Order.inOrder(o) += o IS Order;
Order.isReturnOrder(o) += TRUE IF o IS SaleReturn.Order;

@defineOrderBatch(supplier, supplier);
@defineOrderBatchDialog();

@implementOrderDestination(customer, supplier);

@defineDocumentSkuStockSupplier (userOrder, userOrder, o);
@defineDocumentBatchStockSupplier(userOrder, userOrder, o);

//----------------------------------------------- Операции -----------------------------------------------------//

@defineDocumentOperationContainer(order, o, overName) ;
@extendFormFilterRoleAccess(order, o, orders);
@defineDocumentOperationConstraint(order, orders, 'заказ (продажа-возврат)', SaleReturn);
Order.operationOrder(order) += operationOrder(order);
@defineDocumentOperationPriceListType(userOrder, 'заказа (продажа-возврат)', userOrder, return);

@defineDocumentOperationLegalEntity(userOrder, supplier, 'Поставщик');
@deriveDocumentOperationLegalEntity(userOrder, supplier, userOrder);
@defineDocumentOperationLegalEntity(userOrder, customer, 'Покупатель');
@deriveDocumentOperationLegalEntity(userOrder, customer, userOrder);
@defineDocumentOperationStock(userOrder, supplier, 'Склад поставщика');
@deriveDocumentOperationStock(userOrder, supplier, userOrder);
@defineDocumentOperationStock(userOrder, customer, 'Склад покупателя');
@deriveDocumentOperationStock(userOrder, customer, userOrder);

@defineDocumentOperationRole(userOrder);

overCopyOrder(s, d) += ACTION(s, d) {
    ASSIGN operationUserOrder(d) <- operationOrder(s);
}
@defineOperationFilterProperty(order, o, orders, nameFilterOrderSupplier);
//------------------------------ Ограничение на выбор контрагентов -----------------------------//

CONSTRAINT supplierUserOrder(userOrder) AND NOT isCompanyLegalEntity(supplierUserOrder(userOrder))
    CHECKED BY supplierUserOrder MESSAGE 'Для заказа выбрано в качестве поставщика организация, не являющаяся компанией';
CONSTRAINT customerUserOrder(userOrder) AND NOT isCustomerLegalEntity(customerUserOrder(userOrder))
    CHECKED BY customerUserOrder MESSAGE 'Для заказа выбрано в качестве покупателя организация, не являющаяся покупателем';
    
CONSTRAINT filterOrderSupplier() AND NOT isCompanyLegalEntity(filterOrderSupplier())
    CHECKED BY filterOrderSupplier MESSAGE 'Для фильтра в заказе выбрана в качестве поставщика организация, не являющаяся компанией';
CONSTRAINT filterOrderSupplierStock() AND NOT isCompanyStock(filterOrderSupplierStock())
    CHECKED BY filterOrderSupplierStock MESSAGE 'Для фильтра в заказе выбран в качестве склада поставщика склад, который не принадлежит компании';          
CONSTRAINT filterOrderCustomer() AND NOT isCustomerLegalEntity(filterOrderCustomer())
    CHECKED BY filterOrderCustomer MESSAGE 'Для фильтра в заказе выбрана в качестве покупателя организация, не являющаяся покупателем';       

CONSTRAINT filterOrderSupplier() AND filterOrderSupplierStock() AND NOT inLegalEntityStock(filterOrderSupplier(), filterOrderSupplierStock())
    CHECKED BY filterOrderSupplierStock   MESSAGE 'Поставщик и склад поставщика в фильтре для заказа не имеют связи';
CONSTRAINT filterOrderCustomer() AND filterOrderCustomerStock() AND NOT inLegalEntityStock(filterOrderCustomer(), filterOrderCustomerStock())
    CHECKED BY filterOrderCustomerStock MESSAGE 'Покупатель и склад покупателя в фильтре для заказа не имеют связи';

//------------------------------ Автоматическое проставление свойств -----------------------------//

@defineDocumentSupplierCustomerStockAccess(UserOrder, company, customer, userOrder);

//------------------------------ Ввод в упаковках -----------------------------//

@defineDocumentPack(order, o);
Order.packQuantityOrderDetail(detail) += packQuantityOrderDetail(detail);
DESIGN userOrder {
    headerExtraParams {
        NEW headerPack {
            caption = 'Упаковка';
            MOVE PROPERTY(showPackUserOrder(o));
        }
    }
}

@defineDocumentPackSkuStock(userOrder, sku, supplier);
@extendFormDocumentPackSkuStock(userOrder, userOrder, o, supplier);

overChangeQuantityValueSkuUserOrderDetail(detail) += ACTION (detail) {
    IF  packBarcodeSku(skuUserOrderDetail(detail)) THEN {
        ASSIGN packQuantityUserOrderDetail(detail) <- quantityUserOrderDetail(detail)/amountPackUserOrderDetail(detail);
    }
}

@deriveDocumentOperationProperty(UserOrder, showPack);

//------------------------------ Расширение формы -----------------------------//

// Фильтры
inUserOrderSku (userOrder, sku) = inCustomerSku(customerUserOrder(userOrder), sku);
EXTEND FORM userOrder

    FILTERGROUP filter
        FILTER 'С остатком ' prevCurrentBalanceSkuStock(ks, st) 'F10' DEFAULT
        FILTER 'В заказе ' quantitySkuUserOrderSupplierStock(ks, o, st) 'F9'

    FILTERGROUP filter1
        FILTER 'С продажей' inUserOrderSku(o, ks) 'F8'
        FILTER 'В прайсе' priceSkuStockUserOrder(ks, st, o) 'F7'

;

inUserOrderBatch (userOrder, batch)= inCustomerSku(customerUserOrder(userOrder), skuBatch(batch));
EXTEND FORM userOrder

    FILTERGROUP filter3
        FILTER 'С остатком ' prevCurrentBalanceBatchStock(b, st) 'F10' DEFAULT
        FILTER 'В документе ' quantityBatchUserOrderSupplierStock(b, o, st) 'F9'

    FILTERGROUP filter4
        FILTER 'С продажей ' inUserOrderBatch(o,b) 'F8'
        FILTER 'В прайсе' ledgerPriceBatchStockUserOrder(b, st, o) 'F7'
;

// Резервы
@extendFormDocumentSkuStockOrderLedger(userOrder, userOrder, o);
@extendFormDocumentSkuStockOrderLedgerAll(userOrder, userOrder, o);

@extendFormDocumentBatchStockOrderLedger(userOrder, userOrder, o);
@extendFormDocumentBatchStockOrderLedgerAll(userOrder, userOrder, o);

//--------------------- Проводим по регистру резерва ------------------//
@implementPurchaseOrderLedger(Order, sku, shipmentDateTime, supplierStock, customer);
editOrderLedger(ledger) += ACTION editOrderDetail(ledger);


//-- Запрет на изменение документа другим пользователем
@defineOperationChangesDocument(order, UserOrder, preventChangesDocument, created);

overShowEditOrder (i) += showEditDeleteUserOrder(i) AND isOpenedOrder(i);
overShowDeleteOrder (i) += showEditDeleteUserOrder(i) AND isUserOpenedOrder(i); 

NAVIGATOR {
    saleReturnNavigator {
        ADD orders;
    }
}

CONSTRAINT  isPostedUserOrder(i) AND isContractOperation(operationUserOrder(i)) AND NOT contractSkuOrder(i) 
    MESSAGE 'Для заказа (продажа-возврат) обязан быть задан договор';  
disableShowBatchOrder(o) += disableShowBatchOperation(operationOrder(o));