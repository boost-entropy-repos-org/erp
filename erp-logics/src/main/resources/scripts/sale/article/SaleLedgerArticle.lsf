MODULE SaleLedgerArticle;

REQUIRE SaleLedger, SaleLedgerItemArticle, StockArticle;

NAMESPACE SaleLedger;

//------------------- Продажи по артикулам -------------------------//


nameArticleBalanceBStock (stock)= nameStock(stock) + ' (остаток на начало)';    
nameArticleSumBStock (stock)= nameStock(stock) + ' (сумма на начало)';    
nameArticleBalanceAStock (stock)= nameStock(stock) + ' (остаток на конец)';    
nameArticleSumAStock (stock)= nameStock(stock) + ' (сумма на конец)';    
nameArticleQuantitySoldStock (stock)= nameStock(stock) + ' (продано кол-во)';    
nameArticleSumSoldStock (stock)= nameStock(stock) + ' (продано сумма)';    
nameArticleDaysOnStockStock (stock)= nameStock(stock) + ' (дней на складе)';    
nameArticleAverageReportSoldStock (stock)= nameStock(stock) + ' (продаж в день)';    
nameArticleTurnoverStock (stock)= nameStock(stock) + ' (товарооборачиваемость)';

nameArticleContractSumSoldStock (stock)= nameStock(stock) + ' (сумма контр. вал.)';    
nameArticleCostSumSoldStock (stock)= nameStock(stock) + ' (сумма себестоимость)';    
nameArticleMarkupSumSoldStock (stock)= nameStock(stock) + ' (сумма торг. надбавки)';    
nameArticleVATSumSoldStock (stock)= nameStock(stock) + ' (сумма НДС)';    
nameArticleAveragePriceSoldStock (stock)= nameStock(stock) + ' (цена ср.)';    
nameArticleContractSumByrSoldStock (stock)= nameStock(stock) + ' (сумма контр. руб.)';    
nameArticleAverageMarkupSoldStock (stock)= nameStock(stock) + ' (ср. % надбавки)';    
    
inArticleReportStock 'Отм.' = DATA LOCAL BOOLEAN (Stock);

filterBalanceArticleDateFromTo 'С остатком' (ar,dFrom, dTo) = GROUP SUM 1 IF inArticleReportStock(st) AND (balanceBArticleStockDate(ar,st,dFrom) OR balanceAArticleStockDate(ar,st,dTo))
    BY ar,dFrom, dTo;
filterSoldArticleDateFromTo 'С продажей' (ar,dFrom,dTo) = GROUP SUM quantitySoldArticleStockDateFromTo(ar,st,dFrom,dTo) IF inArticleReportStock(st) 
    BY ar,dFrom,dTo;   

daysOnStockArticleStock 'Дней на складе' = DATA LOCAL INTEGER (Article, Stock);
averageReportSoldArticleStock 'Продаж в день' = DATA LOCAL NUMERIC[14,3] (Article, Stock);
turnoverArticleStock 'Товароооборачиваемость' = DATA LOCAL NUMERIC[14,2] (Article, Stock);
//-- Итого по всем выделенным складам 
daysOnStockArticle 'Дней на складе (итого)' = DATA LOCAL INTEGER (Article);
averageReportSoldArticle 'Продаж в день  (итого)' = DATA LOCAL NUMERIC[14,3] (Article);
turnoverArticle 'Товароооборачиваемость  (итого)' = DATA LOCAL NUMERIC[14,2] (Article);

filterBalanceSkuDateFromTo 'С остатком' (ks,dFrom, dTo) = GROUP SUM 1 IF inArticleReportStock(st) AND (balanceBSkuStockDate(ks,st,dFrom) OR balanceASkuStockDate(ks,st,dTo))
    BY ks,dFrom, dTo;
filterSoldSkuDateFromTo 'С продажей' (ks,dFrom,dTo) = GROUP SUM quantitySoldSkuStockDateFromTo(ks,st,dFrom,dTo) IF inArticleReportStock(st) 
    BY ks,dFrom,dTo; 
      
quantitySoldInArticleDateFromTo 'Продано за интервал (кол-во)' (article, dateFrom, dateTo) = GROUP SUM
        quantitySoldArticleStockDateFromTo(article, stock, dateFrom, dateTo) IF inArticleReportStock(stock)
        BY article, dateFrom, dateTo;      
      

calcSaleArticleParamsInterval 'Рассчитать продажи в день и товарооборачиваемость' = ACTION (dateFrom, dateTo) {
    LOCAL balance = NUMERIC[14,3] (Article, Stock);
    LOCAL cumBalance = NUMERIC[16,3] (Article, Stock);
    LOCAL dateCur = DATE();

    dateCur() <- dateFrom;
    balance(a, stock) <- balanceBArticleStockDate(a, stock, dateFrom) WHERE inArticleReportStock(stock);
    daysOnStockArticleStock(a, stock) <- NULL;
    
    daysOnStockArticle(a) <- NULL;
    
    WHILE dateCur() <= dateTo DO {
        daysOnStockArticleStock(a, stock) <- daysOnStockArticleStock(a, stock) (+)
                   (1 IF ((balance(a, stock) > 0) OR (quantityArticleStockDate(a, stock, dateCur()) > 0)));
        cumBalance(a, stock) <- cumBalance(a, stock) (+) balance(a, stock);  
        balance(a, stock) <- balance(a, stock) (+)
                   signedQuantityArticleStockDate(a, stock, dateCur());
        daysOnStockArticle(a) <- daysOnStockArticle(a) (+) (1 IF [ = GROUP SUM 1 IF ((balance(a, stock) > 0) OR (quantityArticleStockDate(a, stock, dateCur()) > 0)) AND inArticleReportStock(stock) BY a](a));                                                

        dateCur() <- sumDate(dateCur(), 1);
    }

    averageReportSoldArticleStock(a, stock) <- quantitySoldArticleStockDateFromTo (a, stock, dateFrom, dateTo) / daysOnStockArticleStock(a, stock);
    turnoverArticleStock(a, stock) <- IF quantitySoldArticleStockDateFromTo (a, stock, dateFrom, dateTo) != 0 THEN
                                         cumBalance(a, stock) / quantitySoldArticleStockDateFromTo (a, stock, dateFrom, dateTo)
                                      ELSE
                                        9999.99;
                                        
    averageReportSoldArticle(a) <- quantitySoldInArticleDateFromTo (a, dateFrom, dateTo) / daysOnStockArticle(a);
    turnoverArticle(a) <- IF quantitySoldInArticleDateFromTo (a, dateFrom, dateTo) != 0 THEN
                                         [ = GROUP SUM cumBalance(a, stock) IF inArticleReportStock(stock) BY a](a) / quantitySoldInArticleDateFromTo (a, dateFrom, dateTo)
                                      ELSE
                                        9999.99;                                    
                                        
} TOOLBAR; 


balanceBInReportArticleDate 'Итого (остаток на начало)' (ar,dFrom)= GROUP SUM balanceBArticleStockDate(ar,st,dFrom) IF inArticleReportStock(st) BY ar, dFrom;
quantitySoldInReportArticleDateFromTo 'Итого (продано кол-во)' (ar,dFrom,dTo)= GROUP SUM quantitySoldArticleStockDateFromTo(ar,st,dFrom,dTo) IF inArticleReportStock(st) BY ar, dFrom, dTo;
balanceAInReportArticleDate 'Итого (остаток на конец)' (ar,dTo)= GROUP SUM balanceAArticleStockDate(ar,st,dTo) IF inArticleReportStock(st) BY ar, dTo;

sumBInReportArticleDate 'Итого (сумма на начало)' (ar,dFrom)= GROUP SUM sumBArticleStockDate(ar,st,dFrom) IF inArticleReportStock(st) BY ar, dFrom;
sumSoldInReportArticleDateFromTo 'Итого (продано сумма)' (ar,dFrom,dTo)= GROUP SUM sumSoldArticleStockDateFromTo(ar,st,dFrom,dTo) IF inArticleReportStock(st) BY ar, dFrom, dTo;
sumAInReportArticleDate 'Итого (сумма на конец)' (ar,dTo)= GROUP SUM sumAArticleStockDate(ar,st,dTo) IF inArticleReportStock(st) BY ar, dTo; 

balanceBInReportSkuDate 'Итого (остаток на начало)' (ks,dFrom)= GROUP SUM balanceBSkuStockDate(ks,st,dFrom) IF inArticleReportStock(st) BY ks, dFrom;
quantitySoldInReportSkuDateFromTo 'Итого (продано кол-во)' (ks,dFrom,dTo)= GROUP SUM quantitySoldSkuStockDateFromTo(ks,st,dFrom,dTo) IF inArticleReportStock(st) BY ks, dFrom, dTo;
balanceAInReportSkuDate 'Итого (остаток на конец)' (ks,dTo)= GROUP SUM balanceASkuStockDate(ks,st,dTo) IF inArticleReportStock(st) BY ks, dTo;

sumBInReportSkuDate 'Итого (сумма на начало)' (ks,dFrom)= GROUP SUM sumBSkuStockDate(ks,st,dFrom) IF inArticleReportStock(st) BY ks, dFrom;
sumSoldInReportSkuDateFromTo 'Итого (продано сумма)' (ks,dFrom,dTo)= GROUP SUM sumSoldSkuStockDateFromTo(ks,st,dFrom,dTo) IF inArticleReportStock(st) BY ks, dFrom, dTo;
sumAInReportSkuDate 'Итого (сумма на конец)' (ks,dTo)= GROUP SUM sumASkuStockDate(ks,st,dTo) IF inArticleReportStock(st) BY ks, dTo; 

FORM saleArticle 'Продажи по артикулам'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)
       
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType    
    
    
    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES READONLY orderGroup(sk), skuTreeName = nameGroup(sk)
    ORDER BY orderGroup(sk), skuTreeName
    FILTERS groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' activeGroup(sk) 'F5' DEFAULT       
     
    OBJECTS ts = Stock
    PROPERTIES tsTreeName = nameStock(ts) READONLY, nameLegalEntityStock(ts) READONLY, inArticleReportStock(ts)
    ORDER BY tsTreeName
    FILTERS isCompanyStock(ts)     
    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(ts) 'ctrl F10' DEFAULT
      
    OBJECTS st = Stock      
    FILTERS isCompanyStock(st),
            activeStock(st),
            inArticleReportStock(st)
            
    OBJECTS ar = Article 
    PROPERTIES(ar) READONLY idArticle, captionArticle,
                   nameSkuGroup3Article, nameSkuGroup4Article, nameSkuGroup5Article, imageArticle FORCE PANEL, imageArticleGrid=imageArticle
    PROPERTIES     READONLY balanceBArticleStockDate(ar,st,dFrom)  COLUMNS (st) HEADER nameArticleBalanceBStock(st), 
                   balanceBInReportArticleDate(ar,dFrom)
    PROPERTIES     READONLY quantitySoldArticleStockDateFromTo(ar,st,dFrom,dTo)  COLUMNS (st) HEADER nameArticleQuantitySoldStock(st),
                   quantitySoldInReportArticleDateFromTo(ar,dFrom,dTo)
    PROPERTIES     READONLY balanceAArticleStockDate(ar,st,dTo)  COLUMNS (st) HEADER nameArticleBalanceAStock(st),
                   balanceAInReportArticleDate(ar,dTo)

    PROPERTIES     READONLY sumBArticleStockDate(ar,st,dFrom)  COLUMNS (st) HEADER nameArticleSumBStock(st),
                   sumBInReportArticleDate(ar,dFrom)
    PROPERTIES     READONLY sumSoldArticleStockDateFromTo(ar,st,dFrom,dTo)  COLUMNS (st) HEADER nameArticleSumSoldStock(st),
                   sumSoldInReportArticleDateFromTo(ar,dFrom,dTo)
    PROPERTIES     READONLY sumAArticleStockDate(ar,st,dTo)  COLUMNS (st) HEADER nameArticleSumAStock(st),    
                   sumAInReportArticleDate(ar,dTo)
                   
    PROPERTIES     READONLY daysOnStockArticleStock(ar, st) COLUMNS (st) HEADER nameArticleDaysOnStockStock(st),
                   daysOnStockArticle(ar)
    PROPERTIES     READONLY averageReportSoldArticleStock(ar, st) COLUMNS (st) HEADER nameArticleAverageReportSoldStock(st),
                   averageReportSoldArticle(ar)
    PROPERTIES     READONLY turnoverArticleStock(ar, st) COLUMNS (st) HEADER nameArticleTurnoverStock(st),
                   turnoverArticle(ar) 

    PROPERTIES(dFrom, dTo) calcSaleArticleParamsInterval FORCE PANEL TODRAW ar
                
    FILTERS           isParentGroupArticle(sk, ar)                
                            
    FILTERGROUP filtersSold
        FILTER 'С движением' filterSoldArticleDateFromTo(ar,dFrom,dTo) OR filterBalanceArticleDateFromTo(ar,dFrom,dTo) 'F11' DEFAULT
        FILTER 'С продажей' filterSoldArticleDateFromTo(ar,dFrom,dTo) 'F10'
        FILTER 'С остатком' filterBalanceArticleDateFromTo(ar,dFrom,dTo) 'F9'      
             
    OBJECTS sts = Stock      
    FILTERS isCompanyStock(sts),
            inArticleReportStock(sts)                             
    FILTERGROUP inactiveStock2 FILTER 'Активный' activeStock(sts) 'ctrl F10' DEFAULT                         
    OBJECTS ks = Sku 
    PROPERTIES(ks) READONLY idBarcodeSku, nameSku, nameSkuGroup3Sku, nameSkuGroup4Sku, nameSkuGroup5Sku                            
    PROPERTIES     READONLY balanceBSkuStockDate(ks,st,dFrom)  COLUMNS (st) HEADER nameArticleBalanceBStock(st),
                   balanceBInReportSkuDate(ks,dFrom)
    PROPERTIES     READONLY quantitySoldSkuStockDateFromTo(ks,st,dFrom,dTo)  COLUMNS (st) HEADER nameArticleQuantitySoldStock(st),
                   quantitySoldInReportSkuDateFromTo(ks,dFrom,dTo)
    PROPERTIES     READONLY balanceASkuStockDate(ks,st,dTo)  COLUMNS (st) HEADER nameArticleBalanceAStock(st),
                   balanceAInReportSkuDate(ks,dTo) 
    
    PROPERTIES     READONLY sumBSkuStockDate(ks,st,dFrom)  COLUMNS (st) HEADER nameArticleSumBStock(st),
                   sumBInReportSkuDate(ks,dFrom)
    PROPERTIES     READONLY sumSoldSkuStockDateFromTo(ks,st,dFrom,dTo)  COLUMNS (st) HEADER nameArticleSumSoldStock(st),
                   sumSoldInReportSkuDateFromTo(ks,dFrom,dTo)
    PROPERTIES     READONLY sumASkuStockDate(ks,st,dTo)  COLUMNS (st) HEADER nameArticleSumAStock(st),
                   sumAInReportSkuDate(ks,dTo)                  
                             
    FILTERS           articleItem(ks) == ar  
              
    FILTERGROUP filtersSold1
        FILTER 'С движением' filterSoldSkuDateFromTo(ks,dFrom,dTo) OR filterBalanceSkuDateFromTo(ks,dFrom,dTo) 'F11' DEFAULT
        FILTER 'С продажей' filterSoldSkuDateFromTo(ks,dFrom,dTo) 'F10'
        FILTER 'С остатком' filterBalanceSkuDateFromTo(ks,dFrom,dTo) 'F9'      
                      
    OBJECTS s = SaleLedger
    PROPERTIES(s) READONLY dateTimeSaleLedger, 
                           nameStockSaleLedger, nameCustomerSaleLedger, 
                           nameSkuSaleLedger,
                           quantitySaleLedger,
                           averagePriceSaleLedger,
                           sumSaleLedger 
    ORDER BY nameStockSaleLedger(s)
                           
    FILTERS activeSaleLedger(s),
            dateSaleLedger(s) >= dFrom, dateSaleLedger(s) <= dTo,                      
            inArticleReportStock(stockSaleLedger(s)),
            articleItem(skuSaleLedger(s)) == ar                                        
;

EXTEND FORM saleArticle FILTERS (ts IS Stock AND NOT limitAccessEmployee(currentUser())) OR accessCompanyEmployeeStock(currentUser(),ts);

DESIGN saleArticle {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box {
            type = CONTAINERH;
        }
        MOVE gt.box;
    }
    REMOVE st.box;
    REMOVE sts.box;
    NEW article {
        type = SPLITH;
        fill = 1;
        NEW skuFilters {
            fill = 1;
            type = SPLITV;
            NEW column{
                fill = 1.5;
                type = SPLITV;
                MOVE ts.box;
                MOVE skuTree.tree.box { caption = 'Группы SKU'; }
            }    
            NEW imageBox {
                fill = 1;
                caption = 'Изображение';                     
                MOVE PROPERTY(imageArticle(ar)) {
                    caption = '';
                    fill = 1;
                }
            }
        }
        NEW row {
            fill = 2.5;
            type = SPLITV;
            MOVE ar.box {  fill = 2;}           
            NEW row1 {
                fill = 1;
                type = TABBED;
                MOVE ks.box { caption = 'Товар';} 
                MOVE s.box { caption = 'Регистр продаж';}
            }    
        }     
    } 
    MOVE functions.box;
    PROPERTY(balanceBArticleStockDate(ar,st,dFrom)) { background = #FFEEEE; minimumCharWidth = 2; preferredCharWidth = 2;}
    PROPERTY(sumBArticleStockDate(ar,st,dFrom)) { background = #FFEEEE; }
    PROPERTY(quantitySoldArticleStockDateFromTo(ar,st,dFrom,dTo)) { background = #CCFFCC; minimumCharWidth = 2; preferredCharWidth = 2;}
    PROPERTY(sumSoldArticleStockDateFromTo(ar,st,dFrom,dTo)) { background = #CCFFCC; }
    PROPERTY(balanceAArticleStockDate(ar,st,dTo)) { background = #BDE3FF; minimumCharWidth = 2; preferredCharWidth = 2;}
    PROPERTY(sumAArticleStockDate(ar,st,dTo)) { background = #BDE3FF; }        

    PROPERTY(balanceBInReportArticleDate(ar,dFrom)) { minimumCharWidth = 4; preferredCharWidth = 4;}
    PROPERTY(quantitySoldInReportArticleDateFromTo(ar,dFrom,dTo)) { minimumCharWidth = 4; preferredCharWidth = 4;}
    PROPERTY(balanceAInReportArticleDate(ar,dTo)) { minimumCharWidth = 4; preferredCharWidth = 4;}
   
    PROPERTY(balanceBSkuStockDate(ks,st,dFrom)) { background = #FFEEEE; minimumCharWidth = 2; preferredCharWidth = 2;}
    PROPERTY(sumBSkuStockDate(ks,st,dFrom)) { background = #FFEEEE; }
    PROPERTY(quantitySoldSkuStockDateFromTo(ks,st,dFrom,dTo)) { background = #CCFFCC; minimumCharWidth = 2; preferredCharWidth = 2;}
    PROPERTY(sumSoldSkuStockDateFromTo(ks,st,dFrom,dTo)) { background = #CCFFCC; }
    PROPERTY(balanceASkuStockDate(ks,st,dTo)) { background = #BDE3FF; minimumCharWidth = 2; preferredCharWidth = 2;}
    PROPERTY(sumASkuStockDate(ks,st,dTo)) { background = #BDE3FF; }

    PROPERTY(balanceBInReportSkuDate(ks,dFrom)) { minimumCharWidth = 4; preferredCharWidth = 4;}
    PROPERTY(quantitySoldInReportSkuDateFromTo(ks,dFrom,dTo)) { minimumCharWidth = 4; preferredCharWidth = 4;}
    PROPERTY(balanceAInReportSkuDate(ks,dTo)) { minimumCharWidth = 4; preferredCharWidth = 4;}
    
    PROPERTY(quantitySaleLedger(s)) { background = #CCFFCC; }
    PROPERTY(sumSaleLedger(s)) { background = #CCFFCC; }
    
}

NAVIGATOR {                                              
    salesReports{
        ADD saleArticle;
    }
}
