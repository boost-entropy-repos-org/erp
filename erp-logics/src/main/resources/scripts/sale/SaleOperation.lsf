MODULE SaleOperation;

REQUIRE Operation, PriceListType, Sale;

NAMESPACE Sale;

@defineOperation('(продажа)');
nameReturn 'Наименование (возврат)' = DATA VARISTRING[100](Operation);
nameReturn(Operation operation) += nameReturn(operation);
EXTEND FORM operation 
    PROPERTIES(o)  nameReturn AFTER name(o)
;  
DESIGN operation {
    propertyContainer{
        MOVE PROPERTY(nameReturn(o)) AFTER PROPERTY(name(o));
    }
} 
overName = OVERRIDE nameReturn(Operation o), name(o);
 
@defineOperationPriceListType();

TABLE legalEntityOperation(LegalEntity, Operation);
TABLE legalEntityGroupOperation(LegalEntityGroup, Operation);

@defineOperationLegalEntity(supplier, , s, 'Поставщики');
@defineCountLegalEntityOperation(supplier, company);
@defineOperationLegalEntity(customer, , c, 'Покупатели');
@defineCountLegalEntityOperation(customer, buyer);

TABLE stockOperation(Stock, Operation);
TABLE stockGroupOperation(StockGroup, Operation);

@defineOperationStock(supplier, sts, 'Склады поставщика');
@defineCountStockOperation(supplier, company, seller);
@defineOperationStock(customer, stc, 'Склады покупателя');
@defineCountStockOperation(customer, customer, buyer);

// Подсчет операций для складов поставщика и покупателя по-умолчанию            
countOperationSupplierCustomer (supplierStock, customerStock) = GROUP SUM 1 
    IF inSupplier(Stock supplierStock,Operation operation)
    AND isCompany(supplierStock) 
    AND inSupplier(legalEntity(supplierStock),operation)
    AND isSeller(legalEntity(supplierStock))                                                     
    AND inCustomer(Stock customerStock,operation)
    AND isCustomer(customerStock) 
    AND inCustomer(legalEntity(customerStock),operation)
    AND isBuyer(legalEntity(customerStock))                                                                                                       
        BY supplierStock, customerStock;
                                                        
                                                        
defaultOperationSupplierCustomer (supplierStock, customerStock) = GROUP MIN Operation operation 
    IF inSupplier(Stock supplierStock,operation)
    AND isCompany(supplierStock) 
    AND inSupplier(legalEntity(supplierStock),operation)
    AND isSeller(legalEntity(supplierStock))                                                     
    AND inCustomer(Stock customerStock,operation)
    AND isCustomer(customerStock) 
    AND inCustomer(legalEntity(customerStock),operation)
    AND isBuyer(legalEntity(customerStock))                                                                                                       
        BY supplierStock, customerStock;

EXTEND FORM operation
    FILTERS countCompanyStock(stsg),
            isCompany(sts),
            countCustomerStock(stcg),
            isCustomer(stc),
            inSupplier(legalEntity(sts), o),
            inCustomer(legalEntity(stc), o),
            isSeller(legalEntity(sts)),
            isBuyer(legalEntity(stc))
;

@defineOperationRole();
@extendFormFilterRole(o, dialogOperations);
//@extendFormFilterRole(o, operations);     //-- пока не нужен

EXTEND FORM operation
    FILTERS isCompany(s),
            isBuyer(c)
;

countOperationSupplierCustomerSupplierCustomer 'Кол-во операций' (s, c, ss, cs, u) = 
    GROUP SUM 1 IF inSupplier(LegalEntity s, Operation o) AND inSupplier(Stock ss, o)
               AND inCustomer(LegalEntity c, o) AND inCustomer(Stock cs, o) 
               AND in(User u, o) BY s, c, ss, cs, u COMPLEX;   

defaultOperationSupplierCustomerSupplierCustomer 'Операция по умолчанию' (s, c, ss, cs, u) = 
    GROUP MAX Operation o IF inSupplier(LegalEntity s, o) AND inSupplier(Stock ss, o)
               AND inCustomer(LegalEntity c, o) AND inCustomer(Stock cs, o) 
               AND in(User u, o) BY s, c, ss, cs, u COMPLEX;   

DESIGN operation{
    tabContainer {
        NEW createContainer {
            caption = 'Производные документы';
            type = CONTAINERV;
            NEW saleContainer{
                type = CONTAINERV;
            }
        }
        NEW showContainer {
            caption = 'Отображение свойств';
            type = CONTAINERV;
        }
    }
}

@defineOperationProperty(showPack, 'Упаковка', showContainer);

@defineOperationProperty(skipSaleLedger, 'Не проводить по регистру продаж', showContainer);

@defineOperationProperty(individualLegalEntitySale, 'Продажа ф.л.', showContainer);

@defineOperationProperty(userShippedQuantity, 'Использовать в регистре продаж поставленное количество', showContainer);

@defineOperationProperty(preventChangesDocument, 'Запрет на изменение документа другим пользователем', showContainer);

@defineOperationProperty(useCountVAT, 'Использовать расчетную ставку НДС', showContainer);
@defineOperationProperty(disableShowBatch, 'Не показывать партии', showContainer);

NAVIGATOR {
    saleMasterData {
        ADD operations;
    }
}

@defineOperationProperty(isContract, 'Должен быть задан договор', showContainer);
@defineOperationProperty(isPrice, 'Должна быть задана цена', showContainer);

@defineOperationProperty(isSaleBatch, 'Запретить оприходование накладных (продажа) без партий', BOOLEAN, showContainer);
@defineOperationProperty(isSaleReturnBatch, 'Запретить оприходование накладных (продажа-возврат) без партий', BOOLEAN, showContainer);

@defineOperationProperty(isShipmentPrice, 'Должна быть задана учетная цена', BOOLEAN, showContainer);

@defineOperationProperty(isCharLength, 'Проверять номер накладной', showContainer);

@defineOperationProperty(isShipmentDate, 'Создавать накладную по заказу с датой поставки', showContainer); 
@defineOperationProperty(banNegativeQuantityInvoice, 'Запретить ввод отрицательного количества в накладную', showContainer);

@defineOperationProperty(daysBeforeShipmentDate, 'Дней до поставки по умолчанию', INTEGER, createContainer);