MODULE SaleLedger;

REQUIRE System, Stock, Tax, Employee, Sale, PriceRound;

CLASS ABSTRACT SaleLedger 'Продажа товара';
TABLE saleLedger(SaleLedger);

dateTimeSaleLedger 'Дата/время' (ledger) = ABSTRACT DATETIME (SaleLedger) PERSISTENT INDEXED;
dateSaleLedger 'Дата' (ledger) = toDate(dateTimeSaleLedger(ledger)) PERSISTENT INDEXED;

isPostedSaleLedger 'Проведен' (ledger) = ABSTRACT BOOLEAN (SaleLedger) PERSISTENT;

skuSaleLedger (ledger) = ABSTRACT Sku (SaleLedger) PERSISTENT INDEXED;
nameSkuSaleLedger 'SKU' (ledger) = nameSku(skuSaleLedger(ledger));

stockSaleLedger (ledger) = ABSTRACT Stock (SaleLedger) PERSISTENT INDEXED;
nameStockSaleLedger 'Склад' (ledger) = nameStock(stockSaleLedger(ledger));

descriptionSaleLedger 'Название документа' (ledger) = ABSTRACT STRING[200] (SaleLedger) PERSISTENT;

quantitySaleLedger 'Кол-во' (ledger) = ABSTRACT NUMERIC[14,3] (SaleLedger) PERSISTENT;

sumSaleLedger 'Сумма продажи' (ledger) = ABSTRACT NUMERIC[16,2] (SaleLedger) PERSISTENT;

costSumSaleLedger 'Себестоимость продажи' (ledger) = ABSTRACT NUMERIC[16,3] (SaleLedger) PERSISTENT;

customerSaleLedger (ledger) = ABSTRACT LegalEntity (SaleLedger) PERSISTENT INDEXED;
nameCustomerSaleLedger 'Покупатель' (ledger) = nameLegalEntity(customerSaleLedger(ledger));

inCustomerSku 'Кол-во продано' (legalEntity, sku)= GROUP SUM quantitySaleLedger(ledger) IF isPostedSaleLedger(ledger)
    BY customerSaleLedger(ledger), skuSaleLedger(ledger) PERSISTENT;
prevInCustomerSku 'Были продажи (пред.)' (legalEntity, sku) = PREV(inCustomerSku(legalEntity, sku));

sumSoldTypeExchangeSaleLedger 'Сумма продажи валюта' (typeExchange, saleLedger)= roundPriceCurrency((sumSaleLedger (saleLedger)*
    rateTypeExchangeCurrencyDate(typeExchange, currencyStock(stockSaleLedger(saleLedger)), dateSaleLedger(saleLedger))), currencyTypeExchange(typeExchange));


CONSTRAINT quantitySaleLedger(saleLedger) == 0 MESSAGE 'ошибка: Количество продажи не должно быть равно нулю';

averagePriceSaleLedger 'Цена продажи (средняя)' (saleLedger) = sumSaleLedger(saleLedger)/quantitySaleLedger(saleLedger);

VATSaleLedger (ledger) = ABSTRACT Range (SaleLedger) PERSISTENT;
valueVATSaleLedger 'НДС, %' (ledger) = valueRateRangeDate(VATSaleLedger(ledger), dateSaleLedger(ledger));

sumVATSaleLedger 'Сумма НДС' (ledger) = [X*Y/(100+Y)](
        sumSaleLedger(ledger), valueVATSaleLedger(ledger));

markupSumSaleLedger 'Надбавка' (ledger) = sumSaleLedger(ledger) (-) sumVATSaleLedger(ledger) (-) costSumSaleLedger(ledger);

quantitySoldSkuStockDate (sku, stock, date) = GROUP SUM quantitySaleLedger(ledger) IF isPostedSaleLedger(ledger)
    BY skuSaleLedger(ledger), stockSaleLedger(ledger), dateSaleLedger(ledger) PERSISTENT;

sumSoldSkuStockDate (sku, stock, date) = GROUP SUM sumSaleLedger(ledger) IF isPostedSaleLedger(ledger)
    BY skuSaleLedger(ledger), stockSaleLedger(ledger), dateSaleLedger(ledger) PERSISTENT;


quantitySoldSkuStockWeekDateFromTo 'Продано за неделю (кол-во)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, weekInDate(date), dateFrom, dateTo;

sumSoldSkuStockWeekDateFromTo 'Продано за неделю (сумма)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, weekInDate(date), dateFrom, dateTo;

averagePriceSoldSkuStockWeekDateFromTo 'Средняя цена за неделю' (sku, stock, week, dateFrom, dateTo)=  round2(sumSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) /
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo));

averageSoldSkuStockWeekDateFromTo 'Продано за неделю кол-во/цена' (sku, stock, week, dateFrom, dateTo)= [FORMULA STRING[30] 'CAST($1 AS TEXT) || \'(\' || CAST($2 AS TEXT) || \')\'']
        (quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo), averagePriceSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo)) MINCHARWIDTH 10 PREFCHARWIDTH 15;

quantitySkuSoldWeekDateFromTo 'Продано за неделю (кол-во)' (week, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) BY  week, dateFrom, dateTo;

sumSoldWeekDateFromTo 'Продано за неделю (сумма)' (week, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) BY  week, dateFrom, dateTo;

quantitySoldSkuStockDateFromTo 'Продано за интервал (кол-во)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, dateFrom, dateTo;

sumSoldSkuStockDateFromTo 'Продано за интервал (сумма)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, dateFrom, dateTo;

quantitySoldSkuDateFromTo 'Продано за интервал (кол-во)' (sku, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, dateFrom, dateTo;

sumSoldSkuDateFromTo 'Продано за интервал (сумма)' (sku, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, dateFrom, dateTo;

quantityDaysDateToFrom 'Кол-во дней' (dateTo, dateFrom) = subtractInteger(dateTo, dateFrom) + 1;
averageSoldInDaySkuStockDateFromTo 'Продаж в день' (sku, stock, dateFrom, dateTo) =  quantitySoldSkuStockDateFromTo(sku, stock, dateFrom, dateTo) / quantityDaysDateToFrom(dateTo, dateFrom);

//---------------------------------Тип обмена---------------------------------------//

sumSoldTypeExchangeSkuStockDate (typeExchange, sku, stock, date)= roundPriceCurrency((sumSoldSkuStockDate (sku, stock, date)/ rateTypeExchangeCurrencyDate(typeExchange, currencyStock(stock), date)), currencyTypeExchange(typeExchange));

sumSoldTypeExchangeSkuStockDateFromTo 'Продано за интервал (сумма-валюта)' (typeExchange, sku, stock, dateFrom, dateTo) = GROUP SUM
        sumSoldTypeExchangeSkuStockDate (typeExchange, sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY typeExchange, sku, stock, dateFrom, dateTo;

sumSoldTypeExchangeSkuDateFromTo 'Продано за интервал (сумма-валюта)' (typeExchange, sku, dateFrom, dateTo) = GROUP SUM
        sumSoldTypeExchangeSkuStockDateFromTo(typeExchange, sku, stock, dateFrom, dateTo) BY typeExchange, sku, dateFrom, dateTo;


skuStockAverageInterval 'Интервал расчета продаж в день' = DATA INTEGER ();

averageSoldSkuStock 'Продаж в день' = DATA NUMERIC[14,3] (Sku, Stock);

calcQuantitySoldInterval = ACTION (dateFrom, dateTo) {
    LOCAL balance = NUMERIC[14,3] (Sku, Stock);
    LOCAL days = INTEGER (Sku, Stock);
    LOCAL dateCur = DATE();

    ASSIGN dateCur() <- dateFrom;
    ASSIGN balance(sku, stock) <- balanceBSkuStockDate(sku, stock, dateFrom);

    WHILE dateCur() <= dateTo DO {
        ASSIGN days(sku, stock) <- days(sku, stock) (+)
                   (1 IF ((balance(sku, stock) > 0) OR (quantitySkuStockDate(sku, stock, dateCur()) > 0)));
        ASSIGN balance(sku, stock) <- balance(sku, stock) (+)
                   signedQuantitySkuStockDate(sku, stock, dateCur());
        ASSIGN dateCur() <- sumDate(dateCur(), 1);
    }

    ASSIGN averageSoldSkuStock(sku, stock) <- quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) / days(sku, stock);
}
calcCurrentQuantitySoldInterval 'Пересчитать однодневные продажи' = ACTION () NEWSESSION AUTOAPPLY {
    EXEC calcQuantitySoldInterval(subtractDate(currentDate(), skuStockAverageInterval()), subtractDate(currentDate(), 1));
}

EXTEND FORM options
    PROPERTIES() skuStockAverageInterval, calcCurrentQuantitySoldInterval
;
EXTEND DESIGN options {
    commons {
        ADD PROPERTY(skuStockAverageInterval);
        ADD PROPERTY(calcCurrentQuantitySoldInterval);
    }
}

//---------------------------------------------- Формы продаж -------------------------------------//
FORM saleLedger 'Продажи по позициям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS te = TypeExchange FIXED PANEL
    PROPERTIES nameType = nameTypeExchange(te) SELECTOR, nameCurrencyTypeExchange(te) READONLY

    OBJECTS s = SaleLedger
    PROPERTIES(s) READONLY dateSaleLedger, dateTimeSaleLedger, nameStockSaleLedger, nameCustomerSaleLedger, nameSkuSaleLedger, descriptionSaleLedger,
                           quantitySaleLedger, costSumSaleLedger, markupSumSaleLedger, sumVATSaleLedger, sumSaleLedger, averagePriceSaleLedger
    PROPERTIES    READONLY sumSoldTypeExchangeSaleLedger (te, s)
    FILTERS isPostedSaleLedger(s)
    FILTERS dateSaleLedger(s) >= dFrom, dateSaleLedger(s) <= dTo
;
@extendFormFilterAccess(SaleLedger, s, saleLedger, stock);

DESIGN saleLedger FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        ADD dates.box {
            childConstraints = TO THE RIGHT;
            PROPERTY(nameCurrencyTypeExchange(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
            PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
        }
        ADD te.box {
            childConstraints = TO THE RIGHT;
            PROPERTY(nameCurrencyTypeExchange(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
            PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
        }
        ADD s.box{
            childConstraints = TO THE BOTTOM;
        }
    }
    ADD functions.box;
}

FORM saleSkuStock 'Продажи по SKU'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS te = TypeExchange FIXED PANEL
    PROPERTIES nameType = nameTypeExchange(te) SELECTOR, nameCurrencyTypeExchange(te) READONLY

    OBJECTS w = INTEGER
    FILTERS quantitySkuSoldWeekDateFromTo(w, dFrom, dTo)

    TREE stockTree sg = StockGroup PARENT parentStockGroup, ts = Stock
    PROPERTIES READONLY sgTreeName = nameStockGroup(sg), tsTreeName = nameStock(ts)
    FILTERS stockGroupStock(ts) == sg

    TREE skuTree sk = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = nameSkuGroup(sk)
    ORDER BY skuTreeName

    OBJECTS           sts=(st=Stock, s=Sku)
    PROPERTIES        READONLY nameSku(s), stockName = nameStock(st)

    FILTERS           isParentSkuGroupSku(sk, s),
                      (st == ts AND sg IS StockGroup) OR (isParentStockGroupStock(sg, st) AND NOT ts)

    ORDER BY          nameSku

    PROPERTIES        balanceBSkuStockDate(s, st, dFrom), quantitySoldSkuStockWeekDateFromTo(s, st, w, dFrom, dTo)  COLUMNS (w) HEADER toString4 (w),
                      quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo),
                      balanceASkuStockDate(s, st, dTo), sumSoldSkuStockDateFromTo (s, st, dFrom, dTo),  sumSoldTypeExchangeSkuStockDateFromTo(te,s, st, dFrom, dTo)

    FILTERGROUP filtersSold
        FILTER 'Показывать проданные за интервал' 'F11' quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo)

    FILTERGROUP filtersSold
        FILTER 'Показывать с остатками' 'F10' balanceASkuStockDate(s, st, dTo)

;
@extendFormFilterStockAccess(Stock, st, saleSkuStock);
@extendFormFilterStockGroupAccess(Stock, ts, saleSkuStock, accessEmployeeEmployeeDivision);
@extendFormFilterStockGroupAccess(StockGroup, sg, saleSkuStock, countAccessEmployeeEmployeeDivisionGroup);

DESIGN saleSkuStock FROM DEFAULT {

    main {
        NEW topContainer {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            NEW firstCase {
                type = SPLITV;
                childConstraints = TO THE BOTTOM;

                ADD stockTree.tree.box {caption = 'Магазины'; }
                ADD skuTree.tree.box { caption = 'Товарные группы'; }
            }

            NEW secondCase {
                fillHorizontal = 2;
                childConstraints = TO THE BOTTOM;

                ADD dates.box { childConstraints = TO THE RIGHT; }
                ADD te.box {
                    childConstraints = TO THE RIGHT;
                    PROPERTY(nameCurrencyTypeExchange(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
                    PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
                }

                REMOVE w.box;
                ADD sts.box { fillVertical = 2; }

            }
        }
        ADD functions.box;
    }
}

//отчет по продажам

dataInSessionGroup 'Отм.' = DATA SESSION BOOLEAN (Group);

levelParentGroup (group) = GROUP MIN levelGroupGroup(group, parent) IF dataInSessionGroup(parent)
    BY group PERSISTENT;

inParentGroup (group) = TRUE IF levelParentGroup(group) PERSISTENT;

inSessionGroup 'Отм.' (group) = OVERRIDE
    inParentGroup(group),
    dataInSessionGroup(group);

sessionConcatGroups 'Группы' (groupType) =
    GROUP CONCAT nameGroup(group) IF inSessionGroup(group) AND NOT inSessionGroup(parentGroup(group)),','
    BY groupTypeGroup(group);

//суммы продаж по группе
costSumSalesGroupStockDateDate 'Себестоимость продаж (по группе)' (group, stock, dateFrom, dateTo) =
    GROUP SUM costSumSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                               dateSaleLedger(saleLedger) >= dateFrom AND
                                               dateSaleLedger(saleLedger) <= dateTo
    BY groupGroupTypeSku(groupType, skuSaleLedger(saleLedger)), stockSaleLedger(saleLedger), dateFrom, dateTo;
markupSumSalesGroupStockDateDate 'Надбавка (по группе)'(group, stock, dateFrom, dateTo) =
    GROUP SUM markupSumSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                               dateSaleLedger(saleLedger) >= dateFrom AND
                                               dateSaleLedger(saleLedger) <= dateTo
    BY groupGroupTypeSku(groupType, skuSaleLedger(saleLedger)), stockSaleLedger(saleLedger), dateFrom, dateTo;
sumVATSalesGroupStockDateDate 'Сумма НДС (по группе)'(group, stock, dateFrom, dateTo) =
    GROUP SUM sumVATSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                              dateSaleLedger(saleLedger) >= dateFrom AND
                                              dateSaleLedger(saleLedger) <= dateTo
    BY groupGroupTypeSku(groupType, skuSaleLedger(saleLedger)), stockSaleLedger(saleLedger), dateFrom, dateTo;
sumSalesSkuGroupStockDateDate 'Сумма продаж (по группе)' (group, stock, dateFrom, dateTo) =
    GROUP SUM sumSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                           dateSaleLedger(saleLedger) >= dateFrom AND
                                           dateSaleLedger(saleLedger) <= dateTo
    BY groupGroupTypeSku(groupType, skuSaleLedger(saleLedger)), stockSaleLedger(saleLedger), dateFrom, dateTo;
sumRecSalesSkuGroupStockDateDate 'Сумма продаж (всего)' (group, stock, dateFrom, dateTo) =
    GROUP SUM sumSaleLedger(saleLedger) IF isParentGroupSku(group, skuSaleLedger(saleLedger)) AND
                                           isPostedSaleLedger(saleLedger) AND
                                           dateSaleLedger(saleLedger) >= dateFrom AND
                                           dateSaleLedger(saleLedger) <= dateTo
    BY group, stockSaleLedger(saleLedger), dateFrom, dateTo;

//суммы продаж по покупателю
costSumSalesGroupTypeCustomerStockDateDate 'Себестоимость продаж (по покупателю)' (groupType, customer, stock, dateFrom, dateTo) =
    GROUP SUM costSumSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                               inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                               dateSaleLedger(saleLedger) >= dateFrom AND
                                               dateSaleLedger(saleLedger) <= dateTo
    BY groupType, customerSaleLedger(saleLedger), stockSaleLedger(saleLedger), dateFrom, dateTo;
markupSumSalesGroupTypeCustomerStockDateDate 'Надбавка (по покупателю)'(groupType, customer, stock, dateFrom, dateTo) =
    GROUP SUM markupSumSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                               inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                               dateSaleLedger(saleLedger) >= dateFrom AND
                                               dateSaleLedger(saleLedger) <= dateTo
    BY groupType, customerSaleLedger(saleLedger), stockSaleLedger(saleLedger), dateFrom, dateTo;
sumVATSalesGroupTypeCustomerStockDateDate 'Сумма НДС (по покупателю)'(groupType, customer, stock, dateFrom, dateTo) =
    GROUP SUM sumVATSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                              inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                              dateSaleLedger(saleLedger) >= dateFrom AND
                                              dateSaleLedger(saleLedger) <= dateTo
    BY groupType, customerSaleLedger(saleLedger), stockSaleLedger(saleLedger), dateFrom, dateTo;
sumSalesGroupTypeCustomerStockDateDate 'Сумма продаж (по покупателю)' (groupType, customer, stock, dateFrom, dateTo) =
    GROUP SUM sumSaleLedger(saleLedger) IF isPostedSaleLedger(saleLedger) AND
                                           inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                           dateSaleLedger(saleLedger) >= dateFrom AND
                                           dateSaleLedger(saleLedger) <= dateTo
    BY groupType, customerSaleLedger(saleLedger), stockSaleLedger(saleLedger), dateFrom, dateTo;

//итоговые суммы продаж
costSumSalesGroupTypeStockDateDate 'Итоговая себестоимость продаж' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM costSumSaleLedger(saleLedger) IF inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                               isPostedSaleLedger(saleLedger) AND
                                               dateSaleLedger(saleLedger) >= dateFrom AND
                                               dateSaleLedger(saleLedger) <= dateTo
    BY groupType, stockSaleLedger(saleLedger), dateFrom, dateTo PREFCHARWIDTH 15;
markupSumSalesGroupTypeStockDateDate 'Итоговая надбавка продаж' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM markupSumSaleLedger(saleLedger) IF inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                                 isPostedSaleLedger(saleLedger) AND
                                                 dateSaleLedger(saleLedger) >= dateFrom AND
                                                 dateSaleLedger(saleLedger) <= dateTo
    BY groupType, stockSaleLedger(saleLedger), dateFrom, dateTo PREFCHARWIDTH 15;
sumVATSalesGroupTypeStockDateDate 'Итоговая сумма НДС продаж' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM sumVATSaleLedger(saleLedger) IF inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                              isPostedSaleLedger(saleLedger) AND
                                              dateSaleLedger(saleLedger) >= dateFrom AND
                                              dateSaleLedger(saleLedger) <= dateTo
    BY groupType, stockSaleLedger(saleLedger), dateFrom, dateTo PREFCHARWIDTH 15;
sumSalesGroupTypeStockDateDate 'Итоговая сумма продаж' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM sumSaleLedger(saleLedger) IF inSessionGroup(groupGroupTypeSku(groupType, skuSaleLedger(saleLedger))) AND
                                           isPostedSaleLedger(saleLedger) AND
                                           dateSaleLedger(saleLedger) >= dateFrom AND
                                           dateSaleLedger(saleLedger) <= dateTo
    BY groupType, stockSaleLedger(saleLedger), dateFrom, dateTo PREFCHARWIDTH 15;

//формы
FORM printListSalesReport 'Отчет по продажам' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY costSumSalesGroupTypeStockDateDate, markupSumSalesGroupTypeStockDateDate,
                                        sumVATSalesGroupTypeStockDateDate, sumSalesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS s = SaleLedger
    PROPERTIES(s) READONLY dateTimeSaleLedger, nameCustomerSaleLedger, nameSkuSaleLedger, descriptionSaleLedger,
                           quantitySaleLedger, costSumSaleLedger, markupSumSaleLedger, sumVATSaleLedger, sumSaleLedger, averagePriceSaleLedger
    ORDER BY nameSkuSaleLedger
    FILTERS isPostedSaleLedger(s),
            stockSaleLedger(s) == st,
            dateSaleLedger(s) >= df,
            dateSaleLedger(s) <= dt,
            inSessionGroup(groupGroupTypeSku(gt, skuSaleLedger(s)))
;

printListSalesReport 'Списком' (stock, dateFrom, dateTo) =
    ACTION FORM printListSalesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printSalesReport 'Отчет по продажам' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY costSumSalesGroupTypeStockDateDate, markupSumSalesGroupTypeStockDateDate,
                                        sumVATSalesGroupTypeStockDateDate, sumSalesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    PROPERTIES(sk, st, df, dt) READONLY costSumSalesGroupStockDateDate, markupSumSalesGroupStockDateDate, sumVATSalesGroupStockDateDate, sumSalesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt

    OBJECTS s = SaleLedger
    PROPERTIES(s) READONLY dateTimeSaleLedger, nameCustomerSaleLedger, nameSkuSaleLedger, descriptionSaleLedger,
                           quantitySaleLedger, costSumSaleLedger, markupSumSaleLedger, sumVATSaleLedger, sumSaleLedger, averagePriceSaleLedger
    ORDER BY nameSkuSaleLedger
    FILTERS isPostedSaleLedger(s),
            stockSaleLedger(s) == st,
            dateSaleLedger(s) >= df,
            dateSaleLedger(s) <= dt,
            groupGroupTypeSku(gt, skuSaleLedger(s)) == sk
;

printSalesReport 'По группам' (stock, dateFrom, dateTo) =
    ACTION FORM printSalesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printGroupSalesReport 'Отчет по продажам' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY costSumSalesGroupTypeStockDateDate, markupSumSalesGroupTypeStockDateDate,
                                        sumVATSalesGroupTypeStockDateDate, sumSalesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    PROPERTIES(sk, st, df, dt) READONLY costSumSalesGroupStockDateDate, markupSumSalesGroupStockDateDate, sumVATSalesGroupStockDateDate, sumSalesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt,
            costSumSalesGroupStockDateDate(sk, st, df, dt) OR markupSumSalesGroupStockDateDate(sk, st, df, dt) OR
            sumVATSalesGroupStockDateDate(sk, st, df, dt) OR sumSalesSkuGroupStockDateDate(sk, st, df, dt)
;

printGroupSalesReport 'По группам' (stock, dateFrom, dateTo) =
    ACTION FORM printGroupSalesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printGroupCustomerSalesReport 'Отчет по продажам' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY costSumSalesGroupTypeStockDateDate, markupSumSalesGroupTypeStockDateDate,
                                        sumVATSalesGroupTypeStockDateDate, sumSalesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity
    PROPERTIES(gt, l, st, df, dt) READONLY costSumSalesGroupTypeCustomerStockDateDate, markupSumSalesGroupTypeCustomerStockDateDate,
                                           sumVATSalesGroupTypeCustomerStockDateDate, sumSalesGroupTypeCustomerStockDateDate
    FILTERS isBuyerLegalEntity(l)

    OBJECTS s = SaleLedger
    PROPERTIES(s) READONLY dateTimeSaleLedger, nameCustomerSaleLedger, nameSkuSaleLedger, descriptionSaleLedger,
                           quantitySaleLedger, costSumSaleLedger, markupSumSaleLedger, sumVATSaleLedger, sumSaleLedger, averagePriceSaleLedger
    ORDER BY nameSkuSaleLedger
    FILTERS isPostedSaleLedger(s),
            inSessionGroup(groupGroupTypeSku(gt, skuSaleLedger(s))),
            stockSaleLedger(s) == st,
            dateSaleLedger(s) >= df,
            dateSaleLedger(s) <= dt,
            customerSaleLedger(s) == l
;

printGroupCustomerSalesReport 'По покупателям' (stock, dateFrom, dateTo) =
    ACTION FORM printGroupCustomerSalesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printCustomerSalesReport 'Отчет по продажам' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY costSumSalesGroupTypeStockDateDate, markupSumSalesGroupTypeStockDateDate,
                                        sumVATSalesGroupTypeStockDateDate, sumSalesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity
    PROPERTIES(gt, l, st, df, dt) READONLY costSumSalesGroupTypeCustomerStockDateDate, markupSumSalesGroupTypeCustomerStockDateDate,
                                           sumVATSalesGroupTypeCustomerStockDateDate, sumSalesGroupTypeCustomerStockDateDate
    FILTERS isBuyerLegalEntity(l),
            costSumSalesGroupTypeCustomerStockDateDate(gt, l, st, df, dt) OR markupSumSalesGroupTypeCustomerStockDateDate(gt, l, st, df, dt) OR
            sumVATSalesGroupTypeCustomerStockDateDate(gt, l, st, df, dt) OR sumSalesGroupTypeCustomerStockDateDate(gt, l, st, df, dt)
;

printCustomerSalesReport 'По покупателям' (stock, dateFrom, dateTo) =
    ACTION FORM printCustomerSalesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM salesReport 'Отчет по продажам'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumSalesGroupTypeStockDateDate
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES READONLY skuTreeName = nameGroup(sk)
    PROPERTIES(sk, st, df, dt) READONLY sumRecSalesSkuGroupStockDateDate
    PROPERTIES inSessionGroup(sk)
    ORDER BY skuTreeName
    FILTERS groupTypeGroup(sk) == gt

    OBJECTS s = SaleLedger
    PROPERTIES(s) READONLY dateTimeSaleLedger, nameCustomerSaleLedger, nameSkuSaleLedger, descriptionSaleLedger,
                           quantitySaleLedger, costSumSaleLedger, markupSumSaleLedger, sumVATSaleLedger, sumSaleLedger, averagePriceSaleLedger
    ORDER BY nameSkuSaleLedger
    FILTERS isParentGroupSku(sk, skuSaleLedger(s)),
            isPostedSaleLedger(s),
            stockSaleLedger(s) == st,
            dateSaleLedger(s) >= df,
            dateSaleLedger(s) <= dt
    PROPERTIES(st, df, dt) printListSalesReport, printSalesReport, printGroupSalesReport,
                           printGroupCustomerSalesReport, printCustomerSalesReport
;

DESIGN salesReport FROM DEFAULT{
    NEW topContainer{
        childConstraints = TO THE RIGHT;
        NEW dateContainer{
            caption = 'Период';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(dateFrom){caption = 'Дата (с)';}
            ADD PROPERTY(dateTo){caption = 'Дата (по)';}
        }
        ADD st.box;
        ADD gt.box{ADD PROPERTY(inSessionGroup);}
        NEW sumContainer{
            caption = 'Итоговые суммы';
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY(sumSalesGroupTypeStockDateDate);
        }
    }
    NEW bottomContainer{
        childConstraints = TO THE RIGHT;
        type = SPLITH;
        ADD skuTree.tree.box;
        NEW tabContainer{
            type = TABBED;
            fillHorizontal = 2;
            NEW salesContainer{
                caption = 'Продажи';
                childConstraints = TO THE BOTTOM;
                NEW printSalesContainer{
                    childConstraints = TO THE RIGHT;
                    caption = 'Печать';
                    NEW rowSalesContainer{
                        childConstraints = TO THE RIGHTBOTTOM;
                        NEW firstColumnSalesContainer{
                            caption = 'С детализацией';
                            childConstraints = TO THE RIGHT;
                            ADD PROPERTY(printSalesReport);
                            ADD PROPERTY(printGroupCustomerSalesReport);
                        }
                        NEW secondColumnSalesContainer{
                            caption = 'Без детализации';
                            childConstraints = TO THE RIGHT;
                            ADD PROPERTY(printGroupSalesReport);
                            ADD PROPERTY(printCustomerSalesReport);
                        }
                        NEW thirdColumnSalesContainer{
                            caption = 'Список';
                            ADD PROPERTY(printListSalesReport);
                        }
                    }
                }
                ADD s.box;
            }
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    saleNavigator {
        NEW saleLedgerNavigator 'Продажи' BEFORE saleMasterData {
            ADD saleLedger;
            ADD saleSkuStock;
        }
        NEW salesReports 'Отчеты'{
            ADD salesReport;
        }
    }
}

//---------------------------------------------- Расширение формы подбор товаров -------------------------------------//
META defineCustomerDialogStockSku (form)

    form###customer = DATA SESSION LegalEntity ();
    form###nameCustomer 'Покупатель' () = nameLegalEntity(form###customer()) PREFCHARWIDTH 30;
    form###customersFilter (sku) =  (TRUE IF inCustomerSku(form###customer(), sku)) OR (sku IS Sku AND NOT form###customer());

    EXTEND FORM form
        PROPERTIES() form###nameCustomer
        FILTERS form###customersFilter(s)
    ;

    EXTEND DESIGN form {
        filterContainer {
            ADD PROPERTY(form###nameCustomer());
        }
    }
END
@defineCustomerDialogStockSku(dialogSku);

//---------------------------------------------- Макросы для имплементаций -------------------------------------//

META implementSaleLedgerCustom(concrete, skuProp, stockProp)
    dateTimeSaleLedger (ledger) += dateTime###concrete##Detail(ledger);
    isPostedSaleLedger (ledger) += isPosted###concrete##Detail(ledger);
    skuSaleLedger (ledger) += skuProp###concrete##Detail(ledger);
    stockSaleLedger (ledger) += stockProp###concrete##Detail(ledger);
    descriptionSaleLedger (ledger) += description###concrete##Detail(ledger);
END
META implementSaleLedger(concrete, skuProp, stockProp)
    EXTEND CLASS concrete##Detail : SaleLedger;
    @implementSaleLedgerCustom(concrete, skuProp, stockProp);
END

EXTEND FORM currentBalanceSkuStock PROPERTIES(s, st) averageSoldSkuStock READONLY;
EXTEND FORM balanceSkuStock PROPERTIES(s, st) averageSoldSkuStock READONLY;