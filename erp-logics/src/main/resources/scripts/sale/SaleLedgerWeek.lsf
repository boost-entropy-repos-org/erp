MODULE SaleLedgerWeek;

REQUIRE SaleLedger;

NAMESPACE SaleLedger;

quantitySold 'Продано за неделю (кол-во)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        quantitySold(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY sku, stock, extractWeek(date), dateFrom, dateTo;

sumSold 'Продано за неделю (сумма)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        sumSold(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY sku, stock, extractWeek(date), dateFrom, dateTo;

averagePriceSold 'Средняя цена за неделю' (Sku sku, Stock stock, INTEGER week, DATE dateFrom, DATE dateTo)=  round2(sumSold(sku, stock, week, dateFrom, dateTo) /
        quantitySold(sku, stock, week, dateFrom, dateTo));

averageSold 'Продано за неделю кол-во/цена' (Sku sku, Stock stock, INTEGER week, DATE dateFrom, DATE dateTo)= 
        quantitySold(sku, stock, week, dateFrom, dateTo) + '(' + averagePriceSold(sku, stock, week, dateFrom, dateTo) + ')' MINCHARWIDTH 10 PREFCHARWIDTH 15;

quantitySkuSold 'Продано за неделю (кол-во)' (week, dateFrom, dateTo) = GROUP SUM
        quantitySold(Sku sku, Stock stock, INTEGER week, DATE dateFrom, DATE dateTo) BY  week, dateFrom, dateTo;

sumSold 'Продано за неделю (сумма)' (week, dateFrom, dateTo) = GROUP SUM
        sumSold(Sku sku, Stock stock, INTEGER week, DATE dateFrom, DATE dateTo) BY  week, dateFrom, dateTo;

toString4 = FORMULA STRING[4] 'CAST($1 AS character(4))' FIXEDCHARWIDTH 4;

FORM saleSkuStock 'Продажи по неделям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS te = TypeExchange FIXED PANEL
    PROPERTIES nameType = name(te) SELECTOR, nameCurrency(te) READONLY

    OBJECTS w = INTEGER FIXED GRID
    FILTERS quantitySkuSold(w, dFrom, dTo)

    TREE stockTree sg = StockGroup PARENT parent, ts = Stock
    PROPERTIES READONLY sgTreeName = name(sg), tsTreeName = name(ts)
    FILTERS stockGroup(ts) == sg
    FILTERGROUP inactiveStock FILTER 'Активный' active(ts) 'ctrl F10' DEFAULT
    TREE skuTree sk = SkuGroup PARENT parent
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    ORDER BY order(sk), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS           sts=(st=Stock, s=Sku)
    PROPERTIES        READONLY name(s), stockName = name(st)

    FILTERS           isParent(sk, s),
                      (st == ts AND sg IS StockGroup) OR (isParent(sg, st) AND NOT ts),
                      quantitySold(s, st, dFrom, dTo)
    FILTERGROUP inactiveStock2 FILTER 'Активный' active(st) 'ctrl F10' DEFAULT
    ORDER BY          name(s)

    PROPERTIES        balanceB(s, st, dFrom), quantitySold(s, st, w, dFrom, dTo)  COLUMNS (w) HEADER toString4 (w),
                      quantitySold(s, st, dFrom, dTo),
                      balanceA(s, st, dTo), sumSold (s, st, dFrom, dTo),  sumSold(te,s, st, dFrom, dTo)
;
@extendFormFilterStockAccess(st, saleSkuStock);
@extendFormFilterStockAccess(ts, saleSkuStock);
@extendFormFilterStockGroupAccess(sg, saleSkuStock);

DESIGN saleSkuStock {

    NEW topContainer {
        fill = 1;
        type = SPLITH;

        NEW firstCase {
            fill = 1;
            type = SPLITV;

            MOVE stockTree.tree.box {caption = 'Магазины'; }
            MOVE skuTree.tree.box { caption = 'Товарные группы'; }
        }

        NEW secondCase {
            fill = 3;

            NEW params {
                type = CONTAINERH;
                MOVE dates.box {
                    type = CONTAINERH;
                }
                MOVE te.box {
                    type = CONTAINERH;
                    PROPERTY(nameCurrency(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
                    PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
                }
            }

            MOVE sts.box;
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    salesReports {
        ADD saleSkuStock;
    }
}