MODULE SaleLedgerWeek;

REQUIRE SaleLedger;

NAMESPACE SaleLedger;

quantitySoldSkuStockWeekDateFromTo 'Продано за неделю (кол-во)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, extractWeek(date), dateFrom, dateTo;

sumSoldSkuStockWeekDateFromTo 'Продано за неделю (сумма)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, extractWeek(date), dateFrom, dateTo;

averagePriceSoldSkuStockWeekDateFromTo 'Средняя цена за неделю' (sku, stock, week, dateFrom, dateTo)=  round2(sumSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) /
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo));

averageSoldSkuStockWeekDateFromTo 'Продано за неделю кол-во/цена' (sku, stock, week, dateFrom, dateTo)= 
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) + '(' + averagePriceSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) + ')' MINCHARWIDTH 10 PREFCHARWIDTH 15;

quantitySkuSoldWeekDateFromTo 'Продано за неделю (кол-во)' (week, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) BY  week, dateFrom, dateTo;

sumSoldWeekDateFromTo 'Продано за неделю (сумма)' (week, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) BY  week, dateFrom, dateTo;

toString4 = FORMULA STRING[4] 'CAST($1 AS character(4))' FIXEDCHARWIDTH 4;

FORM saleSkuStock 'Продажи по неделям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS te = TypeExchange FIXED PANEL
    PROPERTIES nameType = nameTypeExchange(te) SELECTOR, nameCurrencyTypeExchange(te) READONLY

    OBJECTS w = INTEGER FIXED GRID
    FILTERS quantitySkuSoldWeekDateFromTo(w, dFrom, dTo)

    TREE stockTree sg = StockGroup PARENT parentStockGroup, ts = Stock
    PROPERTIES READONLY sgTreeName = nameStockGroup(sg), tsTreeName = nameStock(ts)
    FILTERS stockGroupStock(ts) == sg
    FILTERGROUP inactiveStock FILTER 'Активный' 'ctrl F10' activeStock(ts) DEFAULT
    TREE skuTree sk = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = nameSkuGroup(sk)
    ORDER BY skuTreeName
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(sk) DEFAULT

    OBJECTS           sts=(st=Stock, s=Sku)
    PROPERTIES        READONLY nameSku(s), stockName = nameStock(st)

    FILTERS           isParentSkuGroupSku(sk, s),
                      (st == ts AND sg IS StockGroup) OR (isParentStockGroupStock(sg, st) AND NOT ts)
    FILTERGROUP inactiveStock2 FILTER 'Активный' 'ctrl F10' activeStock(st) DEFAULT
    ORDER BY          nameSku(s)

    PROPERTIES        balanceBSkuStockDate(s, st, dFrom), quantitySoldSkuStockWeekDateFromTo(s, st, w, dFrom, dTo)  COLUMNS (w) HEADER toString4 (w),
                      quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo),
                      balanceASkuStockDate(s, st, dTo), sumSoldSkuStockDateFromTo (s, st, dFrom, dTo),  sumSoldTypeExchangeSkuStockDateFromTo(te,s, st, dFrom, dTo)

    FILTERGROUP filtersSold
        FILTER 'Показывать проданные за интервал' 'F11' quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo)

    FILTERGROUP filtersSold2
        FILTER 'Показывать с остатками' 'F10' balanceASkuStockDate(s, st, dTo)

;
@extendFormFilterStockAccess(Stock, st, saleSkuStock);
@extendFormFilterStockAccess(Stock, ts, saleSkuStock);
@extendFormFilterStockGroupAccess(StockGroup, sg, saleSkuStock);

DESIGN saleSkuStock FROM DEFAULT {

    NEW topContainer {
        fill = 1;
        type = SPLITH;

        NEW firstCase {
            fill = 1;
            type = SPLITV;

            ADD stockTree.tree.box {caption = 'Магазины'; }
            ADD skuTree.tree.box { caption = 'Товарные группы'; }
        }

        NEW secondCase {
            fill = 3;

            NEW params {
                type = CONTAINERH;
                ADD dates.box {
                    type = CONTAINERH;
                }
                ADD te.box {
                    type = CONTAINERH;
                    PROPERTY(nameCurrencyTypeExchange(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
                    PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
                }
            }

            ADD sts.box;
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    salesReports {
        ADD saleSkuStock;
    }
}