MODULE SaleLedgerBatch;

REQUIRE SaleLedger, TaxItem, SkuLedgerTax;

NAMESPACE SaleLedger;

//------------------- Продажи по партиям -------------------------//

nameBatchBalanceBStock (stock)= nameStock(stock) + ' (остаток на начало)';    
nameBatchSumBStock (stock)= nameStock(stock) + ' (сумма на начало)';    
nameBatchBalanceAStock (stock)= nameStock(stock) + ' (остаток на конец)';    
nameBatchSumAStock (stock)= nameStock(stock) + ' (сумма на конец)';    
nameBatchQuantitySoldStock (stock)= nameStock(stock) + ' (продано кол-во)';    
nameBatchSumSoldStock (stock)= nameStock(stock) + ' (продано сумма)';    
 
inBatchReportStock 'Отм.' = DATA LOCAL BOOLEAN (Stock);

                  
filterBalanceBatchDateFromTo 'С остатком' (b,dFrom, dTo) = GROUP SUM 1 IF inBatchReportStock(st) AND (balanceBBatchStockDate(b,st,dFrom) OR balanceABatchStockDate(b,st,dTo))
    BY b,dFrom, dTo;
filterSoldBatchDateFromTo 'С продажей' (b,dFrom,dTo) = GROUP SUM quantitySoldBatchStockDateFromTo(b,st,dFrom,dTo) IF inBatchReportStock(st) 
    BY b,dFrom,dTo;           
       
//-------------------------------------------------------------------------//
   
backgroundLavenderBatch 'Розовато-лавандовый' (b) = RGB(255,238,238) IF b IS Batch;  
backgroundGreenTeaBatch 'Зеленого чая' (b) = RGB(204,255,204) IF b IS Batch;  
backgroundSkyBatch 'Небесный' (b) = RGB(127,199,255) IF b IS Batch;  

dbalanceBBatchStock 'Остаток на начало дня' = DATA LOCAL NUMERIC[14,3] (Batch, Stock);
dcostBBatchStock 'Себестоимость на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dVATSumBBatchStock 'НДС поставщика на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dSumBBatchStock 'Сумма поставщика с НДС на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
daccountSumBBatchStock 'Учетная сумма на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
        
dquantitySoldBatchStock 'Продано за интервал (кол-во)' = DATA LOCAL NUMERIC[14,3] (Batch, Stock);  
dcostSumSoldBatchStock 'Себестоимость проданного за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);   
dmarkupSumSoldBatchStock 'Надбавка проданного за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  

dsumVATSoldBatchStock 'Сумма НДС проданного за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
daveragePriceSoldBatchStock 'Цена за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
dsumSoldBatchStock 'Продано за интервал (сумма)' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
daverageMarkupSoldBatchStock 'Средний процент надбавки' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
   
dbalanceABatchStock 'Остаток на конец дня' = DATA LOCAL NUMERIC[14,3] (Batch, Stock);
dcostABatchStock 'Себестоимость на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dVATSumABatchStock 'НДС поставщика на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dSumABatchStock 'Сумма поставщика с НДС на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
daccountSumABatchStock 'Учетная сумма на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);                     
    
dmovedBatchStock = DATA LOCAL BOOLEAN (Batch, Stock);

overFillPropertyDateFromTo = ABSTRACT ACTION LIST (DATE, DATE);   
                   
fillPropertyBatchStockDateFromTo 'Заполнить данные'  = ACTION (dFrom,dTo) {
    dbalanceBBatchStock(bb,sts) <- balanceBBatchStockDate(bb,sts,dFrom) 
        WHERE inBatchReportStock(sts);
    dcostBBatchStock(bb,sts) <- NUMERIC[15,3](dbalanceBBatchStock(bb,sts) * costBatch(bb)) 
        WHERE inBatchReportStock(sts); 
    dVATSumBBatchStock(bb, sts) <- NUMERIC[15,3](dcostBBatchStock(bb, sts) * valueVATStockBatchDate(sts, bb, dateBatch(bb)) / 100.0)
        WHERE inBatchReportStock(sts);
    dSumBBatchStock(bb, sts) <- NUMERIC[15,3](dcostBBatchStock(bb, sts) (+) dVATSumBBatchStock(bb, sts))
        WHERE inBatchReportStock(sts);         
    daccountSumBBatchStock(bb,sts) <- NUMERIC[15,3](dbalanceBBatchStock(bb,sts) * accountPriceBBatchStockDate(bb,sts,dFrom)) 
        WHERE inBatchReportStock(sts); 
//--            
    dquantitySoldBatchStock(bb,sts) <- quantitySoldBatchStockDateFromTo(bb,sts,dFrom,dTo) 
        WHERE inBatchReportStock(sts);
    dcostSumSoldBatchStock(bb,sts) <- costSumSoldBatchStockDateFromTo(bb,sts,dFrom,dTo) 
        WHERE inBatchReportStock(sts);
    dmarkupSumSoldBatchStock(bb,sts) <- markupSumSoldBatchStockDateFromTo(bb,sts,dFrom,dTo) 
        WHERE inBatchReportStock(sts);     
    dsumVATSoldBatchStock(bb,sts) <- sumVATSoldBatchStockDateFromTo(bb,sts,dFrom,dTo) 
        WHERE inBatchReportStock(sts);
    dsumSoldBatchStock(bb,sts) <- sumSoldBatchStockDateFromTo(bb,sts,dFrom,dTo) 
        WHERE inBatchReportStock(sts);
    daveragePriceSoldBatchStock(bb,sts) <- NUMERIC[22,8](dsumSoldBatchStock(bb,sts) / dquantitySoldBatchStock(bb,sts))
        WHERE inBatchReportStock(sts); 
    daverageMarkupSoldBatchStock(bb,sts) <- NUMERIC[22,8](dmarkupSumSoldBatchStock(bb,sts) / dcostSumSoldBatchStock(bb,sts))
        WHERE inBatchReportStock(sts);
    //--
    dbalanceABatchStock(bb,sts) <- balanceABatchStockDate(bb,sts,dTo) 
        WHERE inBatchReportStock(sts);
    dcostABatchStock(bb,sts) <- NUMERIC[15,3](dbalanceABatchStock(bb,sts) * costBatch(bb))
        WHERE inBatchReportStock(sts); 
    dVATSumABatchStock(bb, sts) <- NUMERIC[15,3](dcostABatchStock(bb, sts) * valueVATStockBatchDate(sts, bb, dateBatch(bb)) / 100.0)
        WHERE inBatchReportStock(sts);
    dSumABatchStock(bb, sts) <- NUMERIC[15,3](dcostABatchStock(bb, sts) (+) dVATSumABatchStock(bb, sts))
        WHERE inBatchReportStock(sts);           
    daccountSumABatchStock(bb,sts) <- NUMERIC[15,3](dbalanceABatchStock(bb,sts) * accountPriceABatchStockDate(bb,sts,dTo))
        WHERE inBatchReportStock(sts); 
    
    dmovedBatchStock(bb, sts) <- dquantitySoldBatchStock(bb,sts) OR dbalanceBBatchStock(bb,sts) OR dbalanceABatchStock(bb,sts);
    
    overFillPropertyDateFromTo(dFrom,dTo);
}                      
        
FORM saleBatch 'Продажи по партиям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)
       
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType    
   
    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES READONLY orderGroup(sk), skuTreeName = nameGroup(sk)
    ORDER BY orderGroup(sk), skuTreeName
    FILTERS groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' activeGroup(sk) 'F5' DEFAULT       
     
    OBJECTS ts = Stock
    PROPERTIES tsTreeName = nameStock(ts) READONLY, nameLegalEntityStock(ts) READONLY, inBatchReportStock(ts)
    ORDER BY tsTreeName
    FILTERS isCompanyStock(ts)     
    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(ts) 'ctrl F10' DEFAULT  
    OBJECTS st = Stock      
    FILTERS inBatchReportStock(st)
    OBJECTS b = Batch 
    PROPERTIES(b)  READONLY nameSkuBatch, idBarcodeSkuBatch, nameSkuGroup3Batch, nameSkuGroup4Batch, dateBatch, costBatch,                     
                   currentValueVATBatch                    
    FILTERGROUP inactiveStock2 FILTER 'Активный' activeStock(st) 'ctrl F10' DEFAULT                   
    PROPERTIES(b)  READONLY imageBatch FORCE PANEL
    PROPERTIES     READONLY BACKGROUND backgroundLavenderBatch(b) balanceBBatchStockDate(b,st,dFrom)  COLUMNS (st) HEADER nameBatchBalanceBStock(st)
    PROPERTIES     READONLY BACKGROUND backgroundLavenderBatch(b) accountSumBBatchStockDate(b,st,dFrom)  COLUMNS (st) HEADER nameBatchSumBStock(st)
    PROPERTIES     READONLY BACKGROUND backgroundGreenTeaBatch(b) quantitySoldBatchStockDateFromTo(b,st,dFrom,dTo)  COLUMNS (st) HEADER nameBatchQuantitySoldStock(st)  
    PROPERTIES     READONLY BACKGROUND backgroundGreenTeaBatch(b) sumSoldBatchStockDateFromTo(b,st,dFrom,dTo)  COLUMNS (st) HEADER nameBatchSumSoldStock(st)                       
    PROPERTIES     READONLY BACKGROUND backgroundSkyBatch(b) balanceABatchStockDate(b,st,dTo)  COLUMNS (st) HEADER nameBatchBalanceAStock(st)
    PROPERTIES     READONLY BACKGROUND backgroundSkyBatch(b) accountSumABatchStockDate(b,st,dTo)  COLUMNS (st) HEADER nameBatchSumAStock(st)    
                
    FILTERS           isParentGroupBatch(sk, b)                
                            
    FILTERGROUP filtersSold
        FILTER 'С движением' filterSoldBatchDateFromTo(b,dFrom,dTo) OR filterBalanceBatchDateFromTo(b,dFrom,dTo) 'F11' DEFAULT
        FILTER 'С продажей' filterSoldBatchDateFromTo(b,dFrom,dTo) 'F10'
        FILTER 'С остатком' filterBalanceBatchDateFromTo(b,dFrom,dTo) 'F9'      
                             
    OBJECTS stb = (sts=Stock, bb=Batch)                              
    PROPERTIES(sts)READONLY stsName = nameStock                          
    PROPERTIES(bb) READONLY nameSkuBatch, idBarcodeSkuBatch, nameSupplierBatch, nameSkuGroup3Batch, nameSkuGroup4Batch, dateBatch,  
                   costBatch
    PROPERTIES(sts, bb) READONLY currentValueVATStockBatch
                       
    PROPERTIES(bb,sts,dFrom) READONLY READONLY BACKGROUND backgroundLavenderBatch(bb) balanceBBatchStockDate, 
                   costBBatchStockDate, accountSumBBatchStockDate         
    PROPERTIES(bb,sts,dFrom,dTo) READONLY BACKGROUND backgroundGreenTeaBatch(bb) quantitySoldBatchStockDateFromTo, 
                   costSumSoldBatchStockDateFromTo, markupSumSoldBatchStockDateFromTo
    PROPERTIES(bb,sts,dFrom,dTo) READONLY BACKGROUND backgroundGreenTeaBatch(bb) sumVATSoldBatchStockDateFromTo, averagePriceSoldBatchStockDateFromTo,  
                   sumSoldBatchStockDateFromTo, averageMarkupSoldBatchStockDateFromTo
    FILTERGROUP inactiveStock3 FILTER 'Активный' activeStock(sts) 'ctrl F10' DEFAULT
                                       
    PROPERTIES(bb,sts,dTo) READONLY BACKGROUND backgroundSkyBatch(bb) balanceABatchStockDate, costABatchStockDate, accountSumABatchStockDate                 
                             
    FILTERS inBatchReportStock(sts),
            isParentGroupBatch(sk, bb)  
    FILTERGROUP filtersSold1
        FILTER 'С движением' quantitySoldBatchStockDateFromTo(bb,sts,dFrom,dTo) OR balanceBBatchStockDate(bb,sts,dFrom) OR balanceABatchStockDate(bb,sts,dTo) 'F11' DEFAULT
        FILTER 'С продажей' quantitySoldBatchStockDateFromTo(bb,sts,dFrom,dTo) 'F10'
        FILTER 'С остатком' balanceBBatchStockDate(bb,sts,dFrom) OR balanceABatchStockDate(bb,sts,dTo) 'F9'      

//-- Таблица                                                 
    OBJECTS stbs = (stst=Stock, bbb=Batch)                              
    PROPERTIES(stst)READONLY nameStock                          
    PROPERTIES(bbb) READONLY nameSkuBatch, idBarcodeSkuBatch, nameSupplierBatch, nameSkuGroup3Batch, nameSkuGroup4Batch, dateBatch,  
                   costBatch
    PROPERTIES(stst, bbb) READONLY currentValueVATStockBatch
                       
    PROPERTIES(bbb,stst) READONLY READONLY BACKGROUND backgroundLavenderBatch(bbb) dbalanceBBatchStock, 
                   dcostBBatchStock, dVATSumBBatchStock, dSumBBatchStock, daccountSumBBatchStock         
    PROPERTIES(bbb,stst) READONLY BACKGROUND backgroundGreenTeaBatch(bbb) dquantitySoldBatchStock, 
                   dcostSumSoldBatchStock, dmarkupSumSoldBatchStock
    PROPERTIES(bbb,stst) READONLY BACKGROUND backgroundGreenTeaBatch(bbb) dsumVATSoldBatchStock, daveragePriceSoldBatchStock,  
                   dsumSoldBatchStock, daverageMarkupSoldBatchStock
                   
                   
    FILTERGROUP inactiveStock4 FILTER 'Активный' activeStock(stst) 'ctrl F10' DEFAULT
                                       
    PROPERTIES(bbb,stst) READONLY BACKGROUND backgroundSkyBatch(bbb) dbalanceABatchStock, 
                    dcostABatchStock, dVATSumABatchStock, dSumABatchStock, daccountSumABatchStock                 
                             
    FILTERS inBatchReportStock(stst),
            isParentGroupBatch(sk, bbb)  
    FILTERGROUP filtersSold2
        FILTER 'С движением' dmovedBatchStock(bbb, stst) 'F11'
        FILTER 'С продажей' dquantitySoldBatchStock(bbb,stst) 'F10'
        FILTER 'С остатком' dbalanceBBatchStock(bbb,stst) OR dbalanceABatchStock(bbb,stst) 'F9'                                                      
    PROPERTIES fillPropertyBatchStockDateFromTo(dFrom,dTo) TODRAW  stbs FORCE PANEL TOOLBAR                                 
;

EXTEND FORM saleBatch FILTERS (ts IS Stock AND NOT limitAccessEmployee(currentUser())) OR accessCompanyEmployeeStock(currentUser(),ts);
EXTEND FORM saleBatch FILTERS (st IS Stock AND NOT limitAccessEmployee(currentUser())) OR accessCompanyEmployeeStock(currentUser(),st);
EXTEND FORM saleBatch FILTERS (sts IS Stock AND NOT limitAccessEmployee(currentUser())) OR accessCompanyEmployeeStock(currentUser(),sts);
EXTEND FORM saleBatch FILTERS (stst IS Stock AND NOT limitAccessEmployee(currentUser())) OR accessCompanyEmployeeStock(currentUser(),stst);
DESIGN saleBatch {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box {
            type = CONTAINERH;
        }
        MOVE gt.box;
    }
    REMOVE st.box;
    NEW article {
        type = SPLITH;
        fill = 1;
        NEW skuFilters {
            fill = 1;
            type = SPLITV;
            NEW column{
                fill = 1.5;
                type = SPLITV;
                MOVE ts.box;
                MOVE skuTree.tree.box { caption = 'Группы SKU'; }
            }    
            NEW imageBox {
                fill = 1;
                caption = 'Изображение';                     
                MOVE PROPERTY(imageBatch(b)) {
                    caption = '';
                    fill = 1;
                }
            }
        }
        NEW insetContainer {
            fill = 2.5;
            type = TABBED;            
            MOVE stbs.box { caption = 'Партия/склад (таблица)';}
            MOVE stb.box { caption = 'Партия/склад';}
            MOVE b.box; 
        }            
    } 
    MOVE functions.box;
}

//регистр продаж по партиям

costSumSaleLedgerBatch 'Себестоимость продажи' (s,b) = NUMERIC[16,2](costSumSaleLedger(s) * coeffSaleLedgerBatch(s,b));
markupSumSaleLedgerBatch 'Надбавка' (s,b) = NUMERIC[16,2](markupSumSaleLedger(s) * coeffSaleLedgerBatch(s,b));
sumVATSaleLedgerBatch 'Сумма НДС' (s,b) = NUMERIC[16,2](sumVATSaleLedger(s) * coeffSaleLedgerBatch(s,b));
sumSaleLedgerBatch 'Сумма продажи' (s,b) = NUMERIC[16,2](sumSaleLedger(s) * coeffSaleLedgerBatch(s,b));
averagePriceSaleLedgerBatch 'Цена продажи (средняя)'(s,b) = NUMERIC[16,2](averagePriceSaleLedger(s) * coeffSaleLedgerBatch(s,b));

saleSupplier = DATA LOCAL LegalEntity ();
nameSaleSupplier 'Поставщик' = nameLegalEntity(saleSupplier());
filterSaleSupplierBatch = supplierBatch(batch)==saleSupplier() OR (batch IS Batch AND NOT saleSupplier());

saleSupplierStock  = DATA LOCAL Stock ();
nameSaleSupplierStock 'Склад поставщика' = nameStock(saleSupplierStock());
filterSaleSupplierStockBatch = supplierStockBatch(batch)==saleSupplierStock() OR (batch IS Batch AND NOT saleSupplierStock());

CONSTRAINT saleSupplier() AND saleSupplierStock() AND NOT inLegalEntityStock(saleSupplier(), saleSupplierStock())
    CHECKED BY saleSupplierStock MESSAGE 'Поставщик и склад поставщика в фильтре для регистра продаж не имеют связи';

FORM saleLedgerBatch 'Регистр продаж по партиям'
    PROPERTIES() nameSaleStock, nameSaleOperation, nameSaleCustomer, nameSaleCustomerStock, sessionConcatSkuGroups ON CHANGE selectSkuGroups(),
            nameSaleSupplier, nameSaleSupplierStock
    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS sb = (s = SaleLedger, b = Batch)
    PROPERTIES(s) READONLY nameMonthSaleLedger, nameDOWSaleLedger, dateSaleLedger, hourSaleLedger, dateTimeSaleLedger, 
                           nameStockSaleLedger, nameCustomerSaleLedger, nameOperationSaleLedger, descriptionSaleLedger, 
                           numberDocumentSaleLedger, nameSkuGroup2SkuSaleLedger, nameSkuGroup3SkuSaleLedger, nameSkuGroup4SkuSaleLedger, 
                           nameSkuSaleLedger 
    PROPERTIES(b) READONLY shippedQuantityBatch, costBatch, nameBatch, nameSupplierBatch, nameSupplierStockBatch
    PROPERTIES(s,b) READONLY costSaleLedgerBatch, costSumSaleLedgerBatch, markupSumSaleLedgerBatch, sumVATSaleLedgerBatch, sumSaleLedgerBatch, averagePriceSaleLedgerBatch
    PROPERTIES(s) READONLY valueVATSaleLedger
    
    FILTERS activeSaleLedger(s), filterSaleStockSaleLedger(s), filterSaleOperationSaleLedger(s), filterSaleCustomerSaleLedger(s), filterSaleCustomerStockSaleLedger(s),
            filterSaleSupplierBatch(b), filterSaleSupplierStockBatch(b),
            dateSaleLedger(s) >= dFrom, dateSaleLedger(s) <= dTo    
    FILTERS costSaleLedgerBatch(s,b)    
    FILTERS inSessionSkuGroup(skuGroupSku(skuSaleLedger(s))) OR (s IS SaleLedger AND NOT countInSessionSkuGroups())
;

DESIGN saleLedgerBatch {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box {
            type = CONTAINERH;
        }
        NEW supCont{
            type = CONTAINERH;
            caption = 'Поставщик';
            MOVE PROPERTY(nameSaleSupplier());
            MOVE PROPERTY(nameSaleSupplierStock()); 
        }
    }
    NEW operationContainer{
        type = CONTAINERH;
        caption = 'Фильтры';
        MOVE PROPERTY(nameSaleStock());
        MOVE PROPERTY(nameSaleOperation());
        MOVE PROPERTY(nameSaleCustomer());
        MOVE PROPERTY(nameSaleCustomerStock()); 
        MOVE PROPERTY(sessionConcatSkuGroups());
    }

    MOVE sb.box;
    MOVE functions.box;
}

NAVIGATOR {                                              
    salesReports{
        ADD saleBatch;
        ADD saleLedgerBatch;
    }
}