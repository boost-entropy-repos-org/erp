MODULE SaleLedgerBatch;

REQUIRE SaleLedger, TaxItem, SkuLedgerTax;

NAMESPACE SaleLedger;

//------------------- Продажи по партиям -------------------------//

nameBatchBalanceB (Stock stock)= name(stock) + ' (остаток на начало)';    
nameBatchSumB (Stock stock)= name(stock) + ' (сумма на начало)';    
nameBatchBalanceA (Stock stock)= name(stock) + ' (остаток на конец)';    
nameBatchSumA (Stock stock)= name(stock) + ' (сумма на конец)';    
nameBatchQuantitySold (Stock stock)= name(stock) + ' (продано кол-во)';    
nameBatchSumSold (Stock stock)= name(stock) + ' (продано сумма)';    
 
inBatchReport 'Отм.' = DATA LOCAL BOOLEAN (Stock);
                  
filterBalanceDate 'С остатком' (b,dFrom, dTo) = GROUP SUM 1 IF inBatchReport(Stock st) AND (balanceB(Batch b,st,DATE dFrom) OR balanceA(b,st,DATE dTo))
    BY b,dFrom, dTo;
filterSoldDate 'С продажей' (b,dFrom,dTo) = GROUP SUM quantitySold(Batch b,Stock st,DATE dFrom,DATE dTo) IF inBatchReport(st) 
    BY b,dFrom,dTo;           
       
//-------------------------------------------------------------------------//
   
backgroundLavender 'Розовато-лавандовый' (Batch b) = RGB(255,238,238) IF b IS Batch;  
backgroundGreenTea 'Зеленого чая' (Batch b) = RGB(204,255,204) IF b IS Batch;  
backgroundSky 'Небесный' (Batch b) = RGB(127,199,255) IF b IS Batch;  

dbalanceB 'Остаток на начало дня' = DATA LOCAL NUMERIC[14,3] (Batch, Stock);
dcostB 'Себестоимость на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dVATSumB 'НДС поставщика на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dSumB 'Сумма поставщика с НДС на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
daccountSumB 'Учетная сумма на начало дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
        
dquantitySold 'Продано за интервал (кол-во)' = DATA LOCAL NUMERIC[14,3] (Batch, Stock);  
dcostSumSold 'Себестоимость проданного за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);   
dmarkupSumSold 'Надбавка проданного за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  

dsumVATSold 'Сумма НДС проданного за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
daveragePriceSold 'Цена за интервал' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
dsumSold 'Продано за интервал (сумма)' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
daverageMarkupSold 'Средний процент надбавки' = DATA LOCAL NUMERIC[22,8] (Batch, Stock);  
   
dbalanceA 'Остаток на конец дня' = DATA LOCAL NUMERIC[14,3] (Batch, Stock);
dcostA 'Себестоимость на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dVATSumA 'НДС поставщика на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
dSumA 'Сумма поставщика с НДС на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);
daccountSumA 'Учетная сумма на конец дня' = DATA LOCAL NUMERIC[15,3] (Batch, Stock);                     
    
dmoved = DATA LOCAL BOOLEAN (Batch, Stock);

overFillPropertyFromTo = ACTION ABSTRACT LIST (DATE, DATE);   
                   
fillPropertyBatchStockFromTo 'Заполнить данные'(DATE dFrom,DATE dTo)  = ACTION {
    dbalanceB(Batch bb,Stock sts) <- balanceB(bb,sts,dFrom) 
        WHERE inBatchReport(sts);
    dcostB(Batch bb,Stock sts) <- NUMERIC[15,3](dbalanceB(bb,sts) * cost(bb)) 
        WHERE inBatchReport(sts); 
    dVATSumB(Batch bb, Stock sts) <- NUMERIC[15,3](dcostB(bb, sts) * valueVAT(sts, bb, date(bb)) / 100.0)
        WHERE inBatchReport(sts);
    dSumB(Batch bb, Stock sts) <- NUMERIC[15,3](dcostB(bb, sts) (+) dVATSumB(bb, sts))
        WHERE inBatchReport(sts);         
    daccountSumB(Batch bb,Stock sts) <- NUMERIC[15,3](dbalanceB(bb,sts) * accountPriceB(bb,sts,dFrom)) 
        WHERE inBatchReport(sts); 
//--            
    dquantitySold(Batch bb,Stock sts) <- quantitySold(bb,sts,dFrom,dTo) 
        WHERE inBatchReport(sts);
    dcostSumSold(Batch bb,Stock sts) <- costSumSold(bb,sts,dFrom,dTo) 
        WHERE inBatchReport(sts);
    dmarkupSumSold(Batch bb,Stock sts) <- markupSumSold(bb,sts,dFrom,dTo) 
        WHERE inBatchReport(sts);     
    dsumVATSold(Batch bb,Stock sts) <- sumVATSold(bb,sts,dFrom,dTo) 
        WHERE inBatchReport(sts);
    dsumSold(Batch bb,Stock sts) <- sumSold(bb,sts,dFrom,dTo) 
        WHERE inBatchReport(sts);
    daveragePriceSold(Batch bb,Stock sts) <- NUMERIC[22,8](dsumSold(bb,sts) / dquantitySold(bb,sts))
        WHERE inBatchReport(sts); 
    daverageMarkupSold(Batch bb,Stock sts) <- NUMERIC[22,8](dmarkupSumSold(bb,sts) / dcostSumSold(bb,sts))
        WHERE inBatchReport(sts);
    //--
    dbalanceA(Batch bb,Stock sts) <- balanceA(bb,sts,dTo) 
        WHERE inBatchReport(sts);
    dcostA(Batch bb,Stock sts) <- NUMERIC[15,3](dbalanceA(bb,sts) * cost(bb))
        WHERE inBatchReport(sts); 
    dVATSumA(Batch bb, Stock sts) <- NUMERIC[15,3](dcostA(bb, sts) * valueVAT(sts, bb, date(bb)) / 100.0)
        WHERE inBatchReport(sts);
    dSumA(Batch bb, Stock sts) <- NUMERIC[15,3](dcostA(bb, sts) (+) dVATSumA(bb, sts))
        WHERE inBatchReport(sts);           
    daccountSumA(Batch bb,Stock sts) <- NUMERIC[15,3](dbalanceA(bb,sts) * accountPriceA(bb,sts,dTo))
        WHERE inBatchReport(sts); 
    
    dmoved(Batch bb, Stock sts) <- dquantitySold(bb,sts) OR dbalanceB(bb,sts) OR dbalanceA(bb,sts);
    
    overFillPropertyFromTo(dFrom,dTo);
}                      
        
FORM saleBatch 'Продажи по партиям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)
       
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR name    
   
    TREE skuTree sk = Group PARENT parent
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    ORDER BY order(sk), skuTreeName
    FILTERS groupType(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT       
     
    OBJECTS ts = Stock
    PROPERTIES tsTreeName = name(ts) READONLY, nameLegalEntity(ts) READONLY, inBatchReport(ts)
    ORDER BY tsTreeName
    FILTERS isCompany(ts)     
    FILTERGROUP inactiveStock FILTER 'Активный' active(ts) 'ctrl F10' DEFAULT  
    OBJECTS st = Stock      
    FILTERS inBatchReport(st)
    OBJECTS b = Batch 
    PROPERTIES(b)  READONLY nameSku, idBarcodeSku, nameSkuGroup3, nameSkuGroup4, date, cost,                     
                   currentValueVAT                    
    FILTERGROUP inactiveStock2 FILTER 'Активный' active(st) 'ctrl F10' DEFAULT                   
    PROPERTIES(b)  READONLY image FORCE PANEL
    PROPERTIES     READONLY BACKGROUND backgroundLavender(b) balanceB(b,st,dFrom)  COLUMNS (st) HEADER nameBatchBalanceB(st)
    PROPERTIES     READONLY BACKGROUND backgroundLavender(b) accountSumB(b,st,dFrom)  COLUMNS (st) HEADER nameBatchSumB(st)
    PROPERTIES     READONLY BACKGROUND backgroundGreenTea(b) quantitySold(b,st,dFrom,dTo)  COLUMNS (st) HEADER nameBatchQuantitySold(st)  
    PROPERTIES     READONLY BACKGROUND backgroundGreenTea(b) sumSold(b,st,dFrom,dTo)  COLUMNS (st) HEADER nameBatchSumSold(st)                       
    PROPERTIES     READONLY BACKGROUND backgroundSky(b) balanceA(b,st,dTo)  COLUMNS (st) HEADER nameBatchBalanceA(st)
    PROPERTIES     READONLY BACKGROUND backgroundSky(b) accountSumA(b,st,dTo)  COLUMNS (st) HEADER nameBatchSumA(st)    
                
    FILTERS           isParent(sk, b)                
                            
    FILTERGROUP filtersSold
        FILTER 'С движением' filterSoldDate(b,dFrom,dTo) OR filterBalanceDate(b,dFrom,dTo) 'F11' DEFAULT
        FILTER 'С продажей' filterSoldDate(b,dFrom,dTo) 'F10'
        FILTER 'С остатком' filterBalanceDate(b,dFrom,dTo) 'F9'      
                             
    OBJECTS stb = (sts=Stock, bb=Batch)                              
    PROPERTIES(sts)READONLY stsName = name                          
    PROPERTIES(bb) READONLY nameSku, idBarcodeSku, nameSupplier, nameSkuGroup3, nameSkuGroup4, date,  
                   cost
    PROPERTIES(sts, bb) READONLY currentValueVAT
                       
    PROPERTIES(bb,sts,dFrom) READONLY READONLY BACKGROUND backgroundLavender(bb) balanceB, 
                   costB, accountSumB         
    PROPERTIES(bb,sts,dFrom,dTo) READONLY BACKGROUND backgroundGreenTea(bb) quantitySold, 
                   costSumSold, markupSumSold
    PROPERTIES(bb,sts,dFrom,dTo) READONLY BACKGROUND backgroundGreenTea(bb) sumVATSold, averagePriceSold,  
                   sumSold, averageMarkupSold
    FILTERGROUP inactiveStock3 FILTER 'Активный' active(sts) 'ctrl F10' DEFAULT
                                       
    PROPERTIES(bb,sts,dTo) READONLY BACKGROUND backgroundSky(bb) balanceA, costA, accountSumA                 
                             
    FILTERS inBatchReport(sts),
            isParent(sk, bb)  
    FILTERGROUP filtersSold1
        FILTER 'С движением' quantitySold(bb,sts,dFrom,dTo) OR balanceB(bb,sts,dFrom) OR balanceA(bb,sts,dTo) 'F11' DEFAULT
        FILTER 'С продажей' quantitySold(bb,sts,dFrom,dTo) 'F10'
        FILTER 'С остатком' balanceB(bb,sts,dFrom) OR balanceA(bb,sts,dTo) 'F9'      

//-- Таблица                                                 
    OBJECTS stbs = (stst=Stock, bbb=Batch)                              
    PROPERTIES(stst)READONLY name                          
    PROPERTIES(bbb) READONLY nameSku, idBarcodeSku, nameSupplier, nameSkuGroup3, nameSkuGroup4, date,  
                   cost
    PROPERTIES(stst, bbb) READONLY currentValueVAT
                       
    PROPERTIES(bbb,stst) READONLY READONLY BACKGROUND backgroundLavender(bbb) dbalanceB, 
                   dcostB, dVATSumB, dSumB, daccountSumB         
    PROPERTIES(bbb,stst) READONLY BACKGROUND backgroundGreenTea(bbb) dquantitySold, 
                   dcostSumSold, dmarkupSumSold
    PROPERTIES(bbb,stst) READONLY BACKGROUND backgroundGreenTea(bbb) dsumVATSold, daveragePriceSold,  
                   dsumSold, daverageMarkupSold
                   
                   
    FILTERGROUP inactiveStock4 FILTER 'Активный' active(stst) 'ctrl F10' DEFAULT
                                       
    PROPERTIES(bbb,stst) READONLY BACKGROUND backgroundSky(bbb) dbalanceA, 
                    dcostA, dVATSumA, dSumA, daccountSumA                 
                             
    FILTERS inBatchReport(stst),
            isParent(sk, bbb)  
    FILTERGROUP filtersSold2
        FILTER 'С движением' dmoved(bbb, stst) 'F11'
        FILTER 'С продажей' dquantitySold(bbb,stst) 'F10'
        FILTER 'С остатком' dbalanceB(bbb,stst) OR dbalanceA(bbb,stst) 'F9'                                                      
    PROPERTIES fillPropertyBatchStockFromTo(dFrom,dTo) TODRAW  stbs FORCE PANEL TOOLBAR                                 
;

EXTEND FORM saleBatch FILTERS (ts IS Stock AND NOT limitAccess(currentUser())) OR accessCompany(currentUser(),ts);
EXTEND FORM saleBatch FILTERS (st IS Stock AND NOT limitAccess(currentUser())) OR accessCompany(currentUser(),st);
EXTEND FORM saleBatch FILTERS (sts IS Stock AND NOT limitAccess(currentUser())) OR accessCompany(currentUser(),sts);
EXTEND FORM saleBatch FILTERS (stst IS Stock AND NOT limitAccess(currentUser())) OR accessCompany(currentUser(),stst);
DESIGN saleBatch {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box {
            type = CONTAINERH;
        }
        MOVE gt.box;
    }
    REMOVE st.box;
    NEW article {
        type = SPLITH;
        fill = 1;
        NEW skuFilters {
            fill = 1;
            type = SPLITV;
            NEW column{
                fill = 1.5;
                type = SPLITV;
                MOVE ts.box;
                MOVE skuTree.tree.box { caption = 'Группы SKU'; }
            }    
            NEW imageBox {
                fill = 1;
                caption = 'Изображение';                     
                MOVE PROPERTY(image(b)) {
                    caption = '';
                    fill = 1;
                }
            }
        }
        NEW insetContainer {
            fill = 2.5;
            type = TABBED;            
            MOVE stbs.box { caption = 'Партия/склад (таблица)';}
            MOVE stb.box { caption = 'Партия/склад';}
            MOVE b.box; 
        }            
    } 
    MOVE functions.box;
}

//регистр продаж по партиям

averagePrice 'Цена продажи (средняя)'(SaleLedger s,Batch b) = NUMERIC[16,2](averagePrice(s) * coeff(s,b));

saleSupplier = DATA LOCAL LegalEntity ();
nameSaleSupplier 'Поставщик' = name(saleSupplier());
filterSaleSupplier = supplier(Batch batch)==saleSupplier() OR (batch IS Batch AND NOT saleSupplier());

saleSupplierStock  = DATA LOCAL Stock ();
nameSaleSupplierStock 'Склад поставщика' = name(saleSupplierStock());
filterSaleSupplierStock = supplierStock(Batch batch)==saleSupplierStock() OR (batch IS Batch AND NOT saleSupplierStock());

CONSTRAINT saleSupplier() AND saleSupplierStock() AND NOT in(saleSupplier(), saleSupplierStock())
    CHECKED BY saleSupplierStock[] MESSAGE 'Поставщик и склад поставщика в фильтре для регистра продаж не имеют связи';

FORM saleLedgerBatch 'Регистр продаж по партиям'
    PROPERTIES() nameSaleStock, nameSaleOperation, nameSaleCustomer, nameSaleCustomerStock, sessionConcatSkuGroups ON CHANGE selectSkuGroups(),
            nameSaleSupplier, nameSaleSupplierStock
    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS sb = (s = SaleLedger, b = Batch)
    PROPERTIES(s) READONLY nameMonth, nameDOW, date, hour, dateTime, 
                           nameStock, nameCustomer, nameOperation, description, 
                           numberDocument, nameSkuGroup2Sku, nameSkuGroup3Sku, nameSkuGroup4Sku, 
                           nameSku, idBarcodeSku
    PROPERTIES(b) READONLY shippedQuantity, cost, name, nameSupplier, nameSupplierStock
    PROPERTIES(s,b) READONLY cost, costSum, markupSum, sumVAT, sum, averagePrice
    PROPERTIES(s) READONLY valueVAT
    PROPERTIES READONLY valueVAT(b)
    
    FILTERS active(s), filterSaleStock(s), filterSaleOperation(s), filterSaleCustomer(s), filterSaleCustomerStock(s),
            filterSaleSupplier(b), filterSaleSupplierStock(b),
            date(s) >= dFrom, date(s) <= dTo    
    FILTERS cost(s,b)    
    FILTERS inSession(skuGroup(sku(s))) OR (s IS SaleLedger AND NOT countInSessionSkuGroups())
;

DESIGN saleLedgerBatch {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box {
            type = CONTAINERH;
        }
        NEW supCont{
            type = CONTAINERH;
            caption = 'Поставщик';
            MOVE PROPERTY(nameSaleSupplier());
            MOVE PROPERTY(nameSaleSupplierStock()); 
        }
    }
    NEW operationContainer{
        type = CONTAINERH;
        caption = 'Фильтры';
        MOVE PROPERTY(nameSaleStock());
        MOVE PROPERTY(nameSaleOperation());
        MOVE PROPERTY(nameSaleCustomer());
        MOVE PROPERTY(nameSaleCustomerStock()); 
        MOVE PROPERTY(sessionConcatSkuGroups());
    }

    MOVE sb.box;
    MOVE functions.box;
}

NAVIGATOR {                                              
    salesReports{
        ADD saleBatch;
        ADD saleLedgerBatch;
    }
}