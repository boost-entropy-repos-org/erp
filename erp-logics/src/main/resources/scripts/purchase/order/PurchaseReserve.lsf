MODULE PurchaseReserve;

REQUIRE StockReserve, PurchaseShipment;

NAMESPACE Purchase;

//-----------------------------------------------------------------------------------------------------------//

dateFromSoldPeriodUserOrder 'Дата с' = DATA DATE (UserOrder);
dateToSoldPeriodUserOrder 'Дата по' = DATA DATE (UserOrder);

WHEN SESSION SET(o IS UserOrder) DO {
    IF o IS UserOrder AND NOT dateFromSoldPeriodUserOrder(o) THEN dateFromSoldPeriodUserOrder(o) <- currentDate();
    IF o IS UserOrder AND NOT dateToSoldPeriodUserOrder(o) THEN dateToSoldPeriodUserOrder(o) <- currentDate();
};

WHEN SESSION FORMS userOrder CHANGED (fromShipmentDateUserOrder(o)) DO {
    dateFromSoldPeriodUserOrder(o) <-  fromShipmentDateUserOrder(o);   
}
WHEN SESSION FORMS userOrder CHANGED (dateUserOrder(o)) DO {
    dateToSoldPeriodUserOrder(o) <-  dateUserOrder(o);   
}

quantitySoldSkuStockUserOrder 'Продано' (sku, stock, userOrder) = quantitySoldSkuStockDateFromTo(sku, stock, dateFromSoldPeriodUserOrder(userOrder), dateToSoldPeriodUserOrder(userOrder));
recQuantitySkuStockUserOrder  (sku, stock, userOrder) = stockReserveStockSkuDate(stock, sku, dateUserOrder(userOrder))  (+) quantitySoldSkuStockUserOrder(sku, stock, userOrder) (-)
                                                        (currentReservePurchaseSkuStock(sku, stock) IF userOrder IS UserOrder)  (-) (currentBalanceSkuStock(sku, stock) IF userOrder IS UserOrder);

recommendedQuantitySkuStockUserOrder 'Рекомендуемое к закупке кол-во' (sku, stock, userOrder) = recQuantitySkuStockUserOrder(sku, stock, userOrder) IF recQuantitySkuStockUserOrder(sku, stock, userOrder) > 0;
stockReserveSkuStockUserOrder 'Страховой запас (кол-во)' (sku, stock, userOrder) = stockReserveStockSkuDate(stock, sku, dateUserOrder(userOrder));

diffStockReserveStockSkuDate 'Потребность' (stock, sku, date) = (overStockReserveStockSkuDate(stock, sku, date) (-) (currentBalanceSkuStock(sku, stock) IF date IS DATE)) IF overStockReserveStockSkuDate(stock, sku, date)>0;
diffStockReserveStockSkuDateTime 'Потребность' (stock, sku, dateTime) = diffStockReserveStockSkuDate(stock, sku, toDate(dateTime));

overStockReserveStockSkuDateTime 'Страховой запас (кол-во)'(stock, sku, dateTime) = overStockReserveStockSkuDate(stock, sku, toDate(dateTime));

numberSkuStockReserveSupplierStockPriceListTypeDateTime 'Кол-во товаров' (supplier, stock, priceListType, dateTime) =
    GROUP SUM 1 IF (diffStockReserveStockSkuDateTime(stock, sku, dateTime) > 0 AND
                    companyALedgerPriceListTypeSkuStockDateTime(priceListType, sku, stock, dateTime) == supplier)
          BY supplier, stock, priceListType, dateTime;

limitRecommendedQuantitySkuUserOrder = ABSTRACT BOOLEAN (Sku, UserOrder);

fillRecommendedQuantityStockUserOrder 'Заполнить рекомендуемым' = ACTION (userOrder) {
    FOR priceALedgerPriceListTypeSkuStockCompanyDateTime(
            ledgerPriceListTypePriceListType(priceListTypeUserOrderSku(userOrder, sku)),
            sku, customerStockUserOrder(userOrder), supplierUserOrder(userOrder), dateTimeUserOrder(userOrder))
        AND NOT limitRecommendedQuantitySkuUserOrder(sku, userOrder) 
        AND recommendedQuantitySkuStockUserOrder (sku, customerStockUserOrder(userOrder), userOrder)
        ADDOBJ d = UserOrderDetail DO {
            userOrderUserOrderDetail(d) <- userOrder;
            skuUserOrderDetail(d) <- sku;
            quantityUserOrderDetail (d) <- recommendedQuantitySkuStockUserOrder (sku, customerStockUserOrder(userOrder), userOrder);
        }
} TOOLBAR CONFIRM;

EXTEND FORM userOrder
    PROPERTIES(o) TODRAW sts FORCE PANEL dateFromSoldPeriodUserOrder, dateToSoldPeriodUserOrder

    PROPERTIES READONLY AFTER prevCurrentBalanceSkuStock(ks,st) stockReserveSkuStockUserOrder(ks,st,o), quantitySoldSkuStockUserOrder(ks,st,o)
    PROPERTIES READONLY AFTER quantitySkuUserOrderCustomerStock(ks, o, st)
        recommendedQuantitySkuStockUserOrder(ks,st,o) ON SHORTCUT fillRecommendedQuantityStockUserOrder(o)
;

EXTEND DESIGN userOrder {
    sts.box {           
        NEW filterDate FIRST {
            caption= 'Период для расчета продаж';
            type = CONTAINERH;
            ADD PROPERTY (dateFromSoldPeriodUserOrder(o));
            ADD PROPERTY (dateToSoldPeriodUserOrder(o));
      
        }
    }
}