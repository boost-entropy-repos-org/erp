MODULE PurchaseReserve;

REQUIRE PurchaseShipment, PurchaseReturnShipment, PurchaseCreditNote, PurchaseReturnCreditNote;

NAMESPACE Purchase;

//-----------------------------------------------------------------------------------------------------------//

quantitySoldSkuStockUserOrder 'Продано' (sku, stock, userOrder) = quantitySoldSkuStockDateFromTo(sku, stock, fromShipmentDateUserOrder(userOrder), dateUserOrder(userOrder));
recQuantitySkuStockUserOrder  (sku, stock, userOrder) = stockReserveStockSkuDate(stock, sku, dateUserOrder(userOrder))  (+) quantitySoldSkuStockUserOrder(sku, stock, userOrder) (-)
                                                        (currentReservePurchaseSkuStock(sku, stock) IF userOrder IS UserOrder)  (-) (currentBalanceSkuStock(sku, stock) IF userOrder IS UserOrder);

recommendedQuantitySkuStockUserOrder 'Рекомендуемое к закупке кол-во' (sku, stock, userOrder) = recQuantitySkuStockUserOrder(sku, stock, userOrder) IF recQuantitySkuStockUserOrder(sku, stock, userOrder) >0;
stockReserveSkuStockUserOrder 'Страховой запас (кол-во)' (sku, stock, userOrder) = stockReserveStockSkuDate(stock, sku, dateUserOrder(userOrder));

fillRecommendedQuantityStockUserOrder 'Заполнить рекомендуемым' = ACTION (userOrder) {
    FOR priceLedgerPriceListTypeSkuStockCompanyDateTime(
            ledgerPriceListTypePriceListType(priceListTypeUserOrderSku(userOrder, sku)),
            sku, customerStockUserOrder(userOrder), supplierUserOrder(userOrder), dateTimeUserOrder(userOrder))
        AND recommendedQuantitySkuStockUserOrder (sku, customerStockUserOrder(userOrder), userOrder)
        ADDOBJ d = UserOrderDetail DO {
            SET userOrderUserOrderDetail(d) <- userOrder;
            SET skuUserOrderDetail(d) <- sku;
            SET quantityUserOrderDetail (d) <- recommendedQuantitySkuStockUserOrder (sku, customerStockUserOrder(userOrder), userOrder);
        }
} TOOLBAR CONFIRM;

EXTEND FORM userOrder
    PROPERTIES READONLY AFTER currentBalanceSkuStock(ks,st) quantitySoldSkuStockUserOrder(ks,st,o), stockReserveSkuStockUserOrder(ks,st,o)
    PROPERTIES READONLY AFTER quantitySkuUserOrderCustomerStock(ks,o,st) recommendedQuantitySkuStockUserOrder(ks,st,o)
    PROPERTIES  fillRecommendedQuantityStockUserOrder(o) TODRAW ks
;