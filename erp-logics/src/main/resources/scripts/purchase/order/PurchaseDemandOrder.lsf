MODULE PurchaseDemandOrder;

REQUIRE System,
        Utils,
        Historizable,
        Stock,
        Barcode,
        PurchaseOrder,
        StockReserve,
        PurchaseReserve,
        PriceList,
        Document;

NAMESPACE Purchase;

diffStockReserveStockSkuDate 'Потребность' (stock, sku, date) = (overStockReserveStockSkuDate(stock, sku, date) (-) (currentBalanceSkuStock(sku, stock) IF date IS DATE)) IF overStockReserveStockSkuDate(stock, sku, date)>0;
diffStockReserveStockSkuDateTime 'Потребность' (stock, sku, dateTime) = diffStockReserveStockSkuDate(stock, sku, toDate(dateTime));

overStockReserveStockSkuDateTime 'Страховой запас (кол-во)'(stock, sku, dateTime) = overStockReserveStockSkuDate(stock, sku, toDate(dateTime));

numberSkuStockReserveSupplierStockPriceListTypeDateTime 'Кол-во товаров' (supplier, stock, priceListType, dateTime) =
    GROUP SUM 1 IF (diffStockReserveStockSkuDateTime(stock, sku, dateTime) > 0 AND
                    companyALedgerPriceListTypeSkuStockDateTime(priceListType, sku, stock, dateTime) == supplier)
          BY supplier, stock, priceListType, dateTime;

createDemandUserOrder 'Создать заказ' = ACTION (supplier, stock, priceListType, dateTime) NEWSESSION {
    FOR ADDOBJ o = Purchase.UserOrder DO {
        ASSIGN dateUserOrder(o) <- toDate(dateTime);
        ASSIGN timeUserOrder(o) <- toTime(dateTime);
        ASSIGN supplierUserOrder(o) <- supplier;
        ASSIGN supplierStockUserOrder(o) <- defaultSupplierStockEmployeeLegalEntity(currentUser(), supplier)
            IF countAccessSupplierStockEmployeeLegalEntity (currentUser(), supplier) == 1;
        ASSIGN customerUserOrder(o) <- legalEntityStock(stock);
        ASSIGN customerStockUserOrder(o) <- stock;
        ASSIGN priceListTypeUserOrder(o) <- priceListType;

        FOR priceALedgerPriceListTypeSkuStockCompanyDateTime(priceListType, sku, stock, supplier, dateTime)
            AND recommendedQuantitySkuStockUserOrder (sku, stock, o)
            ADDOBJ d = UserOrderDetail DO {
                ASSIGN userOrderUserOrderDetail(d) <- o;
                ASSIGN skuUserOrderDetail(d) <- sku;
                ASSIGN quantityUserOrderDetail (d) <- recommendedQuantitySkuStockUserOrder (sku, stock, o);
        }

        FORM userOrder OBJECTS o = o MANAGESESSION DOCKEDMODAL;
    }
}

FORM demandOrder 'Заказы по потребностям'
    OBJECTS dt = DATETIME FIXED PANEL
    PROPERTIES valDt = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES (st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS p = PriceListType FIXED PANEL
    PROPERTIES (p) SELECTOR namePriceListType

    OBJECTS l = LegalEntity
    PROPERTIES(l) READONLY nameLegalEntity
    PROPERTIES createDemandUserOrder(l, st, p, dt) FORCE PANEL TODRAW l TOOLBAR
    ORDER BY nameLegalEntity
    FILTERS isSupplierLegalEntity(l)

    PROPERTIES numberSkuStockReserveSupplierStockPriceListTypeDateTime(l, st, p, dt)
    FILTERGROUP filtersLegalEntity
        FILTER 'Необходим заказ' 'F10' numberSkuStockReserveSupplierStockPriceListTypeDateTime(l, st, p, dt)>0

    TREE skuTree sk = SkuGroup PARENT parentSkuGroup
    PROPERTIES skuTreeName = nameSkuGroup(sk) READONLY
    ORDER BY skuTreeName
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(sk) DEFAULT

    OBJECTS s = Sku
    PROPERTIES READONLY nameSku(s), idBarcodeSku(s), shortNameUOMSku(s)
    ORDER BY nameSku
    FILTERS isParentSkuGroupSku(sk, s)

    PROPERTIES overStockReserveStockSkuDateTime(st, s, dt)
    PROPERTIES currentBalanceSkuStock(s, st)
    PROPERTIES diffStockReserveStockSkuDateTime(st, s, dt)

    FILTERS companyALedgerPriceListTypeSkuStockDateTime(p, s, st, dt) == l

    FILTERGROUP filterdiff
        FILTER 'С потребностью ' 'F11' diffStockReserveStockSkuDateTime(st, s, dt) > 0
;

DESIGN demandOrder FROM DEFAULT {
    main {
        NEW row2 BEFORE functions.box {
            fillVertical = 3;
            type = SPLITH;
            childConstraints = TO THE RIGHTBOTTOM;
            ADD skuTree.tree.box { caption = 'Товарные группы'; fillHorizontal = 1;}
            ADD s.box { fillHorizontal = 2;};
        };
        NEW row1 BEFORE row2 {
            childConstraints = TO THE BOTTOM;
            fillVertical = 2;
            NEW row11 {
                childConstraints = TO THE RIGHT;
                ADD dt.box;
                ADD st.box;
                ADD p.box;
            }
            NEW row12 {
                ADD l.box;
            }
        }
    }
}

NAVIGATOR {
    purchaseNavigator {
        stockReserveNavigator {
            ADD demandOrder;
        }
    }
}