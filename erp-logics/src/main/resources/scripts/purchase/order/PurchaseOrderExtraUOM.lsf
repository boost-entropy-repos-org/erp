MODULE PurchaseOrderExtraUOM;

REQUIRE PurchaseOrder, ExtraUOM, LegalEntityExtraUOM, PurchaseLedgerExtraUOM;

NAMESPACE Purchase;

extraQuantity 'Кол-во(ед. поставщика)' = ABSTRACT NUMERIC[16,5] (OrderDetail) MATERIALIZED;
extraQuantity 'Кол-во(ед. поставщика)' = DATA NUMERIC[16,5] (UserOrderDetail);
extraQuantity (UserOrderDetail orderDetail) += extraQuantity(orderDetail);

extraUOM = ABSTRACT UOM (OrderDetail) MATERIALIZED;
extraUOM = DATA UOM (UserOrderDetail);
extraUOM (UserOrderDetail orderDetail) += extraUOM(orderDetail);

shortNameExtraUOMSku 'Ед. изм. поставщика' (OrderDetail detail)= shortName(extraUOM(detail));
shortNameExtraUOMSku 'Ед. изм. поставщика' (UserOrderDetail detail)= shortName(extraUOM(detail));

showExtraUOM(OrderDetail detail) = extraUOM(supplier(detail), sku(detail));

WHEN LOCAL CHANGED(sku(UserOrderDetail detail)) OR CHANGED(supplier(detail)) DO {
    extraUOM(detail) <- extraUOM(supplier(detail), sku(detail));
}

WHEN LOCAL CHANGED(extraQuantity(UserOrderDetail detail)) AND extraUOM(detail) DO {
    quantity(detail) <- extraQuantity(detail) * coefficient(extraUOM(detail), UOM(sku(detail)));
}

WHEN LOCAL CHANGED(quantity(UserOrderDetail detail)) AND extraUOM(detail) DO {
    extraQuantity(detail) <- quantity(detail) * coefficient(UOM(sku(detail)), extraUOM(detail));
}

EXTEND FORM userOrder
    PROPERTIES(d) SHOWIF showExtraUOM(d) shortNameExtraUOMSku BEFORE shortNameUOMSku(d), extraQuantity BEFORE quantity(d) 
;

EXTEND FORM orders
    PROPERTIES(d) READONLY SHOWIF showExtraUOM(d) shortNameExtraUOMSku BEFORE shortNameUOMSku(d), extraQuantity BEFORE quantity(d) 
;
