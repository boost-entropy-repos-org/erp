MODULE PurchaseShipment;

REQUIRE Shipment, PurchaseInvoice, PurchaseOperation, OrderShipment, InvoiceShipment, PriceListLedger;

NAMESPACE Purchase;

//----------------------------------------------- Поставка ---------------------------------------------------//

@defineShipment('Поставки', ' (закупка)', customerStock, supplier, company, company);
@extendFormFilterAccessStock(Shipment, s, shipments, customerStock, company);
@extendFormFilterAccessStock(Shipment, s, shipments, supplierStock, supplier);
@extendFormFilterAccessLegalEntity(Shipment, s, shipments, customer, company);
@extendFormFilterAccessLegalEntity(Shipment, s, shipments, supplier, supplier);

@defineShipmentBatch(customerStock);
@defineShipmentBatchDialog();

expiryDateUserShipmentDetail(detail) <- prevExpiryDateBatch(batchUserShipmentDetail(detail)) WHEN CHANGED (batchUserShipmentDetail(detail));

@defineShipmentStockDestination(supplierStock, customerStock);

@defineOrderShipment(' (закупка)', customerStock);
@defineInvoiceShipment(' (закупка)', customerStock, company);
@defineInvoiceShipmentAction(' (закупка)');

@defineInvoiceShipmentBatch();

isShippedInvoice = shippedInvoice(invoice) OR (isPostedInvoice(invoice) AND quantityNotChargeInvoiceDetailInvoice(invoice)==0.0) OR (isPostedInvoice(invoice) AND NOT quantityNotChargeInvoiceDetailInvoice(invoice));

statusShippedInvoice 'Статус приемки' (invoice)= IF isShippedInvoice (invoice) 
                                                    THEN 'Оприходована'
                                                    ELSE 'Не оприходована' IF invoice IS Invoice;
                                                                                                                                                                                                                     
backgroundShipInvoice 'Цвет' (invoice)= RGB(212,255,212) IF invoice IS Invoice AND NOT (shippedInvoice(invoice) OR (isPostedInvoice(invoice) AND quantityNotChargeInvoiceDetailInvoice(invoice)==0.0) OR (isPostedInvoice(invoice) AND NOT quantityNotChargeInvoiceDetailInvoice(invoice)));  

EXTEND FORM invoices
    PROPERTIES (i) READONLY statusShippedInvoice BACKGROUND backgroundShipInvoice(i) AFTER isCommissionInvoice(i)
    
    FILTERGROUP shipped 
        FILTER 'Не оприходованы' 'F11' i IS Invoice AND NOT (shippedInvoice(i) OR (isPostedInvoice(i) AND quantityNotChargeInvoiceDetailInvoice(i)==0.0) OR (isPostedInvoice(i) AND NOT quantityNotChargeInvoiceDetailInvoice(i)))
        FILTER 'Оприходованы' 'F10' shippedInvoice(i) OR (isPostedInvoice(i) AND quantityNotChargeInvoiceDetailInvoice(i)==0.0) OR (isPostedInvoice(i) AND NOT quantityNotChargeInvoiceDetailInvoice(i))
;    

//----------------------------------------------- Операции -----------------------------------------------------//

@defineDocumentOperationContainer(shipment, s);

@defineDocumentOperationLegalEntity(userShipment, supplier, 'Поставщик');
@deriveDocumentOperationLegalEntity(userShipment, supplier, userShipment);
@defineDocumentOperationLegalEntity(userShipment, customer, 'Покупатель');
@deriveDocumentOperationLegalEntity(userShipment, customer, userShipment);
@defineDocumentOperationStock(userShipment, supplierStock, 'Склад поставщика');
@deriveDocumentOperationStock(userShipment, supplierStock, userShipment);
@defineDocumentOperationStock(userShipment, customerStock, 'Склад покупателя');
@deriveDocumentOperationStock(userShipment, customerStock, userShipment);

@defineDocumentOperationRole(userShipment);

@defineOperationProperty(createShipment, 'Поставка', createContainer);
@deriveDocumentOperationProperty(UserInvoice, createShipment);

overFillInvoiceUserShipmentInvoice(s, i) += ACTION (s, i) {
    ASSIGN operationUserShipment(s) <- operationInvoice(i);
}
operationShipment(shipment) += operationInvoice(invoiceInvoiceShipment(shipment));

// Добавляем в копирование поставок
overCopyShipment(s, d) += ACTION(s, d) {
    ASSIGN operationUserShipment(d) <- operationShipment(s);
}
//------------------------------ Ограничение на выбор контрагентов -----------------------------//

CONSTRAINT supplierUserShipment(userShipment) AND NOT isSupplierLegalEntity(supplierUserShipment(userShipment))
    CHECKED BY supplierUserShipment MESSAGE 'Для поставки выбрано в качестве поставщика организация, не являющаяся поставщиком: закупка';
CONSTRAINT customerUserShipment(userShipment) AND NOT isCompanyLegalEntity(customerUserShipment(userShipment))
    CHECKED BY customerUserShipment MESSAGE 'Для поставки выбрано в качестве покупателя организация, не являющаяся компанией: закупка';

//------------------------------ Ввод в упаковках -----------------------------//

@defineDocumentPack(shipment, s);
Shipment.packQuantityShipmentDetail(detail) += packQuantityShipmentDetail(detail);
EXTEND DESIGN userShipment {
    headerExtraParams {
        NEW headerPack {
            caption = 'Упаковка';
            ADD PROPERTY(showPackUserShipment);
        }
    }
}
@deriveDocumentOperationProperty(UserShipment, showPack);

@defineOrderShipmentPack();
@defineInvoiceShipmentPack();

//------------------------------ Автоматическое проставление свойств -----------------------------//

@defineDocumentSupplierCustomerStockAccess(UserShipment, supplier, company, userShipment);

//------------------------------ Расширение формы -----------------------------//

EXTEND FORM userShipment

    FILTERGROUP filter
        FILTER 'С остатком ' 'F10' prevCurrentBalanceSkuUserShipment(ks, s)
        FILTER 'В документе ' 'F9' quantityUserShipmentDetailSkuUserShipment(ks, s)
    FILTERGROUP filter2
        FILTER 'С поступлением ' 'F8' quantityPurchaseSupplierSku(supplierUserShipment(s), ks)
//        FILTER 'В ассортименте ' 'F7' companyALedgerPriceListTypeSkuStockDateTime(priceListTypeUserShipmentSku(s, ks), ks, customerStockUserShipment(s), dateTimeUserShipment(s)) == supplierUserShipment(s)
;
EXTEND FORM userShipment

    FILTERGROUP filter3
        FILTER 'С остатком ' 'F10' prevCurrentBalanceBatchUserShipment(b, s)
        FILTER 'В документе ' 'F9' quantityUserShipmentDetailBatchUserShipment(b, s)
    FILTERGROUP filter4
        FILTER 'С поступлением ' 'F8' quantityPurchaseSupplierSku(supplierUserShipment(s), skuBatch(b))
//        FILTER 'В ассортименте ' 'F7' companyALedgerPriceListTypeBatchStockDateTime(priceListTypeUserShipmentBatch(s, b), b, customerStockUserShipment(s), dateTimeUserShipment(s)) == supplierUserShipment(s)
;

// Резервы
@extendFormDocumentSkuOrderLedger(userShipment, userShipment, s, customerStock);
@extendFormDocumentSkuOrderLedgerAll(userShipment, userShipment, s);

@extendFormDocumentBatchOrderLedger(userShipment, userShipment, s, customerStock);
@extendFormDocumentBatchOrderLedgerAll(userShipment, userShipment, s);

// ------------------------------- Расчет учетной цены для поставки ------------------------ //
overShipmentPriceUserInvoiceDetail = ABSTRACT CASE NUMERIC[14,2] (UserInvoiceDetail) PERSISTENT;
calcShipmentPriceUserInvoiceDetail (detail) = OVERRIDE costPriceUserInvoiceDetail(detail), overShipmentPriceUserInvoiceDetail(detail) PERSISTENT;

extraShipmentPriceUserInvoiceDetail = ABSTRACT NUMERIC[14,2] (UserInvoiceDetail) PERSISTENT;

// Цены учетные
shipmentPriceUserInvoiceDetail(detail) <- calcShipmentPriceUserInvoiceDetail(detail) (+) extraShipmentPriceUserInvoiceDetail(detail) WHEN
    CHANGED(calcShipmentPriceUserInvoiceDetail(detail)) OR
    CHANGED(extraShipmentPriceUserInvoiceDetail(detail));
priceUserShipmentDetail (detail) <- shipmentPriceInvoiceDetail(invoiceDetailUserShipmentDetail(detail))
    WHEN CHANGED(priceInvoiceDetail(invoiceDetailUserShipmentDetail(detail)));

// Цену записываем всегда, чтобы она проставлялась при создании поставки отдельно
//      OR
//     CHANGED(createShipmentUserInvoiceDetail(detail)))
//        AND createShipmentUserInvoiceDetail(detail);

// ------------------------------- Коды партии ----------------------------------- //

idBatchShipmentDetail 'Код партии' = ABSTRACT VARSTRING[100] (ShipmentDetail) MINCHARWIDTH 10 PREFCHARWIDTH 15;
idBatchUserShipmentDetail 'Код партии' = DATA VARSTRING[100] (UserShipmentDetail) MINCHARWIDTH 10 PREFCHARWIDTH 15;
idBatchShipmentDetail(detail) += idBatchUserShipmentDetail(detail);

idBatchShipmentDetail(detail) += idBatchInvoiceDetail(invoiceDetailInvoiceShipmentDetail(detail));

EXTEND FORM userShipment
    PROPERTIES(d) READONLYIF batchUserShipmentDetail(d) SHOWIF showIDs()
                  idBatchUserShipmentDetail AFTER nameBatchUserShipmentDetail(d)
;
EXTEND FORM shipments
    PROPERTIES(d) READONLY SHOWIF showIDs()
                  idBatchShipmentDetail AFTER nameBatchShipmentDetail(d)
;

// ------------------------------- Проведение по регистру остатков ------------------------ //

overCreateBatchShipmentDetail = ABSTRACT BOOLEAN (ShipmentDetail);
createBatchShipmentDetail (detail) = (detail IS ShipmentDetail OR overCreateBatchShipmentDetail(detail)) AND NOT batchShipmentDetail(detail);

// Создаем партию
CLASS ShipmentBatch 'Партия на основе закупки';
@defineAggregation(shipmentDetail, shipmentBatch, createBatchShipmentDetail);

@defineSkuLedgerAggregation(shipmentDetail, shipmentBatch, sku, customerStock);
priceShipmentBatch (batch) = priceShipmentDetail(shipmentDetailShipmentBatch(batch));

@implementBatch(ShipmentBatch, sku, stock, price);
idBatch(ledger) += idBatchShipmentDetail(shipmentDetailShipmentBatch(ledger));
quantityBatch (ledger) += quantityShipmentDetail(shipmentDetailShipmentBatch(ledger));
expiryDateBatch (ledger) += expiryDateShipmentDetail(shipmentDetailShipmentBatch(ledger));
sumInSkuLedger (ledger) += sumShipmentDetail(shipmentDetailShipmentBatch(ledger));
seriesBatch (ledger) += seriesShipment(shipmentShipmentDetail(shipmentDetailShipmentBatch(ledger)));
numberBatch (ledger) += numberShipment(shipmentShipmentDetail(shipmentDetailShipmentBatch(ledger)));
supplierBatch (ledger) += supplierShipmentDetail(shipmentDetailShipmentBatch(ledger));
supplierStockBatch (ledger) += supplierStockShipmentDetail(shipmentDetailShipmentBatch(ledger));

inPriceListLedgerBatch (ledger, batch) += TRUE IF countShipmentDetailInvoiceDetailBatch(ledger, batch); 
inPriceListLedgerBatch (ledger, batch) += TRUE IF countShipmentDetailInvoiceDetailBatch(invoiceDetailInvoicePriceListLedger(ledger), batch); 

nameBatch(batch) += CONCAT '', STRING[10](dateShipmentDetail(shipmentDetailShipmentBatch(batch))), '/ ' + seriesNumberShipment(shipmentShipmentDetail(shipmentDetailShipmentBatch(batch))),
                             '/ ' + nameSupplierShipmentDetail(shipmentDetailShipmentBatch(batch));

// Проводим по регистру учетных цен
supplierShipmentBatch (ledger) = supplierShipmentDetail(shipmentDetailShipmentBatch(ledger));
@implementSystemLedgerPriceListTypeBatch(account, ShipmentBatch, supplier, stock);

// Создаем просто inLIFOSkuLedger
createSkuLedgerShipmentDetail (detail) = detail IS ShipmentDetail AND NOT createBatchShipmentDetail(detail);

CLASS ShipmentSkuLedger 'Изменение остатка на основе закупки';
@defineAggregation(shipmentDetail, shipmentSkuLedger, createSkuLedgerShipmentDetail);

@defineSkuLedgerAggregation(shipmentDetail, shipmentSkuLedger, sku, customerStock);
priceShipmentSkuLedger (ledger) = priceShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));

@implementSkuLedgerInLIFO(ShipmentSkuLedger, sku, stock);
quantityInLIFOSkuLedger (ledger) += quantityShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));
limitInLIFOSkuLedgerBatch(ledger, batch) += quantityShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)) IF batchShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)) == batch;
changedDataSkuLedger(ledger) += CHANGED(batchShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)));
sumInSkuLedger (ledger) += sumShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));

seriesDataSkuLedger (ledger) += seriesShipment(shipmentShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)));
numberDataSkuLedger (ledger) += numberShipment(shipmentShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)));
legalEntityDataSkuLedger (ledger) += supplierShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));
legalEntityStockDataSkuLedger (ledger) += supplierStockShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));

skuLedgerShipmentDetail (detail) = IF createBatchShipmentDetail(detail) THEN shipmentBatchShipmentDetail(detail) ELSE shipmentSkuLedgerShipmentDetail(detail);
ledgerBatchShipmentDetail (detail) = OVERRIDE shipmentBatchShipmentDetail(detail), batchShipmentDetail(detail); 
costShipmentDetailBatch(detail, batch) += costSkuLedgerBatch(skuLedgerShipmentDetail(detail), batch);

// ------------------------------- Проведение регистру учетных цен ------------------------ //

supplierShipmentSkuLedger (ledger) = supplierShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));
@implementSystemLedgerPriceListType(account, ShipmentSkuLedger, supplier, stock);
inPriceListLedgerBatch (ledger, batch) += batchShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)) == batch; 

// ------------------------------- Проведение по товарному отчету ------------------------ //
@implementStockDocumentLedgerInc(Shipment, customerStock);
sumIncStockDocumentLedger (ledger) += sumShipmentDetailShipment(ledger);
sumItemIncStockDocumentLedger (ledger) += sumItemShipmentDetailShipment(ledger);
sumContainerIncStockDocumentLedger (ledger) += sumContainerShipmentDetailShipment(ledger);

//
defaultUserShipmentDetailSkuUserShipmentPrice (sku, shipment, price) = GROUP MIN detail IF NOT batchUserShipmentDetail(detail)
    BY skuUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail), priceUserShipmentDetail(detail);

defaultUserShipmentDetailUserShipmentDetail(detail) = defaultUserShipmentDetailSkuUserShipmentPrice(skuUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail), priceUserShipmentDetail(detail));

mergeBatchUserShipment 'Соединить партии' = ACTION (shipment) {
    CONFIRM 'Документ будет автоматически сохранен, вы действительно хотите выполнить действие "Соединить партии?"';
    IF (confirmed()) THEN {
        EXEC apply();
        FOR userShipmentUserShipmentDetail(detail) == shipment
            AND NOT defaultUserShipmentDetailUserShipmentDetail(detail) == detail
            AND NOT batchUserShipmentDetail(detail) DO {
                ASSIGN batchUserShipmentDetail(detail) <- shipmentBatchShipmentDetail(defaultUserShipmentDetailUserShipmentDetail(detail));

        }
    }
} TOOLBAR;

EXTEND FORM userShipment

    PROPERTIES(s) TODRAW d mergeBatchUserShipment
;
//
invoiceDetailBatch 'Строка накладной партии' = invoiceDetailShipmentDetail(shipmentDetailShipmentBatch(batch));
descriptionInvoiceDetailInvoiceDetailBatch 'Строка накладной партии' = descriptionInvoiceDetail(invoiceDetailBatch(batch));

NAVIGATOR {
    purchaseStockNavigator {
        ADD shipments;
    }
}