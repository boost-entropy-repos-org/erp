MODULE PurchaseReports;

REQUIRE PurchaseLedger;

//отчет по поступлениям

dataInSessionGroup 'Отм.' = DATA SESSION BOOLEAN (Group);

levelParentGroup (group) = GROUP MIN levelGroupGroup(group, parent) IF dataInSessionGroup(parent)
    BY group;

inParentGroup (group) = TRUE IF levelParentGroup(group);

inSessionGroup 'Отм.' (group) = OVERRIDE
    inParentGroup(group),
    dataInSessionGroup(group);

sessionConcatGroups 'Группы' (groupType) =
    GROUP CONCAT nameGroup(group) IF inSessionGroup(group) AND NOT inSessionGroup(parentGroup(group)),','
    BY groupTypeGroup(group);

//суммы поступлений по группе
sumVATPurchasesGroupStockDateDate 'Сумма НДС (по группе)'(group, stock, dateFrom, dateTo) =
    GROUP SUM sumVATPurchaseLedger(purchaseLedger) IF isPostedPurchaseLedger(purchaseLedger) AND
                                                      datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                      datePurchaseLedger(purchaseLedger) <= dateTo
    BY groupGroupTypeSku(groupType, skuPurchaseLedger(purchaseLedger)), stockPurchaseLedger(purchaseLedger), dateFrom, dateTo;
sumPurchasesSkuGroupStockDateDate 'Сумма поступлений (по группе)' (group, stock, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(purchaseLedger) IF isPostedPurchaseLedger(purchaseLedger) AND
                                                   datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                   datePurchaseLedger(purchaseLedger) <= dateTo
    BY groupGroupTypeSku(groupType, skuPurchaseLedger(purchaseLedger)), stockPurchaseLedger(purchaseLedger), dateFrom, dateTo;
sumRecPurchasesSkuGroupStockDateDate 'Сумма поступлений (всего)' (group, stock, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(purchaseLedger) IF isParentGroupSku(group, skuPurchaseLedger(purchaseLedger)) AND
                                                   isPostedPurchaseLedger(purchaseLedger) AND
                                                   datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                   datePurchaseLedger(purchaseLedger) <= dateTo
    BY group, stockPurchaseLedger(purchaseLedger), dateFrom, dateTo;

//суммы поступлений по поставщику

sumVATPurchasesGroupTypeSupplierStockDateDate 'Сумма НДС (по поставщику)'(groupType, supplier, stock, dateFrom, dateTo) =
    GROUP SUM sumVATPurchaseLedger(purchaseLedger) IF isPostedPurchaseLedger(purchaseLedger) AND
                                                      inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(purchaseLedger))) AND
                                                      datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                      datePurchaseLedger(purchaseLedger) <= dateTo
    BY groupType, supplierPurchaseLedger(purchaseLedger), stockPurchaseLedger(purchaseLedger), dateFrom, dateTo;
sumPurchasesGroupTypeSupplierStockDateDate 'Сумма поступлений (по поставщику)' (groupType, supplier, stock, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(purchaseLedger) IF isPostedPurchaseLedger(purchaseLedger) AND
                                                   inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(purchaseLedger))) AND
                                                   datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                   datePurchaseLedger(purchaseLedger) <= dateTo
    BY groupType, supplierPurchaseLedger(purchaseLedger), stockPurchaseLedger(purchaseLedger), dateFrom, dateTo;

//итоговые суммы поступлений
sumVATPurchasesGroupTypeStockDateDate 'Итоговая сумма НДС поступлений' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM sumVATPurchaseLedger(purchaseLedger) IF inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(purchaseLedger))) AND
                                                       isPostedPurchaseLedger(purchaseLedger) AND
                                                       datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                       datePurchaseLedger(purchaseLedger) <= dateTo
    BY groupType, stockPurchaseLedger(purchaseLedger), dateFrom, dateTo PREFCHARWIDTH 15;
sumPurchasesGroupTypeStockDateDate 'Итоговая сумма поступлений' (groupType, stock, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(purchaseLedger) IF inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(purchaseLedger))) AND
                                                   isPostedPurchaseLedger(purchaseLedger) AND
                                                   datePurchaseLedger(purchaseLedger) >= dateFrom AND
                                                   datePurchaseLedger(purchaseLedger) <= dateTo
    BY groupType, stockPurchaseLedger(purchaseLedger), dateFrom, dateTo PREFCHARWIDTH 15;

//формы

FORM printListPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger
    FILTERS isPostedPurchaseLedger(p),
            stockPurchaseLedger(p) == st,
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt,
            inSessionGroup(groupGroupTypeSku(gt, skuPurchaseLedger(p)))
;

printListPurchasesReport 'Списком' (stock, dateFrom, dateTo) =
    ACTION FORM printListPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printListSkuPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(s, st, df, dt) READONLY quantityPurchaseSkuStockDateFromTo, sumVATPurchaseSkuStockDateFromTo,
                                       sumPurchaseSkuStockDateFromTo
    ORDER BY nameSku
    FILTERS inSessionGroup(groupGroupTypeSku(gt, s)),
            quantityPurchaseSkuStockDateFromTo(s, st, df, dt) OR sumVATPurchaseSkuStockDateFromTo(s, st, df, dt) OR
            sumPurchaseSkuStockDateFromTo(s, st, df, dt)
;

printListSkuPurchasesReport 'Списком(по товарам)' (stock, dateFrom, dateTo) =
    ACTION FORM printListSkuPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;


FORM printPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    PROPERTIES(sk, st, df, dt) READONLY sumVATPurchasesGroupStockDateDate, sumPurchasesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeGroup(sk) DEFAULT

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger
    FILTERS isPostedPurchaseLedger(p),
            stockPurchaseLedger(p) == st,
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt,
            groupGroupTypeSku(gt, skuPurchaseLedger(p)) == sk
;

printPurchasesReport 'По группам' (stock, dateFrom, dateTo) =
    ACTION FORM printPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printSkuPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    PROPERTIES(sk, st, df, dt) READONLY sumVATPurchasesGroupStockDateDate, sumPurchasesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeGroup(sk) DEFAULT

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(s, st, df, dt) READONLY quantityPurchaseSkuStockDateFromTo, sumVATPurchaseSkuStockDateFromTo,
                                       sumPurchaseSkuStockDateFromTo
    ORDER BY nameSku
    FILTERS groupGroupTypeSku(gt, s) == sk,
            quantityPurchaseSkuStockDateFromTo(s, st, df, dt) OR sumVATPurchaseSkuStockDateFromTo(s, st, df, dt) OR
            sumPurchaseSkuStockDateFromTo(s, st, df, dt)
;

printSkuPurchasesReport 'По товарам(группы)' (stock, dateFrom, dateTo) =
    ACTION FORM printSkuPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printGroupPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup
    PROPERTIES(sk, st, df, dt) READONLY sumVATPurchasesGroupStockDateDate, sumPurchasesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt,
            sumVATPurchasesGroupStockDateDate(sk, st, df, dt) OR sumPurchasesSkuGroupStockDateDate(sk, st, df, dt)
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeGroup(sk) DEFAULT
;

printGroupPurchasesReport 'По группам' (stock, dateFrom, dateTo) =
    ACTION FORM printGroupPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printGroupSupplierPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity
    PROPERTIES(gt, l, st, df, dt) READONLY sumVATPurchasesGroupTypeSupplierStockDateDate, sumPurchasesGroupTypeSupplierStockDateDate
    FILTERS isSellerLegalEntity(l)

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger
    FILTERS isPostedPurchaseLedger(p),
            inSessionGroup(groupGroupTypeSku(gt, skuPurchaseLedger(p))),
            stockPurchaseLedger(p) == st,
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt,
            supplierPurchaseLedger(p) == l
;

printGroupSupplierPurchasesReport 'По поставщикам' (stock, dateFrom, dateTo) =
    ACTION FORM printGroupSupplierPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printGroupSupplierSkuPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity
    PROPERTIES(gt, l, st, df, dt) READONLY sumVATPurchasesGroupTypeSupplierStockDateDate, sumPurchasesGroupTypeSupplierStockDateDate
    FILTERS isSellerLegalEntity(l)

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(s, st, l, df, dt) READONLY quantityPurchaseSkuStockSupplierDateFromTo, sumVATPurchaseSkuStockSupplierDateFromTo,
                                          sumPurchaseSkuStockSupplierDateFromTo
    ORDER BY nameSku
    FILTERS inSessionGroup(groupGroupTypeSku(gt, s)),
            quantityPurchaseSkuStockSupplierDateFromTo(s, st, l, df, dt) OR sumVATPurchaseSkuStockSupplierDateFromTo(s, st, l, df, dt) OR
            sumPurchaseSkuStockSupplierDateFromTo(s, st, l, df, dt)
;

printGroupSupplierSkuPurchasesReport 'По товарам(поставщики)' (stock, dateFrom, dateTo) =
    ACTION FORM printGroupSupplierSkuPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM printSupplierPurchasesReport 'Отчет по поступлениям' PRINT
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity
    PROPERTIES(gt, l, st, df, dt) READONLY sumVATPurchasesGroupTypeSupplierStockDateDate, sumPurchasesGroupTypeSupplierStockDateDate
    FILTERS isSellerLegalEntity(l),
            sumVATPurchasesGroupTypeSupplierStockDateDate(gt, l, st, df, dt) OR sumPurchasesGroupTypeSupplierStockDateDate(gt, l, st, df, dt)

;

printSupplierPurchasesReport 'По поставщикам' (stock, dateFrom, dateTo) =
    ACTION FORM printSupplierPurchasesReport OBJECTS st = stock, df = dateFrom, dt = dateTo  IMAGE 'print.png' IN printGroup;

FORM purchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES(st) SELECTOR nameStock
    FILTERS isCompanyStock(st)

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, st, df, dt) READONLY sumPurchasesGroupTypeStockDateDate
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES inSessionGroup(sk)
    PROPERTIES READONLY skuTreeName = nameGroup(sk)
    PROPERTIES(sk, st, df, dt) READONLY sumRecPurchasesSkuGroupStockDateDate
    ORDER BY skuTreeName
    FILTERS groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeGroup(sk) DEFAULT

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger
    FILTERS isParentGroupSku(sk, skuPurchaseLedger(p)),
            isPostedPurchaseLedger(p),
            stockPurchaseLedger(p) == st,
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt
    PROPERTIES(st, df, dt) printListPurchasesReport, printListSkuPurchasesReport, printPurchasesReport, printSkuPurchasesReport, printGroupPurchasesReport,
                           printGroupSupplierPurchasesReport, printGroupSupplierSkuPurchasesReport, printSupplierPurchasesReport
;

DESIGN purchasesReport FROM DEFAULT{
    NEW topContainer{
        childConstraints = TO THE RIGHT;
        NEW dateContainer{
            caption = 'Период';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(dateFrom){caption = 'Дата (с)';}
            ADD PROPERTY(dateTo){caption = 'Дата (по)';}
        }
        ADD st.box;
        ADD gt.box;
        NEW sumContainer{
            caption = 'Итоговые суммы';
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY(sumPurchasesGroupTypeStockDateDate);
        }
    }
    NEW bottomContainer{
        childConstraints = TO THE RIGHT;
        type = SPLITH;
        fill = 1;
        ADD skuTree.tree.box;
        NEW tabContainer{
            type = TABBED;
            fill = 2;
            NEW purchasesContainer{
                caption = 'Поступления';
                childConstraints = TO THE BOTTOM;
                NEW printPurchasesContainer{
                    childConstraints = TO THE RIGHT;
                    caption = 'Печать';
                    NEW rowPurchasesContainer{
                        childConstraints = TO THE RIGHTBOTTOM;
                        type = CONTAINERH;
                        NEW firstColumnPurchasesContainer{
                            caption = 'С детализацией';
                            type = COLUMNS;
                            columns = 2;
                            ADD PROPERTY(printPurchasesReport);
                            ADD PROPERTY(printSkuPurchasesReport);
                            ADD PROPERTY(printGroupSupplierPurchasesReport);
                            ADD PROPERTY(printGroupSupplierSkuPurchasesReport);
                        }
                        NEW secondColumnPurchasesContainer{
                            caption = 'Без детализации';
                            type = CONTAINERV;
                            ADD PROPERTY(printGroupPurchasesReport);
                            ADD PROPERTY(printSupplierPurchasesReport);
                        }
                        NEW thirdColumnPurchasesContainer{
                            caption = 'Список';
                            type = CONTAINERV;
                            ADD PROPERTY(printListPurchasesReport);
                            ADD PROPERTY(printListSkuPurchasesReport);
                        }
                    }
                }
                ADD p.box {fill = 1;}
            }
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    purchasesReports {
        ADD purchasesReport;
    }
}
