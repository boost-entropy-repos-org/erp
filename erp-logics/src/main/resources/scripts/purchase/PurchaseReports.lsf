MODULE PurchaseReports;

REQUIRE PurchaseLedger;

//отчет по поступлениям

inPurchaseReport 'Вкл.' = DATA LOCAL BOOLEAN (Stock);

inPurchaseReportStocks 'Склады' () =
    GROUP CONCAT name(Stock st) IF inPurchaseReport(st) ,', ' PREFCHARWIDTH 30;
defaultStockPurchaseReport = GROUP MIN Stock st IF inPurchaseReport(st);

dataInSession 'Отм.' = DATA LOCAL BOOLEAN (Group);

levelParent (group) = GROUP MIN level(Group group, Group parent) IF dataInSession(parent)
    BY group;

inParent (Group group) = TRUE IF levelParent(group);

inSession 'Отм.' (Group group) = OVERRIDE
    inParent(group),
    dataInSession(group);

sessionConcatGroups 'Группы' (groupType) =
    GROUP CONCAT name(Group group) IF inSession(group) AND NOT inSession(parent(group)),','
    BY groupType(group);

//суммы поступлений по группе
sumVATPurchasesStock 'Сумма НДС (по группе)'(group, dateFrom, dateTo) =
    GROUP SUM sumVAT(PurchaseLedger l) IF isParent(Group group, sku(l)) AND active(l) AND inPurchaseReport(stock(l)) AND
                                         date(l) >= DATE dateFrom AND date(l) <= DATE dateTo 
    BY group, dateFrom, dateTo;
sumPurchasesSkuStock 'Сумма поступлений (по группе)' (group, dateFrom, dateTo) =
    GROUP SUM sum(PurchaseLedger l) IF isParent(Group group, sku(l)) AND active(l) AND inPurchaseReport(stock(l)) AND
                                      date(l) >= DATE dateFrom AND date(l) <= DATE dateTo
    BY group, dateFrom, dateTo;
    
sumRecPurchasesSkuStock 'Сумма поступлений (всего)' (group, dateFrom, dateTo) =
    GROUP SUM sum(PurchaseLedger l) IF isParent(Group group, sku(l)) AND
                                      active(l) AND inPurchaseReport(stock(l)) AND
                                      date(l) >= DATE dateFrom AND date(l) <= DATE dateTo
    BY group, dateFrom, dateTo;

//суммы поступлений по поставщику

sumVATPurchasesStock 'Сумма НДС (по поставщику)'(groupType, supplier, dateFrom, dateTo) =
    GROUP SUM sumVAT(PurchaseLedger l) IF active(l) AND inPurchaseReport(stock(l)) AND
                                                      inSession(group(GroupType groupType, sku(l))) AND
                                                      date(l) >= DATE dateFrom AND date(l) <= DATE dateTo
    BY groupType, supplier(l), dateFrom, dateTo;
sumPurchasesStock 'Сумма поступлений (по поставщику)' (groupType, supplier, dateFrom, dateTo) =
    GROUP SUM sum(PurchaseLedger l) IF active(l) AND inPurchaseReport(stock(l)) AND
                                                   inSession(group(GroupType groupType, sku(l))) AND
                                                   date(l) >= DATE dateFrom AND date(l) <= DATE dateTo
    BY groupType, supplier(l), dateFrom, dateTo;

//итоговые суммы поступлений
sumVATPurchasesStock 'Итоговая сумма НДС поступлений' (groupType, dateFrom, dateTo) =
    GROUP SUM sumVAT(PurchaseLedger l) IF inSession(group(GroupType groupType, sku(l))) AND
                                                       active(l) AND inPurchaseReport(stock(l)) AND
                                                       date(l) >= DATE dateFrom AND date(l) <= DATE dateTo
    BY groupType, dateFrom, dateTo PREFCHARWIDTH 15;
sumPurchasesStock 'Итоговая сумма поступлений' (groupType, dateFrom, dateTo) =
    GROUP SUM sum(PurchaseLedger l) IF inSession(group(GroupType groupType, sku(l))) AND
                                                   active(l) AND inPurchaseReport(stock(l)) AND
                                                   date(l) >= DATE dateFrom AND date(l) <= DATE dateTo
    BY groupType, dateFrom, dateTo PREFCHARWIDTH 15;

//формы

FORM printListPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTime, nameSupplier, nameSku, description,
                           quantity, sumVAT, sum, averagePrice
    ORDER BY nameSku(p)
    FILTERS active(p),
            inPurchaseReport(stock(p)),
            date(p) >= df,
            date(p) <= dt,
            inSession(group(gt, sku(p)))
;

printListPurchasesReport 'Списком' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printListPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;

quantityPurchasesStock 'Закуплено за интервал (кол-во)' (sku, dateFrom, dateTo) = GROUP SUM
        quantityPurchase(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inPurchaseReport(stock)
        BY sku, dateFrom, dateTo;

sumVATPurchasesStock 'Сумма НДС закупленного за интервал' (sku, dateFrom, dateTo) = GROUP SUM
        sumVATPurchase(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inPurchaseReport(stock)
        BY sku, dateFrom, dateTo;

sumPurchasesStock 'Закуплено за интервал (сумма)' (sku, dateFrom, dateTo) = GROUP SUM
        sumPurchase(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inPurchaseReport(stock)
        BY sku, dateFrom, dateTo;

FORM printListSkuPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS s = Sku
    PROPERTIES(s) READONLY name
    PROPERTIES(s, df, dt) READONLY quantityPurchasesStock, sumVATPurchasesStock,
                                       sumPurchasesStock
    ORDER BY name(s)
    FILTERS inSession(group(gt, s)),
            quantityPurchasesStock(s, df, dt) OR sumVATPurchasesStock(s, df, dt) OR
            sumPurchasesStock(s, df, dt)
;

printListSkuPurchasesReport 'Списком(по товарам)' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printListSkuPurchasesReport OBJECTS gt =gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;


FORM printPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)
    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDER BY canonicalName(sk)
    PROPERTIES(sk, df, dt) READONLY sumVATPurchasesStock, sumPurchasesSkuStock
    FILTERS inSession(sk) AND countSku(sk, gt),
            groupType(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTime, nameSupplier, nameSku, description,
                           quantity, sumVAT, sum, averagePrice
    ORDER BY nameSku(p)
    FILTERS active(p),
            inPurchaseReport(stock(p)),
            date(p) >= df,
            date(p) <= dt,
            group(gt, sku(p)) == sk
;

printPurchasesReportDetail 'По группам' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;

FORM printSkuPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDER BY canonicalName(sk)
    PROPERTIES(sk, df, dt) READONLY sumVATPurchasesStock, sumPurchasesSkuStock
    FILTERS inSession(sk) AND countSku(sk, gt),
            groupType(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS s = Sku
    PROPERTIES(s) READONLY name
    PROPERTIES(s, df, dt) READONLY quantityPurchasesStock, sumVATPurchasesStock,
                                       sumPurchasesStock
    ORDER BY name(s)
    FILTERS group(gt, s) == sk,
            quantityPurchasesStock(s, df, dt) OR sumVATPurchasesStock(s, df, dt) OR
            sumPurchasesStock(s, df, dt)
;

printSkuPurchasesReport 'По товарам(группы)' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printSkuPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;

FORM printGroupPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDER BY canonicalName(sk)
    PROPERTIES(sk, df, dt) READONLY sumVATPurchasesStock, sumPurchasesSkuStock
    FILTERS inSession(sk) AND countSku(sk, gt),
            groupType(sk) == gt,
            sumVATPurchasesStock(sk, df, dt) OR sumPurchasesSkuStock(sk, df, dt)
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT
;

printPurchasesReport 'По группам' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printGroupPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;

FORM printGroupSupplierPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)
    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY name(l)
    ORDER BY name(l)
    PROPERTIES(gt, l, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    FILTERS isSeller(l)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' active(l) 'shift F10' DEFAULT
    
    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTime, nameSupplier, nameSku, description,
                           quantity, sumVAT, sum, averagePrice
    ORDER BY nameSku(p)
    FILTERS active(p),
            inSession(group(gt, sku(p))),
            inPurchaseReport(stock(p)),
            date(p) >= df,
            date(p) <= dt,
            supplier(p) == l
;

printGroupSupplierPurchasesReportDetail 'По поставщикам' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printGroupSupplierPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;

//по складу, поставщику и SKU
quantityPurchasesStock 'Закуплено за интервал (кол-во)' (sku, supplier, dateFrom, dateTo) = GROUP SUM
        quantityPurchase(Sku sku, Stock stock, LegalEntity supplier, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inPurchaseReport(stock)
        BY sku, supplier, dateFrom, dateTo;

sumVATPurchasesStock 'Сумма НДС закупленного за интервал' (sku, supplier, dateFrom, dateTo) = GROUP SUM
        sumVATPurchase(Sku sku, Stock stock, LegalEntity supplier, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inPurchaseReport(stock)
        BY sku, supplier, dateFrom, dateTo;

sumPurchasesStock 'Закуплено за интервал (сумма)' (sku, supplier, dateFrom, dateTo) = GROUP SUM
        sumPurchase(Sku sku, Stock stock, LegalEntity supplier, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inPurchaseReport(stock)
        BY sku, supplier, dateFrom, dateTo;

FORM printGroupSupplierSkuPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY name(l)
    ORDER BY name(l)
    PROPERTIES(gt, l, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    FILTERS isSeller(l)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' active(l) 'shift F10' DEFAULT
    OBJECTS s = Sku
    PROPERTIES(s) READONLY name
    PROPERTIES(s, l, df, dt) READONLY quantityPurchasesStock, sumVATPurchasesStock,
                                          sumPurchasesStock
    ORDER BY name(s)
    FILTERS inSession(group(gt, s)),
            quantityPurchasesStock(s, l, df, dt) OR sumVATPurchasesStock(s, l, df, dt) OR
            sumPurchasesStock(s, l, df, dt)
;

printGroupSupplierSkuPurchasesReport 'По товарам(поставщики)' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printGroupSupplierSkuPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;

FORM printSupplierPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY name(l)
    ORDER BY name(l)
    PROPERTIES(gt, l, df, dt) READONLY sumVATPurchasesStock, sumPurchasesStock
    FILTERS isSeller(l),
            sumVATPurchasesStock(gt, l, df, dt) OR sumPurchasesStock(gt, l, df, dt)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' active(l) 'shift F10' DEFAULT
;

printSupplierPurchasesReport 'По поставщикам' (GroupType gt, DATE dateFrom, DATE dateTo) =
    ACTION PRINT printSupplierPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo   IMAGE 'print.png' IN print;


FORM dialogStocksPurchaseReport 'Выбор складов'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parent
    PROPERTIES READONLY VALUE(a), name(sg)
    ORDER BY name(sg)
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY id, name
    PROPERTIES (s) inPurchaseReport
    ORDER BY name(s)
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg),
            isCompany(s),
            countCompanyStock(sg)

    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT    
    FILTERGROUP select FILTER 'Отм.' inPurchaseReport(s) 'F9' 
;

DESIGN dialogStocksPurchaseReport {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(name(s)){
                    minimumCharWidth = 35;
                    preferredCharWidth = 35;
                }
            }
        }

        MOVE functions.box;
    }
}
@extendFormFilterStockAccess(s, dialogStocksPurchaseReport, company);    
changeStocksPurchaseReport() = ACTION {
    SHOW dialogStocksPurchaseReport ;
}



FORM purchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE PANEL
    PROPERTIES dateFrom = VALUE(df)

    OBJECTS dt=DATE PANEL
    PROPERTIES dateTo = VALUE(dt)
    
    PROPERTIES inPurchaseReportStocks() ON CHANGE changeStocksPurchaseReport()

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt, df, dt) READONLY sumPurchasesStock
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parent
    PROPERTIES inSession(sk)
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    PROPERTIES(sk, df, dt) READONLY sumRecPurchasesSkuStock
    ORDER BY order(sk), skuTreeName
    FILTERS groupType(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTime, nameSupplier, nameSku, description,
                           quantity, sumVAT, sum, averagePrice
    ORDER BY nameSku(p)
    FILTERS isParent(sk, sku(p)),
            active(p),
            inPurchaseReport(stock(p)),
            date(p) >= df,
            date(p) <= dt
    PROPERTIES(gt, df, dt) printListPurchasesReport, printListSkuPurchasesReport, printPurchasesReportDetail, printSkuPurchasesReport, printPurchasesReport,
                           printGroupSupplierPurchasesReportDetail, printGroupSupplierSkuPurchasesReport, printSupplierPurchasesReport                           
;

DESIGN purchasesReport {
    NEW topContainer{
        type = CONTAINERH;
        NEW dateContainer{
            caption = 'Период';
            type = CONTAINERH;
            MOVE PROPERTY(dateFrom){caption = 'Дата (с)';}
            MOVE PROPERTY(dateTo){caption = 'Дата (по)';}
        }
        NEW stocks {
            caption = 'Склады';
            type = CONTAINERH;
            MOVE PROPERTY(inPurchaseReportStocks());        
        }
        MOVE gt.box;
        NEW sumContainer{
            caption = 'Итоговые суммы';
            type = CONTAINERV;
            MOVE PROPERTY(sumPurchasesStock(gt,df,dt));
        }
    }
    NEW bottomContainer{
        type = CONTAINERH;
        type = SPLITH;
        fill = 1;
        MOVE skuTree.tree.box;
        NEW tabContainer{
            type = TABBED;
            fill = 2;
            MOVE p.box {caption = 'Регистр';}
            NEW printPurchasesContainer{
                type = CONTAINERV;
                caption = 'Печать';
                fill = 1;
                NEW rowPurchasesContainer{
                    type = CONTAINERH;                    
                    NEW firstColumnPurchasesContainer{
                        caption = 'С детализацией';
                        type = COLUMNS;
                        columns = 2;
                        MOVE PROPERTY(printPurchasesReportDetail(gt,df,dt));
                        MOVE PROPERTY(printSkuPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printGroupSupplierPurchasesReportDetail(gt,df,dt));
                        MOVE PROPERTY(printGroupSupplierSkuPurchasesReport(gt,df,dt));
                    }
                    NEW secondColumnPurchasesContainer{
                        caption = 'Без детализации';
                        type = CONTAINERV;
                        MOVE PROPERTY(printPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printSupplierPurchasesReport(gt,df,dt));
                    }
                    NEW thirdColumnPurchasesContainer{
                        caption = 'Список';
                        type = CONTAINERV;
                        MOVE PROPERTY(printListPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printListSkuPurchasesReport(gt,df,dt));
                    }
                }
            }                           
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    purchasesReports {
        ADD purchasesReport;
    }
}
