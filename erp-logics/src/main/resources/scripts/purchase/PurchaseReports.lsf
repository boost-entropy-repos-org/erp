MODULE PurchaseReports;

REQUIRE PurchaseLedger;

//отчет по поступлениям

inPurchaseReportStock 'Вкл.' = DATA LOCAL BOOLEAN (Stock);

inPurchaseReportStocks 'Склады' () =
    GROUP CONCAT nameStock(st) IF inPurchaseReportStock(st) ,', ' PREFCHARWIDTH 30;
defaultStockPurchaseReport = GROUP MIN st IF inPurchaseReportStock(st);

dataInSessionGroup 'Отм.' = DATA LOCAL BOOLEAN (Group);

levelParentGroup (group) = GROUP MIN levelGroupGroup(group, parent) IF dataInSessionGroup(parent)
    BY group;

inParentGroup (group) = TRUE IF levelParentGroup(group);

inSessionGroup 'Отм.' (group) = OVERRIDE
    inParentGroup(group),
    dataInSessionGroup(group);

sessionConcatGroups 'Группы' (groupType) =
    GROUP CONCAT nameGroup(group) IF inSessionGroup(group) AND NOT inSessionGroup(parentGroup(group)),','
    BY groupTypeGroup(group);

//суммы поступлений по группе
sumVATPurchasesGroupStockDateDate 'Сумма НДС (по группе)'(group, dateFrom, dateTo) =
    GROUP SUM sumVATPurchaseLedger(l) IF isParentGroupSku(group, skuPurchaseLedger(l)) AND activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                         datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo 
    BY group, dateFrom, dateTo;
sumPurchasesSkuGroupStockDateDate 'Сумма поступлений (по группе)' (group, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(l) IF isParentGroupSku(group, skuPurchaseLedger(l)) AND activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                      datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo
    BY group, dateFrom, dateTo;
    
sumRecPurchasesSkuGroupStockDateDate 'Сумма поступлений (всего)' (group, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(l) IF isParentGroupSku(group, skuPurchaseLedger(l)) AND
                                      activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                      datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo
    BY group, dateFrom, dateTo;

//суммы поступлений по поставщику

sumVATPurchasesGroupTypeSupplierStockDateDate 'Сумма НДС (по поставщику)'(groupType, supplier, dateFrom, dateTo) =
    GROUP SUM sumVATPurchaseLedger(l) IF activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                                      inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(l))) AND
                                                      datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo
    BY groupType, supplierPurchaseLedger(l), dateFrom, dateTo;
sumPurchasesGroupTypeSupplierStockDateDate 'Сумма поступлений (по поставщику)' (groupType, supplier, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(l) IF activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                                   inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(l))) AND
                                                   datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo
    BY groupType, supplierPurchaseLedger(l), dateFrom, dateTo;

//итоговые суммы поступлений
sumVATPurchasesGroupTypeStockDateDate 'Итоговая сумма НДС поступлений' (groupType, dateFrom, dateTo) =
    GROUP SUM sumVATPurchaseLedger(l) IF inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(l))) AND
                                                       activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                                       datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo
    BY groupType, dateFrom, dateTo PREFCHARWIDTH 15;
sumPurchasesGroupTypeStockDateDate 'Итоговая сумма поступлений' (groupType, dateFrom, dateTo) =
    GROUP SUM sumPurchaseLedger(l) IF inSessionGroup(groupGroupTypeSku(groupType, skuPurchaseLedger(l))) AND
                                                   activePurchaseLedger(l) AND inPurchaseReportStock(stockPurchaseLedger(l)) AND
                                                   datePurchaseLedger(l) >= dateFrom AND datePurchaseLedger(l) <= dateTo
    BY groupType, dateFrom, dateTo PREFCHARWIDTH 15;

//формы

FORM printListPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger(p)
    FILTERS activePurchaseLedger(p),
            inPurchaseReportStock(stockPurchaseLedger(p)),
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt,
            inSessionGroup(groupGroupTypeSku(gt, skuPurchaseLedger(p)))
;

printListPurchasesReport 'Списком' (gt, dateFrom, dateTo) =
    ACTION FORM printListPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;

quantityPurchasesSkuStockDateFromTo 'Закуплено за интервал (кол-во)' (sku, dateFrom, dateTo) = GROUP SUM
        quantityPurchaseSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo AND inPurchaseReportStock(stock)
        BY sku, dateFrom, dateTo;

sumVATPurchasesSkuStockDateFromTo 'Сумма НДС закупленного за интервал' (sku, dateFrom, dateTo) = GROUP SUM
        sumVATPurchaseSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo AND inPurchaseReportStock(stock)
        BY sku, dateFrom, dateTo;

sumPurchasesSkuStockDateFromTo 'Закуплено за интервал (сумма)' (sku, dateFrom, dateTo) = GROUP SUM
        sumPurchaseSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo AND inPurchaseReportStock(stock)
        BY sku, dateFrom, dateTo;

FORM printListSkuPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(s, df, dt) READONLY quantityPurchasesSkuStockDateFromTo, sumVATPurchasesSkuStockDateFromTo,
                                       sumPurchasesSkuStockDateFromTo
    ORDER BY nameSku(s)
    FILTERS inSessionGroup(groupGroupTypeSku(gt, s)),
            quantityPurchasesSkuStockDateFromTo(s, df, dt) OR sumVATPurchasesSkuStockDateFromTo(s, df, dt) OR
            sumPurchasesSkuStockDateFromTo(s, df, dt)
;

printListSkuPurchasesReport 'Списком(по товарам)' (gt, dateFrom, dateTo) =
    ACTION FORM printListSkuPurchasesReport OBJECTS gt =gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;


FORM printPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)
    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup(sk)
    PROPERTIES(sk, df, dt) READONLY sumVATPurchasesGroupStockDateDate, sumPurchasesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' activeGroup(sk) 'F5' DEFAULT

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger(p)
    FILTERS activePurchaseLedger(p),
            inPurchaseReportStock(stockPurchaseLedger(p)),
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt,
            groupGroupTypeSku(gt, skuPurchaseLedger(p)) == sk
;

printPurchasesReport 'По группам' (gt, dateFrom, dateTo) =
    ACTION FORM printPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;

FORM printSkuPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup(sk)
    PROPERTIES(sk, df, dt) READONLY sumVATPurchasesGroupStockDateDate, sumPurchasesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' activeGroup(sk) 'F5' DEFAULT

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(s, df, dt) READONLY quantityPurchasesSkuStockDateFromTo, sumVATPurchasesSkuStockDateFromTo,
                                       sumPurchasesSkuStockDateFromTo
    ORDER BY nameSku(s)
    FILTERS groupGroupTypeSku(gt, s) == sk,
            quantityPurchasesSkuStockDateFromTo(s, df, dt) OR sumVATPurchasesSkuStockDateFromTo(s, df, dt) OR
            sumPurchasesSkuStockDateFromTo(s, df, dt)
;

printSkuPurchasesReport 'По товарам(группы)' (gt, dateFrom, dateTo) =
    ACTION FORM printSkuPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;

FORM printGroupPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalNameGroup(sk)
    ORDER BY canonicalNameGroup(sk)
    PROPERTIES(sk, df, dt) READONLY sumVATPurchasesGroupStockDateDate, sumPurchasesSkuGroupStockDateDate
    FILTERS inSessionGroup(sk) AND countSkuGroupGroupType(sk, gt),
            groupTypeGroup(sk) == gt,
            sumVATPurchasesGroupStockDateDate(sk, df, dt) OR sumPurchasesSkuGroupStockDateDate(sk, df, dt)
    FILTERGROUP inactive FILTER 'Активные' activeGroup(sk) 'F5' DEFAULT
;

printGroupPurchasesReport 'По группам' (gt, dateFrom, dateTo) =
    ACTION FORM printGroupPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;

FORM printGroupSupplierPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)
    PROPERTIES() inPurchaseReportStocks
    
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity(l)
    PROPERTIES(gt, l, df, dt) READONLY sumVATPurchasesGroupTypeSupplierStockDateDate, sumPurchasesGroupTypeSupplierStockDateDate
    FILTERS isSellerLegalEntity(l)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' activeLegalEntity(l) 'shift F10' DEFAULT
    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger(p)
    FILTERS activePurchaseLedger(p),
            inSessionGroup(groupGroupTypeSku(gt, skuPurchaseLedger(p))),
            inPurchaseReportStock(stockPurchaseLedger(p)),
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt,
            supplierPurchaseLedger(p) == l
;

printGroupSupplierPurchasesReport 'По поставщикам' (gt, dateFrom, dateTo) =
    ACTION FORM printGroupSupplierPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;

//по складу, поставщику и SKU
quantityPurchasesSkuStockSupplierDateFromTo 'Закуплено за интервал (кол-во)' (sku, supplier, dateFrom, dateTo) = GROUP SUM
        quantityPurchaseSkuStockSupplierDate(sku, stock, supplier, date) IF date >= dateFrom AND date <= dateTo AND inPurchaseReportStock(stock)
        BY sku, supplier, dateFrom, dateTo;

sumVATPurchasesSkuStockSupplierDateFromTo 'Сумма НДС закупленного за интервал' (sku, supplier, dateFrom, dateTo) = GROUP SUM
        sumVATPurchaseSkuStockSupplierDate(sku, stock, supplier, date) IF date >= dateFrom AND date <= dateTo AND inPurchaseReportStock(stock)
        BY sku, supplier, dateFrom, dateTo;

sumPurchasesSkuStockSupplierDateFromTo 'Закуплено за интервал (сумма)' (sku, supplier, dateFrom, dateTo) = GROUP SUM
        sumPurchaseSkuStockSupplierDate(sku, stock, supplier, date) IF date >= dateFrom AND date <= dateTo AND inPurchaseReportStock(stock)
        BY sku, supplier, dateFrom, dateTo;

FORM printGroupSupplierSkuPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity(l)
    PROPERTIES(gt, l, df, dt) READONLY sumVATPurchasesGroupTypeSupplierStockDateDate, sumPurchasesGroupTypeSupplierStockDateDate
    FILTERS isSellerLegalEntity(l)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' activeLegalEntity(l) 'shift F10' DEFAULT
    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(s, l, df, dt) READONLY quantityPurchasesSkuStockSupplierDateFromTo, sumVATPurchasesSkuStockSupplierDateFromTo,
                                          sumPurchasesSkuStockSupplierDateFromTo
    ORDER BY nameSku(s)
    FILTERS inSessionGroup(groupGroupTypeSku(gt, s)),
            quantityPurchasesSkuStockSupplierDateFromTo(s, l, df, dt) OR sumVATPurchasesSkuStockSupplierDateFromTo(s, l, df, dt) OR
            sumPurchasesSkuStockSupplierDateFromTo(s, l, df, dt)
;

printGroupSupplierSkuPurchasesReport 'По товарам(поставщики)' (gt, dateFrom, dateTo) =
    ACTION FORM printGroupSupplierSkuPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;

FORM printSupplierPurchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)

    PROPERTIES() inPurchaseReportStocks

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumVATPurchasesGroupTypeStockDateDate, sumPurchasesGroupTypeStockDateDate
    PROPERTIES(gt) READONLY sessionConcatGroups

    OBJECTS l = LegalEntity
    PROPERTIES READONLY nameLegalEntity(l)
    ORDER BY nameLegalEntity(l)
    PROPERTIES(gt, l, df, dt) READONLY sumVATPurchasesGroupTypeSupplierStockDateDate, sumPurchasesGroupTypeSupplierStockDateDate
    FILTERS isSellerLegalEntity(l),
            sumVATPurchasesGroupTypeSupplierStockDateDate(gt, l, df, dt) OR sumPurchasesGroupTypeSupplierStockDateDate(gt, l, df, dt)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' activeLegalEntity(l) 'shift F10' DEFAULT
;

printSupplierPurchasesReport 'По поставщикам' (gt, dateFrom, dateTo) =
    ACTION FORM printSupplierPurchasesReport OBJECTS gt = gt, df = dateFrom, dt = dateTo PRINT  IMAGE 'print.png' IN print;


FORM dialogStocksPurchaseReport 'Выбор складов'

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parentStockGroup
    PROPERTIES READONLY OBJVALUE(a), nameStockGroup(sg)
    ORDER BY nameStockGroup(sg)
    FILTERS stringEqualsAll(a)

    OBJECTS s = Stock
    PROPERTIES(s) READONLY idStock, nameStock
    PROPERTIES (s) inPurchaseReportStock
    ORDER BY nameStock(s)
    FILTERS isParentStockGroupStock(sg, s) OR (s IS Stock AND NOT sg),
            isCompanyStock(s),
            countCompanyStockStockGroup(sg)

    FILTERGROUP inactiveStock FILTER 'Активный' activeStock(s) 'ctrl F10' DEFAULT    
    FILTERGROUP select FILTER 'Отм.' inPurchaseReportStock(s) 'F9' 
;

DESIGN dialogStocksPurchaseReport {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            fill = 1;

            MOVE stockTree.tree.box {
                caption = 'Склады';
            }

            MOVE s.box {
                fill = 3;
                s.grid {
                    defaultComponent = TRUE;
                }
                PROPERTY(nameStock(s)){
                    minimumCharWidth = 35;
                    preferredCharWidth = 35;
                }
            }
        }

        MOVE functions.box;
    }
}
@extendFormFilterStockAccess(s, dialogStocksPurchaseReport, company);    
changeStocksPurchaseReport = ACTION () {
    FORM dialogStocksPurchaseReport MODAL;
}



FORM purchasesReport 'Отчет по поступлениям'
    OBJECTS df=DATE FIXED PANEL
    PROPERTIES dateFrom = OBJVALUE(df)

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES dateTo = OBJVALUE(dt)
    
    PROPERTIES inPurchaseReportStocks() ON CHANGE changeStocksPurchaseReport()

    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR nameGroupType
    PROPERTIES(gt, df, dt) READONLY sumPurchasesGroupTypeStockDateDate
//    PROPERTIES(gt) READONLY sessionConcatGroups

    TREE skuTree sk = Group PARENT parentGroup
    PROPERTIES inSessionGroup(sk)
    PROPERTIES READONLY orderGroup(sk), skuTreeName = nameGroup(sk)
    PROPERTIES(sk, df, dt) READONLY sumRecPurchasesSkuGroupStockDateDate
    ORDER BY orderGroup(sk), skuTreeName
    FILTERS groupTypeGroup(sk) == gt
    FILTERGROUP inactive FILTER 'Активные' activeGroup(sk) 'F5' DEFAULT

    OBJECTS p = PurchaseLedger
    PROPERTIES(p) READONLY dateTimePurchaseLedger, nameSupplierPurchaseLedger, nameSkuPurchaseLedger, descriptionPurchaseLedger,
                           quantityPurchaseLedger, sumVATPurchaseLedger, sumPurchaseLedger, averagePricePurchaseLedger
    ORDER BY nameSkuPurchaseLedger(p)
    FILTERS isParentGroupSku(sk, skuPurchaseLedger(p)),
            activePurchaseLedger(p),
            inPurchaseReportStock(stockPurchaseLedger(p)),
            datePurchaseLedger(p) >= df,
            datePurchaseLedger(p) <= dt
    PROPERTIES(gt, df, dt) printListPurchasesReport, printListSkuPurchasesReport, printPurchasesReport, printSkuPurchasesReport, printGroupPurchasesReport,
                           printGroupSupplierPurchasesReport, printGroupSupplierSkuPurchasesReport, printSupplierPurchasesReport                           
;

DESIGN purchasesReport {
    NEW topContainer{
        type = CONTAINERH;
        NEW dateContainer{
            caption = 'Период';
            type = CONTAINERH;
            MOVE PROPERTY(dateFrom){caption = 'Дата (с)';}
            MOVE PROPERTY(dateTo){caption = 'Дата (по)';}
        }
        NEW stocks {
            caption = 'Склады';
            type = CONTAINERH;
            MOVE PROPERTY(inPurchaseReportStocks());        
        }
        MOVE gt.box;
        NEW sumContainer{
            caption = 'Итоговые суммы';
            type = CONTAINERV;
            MOVE PROPERTY(sumPurchasesGroupTypeStockDateDate(gt,df,dt));
        }
    }
    NEW bottomContainer{
        type = CONTAINERH;
        type = SPLITH;
        fill = 1;
        MOVE skuTree.tree.box;
        NEW tabContainer{
            type = TABBED;
            fill = 2;
            MOVE p.box {caption = 'Регистр';}
            NEW printPurchasesContainer{
                type = CONTAINERV;
                caption = 'Печать';
                fill = 1;
                NEW rowPurchasesContainer{
                    type = CONTAINERH;                    
                    NEW firstColumnPurchasesContainer{
                        caption = 'С детализацией';
                        type = COLUMNS;
                        columns = 2;
                        MOVE PROPERTY(printPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printSkuPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printGroupSupplierPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printGroupSupplierSkuPurchasesReport(gt,df,dt));
                    }
                    NEW secondColumnPurchasesContainer{
                        caption = 'Без детализации';
                        type = CONTAINERV;
                        MOVE PROPERTY(printGroupPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printSupplierPurchasesReport(gt,df,dt));
                    }
                    NEW thirdColumnPurchasesContainer{
                        caption = 'Список';
                        type = CONTAINERV;
                        MOVE PROPERTY(printListPurchasesReport(gt,df,dt));
                        MOVE PROPERTY(printListSkuPurchasesReport(gt,df,dt));
                    }
                }
            }                           
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    purchasesReports {
        ADD purchasesReport;
    }
}
