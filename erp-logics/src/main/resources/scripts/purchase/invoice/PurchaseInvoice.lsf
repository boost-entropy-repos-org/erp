MODULE PurchaseInvoice;

REQUIRE Utils, PurchaseOrder, OrderInvoice, PurchaseOperation, CustomsGroup, PriceListStock, PriceListLedger;

NAMESPACE Purchase;

//----------------------------------------------- Накладная ---------------------------------------------------//
@defineInvoice(' (закупка)', customerStock);

@defineInvoiceBatch(customerStock);

@defineDocumentBatchPriceListType(userInvoice, customerStock);
@extendFormDocumentBatch(userInvoice, userInvoice, i);
@extendFormDocumentBatchAll(userInvoice, userInvoice, i);

@defineInvoiceDestination(supplier, customer);
@defineInvoiceStockDestination(supplierStock, customerStock);

// вторая валюта
@defineInvoiceHomeCurrency();

@deriveDocumentDetailVAT (userInvoice, , date,  sku, customerStock); // записываем ставку

//----------------------------------------------- Операции -----------------------------------------------------//

@defineDocumentOperation(invoice, i);

@defineDocumentOperationPriceListType(userInvoice, 'накладной (закупка)');

@defineDocumentOperationLegalEntity(userInvoice, supplier, 'Поставщик');
@defineDocumentOperationLegalEntity(userInvoice, customer, 'Покупатель');

@defineDocumentOperationRole(userInvoice);

@defineOperationProperty(isHomeCurrency, 'Конвертация', showContainer);
@deriveDocumentOperationProperty(UserInvoice, isHomeCurrency);

// -------------------------------------------- Инвойс на основе заказа -------------------------------------- //

@defineOrderInvoice(' (закупка)', customerStock);
@defineOrderInvoiceAction (' (закупка)');

// --------------------------- Себестоимость ---------------------------------- //

extraCostPriceInvoiceDetail = ABSTRACT NUMERIC[16,4] (InvoiceDetail) PERSISTENT;
extraCostPriceUserInvoiceDetail = ABSTRACT NUMERIC[16,4] (UserInvoiceDetail) PERSISTENT;
extraCostPriceInvoiceDetail (detail) += extraCostPriceUserInvoiceDetail(detail);

customCostPriceInvoiceDetail = ABSTRACT NUMERIC[16,4](InvoiceDetail) PERSISTENT;
customCostPriceUserInvoiceDetail = ABSTRACT NUMERIC[16,4](UserInvoiceDetail) PERSISTENT;
customCostPriceInvoiceDetail(detail) += customCostPriceUserInvoiceDetail(detail);

certificateCostPriceInvoiceDetail = ABSTRACT NUMERIC[16,4](InvoiceDetail) PERSISTENT;
certificateCostPriceUserInvoiceDetail = ABSTRACT NUMERIC[16,4](UserInvoiceDetail) PERSISTENT;
certificateCostPriceInvoiceDetail(detail) += certificateCostPriceUserInvoiceDetail(detail);

incomePriceInvoiceDetail (detail) = IF isHomeCurrencyInvoice(invoiceInvoiceDetail(detail)) THEN
                                       homePriceInvoiceDetail(detail)
                                    ELSE priceInvoiceDetail(detail);

incomePriceUserInvoiceDetail (detail) = IF isHomeCurrencyUserInvoice(userInvoiceUserInvoiceDetail(detail)) THEN
                                           homePriceUserInvoiceDetail(detail)
                                        ELSE priceUserInvoiceDetail(detail);

costPriceInvoiceDetail(detail) = NUMERIC[14,2] (incomePriceInvoiceDetail(detail) (+) extraCostPriceInvoiceDetail(detail) (+)
                                                customCostPriceInvoiceDetail(detail) (+) certificateCostPriceInvoiceDetail(detail));

costPriceUserInvoiceDetail(detail) = NUMERIC[14,2] (incomePriceUserInvoiceDetail(detail) (+) extraCostPriceUserInvoiceDetail(detail) (+)
                                                    customCostPriceUserInvoiceDetail(detail) (+) certificateCostPriceUserInvoiceDetail(detail));

addCostPriceToManufacturingPriceUserInvoice 'Учитывать в цене изготовителя доп.расходы' (invoice) = DATA BOOLEAN (Purchase.UserInvoice) IN documentPrmGroup;

// -------------------------------------- Формирование задолженности ---------------------------------------------------

CONSTRAINT supplierUserInvoice(userInvoice) AND NOT isSupplierLegalEntity(supplierUserInvoice(userInvoice))
    CHECKED BY supplierUserInvoice MESSAGE 'Для накладной выбрано в качестве поставщика организация, не являющаяся поставщиком';
CONSTRAINT customerUserInvoice(userInvoice) AND NOT isCompanyLegalEntity(customerUserInvoice(userInvoice))
    CHECKED BY customerUserInvoice MESSAGE 'Для накладной выбрано в качестве покупателя организация, не являющаяся компанией';

//------------------------------ Автоматическое проставление свойств -----------------------------//

@defineDocumentSupplierCustomerStockAccess(UserInvoice, supplier, company, userInvoice);

//------------------------------ Ввод в упаковках -----------------------------//

@defineDocumentPack(invoice, i);
Invoice.packQuantityInvoiceDetail(detail) += packQuantityInvoiceDetail(detail);
EXTEND DESIGN userInvoice {
    headerExtraParams {
        NEW headerPack {
            caption = 'Упаковка';
            ADD PROPERTY(showPackUserInvoice);
        }
    }
}

@defineOrderInvoicePack(showPack);

@defineDocumentPackSku(userInvoice, sku, customerStock);
@extendFormDocumentPackSku(userInvoice, userInvoice, i);

//overChangeQuantityValueSkuUserInvoiceDetail(detail) += ACTION (detail) {
//    IF  packBarcodeSku(skuUserInvoiceDetail(detail)) THEN {
//        ASSIGN packQuantityUserInvoiceDetail(detail) <- quantityUserInvoiceDetail(detail)/amountPackUserInvoiceDetail(detail);
//    }
//}

// -- Операция
@defineOperationProperty(showPack, 'Упаковка', showContainer);
@deriveDocumentOperationProperty(UserInvoice, showPack);

//------------------------------ Расширение формы -----------------------------//

EXTEND FORM userInvoice

    FILTERGROUP filter
        FILTER 'С остатком ' 'F10' prevCurrentBalanceSkuUserInvoice(ks, i)
        FILTER 'В документе ' 'F9' quantityUserInvoiceDetailSkuUserInvoice(ks, i)
    FILTERGROUP filter2
        FILTER 'С поступлением ' 'F8' quantityPurchaseSupplierSku(supplierUserInvoice(i), ks)
        FILTER 'В ассортименте ' 'F7' companyLedgerPriceListTypeSkuStockDateTime(priceListTypeUserInvoiceSku(i, ks), ks, customerStockUserInvoice(i), dateTimeUserInvoice(i)) == supplierUserInvoice(i)
;
EXTEND FORM userInvoice

    FILTERGROUP filter3
        FILTER 'С остатком ' 'F10' prevCurrentBalanceBatchUserInvoice(b, i)
        FILTER 'В документе ' 'F9' quantityUserInvoiceDetailBatchUserInvoice(b, i)
    FILTERGROUP filter4
        FILTER 'С поступлением ' 'F8' quantityPurchaseSupplierSku(supplierUserInvoice(i), skuBatch(b))
        FILTER 'В ассортименте ' 'F7' companyLedgerPriceListTypeBatchStockDateTime(priceListTypeUserInvoiceBatch(i, b), b, customerStockUserInvoice(i), dateTimeUserInvoice(i)) == supplierUserInvoice(i)
;

// Резервы
@extendFormDocumentOrderLedger(userInvoice, userInvoice, customerStock, i);
@extendFormDocumentOrderLedgerAll(userInvoice, userInvoice, i);

// --------------- Проведение по регистру закупок ------------- //

@implementPurchaseLedger(Invoice, sku, customerStock);
supplierPurchaseLedger (ledger) += supplierInvoiceDetail(ledger);
quantityPurchaseLedger (ledger) += quantityInvoiceDetail(ledger);
VATPurchaseLedger (ledger) += VATInvoiceDetail(ledger);
sumPurchaseLedger (ledger) += sumInvoiceDetail(ledger);

// --------------- Проведение по регистру цен ------------- //

EXTEND CLASS InvoiceDetail : PriceListLedger;

fromDateTimePriceListLedger (ledger) += dateTimeInvoiceDetail(ledger);

isPostedPriceListLedger(ledger) += isPostedInvoiceDetail(ledger);

skuPriceListLedger (ledger) += skuInvoiceDetail(ledger);

descriptionPriceListLedger (ledger) += descriptionInvoiceDetail(ledger);

companyPriceListLedger (ledger) += supplierInvoiceDetail(ledger);

inPriceListLedgerStock (ledger, stock) += customerStockInvoiceDetail(ledger) == stock;

EXTEND CLASS SystemLedgerPriceListType { purchaseInvoiceStockPriceListType 'Закупки (последняя по складу)' }
batchLedgerPriceListType(type) += WHEN type == SystemLedgerPriceListType.purchaseInvoiceStockPriceListType THEN TRUE;
inPriceListLedgerSystemLedgerPriceListType (ledger, type) += WHEN CLASS(priceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.purchaseInvoiceStockPriceListType THEN TRUE;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += WHEN CLASS(priceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.purchaseInvoiceStockPriceListType THEN priceInvoiceDetail(ledger);

//Учетные цены и суммы для партий
purchaseInvoiceStockPriceBBatchStockDate 'Цена поставщика товара в партии на начало дня' (batch, stock, date) =
    prevPricePriceListTypeBatchStockDateTime(SystemLedgerPriceListType.purchaseInvoiceStockPriceListType, batch, stock, toDateTime(date));
purchaseInvoiceStockPriceABatchStockDate 'Цена  поставщика товара в партии на конец дня' (batch, stock, date) =
    prevPricePriceListTypeBatchStockDateTime(SystemLedgerPriceListType.purchaseInvoiceStockPriceListType, batch, stock, toDateTime(sumDate(date,1)));
purchaseInvoiceStockSumBBatchStockDate 'Сумма поставщика на начало дня' (batch, stock, date) =
    balanceBBatchStockDate(batch, stock, date) * purchaseInvoiceStockPriceBBatchStockDate(batch, stock, date);
purchaseInvoiceStockSumABatchStockDate 'Сумма поставщика на конец дня' (batch, stock, date) =
    balanceABatchStockDate(batch, stock, date) * purchaseInvoiceStockPriceABatchStockDate(batch, stock, date);
purchaseInvoiceStockSumBBatchesStockDate 'Сумма поставщика склада(по партиям) на начало дня' (stock, date) =
    GROUP SUM purchaseInvoiceStockSumBBatchStockDate(batch, stock, date)
    BY stock, date;
purchaseInvoiceStockSumABatchesStockDate 'Сумма поставщика склада(по партиям) на конец дня' (stock, date) =
    GROUP SUM purchaseInvoiceStockSumABatchStockDate(batch, stock, date)
    BY stock, date;

purchaseInvoiceStockSumBSkuGroupBatchStockDate 'Сумма постащика  на начало дня(по партиям)' (group, stock, date) =
    GROUP SUM purchaseInvoiceStockSumBBatchStockDate(batch, stock, date)
    BY groupGroupTypeSku(groupType, skuBatch(batch)), stock, date;
purchaseInvoiceStockSumRecBSkuGroupBatchStockDate 'Сумма постащика  на начало дня(по партиям)' (group, stock, date) =
    GROUP SUM purchaseInvoiceStockSumBBatchStockDate(batch, stock, date) IF isParentGroupBatch(group, batch)
    BY group, stock, date;

purchaseInvoiceStockSumASkuGroupBatchStockDate 'Сумма поставщика на конец дня(по партиям)' (group, stock, date) =
    GROUP SUM purchaseInvoiceStockSumABatchStockDate(batch, stock, date)
    BY groupGroupTypeSku(groupType, skuBatch(batch)), stock, date;
purchaseInvoiceStockSumRecASkuGroupBatchStockDate 'Сумма поставщика на конец дня(по партиям)' (group, stock, date) =
    GROUP SUM purchaseInvoiceStockSumABatchStockDate(batch, stock, date) IF isParentGroupBatch(group, batch)
    BY group, stock, date;
//--
currentpurchaseInvoiceStockPriceBatchStock 'Цена поставщика' (batch, stock) =
    prevPricePriceListTypeBatchStockDateTime(SystemLedgerPriceListType.purchaseInvoiceStockPriceListType, batch, stock, currentDateTime());
currentpurchaseInvoiceStockSumBatchStock 'Сумма поставщика' (batch, stock) =
    currentBalanceBatchStock(batch, stock) * currentpurchaseInvoiceStockPriceBatchStock(batch, stock);

purchaseInvoiceStockPriceBatchStockDateTime 'Цена поставщика' (batch, stock, dateTime) =
    prevPricePriceListTypeBatchStockDateTime(SystemLedgerPriceListType.purchaseInvoiceStockPriceListType, batch, stock, dateTime);
purchaseInvoiceStockSumBBatchStockDateTime 'Сумма поставщика' (batch, stock, dateTime) =
    balanceBBatchStockDateTime(batch, stock, dateTime) * purchaseInvoiceStockPriceBatchStockDateTime(batch, stock, dateTime);

// Создаем вид цен по всем складам компании
CLASS InvoicePriceListLedger : PriceListLedger;

@defineAggregation(invoiceDetail, invoicePriceListLedger, isInvoiceDetail);

fromDateTimePriceListLedger (ledger) += dateTimeInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));

isPostedPriceListLedger(ledger) += isPostedInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));

skuPriceListLedger (ledger) += skuInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));

descriptionPriceListLedger (ledger) += descriptionInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));

companyPriceListLedger (ledger) += supplierInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));

inPriceListLedgerStock (ledger, stock) += isCompanyStock(stock) AND priceStockGroupStock(customerStockInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) == priceStockGroupStock(stock);

EXTEND CLASS SystemLedgerPriceListType { purchaseInvoicePriceListType 'Закупки (последняя по ценовой группе)' }
batchLedgerPriceListType(type) += WHEN type == SystemLedgerPriceListType.purchaseInvoicePriceListType THEN TRUE;
inPriceListLedgerSystemLedgerPriceListType (ledger, type) += WHEN CLASS(priceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.purchaseInvoicePriceListType THEN TRUE;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += WHEN CLASS(priceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.purchaseInvoicePriceListType THEN priceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));


NAVIGATOR {
    purchasePurchaseNavigator {
        ADD invoices;
    }
}

//Платежи
@defineInvoicePayment();

