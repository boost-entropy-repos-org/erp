MODULE PurchaseInvoiceExtraUOM;

REQUIRE PurchaseInvoice, ExtraUOM, LegalEntityExtraUOM, PurchaseLedgerExtraUOM;

NAMESPACE Purchase;

extraQuantity 'Кол-во(ед. поставщика)' = ABSTRACT NUMERIC[16,5] (InvoiceDetail) MATERIALIZED;
extraQuantity 'Кол-во(ед. поставщика)' = DATA NUMERIC[16,5] (UserInvoiceDetail);
extraQuantity (UserInvoiceDetail invoiceDetail) += extraQuantity(invoiceDetail);
extraQuantity[PurchaseLedger] (InvoiceDetail ledger) += extraQuantity(ledger);

extraUOM = ABSTRACT UOM (InvoiceDetail) MATERIALIZED;
extraUOM = DATA UOM (UserInvoiceDetail);
extraUOM (UserInvoiceDetail invoiceDetail) += extraUOM(invoiceDetail);
extraUOM[PurchaseLedger] (InvoiceDetail ledger) += extraUOM(ledger);

shortNameExtraUOMSku 'Ед. изм. поставщика' (InvoiceDetail detail)= shortName(extraUOM(detail));
shortNameExtraUOMSku 'Ед. изм. поставщика' (UserInvoiceDetail detail)= shortName(extraUOM(detail));

showExtraUOM(InvoiceDetail detail) = extraUOM(supplier(detail), sku(detail));

WHEN LOCAL CHANGED(sku(UserInvoiceDetail detail)) OR CHANGED(supplier(detail)) DO {
    extraUOM(detail) <- extraUOM(supplier(detail), sku(detail));
}

WHEN LOCAL CHANGED(extraQuantity(UserInvoiceDetail detail)) AND extraUOM(detail) DO {
    quantity(detail) <- extraQuantity(detail) * coefficient(extraUOM(detail), UOM(sku(detail)));
}

WHEN LOCAL CHANGED(quantity(UserInvoiceDetail detail)) AND extraUOM(detail) DO {
    extraQuantity(detail) <- quantity(detail) * coefficient(UOM(sku(detail)), extraUOM(detail));
}

EXTEND FORM userInvoice
    PROPERTIES(d) SHOWIF showExtraUOM(d) shortNameExtraUOMSku BEFORE shortNameUOMSku(d), extraQuantity BEFORE quantity(d) 
;

EXTEND FORM invoices
    PROPERTIES(d) READONLY SHOWIF showExtraUOM(d) shortNameExtraUOMSku BEFORE shortNameUOMSku(d), extraQuantity BEFORE quantity(d) 
;