MODULE PurchaseInvoiceAgreement;

REQUIRE PurchaseShipment, PriceList;

NAMESPACE Purchase;
 
// Ограничения на ввод приходной накладной в соответствии с условиями соглашения

//-- Ограничения 
GROUP agreementPercQuantity 'Ограничения (кол-во)': public;
GROUP agreementPercPrice 'Ограничения (цена)': public;
//--------------------------- Допустимый процент отклонения по количеству --------------------------------------//

percDeviationDownQuantityAgreement '% отклонения по количеству вниз' = DATA NUMERIC[8,2] (Agreement) IN agreementPercQuantity;
percDeviationUpQuantityAgreement '% отклонения по количеству вверх' = DATA NUMERIC[8,2] (Agreement) IN agreementPercQuantity;

EXTEND FORM agreement 
    PROPERTIES(a) percDeviationDownQuantityAgreement, percDeviationUpQuantityAgreement  
;
DESIGN agreement {
    orderContainer {
        NEW deviation BEFORE orderContainer1 {
            type = CONTAINERV;
            NEW deviation1 {
                type = CONTAINERH;
                ADD a.agreementPercQuantity;
            }
        }
    }
} 

percDeviationDownQuantityAgreementOrderDetail '% отклонения по количеству вниз' (d) = percDeviationDownQuantityAgreement(agreementOrderDetail(d));
percDeviationUpQuantityAgreementOrderDetail '% отклонения по количеству вверх' (d) = percDeviationUpQuantityAgreement(agreementOrderDetail(d));


maxDeviationQuantityOrderDetail 'Макс. кол-во' (d) = (100.0 + percDeviationUpQuantityAgreementOrderDetail(d)) * 
                                                        quantityOrderDetail(d) /
                                                        100.0;
minDeviationQuantityOrderDetail 'Мин. кол-во' (d) = (100.0 - percDeviationDownQuantityAgreementOrderDetail(d)) *
                                                        quantityOrderDetail(d) /
                                                        100.0;
   
CONSTRAINT isPostedOrderDetail(d) AND shippedOrderDetail(d) > maxDeviationQuantityOrderDetail(d)    
  MESSAGE 'Для заказа превышен максимальный допустимый процент отклонения по количеству';    

CONSTRAINT isPostedOrderDetail(d) AND shippedOrderDetail(d) < minDeviationQuantityOrderDetail(d)    
  MESSAGE 'Для заказа превышен минимальный допустимый процент отклонения по количеству';
  
percDeviationDownQuantityAgreementOrder '% отклонения по количеству вниз' (order) = percDeviationDownQuantityAgreement(agreementOrder(order));
percDeviationUpQuantityAgreementOrder '% отклонения по количеству вверх' (order) = percDeviationUpQuantityAgreement(agreementOrder(order));
    
EXTEND FORM orders
    PROPERTIES(d) READONLY  minDeviationQuantityOrderDetail SHOWIF percDeviationDownQuantityAgreementOrder(o) BEFORE quantityOrderDetail(d), 
                  maxDeviationQuantityOrderDetail SHOWIF percDeviationUpQuantityAgreementOrder(o) AFTER quantityOrderDetail(d)
                  
;
EXTEND FORM userOrder
    PROPERTIES(d) READONLY  minDeviationQuantityOrderDetail SHOWIF percDeviationDownQuantityAgreementOrder(o) BEFORE quantityUserOrderDetail(d), 
                  maxDeviationQuantityOrderDetail SHOWIF percDeviationUpQuantityAgreementOrder(o) AFTER quantityUserOrderDetail(d)
 
;    
  
//--------------------------- Допустимый процент отклонения по цене --------------------------------------//
percDeviationDownPriceAgreement '% отклонения по цене вниз' = DATA NUMERIC[8,2] (Agreement) IN agreementPercPrice;
percDeviationUpPriceAgreement '% отклонения по цене вверх' = DATA NUMERIC[8,2] (Agreement) IN agreementPercPrice;

// Уточнение по группам
//-- вниз
dataPercDeviationDownPriceAgreementGroup '% отклонения по цене вниз' (agreement, group) = DATA NUMERIC[8,2] (Agreement, Group);
levelParentPercDeviationDownPriceAgreementGroup (agreement, group) =
    GROUP MIN levelGroupGroup(group, parent) IF dataPercDeviationDownPriceAgreementGroup(agreement, parent)
    BY agreement, group PERSISTENT;
nearestParentPercDeviationDownPriceAgreementGroup (agreement, group) = groupGroupLevel(group, levelParentPercDeviationDownPriceAgreementGroup (agreement, group));
nearestPercDeviationDownPriceAgreementGroup (agreement, group) =
   dataPercDeviationDownPriceAgreementGroup(agreement, nearestParentPercDeviationDownPriceAgreementGroup(agreement, group)) PERSISTENT;

percDeviationDownPriceAgreementGroup '% отклонения по цене вниз (перегруженный)' (agreement, group) =  OVERRIDE  
    percDeviationDownPriceAgreement(agreement) IF group IS Group,
    nearestPercDeviationDownPriceAgreementGroup(agreement, group), 
    dataPercDeviationDownPriceAgreementGroup(agreement, group) PERSISTENT;
    
//-- вверх        
dataPercDeviationUpPriceAgreementGroup '% отклонения по цене вверх' (agreement, group) = DATA NUMERIC[8,2] (Agreement, Group);

levelParentPercDeviationUpPriceAgreementGroup (agreement, group) =
    GROUP MIN levelGroupGroup(group, parent) IF dataPercDeviationUpPriceAgreementGroup(agreement, parent)
    BY agreement, group PERSISTENT;
nearestParentPercDeviationUpPriceAgreementGroup (agreement, group) = groupGroupLevel(group, levelParentPercDeviationUpPriceAgreementGroup (agreement, group));
nearestPercDeviationUpPriceAgreementGroup (agreement, group) =
   dataPercDeviationUpPriceAgreementGroup(agreement, nearestParentPercDeviationUpPriceAgreementGroup(agreement, group)) PERSISTENT;

percDeviationUpPriceAgreementGroup '% отклонения по цене вверх (перегруженный)' (agreement, group) =  OVERRIDE  
    percDeviationUpPriceAgreement(agreement) IF group IS Group,
    nearestPercDeviationUpPriceAgreementGroup(agreement, group), 
    dataPercDeviationUpPriceAgreementGroup(agreement, group) PERSISTENT;    

// Уточнение по sku
//-- вниз
dataPercDeviationDownPriceAgreementSku '% отклонения по цене вниз' (agreement, sku) = DATA NUMERIC[8,2] (Agreement, Sku);

percDeviationDownPriceAgreementSku '% отклонения по цене вниз (перегруженный)' (agreement, sku) = OVERRIDE     
    percDeviationDownPriceAgreementGroup(agreement, groupGroupTypeSku(groupTypeAgreement(agreement),sku)),
    dataPercDeviationDownPriceAgreementSku(agreement, sku);
    
//-- вверх
dataPercDeviationUpPriceAgreementSku '% отклонения по цене вверх' (agreement, sku) = DATA NUMERIC[8,2] (Agreement, Sku);

percDeviationUpPriceAgreementSku '% отклонения по цене вверх (перегруженный)' (agreement, sku) = OVERRIDE     
    percDeviationUpPriceAgreementGroup(agreement, groupGroupTypeSku(groupTypeAgreement(agreement),sku)),
    dataPercDeviationUpPriceAgreementSku(agreement, sku);
    
    
showPercDeviationUpPriceAgreement = GROUP SUM 1 IF percDeviationUpPriceAgreementSku(agreement, sku) BY agreement;
showPercDeviationDownPriceAgreement = GROUP SUM 1 IF percDeviationDownPriceAgreementSku(agreement, sku) BY agreement;
EXTEND FORM agreement 
    PROPERTIES(a) percDeviationDownPriceAgreement, percDeviationUpPriceAgreement 
    PROPERTIES(a, g) dataPercDeviationDownPriceAgreementGroup, percDeviationDownPriceAgreementGroup,
                     dataPercDeviationUpPriceAgreementGroup, percDeviationUpPriceAgreementGroup
    PROPERTIES(a, s) dataPercDeviationDownPriceAgreementSku, percDeviationDownPriceAgreementSku,
                     dataPercDeviationUpPriceAgreementSku, percDeviationUpPriceAgreementSku        
                         
;
DESIGN agreement {
    deviation1{
        ADD a.agreementPercPrice;
    }
} 

agreementsPriceOrderDetail 'Цена из соглашения' (d) =
    priceBLedgerPriceListTypeSkuStockDateTime(priceListTypeOrderDetail(d),
                                              skuOrderDetail(d),
                                              customerStockOrderDetail(d),
                                              dateTimeOrderDetail(d));
percDeviationUpPriceAgreementOrderDetail '% отклонения по цене вверх' (d) = percDeviationUpPriceAgreementSku(agreementOrderDetail(d), skuOrderDetail(d));
percDeviationDownPriceAgreementOrderDetail '% отклонения по цене вниз' (d) = percDeviationDownPriceAgreementSku(agreementOrderDetail(d), skuOrderDetail(d));

maxDeviationPriceOrderDetail 'Макс. цена' (d) = (100.0 + percDeviationUpPriceAgreementOrderDetail(d)) * 
                                                        priceOrderDetail(d) /
                                                        100.0;
minDeviationPriceOrderDetail 'Мин. цена' (d) = (100.0 - percDeviationDownPriceAgreementOrderDetail(d)) *
                                                        priceOrderDetail(d) /
                                                        100.0;      

CONSTRAINT isPostedOrderDetail(d) AND (priceOrderDetail(d) != agreementsPriceOrderDetail(d))    
    MESSAGE 'Для строки заказа цена не совпадает с ценой из соглашения';    
    
percDeviationUpPriceAgreementOrder '% отклонения по цене вверх' (order) = showPercDeviationUpPriceAgreement(agreementOrder(order));
percDeviationDownPriceAgreementOrder '% отклонения по цене вниз' (order) = showPercDeviationDownPriceAgreement(agreementOrder(order));     

EXTEND FORM orders
    PROPERTIES(d) READONLY  minDeviationPriceOrderDetail SHOWIF percDeviationDownPriceAgreementOrder(o) BEFORE priceOrderDetail(d), 
                  maxDeviationPriceOrderDetail SHOWIF percDeviationUpPriceAgreementOrder(o) AFTER priceOrderDetail(d) 

;
EXTEND FORM userOrder
    PROPERTIES(d) READONLY  minDeviationPriceOrderDetail SHOWIF percDeviationDownPriceAgreementOrder(o) BEFORE priceUserOrderDetail(d), 
                  maxDeviationPriceOrderDetail SHOWIF percDeviationUpPriceAgreementOrder(o) AFTER priceUserOrderDetail(d)

;     

//
maxOrderDetailSkuOrder (sku, order) = GROUP MAX orderDetail BY skuOrderDetail(orderDetail),  orderOrderDetail(orderDetail);
maxDeviationPriceOrderInvoiceDetail 'Макс. цена' (d) = maxDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));
minDeviationPriceOrderInvoiceDetail 'Мин. цена' (d) = minDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 
priceOrderInvoiceDetail 'Цена заказа' (d) = priceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 

agreementsPriceInvoiceDetail 'Цена из соглашения' (d) =
    priceBLedgerPriceListTypeSkuStockDateTime(priceListTypeInvoiceDetail(d),
                                              skuInvoiceDetail(d),
                                              customerStockInvoiceDetail(d),
                                              dateTimeInvoiceDetail(d));

percDeviationUpPriceAgreementInvoiceDetail '% отклонения по цене вверх' (d) = percDeviationUpPriceAgreementSku(agreementInvoiceDetail(d), skuInvoiceDetail(d));
percDeviationDownPriceAgreementInvoiceDetail '% отклонения по цене вниз' (d) = percDeviationDownPriceAgreementSku(agreementInvoiceDetail(d), skuInvoiceDetail(d));
maxDeviationPriceInvoiceDetail 'Макс. цена' (d) = (100.0 + percDeviationUpPriceAgreementInvoiceDetail(d)) * 
                                                        agreementsPriceInvoiceDetail(d) /
                                                        100.0;
minDeviationPriceInvoiceDetail 'Мин. цена' (d) = (100.0 - percDeviationDownPriceAgreementInvoiceDetail(d)) *
                                                        agreementsPriceInvoiceDetail(d) /
                                                        100.0; 
                                                         
overMaxDeviationPriceInvoiceDetail 'Макс. цена' (d) = IF orderInvoiceDetail(d) THEN maxDeviationPriceOrderInvoiceDetail(d) ELSE maxDeviationPriceInvoiceDetail(d);
overMinDeviationPriceInvoiceDetail 'Мин. цена' (d) = IF orderInvoiceDetail(d) THEN minDeviationPriceOrderInvoiceDetail(d) ELSE minDeviationPriceInvoiceDetail(d);                                                                                                                      
                                                         
CONSTRAINT isPostedInvoiceDetail(d) AND priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)    
    MESSAGE 'Для строки накладной превышен максимальный допустимый процент отклонения по цене';    

CONSTRAINT isPostedInvoiceDetail(d) AND priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d)    
    MESSAGE 'Для строки накладной превышен минимальный допустимый процент отклонения по цене';
                
//--------------------------- Запрет на оприходование без заявки --------------------------------------//

deviationNotOrderInvoiceAgreement 'Запретить оприходование накладных без заказа'  = DATA BOOLEAN (Agreement);
EXTEND FORM agreement 
    PROPERTIES(a) deviationNotOrderInvoiceAgreement 
;

maxOrderDetailInvoiceDetail (d) = maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)) IF isPostedOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));

CONSTRAINT isPostedInvoiceDetail(d) AND deviationNotOrderInvoiceAgreement(agreementInvoice(invoiceInvoiceDetail(d))) AND NOT maxOrderDetailInvoiceDetail(d)    
    MESSAGE 'Накладную запрещено оприходовать без заказа';   


//--------------------------- Минимальная сумма заказа для поставщика --------------------------------------//

minSupplierSumOrderAgreement 'Минимальная сумма заказа' = DATA NUMERIC[16,2] (Agreement) MINCHARWIDTH 13 PREFCHARWIDTH 13;
EXTEND FORM agreement 
    PROPERTIES(a) minSupplierSumOrderAgreement 
;
DESIGN agreement {
    orderContainer1 {       
        ADD PROPERTY (deviationNotOrderInvoiceAgreement(a));
        ADD PROPERTY (minSupplierSumOrderAgreement(a));
    }
}

minSumSupplierOrder 'Минимальная сумма заказа' (o) = minSupplierSumOrderAgreement(agreementUserOrder(o)) IN documentSum MINCHARWIDTH 13 PREFCHARWIDTH 13;
backgroundMinSumSupplierOrder (o) = RGB (255,100,100) IF minSumSupplierOrder(o) > sumUserOrderDetailUserOrder(o);

EXTEND FORM userOrder 
    PROPERTIES(o) READONLY minSumSupplierOrder BACKGROUND backgroundMinSumSupplierOrder(o) 
;

//--

maxDeviationQuantityOrderInvoiceDetail 'Макс. к-во' (d) = maxDeviationQuantityOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));
minDeviationQuantityOrderInvoiceDetail 'Мин. к-во' (d) = minDeviationQuantityOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 
quantityOrderInvoiceDetail 'К-во заказа' (d) =quantityOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 

percDeviationUpPriceOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND showPercDeviationUpPriceAgreement(agreementOrder(o)) BY i;  
percDeviationDownPriceOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND showPercDeviationDownPriceAgreement(agreementOrder(o)) BY i;  

percDeviationUpQuantityOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND percDeviationUpQuantityAgreement(agreementOrder(o)) BY i;  
percDeviationDownQuantityOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND percDeviationDownQuantityAgreement(agreementOrder(o)) BY i; 
  
    
backgroundMaxPriceInvoiceDetail (d) = IF priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);

backgroundMinPriceInvoiceDetail (d) = IF priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);

//overShipmentQuantityInvoiceDetail (d) = OVERRIDE shipmentQuantityInvoiceDetail(d), shippedInvoiceDetail(d);    
    
backgroundMaxQuantityInvoiceDetail (d) = IF shipmentQuantityInvoiceDetail(d) > maxDeviationQuantityOrderInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);

backgroundMinQuantityInvoiceDetail (d) = IF shipmentQuantityInvoiceDetail(d) < minDeviationQuantityOrderInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);  
      
  
    
EXTEND FORM userInvoice
    PROPERTIES (d) READONLY BEFORE priceUserInvoiceDetail(d) 
                   minDeviationPriceOrderInvoiceDetail SHOWIF percDeviationDownPriceOrdersInvoice(i) BACKGROUND backgroundMinPriceInvoiceDetail(d), 
                   priceOrderInvoiceDetail SHOWIF countIncludeOrdersInvoice(i), 
                   maxDeviationPriceOrderInvoiceDetail SHOWIF percDeviationUpPriceOrdersInvoice(i) BACKGROUND backgroundMaxPriceInvoiceDetail(d)
     
    PROPERTIES (d) READONLY BEFORE quantityUserInvoiceDetail(d) 
                   minDeviationQuantityOrderInvoiceDetail SHOWIF percDeviationDownQuantityOrdersInvoice(i) BACKGROUND backgroundMinQuantityInvoiceDetail(d), 
                   quantityOrderInvoiceDetail SHOWIF countIncludeOrdersInvoice(i), 
                   maxDeviationQuantityOrderInvoiceDetail SHOWIF percDeviationUpQuantityOrdersInvoice(i) BACKGROUND backgroundMaxQuantityInvoiceDetail(d) 

    FILTERGROUP    deviation
        FILTER  'Есть отклонения' (priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)) OR 
                                       (priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d)) OR 
                                       (shipmentQuantityInvoiceDetail(d) > maxDeviationQuantityOrderInvoiceDetail(d)) OR 
                                       (shipmentQuantityInvoiceDetail(d) < minDeviationQuantityOrderInvoiceDetail(d)) 'F6' 
        FILTER  'По цене' (priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)) OR 
                                    (priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d)) 'ctrl F6'        
        FILTER  'По количеству' (shipmentQuantityInvoiceDetail(d) > maxDeviationQuantityOrderInvoiceDetail(d)) OR 
                                           (shipmentQuantityInvoiceDetail(d) < minDeviationQuantityOrderInvoiceDetail(d)) 'shift F6'         
        

;

DESIGN userInvoice {
    PROPERTY(priceOrderInvoiceDetail(d)) { background = #F9FFD4; }
    PROPERTY(quantityOrderInvoiceDetail(d)) { background = #F9FFD4; }
}


