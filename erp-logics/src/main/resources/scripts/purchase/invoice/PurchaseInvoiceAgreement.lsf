MODULE PurchaseInvoiceAgreement;

REQUIRE PurchaseInvoice, PriceList;

NAMESPACE Purchase;
 
// Ограничения на ввод приходной накладной в соответствии с условиями соглашения

//-- Ограничения 
GROUP agreementPercQuantity 'Ограничения (кол-во)': public;
GROUP agreementPercPrice 'Ограничения (цена)': public;
//--------------------------- Допустимый процент отклонения по количеству --------------------------------------//

TABLE agreementDate(Agreement, DATE);
@defineHistorizable(percDeviationDownQuantity, , '% отклонения по количеству вниз', NUMERIC[8,2], agreement, nameAgreement, agreementPercQuantity);
@defineHistorizable(percDeviationUpQuantity, , '% отклонения по количеству вверх', NUMERIC[8,2], agreement, nameAgreement, agreementPercQuantity);


EXTEND FORM agreement 
    PROPERTIES(a) percDeviationDownQuantityAgreement, percDeviationUpQuantityAgreement  
;
EXTEND DESIGN agreement {
    NEW deviation AFTER propContainer {
        caption = 'Ограничения';
        type = CONTAINERV;
        NEW deviation1 {
            type = CONTAINERH;
            ADD a.agreementPercQuantity;
        }
    }
} 

percDeviationDownQuantityAgreementOrderDetail '% отклонения по количеству вниз' (d) = percDeviationDownQuantityAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));
percDeviationUpQuantityAgreementOrderDetail '% отклонения по количеству вверх' (d) = percDeviationUpQuantityAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));


maxDeviationQuantityOrderDetail 'Макс. кол-во' (d) = (100.0 + percDeviationUpQuantityAgreementOrderDetail(d)) * 
                                                        quantityOrderDetail(d) /
                                                        100.0;
minDeviationQuantityOrderDetail 'Мин. кол-во' (d) = (100.0 - percDeviationDownQuantityAgreementOrderDetail(d)) *
                                                        quantityOrderDetail(d) /
                                                        100.0;
normalDeviationQuantityOrderDetail 'В пределах нормы (кол-во)' (d) = 
    (invoicedOrderDetail(d) >=  minDeviationQuantityOrderDetail(d) AND invoicedOrderDetail(d) <=  maxDeviationQuantityOrderDetail(d)) OR 
    (invoicedOrderDetail(d) >=  minDeviationQuantityOrderDetail(d) AND NOT maxDeviationQuantityOrderDetail(d)) OR 
    (invoicedOrderDetail(d) <=  maxDeviationQuantityOrderDetail(d) AND NOT minDeviationQuantityOrderDetail(d)) OR 
    (invoicedOrderDetail(d) AND NOT  maxDeviationQuantityOrderDetail(d) AND NOT minDeviationQuantityOrderDetail(d))
    ;
   
CONSTRAINT isPostedOrderDetail(d) AND invoicedOrderDetail(d) > maxDeviationQuantityOrderDetail(d)    
  MESSAGE 'Для заказа превышен максимальный допустимый процент отклонения по количеству';    

CONSTRAINT isPostedOrderDetail(d) AND invoicedOrderDetail(d) < minDeviationQuantityOrderDetail(d)    
  MESSAGE 'Для заказа превышен минимальный допустимый процент отклонения по количеству';
  
percDeviationDownQuantityAgreementOrder '% отклонения по количеству вниз' (order) = percDeviationDownQuantityAgreementDate(agreementOrder(order), dateOrder(order));
percDeviationUpQuantityAgreementOrder '% отклонения по количеству вверх' (order) = percDeviationUpQuantityAgreementDate(agreementOrder(order), dateOrder(order));
showNormalDeviationQuantityOrde (order) = percDeviationDownQuantityAgreementOrder(order) OR percDeviationUpQuantityAgreementOrder(order); 
    
EXTEND FORM orders
    PROPERTIES(d) READONLY  minDeviationQuantityOrderDetail SHOWIF percDeviationDownQuantityAgreementOrder(o), 
                  maxDeviationQuantityOrderDetail SHOWIF percDeviationUpQuantityAgreementOrder(o), 
                  normalDeviationQuantityOrderDetail SHOWIF showNormalDeviationQuantityOrde(o) 
;
EXTEND FORM userOrder
    PROPERTIES(d) READONLY  minDeviationQuantityOrderDetail SHOWIF percDeviationDownQuantityAgreementOrder(o), 
                  maxDeviationQuantityOrderDetail SHOWIF percDeviationUpQuantityAgreementOrder(o), 
                  normalDeviationQuantityOrderDetail SHOWIF showNormalDeviationQuantityOrde(o) 
;    
  
//--------------------------- Допустимый процент отклонения по цене --------------------------------------//
@defineHistorizable(percDeviationDownPrice, , '% отклонения по цене вниз', NUMERIC[8,2], agreement, nameAgreement, agreementPercPrice);
@defineHistorizable(percDeviationUpPrice, , '% отклонения по цене вверх', NUMERIC[8,2], agreement, nameAgreement, agreementPercPrice);
EXTEND FORM agreement 
    PROPERTIES(a) percDeviationDownPriceAgreement, percDeviationUpPriceAgreement 
;
EXTEND DESIGN agreement {
    deviation1{
        ADD a.agreementPercPrice;
    }
} 

agreementsPriceOrderDetail 'Цена из соглашения' (d) =
    priceBLedgerPriceListTypeSkuStockDateTime(priceListTypeOrderDetail(d),
                                              skuOrderDetail(d),
                                              customerStockOrderDetail(d),
                                              dateTimeOrderDetail(d));
percDeviationUpPriceAgreementOrderDetail '% отклонения по цене вверх' (d) = percDeviationUpPriceAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));
percDeviationDownPriceAgreementOrderDetail '% отклонения по цене вниз' (d) = percDeviationDownPriceAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));

maxDeviationPriceOrderDetail 'Макс. цена' (d) = (100.0 + percDeviationUpPriceAgreementOrderDetail(d)) * 
                                                        agreementsPriceOrderDetail(d) /
                                                        100.0;
minDeviationPriceOrderDetail 'Мин. цена' (d) = (100.0 - percDeviationDownPriceAgreementOrderDetail(d)) *
                                                        agreementsPriceOrderDetail(d) /
                                                        100.0;      
normalDeviationPriceOrderDetail 'В пределах нормы (цена)' (d) = 
    (priceOrderDetail(d) >=  minDeviationPriceOrderDetail(d) AND priceOrderDetail(d) <=  maxDeviationPriceOrderDetail(d)) OR 
    (priceOrderDetail(d) >=  minDeviationPriceOrderDetail(d) AND NOT maxDeviationPriceOrderDetail(d)) OR 
    (priceOrderDetail(d) <=  maxDeviationPriceOrderDetail(d) AND NOT minDeviationPriceOrderDetail(d)) OR 
    (priceOrderDetail(d) AND NOT  maxDeviationPriceOrderDetail(d) AND NOT minDeviationPriceOrderDetail(d))
    ;
CONSTRAINT isPostedOrderDetail(d) AND priceOrderDetail(d) > maxDeviationPriceOrderDetail(d)    
    MESSAGE 'Для строки заказа превышен максимальный допустимый процент отклонения по цене';    

CONSTRAINT isPostedOrderDetail(d) AND priceOrderDetail(d) < minDeviationPriceOrderDetail(d)    
    MESSAGE 'Для строки заказа превышен минимальный допустимый процент отклонения по цене';
    
    
percDeviationUpPriceAgreementOrder '% отклонения по цене вверх' (order) = percDeviationUpPriceAgreementDate(agreementOrder(order), dateOrder(order));
percDeviationDownPriceAgreementOrder '% отклонения по цене вниз' (order) = percDeviationDownPriceAgreementDate(agreementOrder(order), dateOrder(order));  
showNormalDeviationPriceOrder (order) = percDeviationUpPriceAgreementOrder(order) OR percDeviationDownPriceAgreementOrder(order);     


EXTEND FORM orders
    PROPERTIES(d) READONLY  minDeviationPriceOrderDetail SHOWIF percDeviationDownPriceAgreementOrder(o), 
                  maxDeviationPriceOrderDetail SHOWIF percDeviationUpPriceAgreementOrder(o), 
                  normalDeviationPriceOrderDetail SHOWIF showNormalDeviationPriceOrder(o) 
;
EXTEND FORM userOrder
    PROPERTIES(d) READONLY  minDeviationPriceOrderDetail SHOWIF percDeviationDownPriceAgreementOrder(o), 
                  maxDeviationPriceOrderDetail SHOWIF percDeviationUpPriceAgreementOrder(o), 
                  normalDeviationPriceOrderDetail SHOWIF showNormalDeviationPriceOrder(o) 
;     

//
maxOrderDetailSkuOrder (sku, order) = GROUP MAX orderDetail BY skuOrderDetail(orderDetail),  orderOrderDetail(orderDetail);
agreementsPriceInvoiceDetail 'Цена из соглашения' (d) =
    priceBLedgerPriceListTypeSkuStockDateTime(priceListTypeInvoiceDetail(d),
                                              skuInvoiceDetail(d),
                                              customerStockInvoiceDetail(d),
                                              dateTimeInvoiceDetail(d));

percDeviationUpPriceAgreementInvoiceDetail '% отклонения по цене вверх' (d) = percDeviationUpPriceAgreementDate(agreementInvoice(invoiceInvoiceDetail(d)), dateInvoiceDetail(d));
percDeviationDownPriceAgreementInvoiceDetail '% отклонения по цене вниз' (d) = percDeviationDownPriceAgreementDate(agreementInvoice(invoiceInvoiceDetail(d)), dateInvoiceDetail(d));
maxDeviationPriceInvoiceDetail 'Макс. цена' (d) = (100.0 + percDeviationUpPriceAgreementInvoiceDetail(d)) * 
                                                        agreementsPriceInvoiceDetail(d) /
                                                        100.0;
minDeviationPriceInvoiceDetail 'Мин. цена' (d) = (100.0 - percDeviationDownPriceAgreementInvoiceDetail(d)) *
                                                        agreementsPriceInvoiceDetail(d) /
                                                        100.0; 
                                                         
overMaxDeviationPriceInvoiceDetail 'Макс. цена' (d) = OVERRIDE maxDeviationPriceInvoiceDetail(d), maxDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));
overMinDeviationPriceInvoiceDetail 'Мин. цена' (d) = OVERRIDE minDeviationPriceInvoiceDetail(d), minDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));                                                          
                                                          
normalDeviationPriceInvoiceDetail 'В пределах нормы (цена)' (d) = 
    (priceInvoiceDetail(d) >=  overMinDeviationPriceInvoiceDetail(d) AND priceInvoiceDetail(d) <=  overMaxDeviationPriceInvoiceDetail(d)) OR 
    (priceInvoiceDetail(d) >=  overMinDeviationPriceInvoiceDetail(d) AND NOT overMaxDeviationPriceInvoiceDetail(d)) OR 
    (priceInvoiceDetail(d) <=  overMaxDeviationPriceInvoiceDetail(d) AND NOT overMinDeviationPriceInvoiceDetail(d)) OR 
    (priceInvoiceDetail(d) AND NOT  overMaxDeviationPriceInvoiceDetail(d) AND NOT overMinDeviationPriceInvoiceDetail(d))
    ;     
                                                         
CONSTRAINT isPostedInvoiceDetail(d) AND priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)    
    MESSAGE 'Для строки накладной превышен максимальный допустимый процент отклонения по цене';    

CONSTRAINT isPostedInvoiceDetail(d) AND priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d)    
    MESSAGE 'Для строки накладной превышен минимальный допустимый процент отклонения по цене';
                
//--------------------------- Запрет на оприходование без заявки --------------------------------------//

deviationNotOrderInvoiceAgreement 'Запретить оприходование накладных без заказа'  = DATA BOOLEAN (Agreement);
EXTEND FORM agreement 
    PROPERTIES(a) deviationNotOrderInvoiceAgreement 
;
EXTEND DESIGN agreement {
    deviation{
        ADD PROPERTY (deviationNotOrderInvoiceAgreement(a));
    }
}
maxOrderDetailInvoiceDetail (d) = maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)) IF isPostedOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));

CONSTRAINT isPostedInvoiceDetail(d) AND deviationNotOrderInvoiceAgreement(agreementInvoice(invoiceInvoiceDetail(d))) AND NOT maxOrderDetailInvoiceDetail(d)    
    MESSAGE 'Накладную запрещено оприходовать без заказа';   



