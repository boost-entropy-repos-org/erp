MODULE PurchaseInvoiceAgreement;

REQUIRE PurchaseShipment, PriceList;

NAMESPACE Purchase;
 
// Ограничения на ввод приходной накладной в соответствии с условиями соглашения

//-- Ограничения 
GROUP agreementPercQuantity 'Ограничения (кол-во)': public;
GROUP agreementPercPrice 'Ограничения (цена)': public;
//--------------------------- Допустимый процент отклонения по количеству --------------------------------------//

TABLE agreementDate(Agreement, DATE);
@defineHistorizable(percDeviationDownQuantity, , '% отклонения по количеству вниз', NUMERIC[8,2], agreement, nameAgreement, agreementPercQuantity);
@defineHistorizable(percDeviationUpQuantity, , '% отклонения по количеству вверх', NUMERIC[8,2], agreement, nameAgreement, agreementPercQuantity);


EXTEND FORM agreement 
    PROPERTIES(a) percDeviationDownQuantityAgreement, percDeviationUpQuantityAgreement  
;
EXTEND DESIGN agreement {
    NEW deviation AFTER propContainer {
        caption = 'Ограничения';
        type = CONTAINERV;
        NEW deviation1 {
            type = CONTAINERH;
            ADD a.agreementPercQuantity;
        }
    }
} 

percDeviationDownQuantityAgreementOrderDetail '% отклонения по количеству вниз' (d) = percDeviationDownQuantityAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));
percDeviationUpQuantityAgreementOrderDetail '% отклонения по количеству вверх' (d) = percDeviationUpQuantityAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));


maxDeviationQuantityOrderDetail 'Макс. кол-во' (d) = (100.0 + percDeviationUpQuantityAgreementOrderDetail(d)) * 
                                                        quantityOrderDetail(d) /
                                                        100.0;
minDeviationQuantityOrderDetail 'Мин. кол-во' (d) = (100.0 - percDeviationDownQuantityAgreementOrderDetail(d)) *
                                                        quantityOrderDetail(d) /
                                                        100.0;
   
CONSTRAINT isPostedOrderDetail(d) AND shippedOrderDetail(d) > maxDeviationQuantityOrderDetail(d)    
  MESSAGE 'Для заказа превышен максимальный допустимый процент отклонения по количеству';    

CONSTRAINT isPostedOrderDetail(d) AND shippedOrderDetail(d) < minDeviationQuantityOrderDetail(d)    
  MESSAGE 'Для заказа превышен минимальный допустимый процент отклонения по количеству';
  
percDeviationDownQuantityAgreementOrder '% отклонения по количеству вниз' (order) = percDeviationDownQuantityAgreementDate(agreementOrder(order), dateOrder(order));
percDeviationUpQuantityAgreementOrder '% отклонения по количеству вверх' (order) = percDeviationUpQuantityAgreementDate(agreementOrder(order), dateOrder(order));
    
EXTEND FORM orders
    PROPERTIES(d) READONLY  minDeviationQuantityOrderDetail SHOWIF percDeviationDownQuantityAgreementOrder(o) BEFORE quantityOrderDetail(d), 
                  maxDeviationQuantityOrderDetail SHOWIF percDeviationUpQuantityAgreementOrder(o) AFTER quantityOrderDetail(d)
                  
;
EXTEND FORM userOrder
    PROPERTIES(d) READONLY  minDeviationQuantityOrderDetail SHOWIF percDeviationDownQuantityAgreementOrder(o) BEFORE quantityUserOrderDetail(d), 
                  maxDeviationQuantityOrderDetail SHOWIF percDeviationUpQuantityAgreementOrder(o) AFTER quantityUserOrderDetail(d)
 
;    
  
//--------------------------- Допустимый процент отклонения по цене --------------------------------------//
@defineHistorizable(percDeviationDownPrice, , '% отклонения по цене вниз', NUMERIC[8,2], agreement, nameAgreement, agreementPercPrice);
@defineHistorizable(percDeviationUpPrice, , '% отклонения по цене вверх', NUMERIC[8,2], agreement, nameAgreement, agreementPercPrice);
EXTEND FORM agreement 
    PROPERTIES(a) percDeviationDownPriceAgreement, percDeviationUpPriceAgreement 
;
EXTEND DESIGN agreement {
    deviation1{
        ADD a.agreementPercPrice;
    }
} 

agreementsPriceOrderDetail 'Цена из соглашения' (d) =
    priceBLedgerPriceListTypeSkuStockDateTime(priceListTypeOrderDetail(d),
                                              skuOrderDetail(d),
                                              customerStockOrderDetail(d),
                                              dateTimeOrderDetail(d));
percDeviationUpPriceAgreementOrderDetail '% отклонения по цене вверх' (d) = percDeviationUpPriceAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));
percDeviationDownPriceAgreementOrderDetail '% отклонения по цене вниз' (d) = percDeviationDownPriceAgreementDate(agreementOrder(orderOrderDetail(d)), dateOrderDetail(d));

maxDeviationPriceOrderDetail 'Макс. цена' (d) = (100.0 + percDeviationUpPriceAgreementOrderDetail(d)) * 
                                                        priceOrderDetail(d) /
                                                        100.0;
minDeviationPriceOrderDetail 'Мин. цена' (d) = (100.0 - percDeviationDownPriceAgreementOrderDetail(d)) *
                                                        priceOrderDetail(d) /
                                                        100.0;      

CONSTRAINT isPostedOrderDetail(d) AND (priceOrderDetail(d) != agreementsPriceOrderDetail(d))    
    MESSAGE 'Для строки заказа цена не совпадает с ценой из соглашения';    
    
percDeviationUpPriceAgreementOrder '% отклонения по цене вверх' (order) = percDeviationUpPriceAgreementDate(agreementOrder(order), dateOrder(order));
percDeviationDownPriceAgreementOrder '% отклонения по цене вниз' (order) = percDeviationDownPriceAgreementDate(agreementOrder(order), dateOrder(order));     

EXTEND FORM orders
    PROPERTIES(d) READONLY  minDeviationPriceOrderDetail SHOWIF percDeviationDownPriceAgreementOrder(o) BEFORE priceOrderDetail(d), 
                  maxDeviationPriceOrderDetail SHOWIF percDeviationUpPriceAgreementOrder(o) AFTER priceOrderDetail(d) 

;
EXTEND FORM userOrder
    PROPERTIES(d) READONLY  minDeviationPriceOrderDetail SHOWIF percDeviationDownPriceAgreementOrder(o) BEFORE priceUserOrderDetail(d), 
                  maxDeviationPriceOrderDetail SHOWIF percDeviationUpPriceAgreementOrder(o) AFTER priceUserOrderDetail(d)

;     

//
maxOrderDetailSkuOrder (sku, order) = GROUP MAX orderDetail BY skuOrderDetail(orderDetail),  orderOrderDetail(orderDetail);
agreementsPriceInvoiceDetail 'Цена из соглашения' (d) =
    priceBLedgerPriceListTypeSkuStockDateTime(priceListTypeInvoiceDetail(d),
                                              skuInvoiceDetail(d),
                                              customerStockInvoiceDetail(d),
                                              dateTimeInvoiceDetail(d));

percDeviationUpPriceAgreementInvoiceDetail '% отклонения по цене вверх' (d) = percDeviationUpPriceAgreementDate(agreementInvoice(invoiceInvoiceDetail(d)), dateInvoiceDetail(d));
percDeviationDownPriceAgreementInvoiceDetail '% отклонения по цене вниз' (d) = percDeviationDownPriceAgreementDate(agreementInvoice(invoiceInvoiceDetail(d)), dateInvoiceDetail(d));
maxDeviationPriceInvoiceDetail 'Макс. цена' (d) = (100.0 + percDeviationUpPriceAgreementInvoiceDetail(d)) * 
                                                        agreementsPriceInvoiceDetail(d) /
                                                        100.0;
minDeviationPriceInvoiceDetail 'Мин. цена' (d) = (100.0 - percDeviationDownPriceAgreementInvoiceDetail(d)) *
                                                        agreementsPriceInvoiceDetail(d) /
                                                        100.0; 
                                                         
overMaxDeviationPriceInvoiceDetail 'Макс. цена' (d) = OVERRIDE maxDeviationPriceInvoiceDetail(d), maxDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));
overMinDeviationPriceInvoiceDetail 'Мин. цена' (d) = OVERRIDE minDeviationPriceInvoiceDetail(d), minDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));                                                                                                                      
                                                         
CONSTRAINT isPostedInvoiceDetail(d) AND priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)    
    MESSAGE 'Для строки накладной превышен максимальный допустимый процент отклонения по цене';    

CONSTRAINT isPostedInvoiceDetail(d) AND priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d)    
    MESSAGE 'Для строки накладной превышен минимальный допустимый процент отклонения по цене';
                
//--------------------------- Запрет на оприходование без заявки --------------------------------------//

deviationNotOrderInvoiceAgreement 'Запретить оприходование накладных без заказа'  = DATA BOOLEAN (Agreement);
EXTEND FORM agreement 
    PROPERTIES(a) deviationNotOrderInvoiceAgreement 
;

maxOrderDetailInvoiceDetail (d) = maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)) IF isPostedOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));

CONSTRAINT isPostedInvoiceDetail(d) AND deviationNotOrderInvoiceAgreement(agreementInvoice(invoiceInvoiceDetail(d))) AND NOT maxOrderDetailInvoiceDetail(d)    
    MESSAGE 'Накладную запрещено оприходовать без заказа';   


//--------------------------- Минимальная сумма заказа для поставщика --------------------------------------//

minSupplierSumOrderAgreement 'Минимальная сумма заказа' = DATA NUMERIC[16,2] (Agreement) MINCHARWIDTH 13 PREFCHARWIDTH 13;
EXTEND FORM agreement 
    PROPERTIES(a) minSupplierSumOrderAgreement 
;
EXTEND DESIGN agreement {
    deviation{
        NEW deviation2 AFTER deviation1 {
            type = CONTAINERH;
            ADD PROPERTY (deviationNotOrderInvoiceAgreement(a));
            ADD PROPERTY (minSupplierSumOrderAgreement(a));
        }
    }
}

minSumSupplierOrder 'Минимальная сумма заказа' (o) = minSupplierSumOrderAgreement(agreementUserOrder(o)) IN documentSum MINCHARWIDTH 13 PREFCHARWIDTH 13;
backgroundMinSumSupplierOrder (o) = RGB (255,100,100) IF minSumSupplierOrder(o) > sumUserOrderDetailUserOrder(o);

EXTEND FORM userOrder 
    PROPERTIES(o) READONLY minSumSupplierOrder BACKGROUND backgroundMinSumSupplierOrder(o) 
;

//--

maxDeviationPriceOrderInvoiceDetail 'Макс. цена' (d) = maxDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));
minDeviationPriceOrderInvoiceDetail 'Мин. цена' (d) = minDeviationPriceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 
priceOrderInvoiceDetail 'Цена заказа' (d) = priceOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 


maxDeviationQuantityOrderInvoiceDetail 'Макс. к-во' (d) = maxDeviationQuantityOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d)));
minDeviationQuantityOrderInvoiceDetail 'Мин. к-во' (d) = minDeviationQuantityOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 
quantityOrderInvoiceDetail 'К-во заказа' (d) =quantityOrderDetail(maxOrderDetailSkuOrder(skuInvoiceDetail(d), orderInvoiceDetail(d))); 

percDeviationUpPriceOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND percDeviationUpPriceAgreementDate(agreementOrder(o), dateOrder(o)) BY i;  
percDeviationDownPriceOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND percDeviationDownPriceAgreementDate(agreementOrder(o), dateOrder(o)) BY i;  

percDeviationUpQuantityOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND percDeviationUpQuantityAgreementDate(agreementOrder(o), dateOrder(o)) BY i;  
percDeviationDownQuantityOrdersInvoice (i) = GROUP SUM 1 IF includeOrderUserInvoice(o,i) AND percDeviationDownQuantityAgreementDate(agreementOrder(o), dateOrder(o)) BY i; 
  
    
backgroundMaxPriceInvoiceDetail (d) = IF priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);

backgroundMinPriceInvoiceDetail (d) = IF priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);

//overShipmentQuantityInvoiceDetail (d) = OVERRIDE shipmentQuantityInvoiceDetail(d), shippedInvoiceDetail(d);    
    
backgroundMaxQuantityInvoiceDetail (d) = IF shipmentQuantityInvoiceDetail(d) > maxDeviationQuantityOrderInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);

backgroundMinQuantityInvoiceDetail (d) = IF shipmentQuantityInvoiceDetail(d) < minDeviationQuantityOrderInvoiceDetail(d) 
    THEN  RGB(0,255,119) 
    ELSE RGB(249,255,212);  
      
  
    
EXTEND FORM userInvoice
    PROPERTIES (d) READONLY BEFORE priceUserInvoiceDetail(d) 
                   minDeviationPriceOrderInvoiceDetail SHOWIF percDeviationDownPriceOrdersInvoice(i) BACKGROUND backgroundMinPriceInvoiceDetail(d), 
                   priceOrderInvoiceDetail SHOWIF countIncludeOrdersInvoice(i), 
                   maxDeviationPriceOrderInvoiceDetail SHOWIF percDeviationUpPriceOrdersInvoice(i) BACKGROUND backgroundMaxPriceInvoiceDetail(d)
     
    PROPERTIES (d) READONLY BEFORE quantityUserInvoiceDetail(d) 
                   minDeviationQuantityOrderInvoiceDetail SHOWIF percDeviationDownQuantityOrdersInvoice(i) BACKGROUND backgroundMinQuantityInvoiceDetail(d), 
                   quantityOrderInvoiceDetail SHOWIF countIncludeOrdersInvoice(i), 
                   maxDeviationQuantityOrderInvoiceDetail SHOWIF percDeviationUpQuantityOrdersInvoice(i) BACKGROUND backgroundMaxQuantityInvoiceDetail(d) 

    FILTERGROUP    deviation
        FILTER  'Есть отклонения' 'F6' (priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)) OR 
                                       (priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d)) OR 
                                       (shipmentQuantityInvoiceDetail(d) > maxDeviationQuantityOrderInvoiceDetail(d)) OR 
                                       (shipmentQuantityInvoiceDetail(d) < minDeviationQuantityOrderInvoiceDetail(d)) 
        FILTER  'По цене' 'ctrl F6' (priceInvoiceDetail(d) > overMaxDeviationPriceInvoiceDetail(d)) OR 
                                    (priceInvoiceDetail(d) < overMinDeviationPriceInvoiceDetail(d))        
        FILTER  'По количеству' 'shift F6' (shipmentQuantityInvoiceDetail(d) > maxDeviationQuantityOrderInvoiceDetail(d)) OR 
                                           (shipmentQuantityInvoiceDetail(d) < minDeviationQuantityOrderInvoiceDetail(d))         
        

;

EXTEND DESIGN userInvoice {
    PROPERTY(priceOrderInvoiceDetail(d)) { background = #F9FFD4; }
    PROPERTY(quantityOrderInvoiceDetail(d)) { background = #F9FFD4; }
}


