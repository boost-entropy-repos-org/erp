MODULE PurchaseDeclarationDashboard;

REQUIRE Dashboard, PurchaseDeclarationDetail;

NAMESPACE Purchase;

selected 'Отм.' = DATA LOCAL BOOLEAN (UserInvoice); 
minSelectedUserInvoice = GROUP MIN UserInvoice i IF selected(i); 

countSelectedUserInvoice 'Кол-во отм.' = GROUP SUM 1 IF selected(UserInvoice invoice);

countSelectedCustomerUserInvoices 'Кол-во получателей' = GROUP SUM 1 IF [= GROUP SUM 1 IF selected(UserInvoice invoice) BY customer(invoice)](LegalEntity invoice);
countSelectedCurrencyUserInvoices 'Кол-во валют' = GROUP SUM 1 IF [= GROUP SUM 1 IF selected(UserInvoice invoice) BY currency(invoice)](Currency invoice);

createMultiDeclaration 'Создать декларацию'(UserInvoice invoice) = ACTION  {
    IF countSelectedUserInvoice() THEN {
        IF countSelectedCustomerUserInvoices()!=1 OR countSelectedCurrencyUserInvoices() != 1 THEN {
            MESSAGE 'Выбраны накладные с разными покупателями или валютами';               
        } ELSE {
            FOR ADDOBJ d = Declaration DO {
                legalEntity(d) <- customer(minSelectedUserInvoice());
                currency(d) <- currency(minSelectedUserInvoice());
                FOR selected(UserInvoice i) DO {
                    declaration(i) <- d;
                }
                FORM declaration OBJECTS d=d DOCKEDMODAL;
                IF formResult() == FormResult.ok THEN {
                    apply();
                } ELSE {
                    DELETE d;
                }  
            }         
        }
    } ELSE {
        FOR ADDOBJ d = Declaration DO {
            legalEntity(d) <- customer(invoice);
            currency(d) <- currency(invoice);                
            declaration(invoice) <- d;
          
            FORM declaration OBJECTS d=d DOCKEDMODAL;  
            IF formResult() == FormResult.ok THEN {
                apply();
            } ELSE {
                DELETE d;
            }                           
        }          
    }    
} TOOLBAR;


FORM declarationDashboard 'Таможенное оформление'

    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES(dt) date = OBJVALUE

    OBJECTS i=UserInvoice FIXED GRID
    PROPERTIES (i) selected
    PROPERTIES (i) READONLY isPosted, number, series, date, time,                                 
                   nameSupplier, nameCustomer, 
                   nameCurrency, countUserInvoiceDetail, quantityUserInvoiceDetail, sumUserInvoiceDetail,
                   VATSumUserInvoiceDetail, invoiceSumUserInvoiceDetail

    PROPERTIES (i) createMultiDeclaration FORCE PANEL TOOLBAR
    FILTERS        showDeclaration(i), 
                   TRUE AND NOT  declaration(i)
    
    OBJECTS d = Declaration FIXED GRID
    PROPERTIES(d)  READONLY isPosted FORCE GRID, number, series, date, time,
                   nameLegalEntity, nameHomeCurrency, nameCurrency, 
                   sumDeclarationDetail, deliverySum, chargeSum,
                   homeSumDeclarationDetail, dutySumDeclarationDetail, VATSumDeclarationDetail, 
                   registrationSum
    PROPERTIES(d)  ADDFORM, EDITFORM, deleted =DELETE FORCE PANEL TOOLBAR                       
                       
    FILTERGROUP filters FILTER 'Накладные на дату' date(i)<=dt 'F5' DEFAULT
    FILTERGROUP filters1 FILTER 'Декларации на дату' date(d)==dt 'F5' DEFAULT
                               
;

NAVIGATOR {
    customsDashboardNavigator {
        ADD declarationDashboard;
    }
}