MODULE LegalEntityExtraUOM;

REQUIRE LegalEntity, ExtraUOM;

groupType = DATA GroupType (LegalEntity);
nameGroupType 'Тип классификатора' (LegalEntity legalEntity) = name(groupType(legalEntity));

dataExtraUOM = DATA UOM (LegalEntity, Group);
shortNameDataExtraUOM 'Ед. изм. поставщика' (LegalEntity legalEntity, Group group) = shortName(dataExtraUOM(legalEntity, group));

dataExtraUOM = DATA UOM (LegalEntity, Sku);
shortNameDataExtraUOM 'Ед. изм. поставщика' (LegalEntity legalEntity, Sku sku) = shortName(dataExtraUOM(legalEntity, sku));

levelParentExtraUOM (legalEntity, group) = GROUP MIN level(Group group, Group parent) IF dataExtraUOM(LegalEntity legalEntity, parent)
                                                              BY legalEntity, group PERSISTENT;
nearestParent (LegalEntity legalEntity, Group group) = group(group, levelParentExtraUOM(legalEntity, group));
nearestExtraUOM (LegalEntity legalEntity, Group group) =
    dataExtraUOM(legalEntity, nearestParent(legalEntity, group)) PERSISTENT;
nameNearestExtraUOM 'Ед. изм. поставщика(перегруженная)' (LegalEntity legalEntity, Group group) = shortName(nearestExtraUOM(legalEntity, group));    

extraUOM (LegalEntity legalEntity, Group group) =
    OVERRIDE nearestExtraUOM(legalEntity, group), dataExtraUOM(legalEntity, group) PERSISTENT;
    
extraUOM = OVERRIDE 
    extraUOM(LegalEntity legalEntity, group(groupType(legalEntity), Sku sku)),
    dataExtraUOM(legalEntity, sku) PERSISTENT;
nameExtraUOM 'Ед. изм. поставщика(перегруженная)' (LegalEntity legalEntity, Sku sku) =  shortName(extraUOM(legalEntity, sku));    
   
quantityChildWithExtraUOM  = GROUP SUM 1 IF dataExtraUOM(LegalEntity legalEntity, Group child) AND isParent(child, Group parent) BY parent, legalEntity PERSISTENT;
quantityParentWithExtraUOM  = GROUP SUM 1 IF dataExtraUOM(LegalEntity legalEntity, Group parent) AND isParent(Group child, parent) BY child, legalEntity PERSISTENT;                                                                        

backgroundExtraUOM 'Цвет' (LegalEntity legalEntity, Group group) = CASE 
    WHEN dataExtraUOM(legalEntity, group) THEN RGB(230,248,250) 
    WHEN quantityChildWithExtraUOM (group, legalEntity) != descendantNumber(group) AND NOT quantityParentWithExtraUOM (group, legalEntity) THEN RGB(203,203,203);    

FORM extraUOMLegalEntity 'Единицы измемерения поставщика'
    OBJECTS l = LegalEntity FIXED PANEL 
    PROPERTIES(l) SELECTOR name
    PROPERTIES(l) nameGroupType

    TREE treeGroup g=Group PARENT parent
    PROPERTIES READONLY order(g), name(g)
    FILTERS groupType(g) == groupType(l)
    ORDER BY order(g), name(g)
    FILTERGROUP inactive FILTER 'Активные' active(g) 'F5' DEFAULT
    PROPERTIES(l, g) shortNameDataExtraUOM, nameNearestExtraUOM BACKGROUND backgroundExtraUOM(l,g) READONLY 

    OBJECTS s = Sku
    PROPERTIES(s) READONLY name, idBarcode
    FILTERS isParent(g, s) OR s IS Sku AND NOT g
    PROPERTIES(l, s) shortNameDataExtraUOM, nameExtraUOM
    FILTERGROUP inactiveSku 
        FILTER 'Активный' active(s) 'F5' DEFAULT    
;

DESIGN extraUOMLegalEntity{
    MOVE l.box;
    NEW groupContainer {
        fill = 1;
        NEW treeContainer{
            fill = 1;
            type = SPLITH;
            MOVE treeGroup.tree.box { caption = 'Группы товаров';}
            MOVE s.box {
                fill = 2;
            }
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    skuNavigator {
        ADD extraUOMLegalEntity;            
    }
}