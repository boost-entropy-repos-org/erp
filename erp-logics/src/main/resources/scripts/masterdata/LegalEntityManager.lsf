MODULE LegalEntityManager;

REQUIRE LegalEntity, EmployeeManager;

NAMESPACE LegalEntity;

manager = DATA Employee (LegalEntity);
nameManager 'Менеджер' (LegalEntity l) = shortName(manager(l));
inManager 'Отм' (Employee e, LegalEntity l) = e == manager(l);
countInManager = GROUP SUM 1 IF inManager (Employee e, LegalEntity l) BY e;

EXTEND FORM legalEntity
    PROPERTIES (l) nameManager
;

DESIGN legalEntity {
    extendContainer {
        NEW managerContainer {
            caption = 'Менеджмент';
            MOVE PROPERTY(nameManager(l));
        }
    } 
}

EXTEND FORM employee
    TREE legalEntityTreeM lgm = LegalEntityGroup PARENT parent
    PROPERTIES READONLY  lgmTreeName = name(lgm)
    ORDER BY lgmTreeName

    OBJECTS lm = LegalEntity FIXED GRID
    PROPERTIES(lm) READONLY name, nameManager
    PROPERTIES (e, lm) inManager
    ORDER BY name(lm)
    FILTERS isParent(lgm, lm) OR (lm IS LegalEntity AND NOT lgm)
;

DESIGN employee {
    managerTabContainer {
        NEW managerContainer {
            caption = 'По организации';
            fill = 1;
            type = SPLITH;
            MOVE legalEntityTreeM.tree.box {fill = 1;};
            MOVE lm.box {fill = 3;};
        }
    }
}