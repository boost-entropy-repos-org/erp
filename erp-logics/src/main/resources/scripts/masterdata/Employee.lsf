MODULE Employee;

REQUIRE System, Authentication, MasterData, Security, Email, Hierarchy, Utils, Integration;

// ------------------------------------- Должности -------------------------------------- //
CLASS Position 'Должность';
TABLE position(Position);

@defineExternalizable(position, STRING[100]);

namePosition 'Наименование' = DATA ISTRING[100](Position);

positionEmployee(employee) = DATA Position (Employee);

FORM position 'Должность'
    OBJECTS of=Position FIXED PANEL
    PROPERTIES(of) namePosition
    EDIT Position OBJECT of
;

FORM positions 'Должность'
    OBJECTS of=Position
    PROPERTIES(of) READONLY namePosition, idPosition SHOWIF showIDs()
    PROPERTIES(of) ADDFORM, EDITFORM, DELETE
    DIALOG Position OBJECT of
;

// ------------------------------------- Доступ ------------------------------------------ //

// ------------------------------------- Сотрудники -------------------------------------- //

CLASS Employee 'Сотрудник' : CustomUser;
TABLE employee(Employee);

@defineExternalizable(employee, STRING[100]);

namePositionEmployee 'Должность сотрудника' (employee) = namePosition(positionEmployee(employee)) IN base MINCHARWIDTH 15 PREFCHARWIDTH 15;

positionNameEmployee 'Должность ФИО' (employee) = [FORMULA STRING[40] ' CAST($1 AS TEXT) || \' \' || CAST($2 AS TEXT)']
                                                         (namePositionEmployee(employee), nameContact(employee));

limitAccessEmployee 'Ограничить доступ' = DATA BOOLEAN (Employee);


FORM employee 'Сотрудник'
    OBJECTS e=Employee FIXED PANEL
    PROPERTIES(e)      firstNameContact, lastNameContact, idEmployee SHOWIF showIDs(), namePositionEmployee,
                       loginCustomUser, passwordCustomUser, emailContact, nameMainRoleUser, limitAccessEmployee

    EDIT Employee OBJECT e
;

DESIGN employee FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        childConstraints = TO THE BOTTOM;
        NEW oneCase {
            caption = 'Сотрудник';
            childConstraints = TO THE RIGHT;
            NEW twoCase {
                caption = 'Личные данные';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(firstNameContact(e));
                ADD PROPERTY(lastNameContact(e));
                ADD PROPERTY(idEmployee(e));
                ADD PROPERTY(namePositionEmployee(e));
            };
            NEW threeCase {
                caption = 'Данные пользователя';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(loginCustomUser(e));
                ADD PROPERTY(passwordCustomUser(e));
                ADD PROPERTY(emailContact(e));
                ADD PROPERTY(nameMainRoleUser(e));
            };
            NEW row {
                caption = 'Доступ';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(limitAccessEmployee);
            }

        }
        NEW topContainer{
            type = TABBED;
        }
        ADD functions.box;
    }
}

FORM employees 'Сотрудники'
    OBJECTS e=Employee
    PROPERTIES(e) READONLY firstNameContact, lastNameContact, idEmployee SHOWIF showIDs(), namePositionEmployee
    PROPERTIES(e) ADDFORM, EDITFORM, DELETE
    DIALOG Employee OBJECT e
;

NAVIGATOR {
    masterData {
        ADD employees BEFORE regionalData;
    }
}

