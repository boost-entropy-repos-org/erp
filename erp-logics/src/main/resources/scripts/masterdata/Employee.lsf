MODULE Employee;

REQUIRE System, Authentication, MasterData, Security, Email, Hierarchy, Utils, Integration;

// ------------------------------------- Должности -------------------------------------- //
CLASS Position 'Должность';
TABLE position(Position);

@defineExternalizable(position, VARSTRING[100]);

namePosition 'Наименование' = DATA VARISTRING[100](Position);

positionEmployee(employee) = DATA Position (Employee);

FORM position 'Должность'
    OBJECTS of=Position FIXED PANEL
    PROPERTIES(of) namePosition
    EDIT Position OBJECT of
;

FORM positions 'Должности'
    OBJECTS of=Position
    PROPERTIES(of) READONLY namePosition, idPosition SHOWIF showIDs()
    PROPERTIES(of) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    DIALOG Position OBJECT of
;

// ------------------------------------- Доступ ------------------------------------------ //

// ------------------------------------- Сотрудники -------------------------------------- //

CLASS Employee 'Сотрудник' : CustomUser;
TABLE employee(Employee);

@defineExternalizable(employee, VARSTRING[100]);

initialsEmployee 'Инициалы (И.О.)' = DATA VARSTRING[10] (Contact) MINCHARWIDTH 5;
shortNameContact 'Фамилия И.О.' (contact) = CONCAT ' ', lastNameContact(contact), 
                                                        (OVERRIDE firstNameContact(contact), initialsEmployee(contact));
firstShortNameContact 'И.О. Фамилия' (contact) = CONCAT ' ', (OVERRIDE firstNameContact(contact), initialsEmployee(contact)),
                                                        lastNameContact(contact);
namePositionEmployee 'Должность сотрудника' (employee) = namePosition(positionEmployee(employee)) IN base MINCHARWIDTH 15 PREFCHARWIDTH 15;

positionNameEmployee 'Должность ФИО' (employee) =  CONCAT ' ', namePositionEmployee(employee), nameContact(employee) IF employee IS Employee;
positionShortNameEmployee 'Должность ФИО (инициалы)' (employee) =  CONCAT ' ', namePositionEmployee(employee), shortNameContact(employee) IF employee IS Employee;

limitAccessEmployee 'Ограничить доступ' = DATA BOOLEAN (Employee);
showLimitAccessEmployee = limitAccessEmployee(e) AND showIDs();

FORM employee 'Сотрудник'
    OBJECTS e=Employee FIXED PANEL
    PROPERTIES(e)      objectClassName, lastNameContact, firstNameContact, initialsEmployee, idEmployee SHOWIF showIDs(), namePositionEmployee,
                       loginCustomUser, 
                       sha256PasswordCustomUser ON CHANGE changeSHA256PasswordCustomUser(e),
                       emailContact, nameMainRoleUser, limitAccessEmployee
    OBJECTS r = UserRole
    PROPERTIES (r) READONLY nameUserRole, sidUserRole
    PROPERTIES inUserRole(e,r)
    
    EDIT Employee OBJECT e
;

DESIGN employee {
    main {
        preferredSize = (1024, 768);
        NEW header {
            type = CONTAINERH;
            NEW personal {
                caption = 'Личные данные';
                type = COLUMNS;
                columns = 1;
                MOVE PROPERTY(lastNameContact(e));
                MOVE PROPERTY(firstNameContact(e));   
                MOVE PROPERTY(initialsEmployee(e));                  
                MOVE PROPERTY(idEmployee(e));
                MOVE PROPERTY(objectClassName(e)) {
                    caption = 'Тип';
                }
                MOVE PROPERTY(namePositionEmployee(e));
            }
            NEW authentication {
                caption = 'Данные пользователя';
                type = COLUMNS;
                columns = 1;
                MOVE PROPERTY(loginCustomUser(e));
                MOVE PROPERTY(sha256PasswordCustomUser(e));
                MOVE PROPERTY(emailContact(e));
                MOVE PROPERTY(nameMainRoleUser(e));
            }
            NEW access {
                caption = 'Доступ';
                type = COLUMNS;
                columns = 1;
                MOVE PROPERTY(limitAccessEmployee(e));
            }
        }
        NEW pane {
            fill = 1;
            type = TABBED;
            MOVE r.box {
                fill = 1;
                caption= 'Дополнительные роли';                
            }
        }
        MOVE functions.box;
    }
}
overCopyEmployee = ABSTRACT ACTION LIST (Employee, Employee);
copyEmployee 'Копировать' = ACTION (employee) NEWSESSION {
    FOR ADDOBJ e = Employee DO {

        positionEmployee(e) <- positionEmployee(employee);
        mainRoleUser(e) <- mainRoleUser(employee);
        limitAccessEmployee(e) <- limitAccessEmployee(employee);
        inUserRole(e,r) <- inUserRole(employee,r) WHERE inUserRole(employee,r);

        overCopyEmployee(e, employee);

        FORM employee OBJECTS e = e MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;


FORM employees 'Сотрудники'
    PROPERTIES isEditable()
    OBJECTS e=Employee
    PROPERTIES(e) READONLYIF isReadonly() lastNameContact, firstNameContact, initialsEmployee, idEmployee SHOWIF showIDs(), namePositionEmployee, shortNameContact, nameMainRoleUser
    PROPERTIES(e) addE=ADDFORM, editE=EDITFORM, deleteE=DELETE FORCE PANEL TOOLBAR, copyEmployee
    DIALOG Employee OBJECT e
;

DESIGN employees {
    MOVE functions.box{
        MOVE PROPERTY(isEditable()) BEFORE rightControls;
    }
}

NAVIGATOR {
    masterData {
        ADD employees BEFORE regionalData;
    }
}

// При создании пользователя автоматически делать его сотрудником

WHEN SET(u IS CustomUser) AND NOT u IS Employee DO {
    CHANGECLASS u TO Employee;
}