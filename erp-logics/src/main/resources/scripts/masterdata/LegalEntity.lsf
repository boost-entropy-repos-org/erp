MODULE LegalEntity;

REQUIRE System, MasterData, Historizable, Tax, Country, I18n, Employee;

// ----------------------------------- Форма собственности ------------------------------------------ //

CLASS Ownership 'Форма собственности';
TABLE ownership (Ownership);

nameOwnership 'Наименование' = DATA VARISTRING[100](Ownership);

shortNameOwnership 'Cокращенное название' = DATA STRING[10] (Ownership) IN base FIXEDCHARWIDTH 6;
ownershipShortName (string) = GROUP NAGGR ownership BY shortNameOwnership (ownership) WHERE ownership IS Ownership;

countryOwnership = DATA Country (Ownership);
nameCountryOwnership 'Страна' (ownership) = nameCountry(countryOwnership(ownership)) MINCHARWIDTH 10 PREFCHARWIDTH 15 MAXCHARWIDTH 20;

FORM ownership 'Форма собственности'
    OBJECTS o = Ownership FIXED PANEL
    PROPERTIES(o) nameOwnership, shortNameOwnership, nameCountryOwnership

    EDIT Ownership OBJECT o
;

FORM ownerships 'Формы собственности'
    OBJECTS o = Ownership
    PROPERTIES(o) READONLY nameOwnership, shortNameOwnership, nameCountryOwnership
    PROPERTIES(o)          ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG Ownership OBJECT o
;

// ----------------------------------- Юридическое лицо --------------------------------- //

CLASS LegalEntity 'Организация' : TaxUnitGroup;
TABLE legalEntity (LegalEntity);
TABLE legalEntityDate (LegalEntity, DATE);

@defineExternalizable(legalEntity, VARSTRING[100]);

nameLegalEntity 'Наименование' = DATA VARISTRING[150](LegalEntity) INDEXED MINCHARWIDTH 20 PREFCHARWIDTH 30;

nameTaxUnitGroup(group) += nameLegalEntity(group) IF group IS LegalEntity;

countryLegalEntity = DATA Country(LegalEntity);
nameCountryLegalEntity 'Страна' (legalEntity) = nameCountry(countryLegalEntity(legalEntity)) MINCHARWIDTH 10 PREFCHARWIDTH 15 MAXCHARWIDTH 20;

currencyLegalEntity = DATA Currency(LegalEntity);
nameCurrencyLegalEntity 'Базовая валюта' (legalEntity) = nameCurrency(currencyLegalEntity(legalEntity)) MINCHARWIDTH 10 PREFCHARWIDTH 10 MAXCHARWIDTH 15;
currencyLegalEntity(legalEntity) <- currencyCountry(countryLegalEntity(legalEntity)) WHEN CHANGED(countryLegalEntity(legalEntity));

languageLegalEntity = DATA Language(LegalEntity);
nameLanguageLegalEntity 'Язык' (legalEntity) = nameLanguage(languageLegalEntity(legalEntity)) MINCHARWIDTH 5 PREFCHARWIDTH 10 MAXCHARWIDTH 15;
languageLegalEntity(legalEntity) <- languageCountry(countryLegalEntity(legalEntity)) WHEN CHANGED(countryLegalEntity(legalEntity));

GROUP law 'Реквизиты' : public;

ownershipLegalEntity = DATA Ownership (LegalEntity);
nameOwnershipLegalEntity 'Форма собственности' = nameOwnership(ownershipLegalEntity(legalEntity)) IN law;
shortNameOwnershipLegalEntity 'Форма собственности (сокр.)' = shortNameOwnership(ownershipLegalEntity(legalEntity)) IN law;

CONSTRAINT countryOwnership(ownershipLegalEntity(legalEntity)) != countryLegalEntity(legalEntity)
    CHECKED BY ownershipLegalEntity MESSAGE 'Страна формы собственности должна совпадать со страной организации';

fullNameLegalEntity 'Полное наименование' = DATA VARSTRING[200] (LegalEntity) IN law MINCHARWIDTH 50 PREFCHARWIDTH 50;

@defineHistorizable(address, , 'Юридический адрес', VARSTRING[150], legalEntity, nameLegalEntity, law);
@defineHistorizable(postAddress, , 'Почтовый адрес', VARSTRING[150], legalEntity, nameLegalEntity, law);

GROUP contact 'Контактная информация' : public;

@defineHistorizable(phone, , 'Телефон/факс', VARSTRING[100], legalEntity, nameLegalEntity, contact);
emailLegalEntity 'E-mail' = DATA VARSTRING[100] (LegalEntity) IN contact MINCHARWIDTH 50 PREFCHARWIDTH 50;
siteLegalEntity 'Сайт' = DATA VARSTRING[100] (LegalEntity) IN contact MINCHARWIDTH 50 PREFCHARWIDTH 50;
contactsLegalEntity 'Дополнительная информация' = DATA VARSTRING[300] (LegalEntity) IN contact MINCHARWIDTH 50 PREFCHARWIDTH 50;

GROUP management 'Управление' : public;

@defineHistorizableCustom(chiefLegalEntity, 'Руководитель', Employee, nameContact, legalEntity, nameLegalEntity, management );
@defineHistorizableCustom(bookerLegalEntity, 'Главный бухгалтер', Employee, nameContact, legalEntity, nameLegalEntity, management );
// Должность ФИО
positionNameManagerLegalEntity 'Руководитель' (legalEntity) = positionNameEmployee(chiefLegalEntity(legalEntity)) IN management; 
positionNameAccountantLegalEntity 'Главный бухгалтер' (legalEntity) = positionNameEmployee(bookerLegalEntity(legalEntity)) IN management;

GROUP doc 'Для договора' : public;
GROUP order 'Для заказов' : public;

// Неактивный
inactiveLegalEntity 'Неактивная' = DATA BOOLEAN (LegalEntity);
activeLegalEntity 'Активная' (l) = l IS LegalEntity AND NOT inactiveLegalEntity(l);

// ----------------------------------- Группы юридический лиц ---------------------------- //

CLASS LegalEntityGroup 'Группа организаций' ;
TABLE legalEntityGroup(LegalEntityGroup);

nameLegalEntityGroup 'Наименование' = DATA VARISTRING[100](LegalEntityGroup);
legalEntityGroupLegalEntity (legalEntity) = DATA LegalEntityGroup (LegalEntity) PERSISTENT INDEXED AUTOSET;
nameLegalEntityGroupLegalEntity 'Группа организаций' (legalEntity) = nameLegalEntityGroup(legalEntityGroupLegalEntity(legalEntity));

TABLE legalEntityGroupLegalEntityGroup(LegalEntityGroup, LegalEntityGroup);
@defineHierarchy(legalEntityGroup);

FORM legalEntityGroup 'Группа организаций'

    OBJECTS l = LegalEntityGroup FIXED PANEL
    PROPERTIES(l) nameLegalEntityGroup, nameParentLegalEntityGroup

    EDIT LegalEntityGroup OBJECT l
;

FORM legalEntityGroups 'Группы организаций'

    TREE legalEntityGroupTree lg = LegalEntityGroup PARENT parentLegalEntityGroup
    PROPERTIES READONLY lgTreeName = nameLegalEntityGroup(lg)
    ORDER BY lgTreeName

    DIALOG LegalEntityGroup OBJECT lg
;

// ----------------------------------- Лицензия ------------------------------------------ //
GROUP license 'Лицензионная информация' : base;
CLASS License 'Лицензия';
TABLE license (License);

numberLicense 'Номер лицензии' = DATA VARSTRING[100] (License) IN license;
dateFromLicense 'Действует с ' = DATA DATE (License) IN license;
dateToLicense 'Действует по ' = DATA DATE (License) IN license;

descriptionLicense(license) = VARSTRING[150]( CONCAT '; ', '№ '+numberLicense(license),
                                                           'с '+ dateFromLicense(license),
                                                           'по '+ dateToLicense(license));

countryLicense = DATA Country (License);
nameCountryLicense 'Страна' = nameCountry(countryLicense(license));

legalEntityLicense 'ЮЛ (ИД)' = DATA LegalEntity (License);

licenseLegalEntityDate 'Действующая на дату' (legalEntity, date) = GROUP MAX license IF dateFromLicense(license) <= date AND NOT dateToLicense(license) < date
                                                                         BY legalEntityLicense(license), date;
currentLicenseLegalEntity 'Действующая' (legalEntity) = licenseLegalEntityDate(legalEntity, currentDate());
numberCurrentLicenseLegalEntity 'Действующая лицензия' (legalEntity) = numberLicense(currentLicenseLegalEntity(legalEntity)) IN law;

CONSTRAINT countryLegalEntity(legalEntityLicense(license)) != countryLicense(license)
    CHECKED BY legalEntityLicense MESSAGE 'Страна лицензии должна совпадать со страной организации';

// ------------------------------------ Собственные компании ----------------------------- //
GROUP filterEntity 'Фильтры' : public;

isSupplierLegalEntity 'Явл. поставщиком' = DATA BOOLEAN (LegalEntity) IN filterEntity;
isCompanyLegalEntity 'Явл. компанией' = DATA BOOLEAN (LegalEntity) IN filterEntity;
isCustomerLegalEntity 'Явл. покупателем' = DATA BOOLEAN (LegalEntity) IN filterEntity;
isSellerLegalEntity 'Поставщик или компания' (legalEntity) = isSupplierLegalEntity(legalEntity) OR isCompanyLegalEntity(legalEntity);
isBuyerLegalEntity 'Покупатель или компания' (legalEntity) = isCustomerLegalEntity(legalEntity) OR isCompanyLegalEntity(legalEntity);

differentStocksInDetailsLegalEntity 'Разные склады в строках' = DATA BOOLEAN (LegalEntity) IN filterEntity;

// ------------------------------------ Доступные организации для пользователей -------------------------- //

toShowLegalEntity = ABSTRACT BOOLEAN (LegalEntity);
toShowIndividualLegalEntity = ABSTRACT BOOLEAN (LegalEntity);

toShowLegalEntity(legalEntity) += legalEntity IS LegalEntity AND NOT toShowIndividualLegalEntity(legalEntity);

isParentLegalEntityGroupLegalEntity (lg,l) = isParentLegalEntityGroupLegalEntityGroup(legalEntityGroupLegalEntity(l), lg);

// ---------------------------------------- Формы для ввода компаний ------------------------------------- //

FORM legalEntity 'Организация'
    OBJECTS l=LegalEntity FIXED PANEL
    PROPERTIES(l)  nameLegalEntity, nameLegalEntityGroupLegalEntity, nameOwnershipLegalEntity, shortNameOwnershipLegalEntity,
                   nameCountryLegalEntity, nameLanguageLegalEntity, nameCurrencyLegalEntity, inactiveLegalEntity
    PROPERTIES(l)  SHOWIF toShowLegalEntity(l) fullNameLegalEntity,
                   addressLegalEntity, postAddressLegalEntity, nameContactChiefLegalEntity, nameContactBookerLegalEntity, phoneLegalEntity,
                   emailLegalEntity, siteLegalEntity,
                   isSupplierLegalEntity, isCompanyLegalEntity, isCustomerLegalEntity, idLegalEntity SHOWIF showIDs()
    PROPERTIES(l)  differentStocksInDetailsLegalEntity SHOWIF isCompanyLegalEntity(l)

    OBJECTS li=License FIXED GRID
    PROPERTIES(li) SHOWIF toShowLegalEntity(l) numberLicense, dateFromLicense, dateToLicense, ADDOBJ, DELETESESSION
    FILTERS legalEntityLicense(li) == l

    EDIT LegalEntity OBJECT l
;

DESIGN legalEntity FROM DEFAULT {
    main{
       preferredSize = (1024, 768);
       l.box {
            type = CONTAINERH;

            NEW column1 {
                type = CONTAINERV;

                NEW row11 {
                    type = COLUMNS;
                    columns = 2;
                    ADD PROPERTY(nameLegalEntity(l));
                    ADD PROPERTY(nameLegalEntityGroupLegalEntity(l));
                    ADD PROPERTY(nameOwnershipLegalEntity(l));
                    ADD PROPERTY(shortNameOwnershipLegalEntity(l));
                    ADD PROPERTY(idLegalEntity(l));
                    ADD PROPERTY(inactiveLegalEntity(l));
                }
                NEW regionContainer {
                    type = CONTAINERH;
                    caption = 'Региональные настройки';
                    ADD PROPERTY(nameCountryLegalEntity(l));
                    ADD PROPERTY(nameCurrencyLegalEntity(l));
                    ADD PROPERTY(nameLanguageLegalEntity(l));
                }
                NEW row12 {
                    type = COLUMNS;
                    columns = 2;
                    ADD l.law {
                        columns = 1;
                    }
                    ADD l.filterEntity {
                        columns = 1;
                    }
                }
            }

            NEW column2 {
                type = CONTAINERV;

                ADD l.contact {
                    columns = 1;
                }
                ADD l.management {
                    columns = 1;
                }
            }
       }

       NEW extendContainer BEFORE functions.box {
            type = TABBED;
            fill = 1;
            ADD li.box;
       }
   }
}

FORM legalEntities 'Организации'

    TREE legalEntityGroupTree lg = LegalEntityGroup PARENT parentLegalEntityGroup
    PROPERTIES READONLY lgTreeName = nameLegalEntityGroup(lg)
    PROPERTIES(lg) ADDFORM, EDITFORM, deletelg=DELETE FORCE PANEL TOOLBAR

    OBJECTS l=LegalEntity
    PROPERTIES(l) READONLYIF isReadonly() nameL = nameLegalEntity, idLegalEntity SHOWIF showIDs(), fullNameLegalEntity, shortNameOwnershipLegalEntity,
                                          nameLegalEntityGroupLegalEntity
    PROPERTIES(l) READONLY                addressLegalEntity, phoneLegalEntity
    PROPERTIES(l) READONLYIF isReadonly() isSupplierLegalEntity, isCompanyLegalEntity, isCustomerLegalEntity
    PROPERTIES(l) ADDFORM, EDITFORM, deletel=DELETE FORCE PANEL TOOLBAR
    ORDER BY nameL
    FILTERGROUP inactiveLegalEntity FILTER 'Активная орг-ия' 'shift F10' activeLegalEntity(l) DEFAULT    
    FILTERS isParentLegalEntityGroupLegalEntityGroup(legalEntityGroupLegalEntity(l), lg) OR
            l IS LegalEntity AND NOT lg IS LegalEntityGroup OR
            l IS LegalEntity AND lg IS LegalEntityGroup AND NOT legalEntityGroupLegalEntity(l)

    DIALOG LegalEntity OBJECT l
;

DESIGN legalEntities FROM DEFAULT{
    main {
        preferredSize = (1024, 768);
        NEW topContainer {
            type = SPLITH;
            fill = 1;

            ADD legalEntityGroupTree.tree.box{
                PROPERTY(deletelg){
                    askConfirm = TRUE;
                }
            }
            ADD l.box {
                fill = 3;
                l.grid {
                    defaultComponent = TRUE;
                }
            }
        }
        ADD functions.box;
    }
}

@extendFormEditable(legalEntities);

FORM companyLegalEntities 'Организации'

    TREE legalEntityGroupTree lg = LegalEntityGroup PARENT parentLegalEntityGroup
    PROPERTIES READONLY lgTreeName = nameLegalEntityGroup(lg)
    PROPERTIES(lg) ADDFORM, EDITFORM, deletelg=DELETE FORCE PANEL TOOLBAR

    OBJECTS l=LegalEntity
    PROPERTIES(l) READONLYIF isReadonly() nameL = nameLegalEntity, idLegalEntity SHOWIF showIDs(), fullNameLegalEntity, shortNameOwnershipLegalEntity,
                                          nameLegalEntityGroupLegalEntity
    PROPERTIES(l) READONLY                addressLegalEntity, phoneLegalEntity
    PROPERTIES(l) READONLYIF isReadonly() isSupplierLegalEntity, isCompanyLegalEntity, isCustomerLegalEntity
    PROPERTIES(l) ADDFORM, EDITFORM, deletel=DELETE FORCE PANEL TOOLBAR
    ORDER BY nameL
    FILTERGROUP inactiveLegalEntity FILTER 'Активная орг-ия' 'shift F10' activeLegalEntity(l) DEFAULT
    FILTERS isParentLegalEntityGroupLegalEntityGroup(legalEntityGroupLegalEntity(l), lg) OR
            l IS LegalEntity AND NOT lg IS LegalEntityGroup OR
            l IS LegalEntity AND lg IS LegalEntityGroup AND NOT legalEntityGroupLegalEntity(l)
    FILTERS isCompanyLegalEntity(l)
;

DESIGN companyLegalEntities FROM DEFAULT{
    main {
        preferredSize = (1024, 768);
        NEW topContainer{
            type = SPLITH;
            fill = 1;

            ADD legalEntityGroupTree.tree.box{
                PROPERTY(deletelg){
                    askConfirm = TRUE;
                }
            }
            ADD l.box {
                fill = 3;
                l.grid {
                    defaultComponent = TRUE;
                }
            }
        }
        ADD functions.box;
    }
}

FORM supplierLegalEntities 'Организации'

    TREE legalEntityGroupTree lg = LegalEntityGroup PARENT parentLegalEntityGroup
    PROPERTIES READONLY lgTreeName = nameLegalEntityGroup(lg)
    PROPERTIES(lg) ADDFORM, EDITFORM, deletelg=DELETE FORCE PANEL TOOLBAR

    OBJECTS l=LegalEntity
    PROPERTIES(l) READONLYIF isReadonly() nameL = nameLegalEntity, idLegalEntity SHOWIF showIDs(), fullNameLegalEntity, shortNameOwnershipLegalEntity,
                                          nameLegalEntityGroupLegalEntity
    PROPERTIES(l) READONLY                addressLegalEntity, phoneLegalEntity
    PROPERTIES(l) READONLYIF isReadonly() isSupplierLegalEntity, isCompanyLegalEntity, isCustomerLegalEntity
    PROPERTIES(l) ADDFORM, EDITFORM, deletel=DELETE FORCE PANEL TOOLBAR
    ORDER BY nameL
    FILTERGROUP inactiveLegalEntity FILTER 'Активная орг-ия' 'shift F10' activeLegalEntity(l) DEFAULT
    FILTERS isParentLegalEntityGroupLegalEntityGroup(legalEntityGroupLegalEntity(l), lg) OR
            l IS LegalEntity AND NOT lg IS LegalEntityGroup OR
            l IS LegalEntity AND lg IS LegalEntityGroup AND NOT legalEntityGroupLegalEntity(l)
    FILTERS isSupplierLegalEntity(l)

;

DESIGN supplierLegalEntities FROM DEFAULT{
    main {
        preferredSize = (1024, 768);
        NEW topContainer {
            type = SPLITH;
            fill = 1;

            ADD legalEntityGroupTree.tree.box{
                PROPERTY(deletelg){
                    askConfirm = TRUE;
                }
            }
            ADD l.box {
                fill = 3;
                l.grid {
                    defaultComponent = TRUE;
                }
            }
        }
        ADD functions.box;
    }
}

FORM customerLegalEntities 'Организации'

    TREE legalEntityGroupTree lg = LegalEntityGroup PARENT parentLegalEntityGroup
    PROPERTIES READONLY lgTreeName = nameLegalEntityGroup(lg)
    PROPERTIES(lg) ADDFORM, EDITFORM, deletelg=DELETE FORCE PANEL TOOLBAR

    OBJECTS l=LegalEntity
    PROPERTIES(l) READONLYIF isReadonly() nameL = nameLegalEntity, idLegalEntity SHOWIF showIDs(), fullNameLegalEntity, shortNameOwnershipLegalEntity,
                                          nameLegalEntityGroupLegalEntity
    PROPERTIES(l) READONLY                addressLegalEntity, phoneLegalEntity
    PROPERTIES(l) READONLYIF isReadonly() isSupplierLegalEntity, isCompanyLegalEntity, isCustomerLegalEntity
    PROPERTIES(l) ADDFORM, EDITFORM, deletel=DELETE FORCE PANEL TOOLBAR
    ORDER BY nameL
    FILTERGROUP inactiveLegalEntity FILTER 'Активная орг-ия' 'shift F10' activeLegalEntity(l) DEFAULT
    FILTERS isParentLegalEntityGroupLegalEntityGroup(legalEntityGroupLegalEntity(l), lg) OR
            l IS LegalEntity AND NOT lg IS LegalEntityGroup OR
            l IS LegalEntity AND lg IS LegalEntityGroup AND NOT legalEntityGroupLegalEntity(l)
    FILTERS isCustomerLegalEntity(l)

;

DESIGN customerLegalEntities FROM DEFAULT{
    main {
        preferredSize = (1024, 768);
        NEW topContainer{
            type = SPLITH;
            fill = 1;

            ADD legalEntityGroupTree.tree.box{
                PROPERTY(deletelg){
                    askConfirm = TRUE;
                }
            }
            ADD l.box {
                fill = 3;
                l.grid {
                    defaultComponent = TRUE;
                }
            }
        }
        ADD functions.box;
    }
}
META defineDocumentDialogLegalEntity(objectClass, filter, prefix)
    change###prefix###filter###objectClass = ACTION (o) {
        REQUEST OBJECT l
        FORM filter###legalEntities CONTEXTFILTER l = prefix###objectClass(o) MODAL SHOWDROP;
        IF formResult() == FormResult.ok THEN {
            prefix###objectClass(o) <- requestedObject();

        } ELSE IF formResult() == FormResult.drop THEN {
            prefix###objectClass(o) <- NULL;
        }
    }
END
META defineDocumentDialogSupplierCustomerLegalEntity(objectClass, supplierFilter, customerFilter)
    @defineDocumentDialogLegalEntity(objectClass, supplierFilter, supplier);
    @defineDocumentDialogLegalEntity(objectClass, customerFilter, customer);
END

NAVIGATOR {
    masterData {
        ADD legalEntities BEFORE regionalData;
    }
}
// -------------------------------------------- Макросы --------------------------------------------- //
META defineDocumentHeaderLegalEntity (object, contact, contactCaption)
    contact###object (object) = DATA LegalEntity (###object);
    name###contact###object contactCaption (object)= nameLegalEntity(contact###object(object)) IN documentPrm MINCHARWIDTH 20 PREFCHARWIDTH 20;
    fullName###contact###object contactCaption###' (полное наименование)' (object)= fullNameLegalEntity(contact###object(object)) IN documentPrm MINCHARWIDTH 20 PREFCHARWIDTH 20;    
    address###contact###object contactCaption###' (адрес)' (object)= addressLegalEntity(contact###object(object)) IN documentPrm MINCHARWIDTH 20 PREFCHARWIDTH 20;    
     
END
META defineDocumentAbstractHeaderLegalEntity (object, contact, contactCaption)
    contact###object (object) = ABSTRACT LegalEntity (###object) PERSISTENT;
    name###contact###object contactCaption (object)= nameLegalEntity(contact###object(object)) IN documentPrm MINCHARWIDTH 20 PREFCHARWIDTH 20;
    fullName###contact###object contactCaption###' (полное наименование)' (object)= fullNameLegalEntity(contact###object(object)) IN documentPrm MINCHARWIDTH 20 PREFCHARWIDTH 20;    
    address###contact###object contactCaption###' (адрес)' (object)= addressLegalEntity(contact###object(object)) IN documentPrm MINCHARWIDTH 20 PREFCHARWIDTH 20;    

END
META defineDocumentInterfaceHeaderLegalEntity (object, contact, contactCaption)
    @defineDocumentAbstractHeaderLegalEntity(object, contact, contactCaption);
    @defineDocumentHeaderLegalEntity(user###object, contact, contactCaption);
    contact###object (object) += contact###user###object(object);
END

META defineDocumentDetailLegalEntity (object, detail, contact, contactCaption)
    contact###detail (idetail) = contact###object(object###detail(idetail)) PERSISTENT;
    name###contact###detail contactCaption (idetail) = nameLegalEntity(contact###detail(idetail));
    fullName###contact###detail contactCaption###' (полное наименование)' (idetail) = fullNameLegalEntity(contact###detail(idetail));    
    address###contact###detail contactCaption###' (адрес)' (idetail)= addressLegalEntity(contact###detail(idetail));           
END

META defineDocumentLegalEntity (object, detail, contact, contactCaption)
    @defineDocumentHeaderLegalEntity(object, contact, contactCaption);
    @defineDocumentDetailLegalEntity(object, detail, contact, contactCaption);
END

META defineDocumentDetailDataLegalEntity (object, detail, contact, contactCaption)
    data###contact###detail (detail) = DATA LegalEntity (###detail);
    contact###detail (idetail) = OVERRIDE contact###object(object###detail(idetail)), data###contact###detail(idetail) PERSISTENT;
    name###contact###detail contactCaption (idetail) = nameLegalEntity(contact###detail(idetail));
    fullName###contact###detail contactCaption###' (полное наименование)' (idetail) = fullNameLegalEntity(contact###detail(idetail));  
    address###contact###detail contactCaption###' (адрес)' (idetail)= addressLegalEntity(contact###detail(idetail));                   
END
META defineDocumentAbstractDetailDataLegalEntity (object, detail, contact, contactCaption)
    data###contact###detail (detail) = ABSTRACT LegalEntity (###detail);
    contact###detail (idetail) = OVERRIDE contact###object(object###detail(idetail)), data###contact###detail(idetail) PERSISTENT;
    name###contact###detail contactCaption (idetail) = nameLegalEntity(contact###detail(idetail));
    fullName###contact###detail contactCaption###' (полное наименование)' (idetail) = fullNameLegalEntity(contact###detail(idetail));   
    address###contact###detail contactCaption###' (адрес)' (idetail)= addressLegalEntity(contact###detail(idetail));       
END
META defineDocumentInterfaceDetailDataLegalEntity (object, detail, contact, contactCaption)
    @defineDocumentAbstractDetailDataLegalEntity (object, detail, contact, contactCaption);
    @defineDocumentDetailDataLegalEntity(user###object, User###detail, contact, contactCaption);
    data###contact###detail(d) += data###contact###user###detail(d);
END
META defineDocumentInterfaceDetailDataLegalEntity (object, contact, contactCaption)
    @defineDocumentInterfaceDetailDataLegalEntity(object, ###object###detail, contact, contactCaption);
END
META defineDocumentInterfaceDataLegalEntity (object, contact, contactCaption)
    @defineDocumentInterfaceHeaderLegalEntity(object, contact, contactCaption);
    @defineDocumentInterfaceDetailDataLegalEntity(object, contact, contactCaption);
END

META defineDocumentDataLegalEntity (object, detail, contact, contactCaption)
    @defineDocumentHeaderLegalEntity(object, contact, contactCaption);
    @defineDocumentDetailDataLegalEntity(object, detail, contact, contactCaption);
END
META defineDocumentDataLegalEntity (object, contact, contactCaption)
    @defineDocumentDataLegalEntity(object, object##Detail, contact, contactCaption);
END
META defineDocumentDetailLegalEntity(detail, contact, contactCaption)
    contact###detail (detail) = DATA LegalEntity (###detail);
    name###contact###detail contactCaption (detail) = nameLegalEntity(contact###detail (detail));
    fullName###contact###detail contactCaption###' (полное наименование)' (detail) = fullNameLegalEntity(contact###detail(detail));   
    address###contact###detail contactCaption###' (адрес)' (idetail)= addressLegalEntity(contact###detail(idetail));       
        
END

META defineDocumentAbstractLegalEntity (object, detail, contact, contactCaption)
    @defineDocumentAbstractHeaderLegalEntity(object, contact, contactCaption);
    @defineDocumentDetailLegalEntity(object, detail, contact, contactCaption);
END
META defineDocumentInterfaceLegalEntity (object, detail, contact, contactCaption)
    @defineDocumentInterfaceHeaderLegalEntity(object, contact, contactCaption);
    @defineDocumentDetailLegalEntity(object, detail, contact, contactCaption);
    @defineDocumentDetailLegalEntity(user###object, user###detail, contact, contactCaption);
END

META defineDocumentLegalEntity (object, contact, contactCaption)
    @defineDocumentLegalEntity(object, object##Detail, contact, contactCaption);
END
META defineDocumentAbstractLegalEntity (object, contact, contactCaption)
    @defineDocumentAbstractLegalEntity(object, object##Detail, contact, contactCaption);
END
META defineDocumentInterfaceLegalEntity (object, contact, contactCaption)
    @defineDocumentInterfaceLegalEntity(object, object##Detail, contact, contactCaption);
END

META defineDocumentPhone(object, contact, caption)
    phone###contact###object 'Телефон'###caption (object) = phoneLegalEntity(contact###object(object)) IN documentPrm;
END

// Агрегации

META defineDocumentAggregationHeaderLegalEntityPrefix (primObject, aggrObject, legalEntityProp, legalEntityCaption, prefixP, prefixA)
    prefixA###legalEntityProp###aggrObject (object) = prefixP###legalEntityProp###primObject(primObject###aggrObject(object)) PERSISTENT;
    name###prefixA###legalEntityProp###aggrObject legalEntityCaption (object) = nameLegalEntity(prefixA###legalEntityProp###aggrObject(object)) MINCHARWIDTH 20 PREFCHARWIDTH 40;
END
META defineDocumentAggregationHeaderLegalEntity (primObject, aggrObject, legalEntityProp, legalEntityCaption)
    @defineDocumentAggregationHeaderLegalEntityPrefix (primObject, aggrObject, legalEntityProp, legalEntityCaption, , );
END

META defineDocumentAggregationDetailLegalEntityPrefix (primObject, aggrObject, legalEntityProp, legalEntityCaption, prefixP, prefixA)
    prefixA###legalEntityProp###aggrObject##Detail (detail) = prefixP###legalEntityProp###primObject##Detail(primObject##Detail###aggrObject##Detail(detail)) PERSISTENT;
END
META defineDocumentAggregationDetailLegalEntity (primObject, aggrObject, legalEntityProp, legalEntityCaption)
    @defineDocumentAggregationDetailLegalEntityPrefix(primObject, aggrObject, legalEntityProp, legalEntityCaption, , );
END

META defineDocumentAggregationLegalEntityPrefix (primObject, aggrObject, legalEntityProp, legalEntityCaption, prefixP, prefixA)
    @defineDocumentAggregationHeaderLegalEntityPrefix(primObject, aggrObject, legalEntityProp, legalEntityCaption, prefixP, prefixA);
    @defineDocumentAggregationDetailLegalEntityPrefix(primObject, aggrObject, legalEntityProp, legalEntityCaption, prefixP, prefixA);
END
META defineDocumentAggregationLegalEntity (primObject, aggrObject, legalEntityProp, legalEntityCaption)
    @defineDocumentAggregationLegalEntityPrefix(primObject, aggrObject, legalEntityProp, legalEntityCaption, , );
END

// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultStaticOwnership 'Добавить Формы собственности' = ACTION (iname, isname, sidCountry, static) {    //статик класс
        ASSIGN nameOwnership(t) <- iname WHERE t == static;
        ASSIGN shortNameOwnership(t) <- isname WHERE t == static;       
        ASSIGN countryOwnership(t) <- countrySID(sidCountry) WHERE t == static;
}

loadDefaultOwnership 'Добавить Формы собственности' = ACTION (iname, isname, sidCountry) {
    FOR ADDOBJ t = Ownership DO {
        ASSIGN nameOwnership(t) <- iname;
        ASSIGN shortNameOwnership(t) <- isname;
        ASSIGN countryOwnership(t) <- countrySID(sidCountry);
    }
}

overLoadDefaultOwnerships () = ABSTRACT ACTION LIST ();

loadDefaultOwnerships 'Загрузить стандарные формы собственности' () = ABSTRACT ACTION LIST () IN loadDefault;

@implementLoadDefaultData(loadDefaultOwnerships);
