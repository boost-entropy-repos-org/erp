MODULE EmployeeManager;

REQUIRE Employee;

NAMESPACE Employee;

inManagerEmployee 'Вкл.' = DATA BOOLEAN (Employee, Employee);
descriptionManagersEmployee 'Менеджеры' (e) = GROUP CONCAT shortNameContact(m) IF inManagerEmployee(m ,e), ',' BY e;

overCopyEmployee(e,employee) += ACTION {    
    inManagerEmployee(l,e) <- inManagerEmployee(l,employee) WHERE inManagerEmployee(l,employee);
    inManagerEmployee(e,l) <- inManagerEmployee(employee,l) WHERE inManagerEmployee(employee,l);        
};

FORM inManagerEmployee 'Менеджеры'
    OBJECTS e = Employee FIXED GRID
    OBJECTS m = Employee
    PROPERTIES (m) READONLY lastNameContact, firstNameContact, shortNameContact, nameMainRoleUser
    PROPERTIES (m, e) inManagerEmployee    
    FILTERGROUP include FILTER 'Включены' inManagerEmployee (m, e) 'F9' 
;

fillInManagerEmployee = ACTION (e) {
    FORM inManagerEmployee OBJECTS e=e MODAL;
}

EXTEND FORM employee
    PROPERTIES (e) descriptionManagersEmployee ON CHANGE fillInManagerEmployee(e)
;

DESIGN employee {
    pane { 
        NEW manager{
            caption = 'Менеджмент';
            MOVE PROPERTY(descriptionManagersEmployee(e)); 
            NEW managerTabContainer {
                type = TABBED;
                fill = 1;
            }
        }
    } 
}