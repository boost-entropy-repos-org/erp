MODULE Currency;

REQUIRE System, Time, MasterData, DefaultData;

CLASS Currency 'Валюта';
TABLE currency(Currency);

nameCurrency 'Наименование' = DATA VARISTRING[50](Currency) IN recognize;
sidCurrency 'Код валюты' = DATA STRING[3] (Currency);

symbolCurrency 'Символ' = DATA STRING[5] (Currency);
shortNameCurrency 'Наименование (сокр.)' = DATA STRING[3] (Currency);
documentNameCurrency 'Наименование (в документах)' = DATA STRING[10] (Currency);
currencyShortName (string) = GROUP AGGR currency BY shortNameCurrency(currency) WHERE currency IS Currency;

// Формы

FORM currency 'Валюта'
    OBJECTS c = Currency FIXED PANEL
    PROPERTIES (c)  nameCurrency, shortNameCurrency, sidCurrency, symbolCurrency, documentNameCurrency

    EDIT Currency OBJECT c
;

FORM currencies 'Валюты'
    OBJECTS c = Currency
    PROPERTIES(c) READONLY nameCurrency, shortNameCurrency, sidCurrency, symbolCurrency, documentNameCurrency

    PROPERTIES(c) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG Currency OBJECT c
;

// ---------------------------------- Типы обмена ---------------------------- //

CLASS TypeExchange 'Тип обмена';
TABLE typeExchange(TypeExchange);
TABLE rateExchange (TypeExchange, Currency, DATE);

nameTypeExchange 'Наименование' = DATA VARISTRING[50](TypeExchange) IN recognize;

currencyTypeExchange 'Валюта типа обмена'= DATA Currency (TypeExchange);
nameCurrencyTypeExchange 'Валюта типа обмена' (typeExchange) = nameCurrency(currencyTypeExchange(typeExchange)) IN base;

FORM typeExchange 'Тип обмена'
    OBJECTS t = TypeExchange FIXED PANEL
    PROPERTIES(t) nameTypeExchange, nameCurrencyTypeExchange
    
    EDIT TypeExchange OBJECT t
;

// Расчет курсов на дату

rateExchange 'Курс обмена' = DATA NUMERIC[15,8] (TypeExchange, Currency, DATE);
typeExchangeName (name) = GROUP AGGR typeExchange BY nameTypeExchange(typeExchange) WHERE typeExchange IS TypeExchange;

rateOnTypeExchangeCurrencyDate 'Курс обмена' (typeExchange, currency, date) =
    GROUP LAST rateExchange(typeExchange, currency, dateIn)
          BY   typeExchange, currency, date
          ORDER dateIn
          WHERE rateExchange(typeExchange, currency, dateIn) AND dateIn <= date COMPLEX;

curRateTypeExchangeCurrency 'Текущий курс обмена' (typeExchange, currency) =
    rateOnTypeExchangeCurrencyDate(typeExchange, currency, currentDate());

// -------------------------------------------- Курсы валют --------------------------------- //

FORM dialogTypeExchangeCurrency 'Курс обмена на дату'
    OBJECTS t=TypeExchange FIXED PANEL
    OBJECTS c=Currency FIXED PANEL
    OBJECTS d=DATE FIXED PANEL
    PROPERTIES READONLY nameTypeExchange(t),  nameCurrencyTypeExchange(t), nameCurrency(c)
    PROPERTIES val=OBJVALUE(d), rateExchange(t,c,d)

;
DESIGN dialogTypeExchangeCurrency FROM DEFAULT {
    main {
        type = CONTAINERV;
        ADD t.box {
            ADD PROPERTY(nameTypeExchange(t)) { focusable = FALSE; preferredCharWidth = 40;}
        }
        ADD c.box{
            type = CONTAINERV;
            ADD d.box {
                type = CONTAINERH;

                ADD PROPERTY(rateExchange(t,c,d));
            };
            NEW row {
                caption = 'Валюты обмена';
                type = CONTAINERH;
                ADD PROPERTY(nameCurrency(c)) { caption = 'Валюта (из)'; preferredCharWidth = 20;}
                ADD PROPERTY(nameCurrencyTypeExchange(t)) { caption = 'Валюта (в)'; preferredCharWidth = 20;}
            }
        }
    }
    ADD functions.box;
}

dialogTypeExchangeCurrency 'Добавить' (typeExchange, currency) = ACTION FORM dialogTypeExchangeCurrency OBJECTS t = typeExchange, c = currency MODAL TOOLBAR;
deleteTypeExchangeCurrencyDate 'Удалить' (typeExchange, currency, date) = ACTION ASSIGN rateExchange(typeExchange, currency, date) <- NULL IMAGE 'delete.png';


parentTypeExchange = DATA TypeExchange (TypeExchange);
nameParentTypeExchange 'Родительский '  = nameTypeExchange(parentTypeExchange(t));

typeTypeExchangeCurrency = GROUP AGGR t BY parentTypeExchange(t), currencyTypeExchange(t);

EXTEND FORM typeExchange    
    PROPERTIES(t) nameParentTypeExchange
;

//createCrosTypeExchangeCurrency 'Создать кросс-курс' = ACTION (type, currency) {
//    IF NOT typeTypeExchangeCurrency(type, currency) THEN {
//        FOR ADDOBJ t = TypeExchange DO {
//            currencyTypeExchange(t) <- currency;
//            nameTypeExchange(t) <- VARISTRING[50](CONCAT '', 'Кросс '+nameTypeExchange(type), '-('+shortNameCurrency(currency)+')');   
//            parentTypeExchange(t) <- type;
//            
//            FOR  curRateTypeExchangeCurrency(type, c) DO {
//                rateExchange(t, c, date) <- NUMERIC[15,8](curRateTypeExchangeCurrency(type, currency)/(curRateTypeExchangeCurrency(type, c) IF curRateTypeExchangeCurrency(type, c)!=0)) WHERE date == currentDate();   
//            }
//        }    
//    } ELSE {
//        MESSAGE 'Кросс-курс уже существует для данной валюты и типа обмена';
//    } 
//};


FORM typeExchangeCurrencyDate 'Курсы валют'

    OBJECTS t = TypeExchange
    PROPERTIES(t) READONLY nameTypeExchange, nameCurrencyTypeExchange, nameParentTypeExchange
    PROPERTIES(t) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    OBJECTS c = Currency
    PROPERTIES(c) READONLY nameCurrency
    PROPERTIES(c) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    PROPERTIES(t, c) READONLY curRateTypeExchangeCurrency
//    PROPERTIES(t, c)  createCrosTypeExchangeCurrency FORCE PANEL TOOLBAR 

    FILTERGROUP inactive FILTER 'Есть курс' 'F11' curRateTypeExchangeCurrency(t, c) DEFAULT    

    OBJECTS d = DATE
    PROPERTIES(d) READONLY OBJVALUE

    PROPERTIES(t, c, d) rateExchange
    PROPERTIES(t, c) dialogTypeExchangeCurrency TODRAW d FORCE PANEL
    PROPERTIES(t, c, d) deleteTypeExchangeCurrencyDate

    FILTERS rateExchange(t, c, d)

    DIALOG TypeExchange OBJECT t

;

DESIGN typeExchangeCurrencyDate FROM DEFAULT{
    main {
        type = CONTAINERV;
        NEW split {
            fill = 1;
            type = SPLITV;
            ADD t.box;
            NEW topContainer {
                fill = 2;
                type = TABBED;
                NEW currentExchangeRates {
                    caption = 'Текущие курсы';
                    type = SPLITH;
                    ADD c.box;
                    ADD d.box;
                }
                NEW importExchangeRates {
                    caption = 'Импорт курсов';
                }
            }
        }
        ADD functions.box;
    }
}

NAVIGATOR {
    masterData {
        NEW currencyNavigator 'Валюты и курсы' {
            ADD currencies;
            ADD typeExchangeCurrencyDate;
        }
    }
}

// --------------------------------------------------- Макросы по добавлению валюты в документы ------------------------------------ //
META defineDocumentHeaderCurrency (object)
    currency###object (object) = DATA Currency (###object);
    nameCurrency###object 'Валюта' (object)= nameCurrency(currency###object(object)) IN documentPrm MINCHARWIDTH 10 PREFCHARWIDTH 20;
    shortNameCurrency###object 'Валюта (сокр.)' (object) = shortNameCurrency(currency###object(object));
END
META defineDocumentAbstractHeaderCurrency (object)
    currency###object (object) = ABSTRACT Currency (###object) PERSISTENT;
    nameCurrency###object 'Валюта' (object)= nameCurrency(currency###object(object)) IN documentPrm MINCHARWIDTH 10 PREFCHARWIDTH 20;
    shortNameCurrency###object 'Валюта (сокр.)' (object) = shortNameCurrency(currency###object(object));
END
META defineDocumentInterfaceHeaderCurrency (object)
    @defineDocumentAbstractHeaderCurrency(object);
    @defineDocumentHeaderCurrency(user###object);
    currency###object (object) += currency###user###object(object);
END

META defineDocumentDetailCurrency (object, detail)
    currency###detail (idetail) = currency###object(object###detail(idetail)) PERSISTENT;
    nameCurrency###detail 'Валюта' (idetail) = nameCurrency###object(object###detail(idetail));
    shortNameCurrency###detail 'Валюта (сокр.)' (idetail) = shortNameCurrency(currency###detail(idetail));
END
META defineDocumentInterfaceDetailCurrency (object, detail)
    @defineDocumentDetailCurrency(object, detail);
    @defineDocumentDetailCurrency(user###object, user###detail);
END

META defineDocumentCurrency (object, detail)
    @defineDocumentHeaderCurrency(object);
    @defineDocumentDetailCurrency(object, detail);
END
META defineDocumentAbstractCurrency (object, detail)
    @defineDocumentAbstractHeaderCurrency(object);
    @defineDocumentDetailCurrency(object, detail);
END
META defineDocumentInterfaceCurrency (object, detail)
    @defineDocumentInterfaceHeaderCurrency(object);
    @defineDocumentInterfaceDetailCurrency(object, detail);
END

META defineDocumentCurrency (object)
    @defineDocumentCurrency(object, object##Detail);
END
META defineDocumentAbstractCurrency (object)
    @defineDocumentAbstractCurrency(object, object##Detail);
END
META defineDocumentInterfaceCurrency (object)
    @defineDocumentInterfaceCurrency(object, object##Detail);
END

META deriveDocumentCurrency (object, stockProp)
    @deriveDocumentCurrency(object, stockProp, currencyStock);
END

META deriveDocumentCurrency (object, objectProp, currencyProp)
    currency###object (object) <- currencyProp(objectProp###object(object)) WHEN CHANGED(objectProp###object(object));
END

// --------------------------------------------------- Стандартные значения ------------------------------------ //

loadDefaultCurrency 'Добавить валюты' = ACTION (ishortName, iname, isymbol) {
    FOR ADDOBJ c = Currency DO {
       ASSIGN shortNameCurrency(c) <- ishortName;
       ASSIGN nameCurrency(c) <- iname;
       ASSIGN symbolCurrency(c) <- isymbol;
    }
}

overLoadDefaultCurrencies = ABSTRACT ACTION LIST ();

loadDefaultCurrencies 'Загрузить стандартные валюты' = ACTION () {
    EXEC loadDefaultCurrency('AUD', 'Австралийский доллар', ' ');
    EXEC loadDefaultCurrency('BGN', 'Болгарский лев', ' ');
    EXEC loadDefaultCurrency('HUF', 'Венгерский форинт', ' ');
    EXEC loadDefaultCurrency('UAH', 'Гривна', ' ');
    EXEC loadDefaultCurrency('DKK', 'Датская крона', ' ');
    EXEC loadDefaultCurrency('USD', 'Доллар США', '$');
    EXEC loadDefaultCurrency('EUR', 'Евро', '€');
    EXEC loadDefaultCurrency('PLN', 'Злотый', ' ');
    EXEC loadDefaultCurrency('INR', 'Индийская рупия', ' ');
    EXEC loadDefaultCurrency('ISK', 'Исландская крона', ' ');
    EXEC loadDefaultCurrency('CAD', 'Канадский доллар', ' ');
    EXEC loadDefaultCurrency('CNY', 'Китайский юань', ' ');
    EXEC loadDefaultCurrency('KWD', 'Кувейтский динар', ' ');
    EXEC loadDefaultCurrency('LVL', 'Латвийский лат', ' ');
    EXEC loadDefaultCurrency('LTL', 'Литовский лит', ' ');
    EXEC loadDefaultCurrency('MDL', 'Молдавский лей', ' ');
    EXEC loadDefaultCurrency('NOK', 'Норвежская крона', ' ');
    EXEC loadDefaultCurrency('RUB', 'Российский рубль', ' ');
    EXEC loadDefaultCurrency('RON', 'Румынский лей', ' ');
    EXEC loadDefaultCurrency('XDR', 'СДР', ' ');
    EXEC loadDefaultCurrency('SGD', 'Сингапурский доллар', ' ');
    EXEC loadDefaultCurrency('KGS', 'Сом', ' ');
    EXEC loadDefaultCurrency('KZT', 'Тенге', ' ');
    EXEC loadDefaultCurrency('TRY', 'Турецкая лира', ' ');
    EXEC loadDefaultCurrency('GBP', 'Фунт стерлингов', ' ');
    EXEC loadDefaultCurrency('CZK', 'Чешская крона', ' ');
    EXEC loadDefaultCurrency('SEK', 'Шведская крона', ' ');
    EXEC loadDefaultCurrency('CHF', 'Швейцарский франк', ' ');
    EXEC loadDefaultCurrency('JPY', 'Иена', ' ');
    EXEC loadDefaultCurrency('IRR', 'Иранский риал', ' ');
    EXEC loadDefaultCurrency('BLR', 'Белорусский рубль', ' ');

    EXEC overLoadDefaultCurrencies();
} IN loadDefault;

@implementLoadDefaultData(loadDefaultCurrencies);