MODULE Country;

REQUIRE System, MasterData, Utils, DefaultData, I18n, Currency;

CLASS Country 'Страна' : MultiLanguageNamed;

TABLE country(Country);
TABLE countryDate(Country, DATE);
TABLE countryCountry(Country, Country);

name 'Наименование' = DATA VARISTRING[50](Country);

nameOrigin 'Наименование (ориг.)' (country) = DATA VARISTRING[50] (Country) IN base; //logics.country.name.origin.country
sid 'Код страны' (country) = DATA STRING[3] (Country) IN base; // logics.country.key
//sid3Country 'Код 3 знака' (country) = DATA STRING[3] (Country) IN base MINCHARWIDTH 15;
country 'Страна' (sID) =  GROUP AGGR Country country BY sid(country);  // logics.country

sidOrigin2 'Код 2 знака (ориг.)' (country) = DATA STRING[2] (Country) IN base MINCHARWIDTH 15; // logics.country.sid.origin.2.country
sidOrigin3 'Код 3 знака (ориг.)' (country) = DATA STRING[3] (Country) IN base MINCHARWIDTH 15; // logics.country.sid.origin.3.country

currency 'Валюта' (country) = DATA Currency (Country); // logics.country.currency
nameCurrency 'Валюта' (Country country) = name(currency(country)) IN base PREFCHARWIDTH 10;

generateDates 'Генерировать выходные' (country) = DATA BOOLEAN (Country); // logics.day.generate.days.off

resident 'Отечественный производитель' (country) = DATA BOOLEAN (Country) IN base MINCHARWIDTH 15; // logics.country.resident.country
isEEU'Входит в ЕАЭС' (country) = DATA BOOLEAN (Country) IN base MINCHARWIDTH 15; 

countryName(string) = GROUP NAGGR Country country BY name(country) WHERE country IS Country;

countryOrigin(name) = GROUP NAGGR Country country BY nameOrigin(country) WHERE country IS Country;

countrySIDOrigin2(string) = GROUP NAGGR Country country BY sidOrigin2(country) WHERE country IS Country;

defaultCountry 'Страна по умолчанию' () = DATA Country (); // logics.country.default.country
nameDefaultCountry 'Страна по умолчанию' () = name(defaultCountry()) PREFCHARWIDTH 30; // logics.country.default.country

jumpWorkdays = FORMULA NULL DATE PG 'jumpWorkdays($1, $2, $3)', MS 'dbo.jumpWorkdays($1, $2, $3)'; //1 - country, 2 - date, 3 - days to jump

EXTEND FORM options
    PROPERTIES() nameDefaultCountry
;

DESIGN options {
    commons {
        MOVE PROPERTY(nameDefaultCountry());
    }
}

language = DATA Language (Country);
nameLanguage 'Язык' (Country country) = name(language(country)) MINCHARWIDTH 5 PREFCHARWIDTH 10 MAXCHARWIDTH 15;

FORM country 'Страна'
    OBJECTS c = Country PANEL

    PROPERTIES(c) name, nameOrigin, sid, sidOrigin2, sidOrigin3,
                  resident, isEEU, nameCurrency, nameLanguage

    OBJECTS l = Language
    PROPERTIES name(l) READONLY, languageName(c, l)

    EDIT Country OBJECT c
;

FORM countries 'Страны'
    OBJECTS c = Country
    PROPERTIES(c) READONLY name, nameOrigin, sid, sidOrigin2, sidOrigin3,
                           resident, isEEU, nameCurrency, nameLanguage

    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    ORDER BY name(c)

    LIST Country OBJECT c
;

NAVIGATOR {
    regionalData {
        ADD countries;
    }
}

// ------------------------------------------------ Выходные дни --------------------------------------- //

isDayOff 'Выходной' (country, date) = DATA BOOLEAN (Country, DATE) IN base; // logics.day.off

fillDaysOff 'Заполнить выходные дни' = CUSTOM 'lsfusion.erp.utils.FillDaysOffActionProperty' (Country);

addDayOff 'Добавить выходной день'(Country country) = {
	NEWSESSION {
	    DIALOG dialogDate OBJECTS d INPUT DO {
	        isDayOff (country, d) <- TRUE;
	        apply();
	    }
	}
}

FORM daysOffForm 'Выходные дни' // logics.days.off
    OBJECTS country=Country 'Страна', day=DATE 'День'
    PROPERTIES(country)  fillDaysOff, addDayOff
    PROPERTIES(country) READONLY nameOrigin, sid, sidOrigin2, sidOrigin3, nameCurrency, resident
    PROPERTIES VALUE(day), isDayOff(country, day)
    FILTERS isDayOff(country, day)
;

NAVIGATOR {
    regionalData {
        ADD daysOffForm;
    }
}