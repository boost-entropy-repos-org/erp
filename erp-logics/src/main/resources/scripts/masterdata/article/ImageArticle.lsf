MODULE ImageArticle;

REQUIRE ItemArticle;

NAMESPACE Item;

thumbnailImage 'Изображение' = DATA LOCAL IMAGEFILE (INTEGER);
urlImage 'Ссылка' = DATA LOCAL TEXT (INTEGER);
sizeImage 'Размер' = DATA LOCAL VARSTRING[10] (INTEGER);
startImage 'Страница поиска' = DATA LOCAL INTEGER ();
articleImage 'Артикул' = DATA LOCAL Article ();

searchImageArticle 'Искать изображение в сети' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchImageArticleActionProperty' (Article);  
searchFirstImageArticle 'Мне повезёт' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchFirstImageArticleActionProperty' (Article); 
searchMoreImageArticle 'Следующие' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchMoreImageArticleActionProperty' (); 
loadImageArticle 'Загрузить изображение в артикул' = ACTION CUSTOM 'lsfusion.erp.integration.image.LoadImageArticleActionProperty' (Article, STRING[1000]); 
uploadImageArticleFromDirectory 'Загрузить изображение из папки' = ACTION CUSTOM 'lsfusion.erp.integration.image.UploadImageArticleFromDirectoryActionProperty' (Article);
uploadImageArticleFromURL 'Загрузить изображение по ссылке' = ACTION CUSTOM 'lsfusion.erp.integration.image.UploadImageArticleFromURLActionProperty' (Article);

patternImageBrand 'Шаблон поиска изображений' = DATA VARSTRING[100] (Brand);
patternImageArticle 'Шаблон поиска изображений' (article) = patternImageBrand(brandArticle(article));

urlImageArticle 'Ссылка' = DATA VARSTRING[1000] (Article) MINCHARWIDTH 20 PREFCHARWIDTH 40;
WHEN SESSION CHANGED(urlImageArticle(a)) DO uploadImageArticleFromURL(a); 

idImageArticle 'Идентификатор изображения' = DATA VARSTRING[100] (Article);
overIdImageArticle (article) = OVERRIDE idArticle(article), idImageArticle(article);
timeChangedImageArticle 'Время последнего изменения' = DATA DATETIME (Article);
pathImageArticles 'Папка с изображениями' = DATA STRING[100] ();
uploadAllImagesArticle 'Загрузить все изображения' () = ACTION {
    FOR (overIdImageArticle(a)) DO {
        EXEC uploadImageArticleFromDirectory(a);
    }
};

EXTEND FORM article 
PROPERTIES(a) idImageArticle, searchImageArticle, searchFirstImageArticle, uploadImageArticleFromDirectory, 
              urlImageArticle, patternImageArticle;

DESIGN article {
    imageBox {
        ADD PROPERTY(idImageArticle(a));
        NEW buttonsBox {
            type = CONTAINERH;
            ADD PROPERTY(searchImageArticle(a));
            ADD PROPERTY(searchFirstImageArticle(a));
            ADD PROPERTY(uploadImageArticleFromDirectory(a));
            ADD PROPERTY(urlImageArticle(a));
            ADD PROPERTY(patternImageArticle(a));
        }
    }
}

EXTEND FORM brand PROPERTIES(t) patternImageBrand;
EXTEND FORM attributesItem PROPERTIES(brand) patternImageBrand;

EXTEND FORM options PROPERTIES() pathImageArticles, uploadAllImagesArticle;
DESIGN options {
    items {
        NEW itemImages {
            caption = 'Изображения';
            ADD PROPERTY(pathImageArticles());
            ADD PROPERTY(uploadAllImagesArticle());
        }
    }
}

FORM chooseImage 'Выбор изображения' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY thumbnailImage, urlImage, sizeImage FORCE PANEL
    PROPERTIES() searchMoreImageArticle FORCE PANEL TODRAW i
    FILTERS thumbnailImage(i)
; 

DESIGN chooseImage {
    NEW imageBox {
        caption = '';
        ADD PROPERTY(thumbnailImage(i)) {
                preferredHeight = 200;
                preferredWidth = 300;
        }
        ADD PROPERTY(urlImage(i)) {
                preferredWidth = 400;
        };
        ADD PROPERTY(sizeImage(i));
    }
    ADD PROPERTY(searchMoreImageArticle());
    ADD functions.box;
}

chooseImageAction 'Выбор изображения' (article) = ACTION {
    FORM chooseImage MODAL;
    IF formResult() == FormResult.ok THEN {
        loadImageArticle(article, urlImage(chosenInteger('i')));
        startImage() <- NULL;
        articleImage() <- NULL;
    }   
}