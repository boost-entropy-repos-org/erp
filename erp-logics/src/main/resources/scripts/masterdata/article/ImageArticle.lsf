MODULE ImageArticle;

REQUIRE ItemArticle;

NAMESPACE Item;

thumbnailImage 'Изображение' = DATA LOCAL IMAGEFILE (INTEGER);
urlImage 'Ссылка' = DATA LOCAL TEXT (INTEGER);
sizeImage 'Размер' = DATA LOCAL VARSTRING[10] (INTEGER);
startImage 'Страница поиска' = DATA LOCAL INTEGER ();
articleImage 'Артикул' = DATA LOCAL Article ();

searchImage 'Искать изображение в сети' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchImageArticleActionProperty' (Article);  
searchFirstImage 'Мне повезёт' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchFirstImageArticleActionProperty' (Article); 
searchMoreImageArticle 'Следующие' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchMoreImageArticleActionProperty' (); 
loadImage 'Загрузить изображение в артикул' = ACTION CUSTOM 'lsfusion.erp.integration.image.LoadImageArticleActionProperty' (Article, STRING[1000]); 
uploadImageFromDirectory 'Загрузить изображение из папки' = ACTION CUSTOM 'lsfusion.erp.integration.image.UploadImageArticleFromDirectoryActionProperty' (Article);
uploadImageFromURL 'Загрузить изображение по ссылке' = ACTION CUSTOM 'lsfusion.erp.integration.image.UploadImageArticleFromURLActionProperty' (Article);

patternImage 'Шаблон поиска изображений' = DATA VARSTRING[100] (Brand);
patternImage 'Шаблон поиска изображений' (Article article) = patternImage(brand(article));

urlImage 'Ссылка' = DATA VARSTRING[1000] (Article) MINCHARWIDTH 20 PREFCHARWIDTH 40;
WHEN SESSION CHANGED(urlImage(Article a)) DO uploadImageFromURL(a); 

idImage 'Идентификатор изображения' = DATA VARSTRING[100] (Article);
overIdImage (Article article) = OVERRIDE id(article), idImage(article);
timeChangedImage 'Время последнего изменения' = DATA DATETIME (Article);
pathImageArticles 'Папка с изображениями' = DATA STRING[100] ();
uploadAllImagesArticle 'Загрузить все изображения' () = ACTION {
    FOR (overIdImage(Article a)) DO {
        EXEC uploadImageFromDirectory(a);
    }
};

EXTEND FORM article 
PROPERTIES(a) idImage, searchImage, searchFirstImage, uploadImageFromDirectory, 
              urlImage, patternImage;

DESIGN article {
    imageBox {
        MOVE PROPERTY(idImage(a));
        NEW buttonsBox {
            type = CONTAINERH;
            MOVE PROPERTY(searchImage(a));
            MOVE PROPERTY(searchFirstImage(a));
            MOVE PROPERTY(uploadImageFromDirectory(a));
            MOVE PROPERTY(urlImage(a));
            MOVE PROPERTY(patternImage(a));
        }
    }
}

EXTEND FORM brand PROPERTIES(t) patternImage;
EXTEND FORM attributesItem PROPERTIES(brand) patternImage;

EXTEND FORM options PROPERTIES() pathImageArticles, uploadAllImagesArticle;
DESIGN options {
    items {
        NEW itemImages {
            caption = 'Изображения';
            MOVE PROPERTY(pathImageArticles());
            MOVE PROPERTY(uploadAllImagesArticle());
        }
    }
}

FORM chooseImage 'Выбор изображения' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY thumbnailImage, urlImage, sizeImage FORCE PANEL
    PROPERTIES() searchMoreImageArticle FORCE PANEL TODRAW i
    FILTERS thumbnailImage(i)
; 

DESIGN chooseImage {
    NEW imageBox {
        caption = '';
        MOVE PROPERTY(thumbnailImage(i)) {
                preferredHeight = 200;
                preferredWidth = 300;
        }
        MOVE PROPERTY(urlImage(i)) {
                preferredWidth = 400;
        };
        MOVE PROPERTY(sizeImage(i));
    }
    MOVE PROPERTY(searchMoreImageArticle());
    MOVE functions.box;
}

chooseImageAction 'Выбор изображения' (Article article) = ACTION {
    FORM chooseImage ;
    IF formResult() == FormResult.ok THEN {
        loadImage(article, urlImage(chosenInteger('i')));
        startImage() <- NULL;
        articleImage() <- NULL;
    }   
}