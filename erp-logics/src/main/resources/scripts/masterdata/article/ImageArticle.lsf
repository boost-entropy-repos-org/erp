MODULE ImageArticle;

REQUIRE ItemArticle;

NAMESPACE Item;

thumbnailImage 'Изображение' = DATA SESSION IMAGEFILE (INTEGER);
urlImage 'Ссылка' = DATA SESSION TEXT (INTEGER);
sizeImage 'Размер' = DATA SESSION VARSTRING[10] (INTEGER);
startImage 'Страница поиска' = DATA SESSION INTEGER ();
articleImage 'Артикул' = DATA SESSION Article ();

searchImageArticle 'Искать изображение в сети' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchImageArticleActionProperty';  
searchMoreImageArticle 'Следующие' = ACTION CUSTOM 'lsfusion.erp.integration.image.SearchMoreImageArticleActionProperty'; 
loadImageArticle 'Загрузить изображение в артикул' = ACTION CUSTOM 'lsfusion.erp.integration.image.LoadImageArticleActionProperty'; 
uploadImageArticleFromDirectory 'Загрузить изображение из папки' = ACTION CUSTOM 'lsfusion.erp.integration.image.UploadImageArticleFromDirectoryActionProperty';
uploadImageArticleFromURL 'Загрузить изображение по ссылке' = ACTION CUSTOM 'lsfusion.erp.integration.image.UploadImageArticleFromURLActionProperty';

patternImageBrand 'Шаблон поиска изображений' = DATA VARSTRING[100] (Brand);
patternImageArticle 'Шаблон поиска изображений' (article) = patternImageBrand(brandArticle(article));

urlImageArticle 'Ссылка' = DATA VARSTRING[1000] (Article) MINCHARWIDTH 20 PREFCHARWIDTH 40;
WHEN SESSION CHANGED(urlImageArticle(a)) DO uploadImageArticleFromURL(a); 

idImageArticle 'Идентификатор изображения' = DATA VARSTRING[100] (Article);
overIdImageArticle (article) = OVERRIDE idArticle(article), idImageArticle(article);
timeChangedImageArticle 'Время последнего изменения' = DATA DATETIME (Article);
pathImageArticles 'Папка с изображениями' = DATA STRING[100] ();
uploadAllImagesArticle 'Загрузить все изображения' () = ACTION {
    FOR (overIdImageArticle(a)) DO {
        EXEC uploadImageArticleFromDirectory(a);
    }
};

EXTEND FORM article 
PROPERTIES(a) idImageArticle, searchImageArticle, uploadImageArticleFromDirectory, 
              urlImageArticle, patternImageArticle;

EXTEND DESIGN article {
    imageBox {
        ADD PROPERTY(idImageArticle(a));
        NEW buttonsBox {
            type = CONTAINERH;
            ADD PROPERTY(searchImageArticle(a));
            ADD PROPERTY(uploadImageArticleFromDirectory(a));
            ADD PROPERTY(urlImageArticle(a));
            ADD PROPERTY(patternImageArticle(a));
        }
    }
}

EXTEND FORM brand PROPERTIES(t) patternImageBrand;
EXTEND FORM brands PROPERTIES(t) patternImageBrand;

EXTEND FORM options PROPERTIES() pathImageArticles, uploadAllImagesArticle;
EXTEND DESIGN options {
    items {
        NEW itemImages {
            caption = 'Изображения';
            ADD PROPERTY(pathImageArticles());
            ADD PROPERTY(uploadAllImagesArticle());
        }
    }
}

FORM chooseImage 'Выбор изображения' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY thumbnailImage, urlImage, sizeImage FORCE PANEL
    PROPERTIES() searchMoreImageArticle FORCE PANEL TODRAW i
    FILTERS thumbnailImage(i)
; 

EXTEND DESIGN chooseImage {
    NEW imageBox {
        caption = '';
        ADD PROPERTY(thumbnailImage(i)) {
                preferredHeight = 200;
                preferredWidth = 300;
        }
        ADD PROPERTY(urlImage(i)) {
                preferredWidth = 400;
        };
        ADD PROPERTY(sizeImage(i));
    }
    ADD PROPERTY(searchMoreImageArticle());
    ADD functions.box;
}

chooseImageAction 'Выбор изображения' (article) = ACTION {
    FORM chooseImage MODAL;
    IF formResult() == FormResult.ok THEN {
        loadImageArticle(article, urlImage(chosenInteger('i')));
        startImage() <- NULL;
        articleImage() <- NULL;
    }   
}