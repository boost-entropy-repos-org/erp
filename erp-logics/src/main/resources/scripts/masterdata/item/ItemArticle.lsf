MODULE ItemArticle;

REQUIRE System, Item;

NAMESPACE Item;

GROUP itemArticleGroup 'Артикул' : public;

// Артикул
CLASS Article 'Артикул';
TABLE article(Article);

@defineExternalizable(article, STRING[100]);
captionArticle 'Название' = DATA ISTRING[100](Article);

itemGroupArticle  = DATA ItemGroup(Article);
nameItemGroupArticle 'Товарная группа' (Article) = nameItemGroup(itemGroupArticle(Article));

isParentGroupArticle(group, article) = isParentItemGroupItemGroup(itemGroupArticle(article), group) PERSISTENT INDEXED;
canonicalNameItemGroupArticle 'Каноническое имя' (article) = canonicalNameGroup(itemGroupArticle(article));

FORM article 'Артикул'
    OBJECTS a=Article FIXED PANEL
    PROPERTIES(a) captionArticle, idArticle SHOWIF showIDs(), nameItemGroupArticle
    EDIT Article OBJECT a
;

FORM articles 'Артикулы'

    TREE groupTree g=ItemGroup PARENT parentItemGroup
    PROPERTIES READONLY nameItemGroup(g)
    ORDER BY nameItemGroup(g)
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeItemGroup(g) DEFAULT

    OBJECTS a=Article
    PROPERTIES(a) READONLY captionArticle, idArticle SHOWIF showIDs(), canonicalNameItemGroupArticle, nameItemGroupArticle
    PROPERTIES(a) ADDFORM, EDITFORM, deletea=DELETE
    ORDER BY captionArticle

    FILTERGROUP groupFilters
        FILTER 'С группами' 'F9' isParentGroupArticle(g, a) DEFAULT
        FILTER 'Без групп' 'F8' captionArticle(a) IF NOT itemGroupArticle(a)

    DIALOG Article OBJECT a
;

DESIGN articles FROM DEFAULT {

    NEW rootContainer {
        type = SPLITH;
        childConstraints = TO THE RIGHT;

        ADD groupTree.tree.box;
        ADD a.box {
            defaultComponent = TRUE;
            fillHorizontal = 4;
        }
    }
    ADD functions.box;
}

@defineObjectItemAttribute(article, Article, captionArticle, 'Артикул', itemArticleGroup);
@defineObjectItemAttributeBatch (article, captionArticle, 'Артикул');

// Цвет
CLASS Color 'Цвет товара';
TABLE color (Color);

@defineExternalizable(color, STRING[100]);
nameColor 'Наименование' = DATA ISTRING[50](Color);

FORM color 'Цвет товара'
    OBJECTS t=Color FIXED PANEL
    PROPERTIES(t) nameColor, idColor SHOWIF showIDs()
    EDIT Color OBJECT t
;

FORM colors 'Цвета товара'
    OBJECTS t=Color
    PROPERTIES(t) nameColor READONLY, idColor SHOWIF showIDs() READONLY, DELETE
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameColor
    DIALOG Color OBJECT t
;
DESIGN colors FROM DEFAULT { main{ preferredSize = (600, 400); } }

@defineObjectItemAttribute(color, Color, nameColor, 'Цвет', itemArticleGroup);
@defineObjectItemAttributeBatch (color, nameColor, 'Цвет');

// Размер
CLASS Size 'Размер товара';
TABLE size(Size);

@defineExternalizable(size, STRING[100]);

nameSize 'Наименование' = DATA ISTRING[50](Size);
shortNameSize 'Краткое наименование' = DATA ISTRING[10](Size);

FORM size 'Размер товара'
    OBJECTS t=Size FIXED PANEL
    PROPERTIES(t) nameSize, idSize SHOWIF showIDs(), shortNameSize
    EDIT Size OBJECT t
;

FORM sizes 'Размеры товара'
    OBJECTS t=Size
    PROPERTIES(t) nameSize READONLY, idSize SHOWIF showIDs() READONLY, shortNameSize READONLY, DELETE
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameSize
    DIALOG Size OBJECT t
;
DESIGN sizes FROM DEFAULT { main{ preferredSize = (600, 400); } }

@defineObjectItemAttribute(size, Size, nameSize, 'Размер', itemArticleGroup);
@defineObjectItemAttributeBatch (size, nameSize, 'Размер');
//@implementObjectShortItemAttribute(size, 'Размер (сокр.)');

// Рост
CLASS SizeHeight 'Рост товара';
TABLE sizeHeight (SizeHeight);
@defineExternalizable(sizeHeight, STRING[100]);

nameSizeHeight 'Рост' = DATA ISTRING[50](SizeHeight);

FORM sizeHeight 'Рост товара'
    OBJECTS t=SizeHeight FIXED PANEL
    PROPERTIES(t) nameSizeHeight, idSizeHeight SHOWIF showIDs()
    EDIT SizeHeight OBJECT t
;

FORM sizeHeights 'Роста товара'
    OBJECTS t=SizeHeight
    PROPERTIES(t) nameSizeHeight READONLY, idSizeHeight SHOWIF showIDs() READONLY, DELETE
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameSizeHeight
    DIALOG SizeHeight OBJECT t
;
DESIGN sizeHeights FROM DEFAULT { main{ preferredSize = (600, 400); } }

@defineObjectItemAttribute(sizeHeight, SizeHeight, nameSizeHeight, 'Рост', itemArticleGroup);
@defineObjectItemAttributeBatch (sizeHeight, nameSizeHeight, 'Рост');


overCopyItem(s, d) += ACTION (s, d) {
    ASSIGN articleItem(d) <- articleItem(s);
    ASSIGN colorItem(d) <- colorItem(s);
    ASSIGN sizeItem(d) <- sizeItem(s);
    ASSIGN sizeHeightItem(d) <- sizeHeightItem(s);
}

EXTEND DESIGN item { itemPrimaryColumn2 { ADD i.itemArticleGroup; } }


EXTEND DESIGN itemGroup {
    main {
        itemGroupDetail {
            ADD g.itemArticleGroup {
                childConstraints = TO THE RIGHT;
            };
        }
    }
}


NAVIGATOR {
    skuNavigator {
        ADD articles AFTER itemGroups;
    }
}
