MODULE ItemSection;

REQUIRE Item, Section;

NAMESPACE Item;

TABLE SectionItem (Section, Item);

dataIn 'Отм' = DATA BOOLEAN (Section, Item);

dataIn (Section s, Item sku) += dataIn(s, sku);

EXTEND FORM item
    OBJECTS sec = Section FIXED GRID 
    PROPERTIES dataIn(sec, i), id(sec) READONLY, name(sec) READONLY
;

DESIGN item {
    itemDetail {
        NEW itemSection {
            caption = 'Секция';
            fill = 1;
            MOVE sec.box; 
        }
    }
}

overCopy(Item old, Item new) += ACTION {
    dataIn(s, new) <- dataIn(s, old);
}

checkSection 'Проверять ограничения секции №3 и товаров из группы ПСП'  = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES () checkSection 
;
DESIGN options {
    items {
        MOVE PROPERTY (checkSection());
    }
}

CONSTRAINT dataIn(section('3'), Item i) AND itemGroup(i) AND NOT isParent(itemGroup('КАУ014725'), i)  AND checkSection()
    MESSAGE 'Для товаров не входящих в группу ПСП запрещено задавать секцию №3 (Продукция общепита).';