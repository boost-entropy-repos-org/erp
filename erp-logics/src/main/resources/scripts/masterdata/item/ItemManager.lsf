MODULE ItemManager;

REQUIRE Item, EmployeeManager, PurchaseOrder, PurchaseDemandOrder;

NAMESPACE Item;

@defineItemGroupDefaultValue(itemManager, 'Категорийный менеджер', Employee, nameCustomUser);  
nameItemManager 'Категорийный менеджер' (ItemGroup itemGroup) = name[Contact](itemManager(itemGroup)); 

countItemGroup (employee) = GROUP SUM 1 IF dataItemManager(ItemGroup group) == Employee employee BY employee PERSISTENT;

accessManager 'Доступ разрешен' (Employee employee, ItemGroup itemGroup) = OVERRIDE
    nearestItemManager(itemGroup) == employee,  
    employee IS Employee AND itemGroup IS ItemGroup AND NOT countItemGroup(employee) AND NOT countManagers(employee) PERSISTENT;

countAccessManagers (employee, itemGroup) = GROUP SUM 1 IF accessManager(Employee manager, ItemGroup itemGroup) AND inManager(manager, Employee employee) BY employee, itemGroup;

access = countAccessManagers(Employee employee, ItemGroup itemGroup) OR accessManager(employee, itemGroup) PERSISTENT;

countAccessItem 'Кол-во доступных групп' (employee, itemGroup) =
    GROUP SUM 1 IF access(Employee employee, ItemGroup child)
             AND isParent(child, SkuGroup itemGroup)
       BY employee, itemGroup PERSISTENT;

access = access(Employee employee, itemGroup(Item item));                                     

limitRecommendedQuantity(Item s, UserOrder o) += o IS UserOrder AND NOT access(currentUser(), s) AND NOT currentUser() IS SystemUser;
 
EXTEND FORM itemGroup
    PROPERTIES(g) nameCustomUser 
;
DESIGN itemGroup {
    itemGroupDetail {
        MOVE g.defaultItemGroup; 
    }
}

EXTEND FORM itemGroups
    PROPERTIES(g) READONLYIF  isReadonly() nameItemManager AFTER name(g) 
;
EXTEND FORM userOrder 
    FILTERS countAccessItem(currentUser(), sk) OR NOT sk IS SkuGroup
    FILTERS access(currentUser(), ks)
;

EXTEND FORM demandOrder 
    FILTERS countAccessItem(currentUser(), sk) OR NOT sk IS SkuGroup
    FILTERS access(currentUser(), s)
;

EXTEND FORM items 
    FILTERS countAccessItem(currentUser(), g) OR NOT g IS Group
    FILTERS access(currentUser(), i)
;