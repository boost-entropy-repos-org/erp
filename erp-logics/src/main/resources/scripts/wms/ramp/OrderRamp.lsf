MODULE OrderRamp;

REQUIRE Ramp, Order, Time;

NAMESPACE Order;

TABLE orderRampInterval(Order, Ramp, Interval);
inOrderRampInterval 'Вкл.' = DATA BOOLEAN (Order, Ramp, Interval);

CONSTRAINT inOrderRampInterval(o,r,i) AND NOT inRampInterval(r,i)
    MESSAGE 'В заказ включены рампа и интервал, у которых нет связи';    
    
//orderRampIntervalDate (r,i,d)= GROUP NAGGR o BY r,i,shipmentDateOrder(o) WHERE inOrderRampInterval(o,r,i);   
orderRampIntervalDate (r,i,d)= GROUP MAX o IF inOrderRampInterval(o,r,i) BY r,i,shipmentDateOrder(o);   
countRampIntervalDateSupplier (r,i,d,s)= GROUP SUM 1 IF inOrderRampInterval(o,r,i) BY r,i,shipmentDateOrder(o), supplierOrder(o); 

countSuppliersRampIntervalDate (r,i,d)= GROUP SUM 1 IF countRampIntervalDateSupplier (r,i,d,s) BY r,i,d; 
  
banDiffSupplierRamp'Запрет резервирования рамы на одно время для разных поставщиков (только заказы)'= DATA BOOLEAN (Ramp);
EXTEND FORM ramp
    PROPERTIES (r) banDiffSupplierRamp
;
  
CONSTRAINT countSuppliersRampIntervalDate (r,i,d) >1 AND banDiffSupplierRamp(r)
    MESSAGE 'Рампа не может быть зарезервирована на одно и то же время для разных поставщиков.';    

//descriptionOrdersRampIntervalDate 'Заказ' (r,i,d)= descriptionOrder(orderRampIntervalDate(r,i,d));   
descriptionOrdersRampIntervalDate 'Заказы' (r,i,d)= 
    GROUP CONCAT descriptionOrder(o) IF inOrderRampInterval(o,r,i), ', ' 
    BY r,i,shipmentDateOrder(o)
    ORDER o 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;   

//nameSupplierOrdersRampIntervalDate 'Поставщик' (r,i,d) = nameSupplierOrder(orderRampIntervalDate(r,i,d));
nameSupplierOrdersRampIntervalDate 'Поставщики' (r,i,d)= 
    GROUP CONCAT nameLegalEntity(s) IF countRampIntervalDateSupplier (r,i,d,s), ', ' 
    BY r,i,d
    ORDER s 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;   
    
//nameCustomerOrdersRampIntervalDate 'Покупатель' (r,i,d) = nameCustomerOrder(orderRampIntervalDate(r,i,d)); 
countRampIntervalDateCustomer (r,i,d,c)= GROUP SUM 1 IF inOrderRampInterval(o,r,i) BY r,i,shipmentDateOrder(o), customerOrder(o); 
nameCustomerOrdersRampIntervalDate 'Покупатели' (r,i,d)= 
    GROUP CONCAT nameLegalEntity(c) IF countRampIntervalDateCustomer (r,i,d,c), ', ' 
    BY r,i,d
    ORDER c 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;   
descriptionOrdersRampIntervalOrder'Заказы' (r,i,o) =  descriptionOrdersRampIntervalDate(r,i,shipmentDateOrder(o));    
nameSupplierOrdersRampIntervalOrder 'Поставщики' (r,i,o) = nameSupplierOrdersRampIntervalDate(r,i,shipmentDateOrder(o));
nameCustomerOrdersRampIntervalOrder 'Покупатели' (r,i,o) = nameCustomerOrdersRampIntervalDate(r,i,shipmentDateOrder(o));

descriptionRampsOrder 'Рампы' (o)= 
    GROUP CONCAT (CONCAT ' ', [= FORMULA STRING[10] 'to_char($1,\'DD.MM.YYYY\')'](shipmentDateOrder(o)), timeInterval(i), '№ '+nameRamp(r)) IF inOrderRampInterval(o,r,i), ', ' 
    BY o
    ORDER shipmentDateOrder(o), nameRamp(r), timeInterval(i) 
    MINCHARWIDTH 20 PREFCHARWIDTH 30;            

defaultShipmentTimeOrderDate (o)= GROUP MIN timeInterval(i) IF inOrderRampInterval(o,r,i) BY o;

backgroundOrdersRampIntervalOrder 'Цвет (свой заказ)' (r,i,o) = ABSTRACT CASE COLOR(Ramp,Interval,Order);

backgroundOrdersRampIntervalOrder(r,i,o) +=  WHEN inOrderRampInterval(o,r,i) THEN  RGB(204,255,204);

changeOrderRampInterval = ACTION (o,r,i) {
    REQUEST BOOLEAN INPUT;
    IF requestedLogical() THEN {
//        IF o IS Order AND NOT orderRampIntervalDate(r,i,shipmentDateOrder(o)) THEN {
        inOrderRampInterval(o,r,i) <- TRUE;
//        }
    } ELSE {
        inOrderRampInterval(o,r,i) <- NULL;
    }        
}

inRampOrder (ramp, order) = GROUP SUM 1 IF (inRampSku(ramp, skuOrderDetail(detail)) OR (ramp IS Ramp AND NOT countSkuGroupsRamp(ramp) AND skuOrderDetail(detail)))BY ramp, orderOrderDetail(detail);

FORM orderRamp 'Рампы'
    OBJECTS o=Order FIXED PANEL 
    OBJECTS st=Stock FIXED PANEL 
            
    OBJECTS r=Ramp 
    PROPERTIES(r) READONLY nameRamp
    ORDER BY      nameRamp(r)
    FILTERS stockRamp(r) == st,
            inRampOrder(r,o)
    
    OBJECTS i= Interval
    PROPERTIES(i) READONLY BACKGROUND backgroundOrdersRampIntervalOrder(r,i,o) timeInterval
    ORDER BY timeInterval(i)
    FILTERS inRampInterval(r,i) 
       
    PROPERTIES(r,i,o) BACKGROUND backgroundOrdersRampIntervalOrder(r,i,o) READONLY descriptionOrdersRampIntervalOrder,
                      nameSupplierOrdersRampIntervalOrder, nameCustomerOrdersRampIntervalOrder 
    PROPERTIES    BACKGROUND backgroundOrdersRampIntervalOrder(r,i,o) inOrderRampInterval(o,r,i) ON CHANGE changeOrderRampInterval(o,r,i)           
                                                        
    FILTERGROUP filter
        FILTER 'Только свободное время' o IS Order AND NOT orderRampIntervalDate(r,i,shipmentDateOrder(o)) 'F10'   
                                                             
    FILTERGROUP filter2
        FILTER 'По заказу' inOrderRampInterval(o,r,i) 'F9'                                                                      
;
DESIGN orderRamp {
    main{ 
        preferredSize = (800, 800); 
        MOVE o.box;
        NEW row {
            type = SPLITV;
            fill = 1;
            MOVE r.box { fill = 1;}
            MOVE i.box { fill = 2;}
        }
        MOVE functions.box;
    } 
}

META  defineOrderRamp(stockProp)       
    
    toFillRampOrder 'Зарезервировать рампу' = ACTION (order) {
    
        FORM orderRamp OBJECTS o = order, st = stockProp##Order(order)  MODAL;
        IF formResult() == FormResult.ok THEN {
            shipmentTimeOrder(order) <- defaultShipmentTimeOrderDate(order);         
        }          
    } 
    
    EXTEND FORM userOrder
        PROPERTIES (o) descriptionRampsOrder READONLY, toFillRampOrder 
    ;
    DESIGN userOrder {
        headerExtraParams {                                 
            NEW documentRamp AFTER documentShipmentGroup {
                caption = 'Погрузка/отгрузка';
                type = COLUMNS;
                columns = 1;
                MOVE PROPERTY(descriptionRampsOrder(o));
                MOVE PROPERTY(toFillRampOrder(o));
            }
        }    
    }    
    

END 
