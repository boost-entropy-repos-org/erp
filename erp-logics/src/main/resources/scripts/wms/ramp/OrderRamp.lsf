MODULE OrderRamp;

REQUIRE Ramp, Order, Time;

NAMESPACE Order;

TABLE orderRampInterval(Order, Ramp, Interval);
inOrderRampInterval 'Вкл.' = DATA BOOLEAN (Order, Ramp, Interval);

CONSTRAINT inOrderRampInterval(o,r,i) AND NOT inRampInterval(r,i)
    MESSAGE 'В заказ включены рампа и интервал, у которых нет связи';    
    
orderRampIntervalDate (r,i,d)= GROUP NAGGR o BY r,i,shipmentDateOrder(o) WHERE inOrderRampInterval(o,r,i);   
descriptionOrdersRampIntervalDate 'Заказ' (r,i,d)= descriptionOrder(orderRampIntervalDate(r,i,d));   
nameSupplierOrdersRampIntervalDate 'Поставщик' (r,i,d) = nameSupplierOrder(orderRampIntervalDate(r,i,d));
nameCustomerOrdersRampIntervalDate 'Покупатель' (r,i,d) = nameCustomerOrder(orderRampIntervalDate(r,i,d)); 
 
descriptionOrdersRampIntervalOrder'Заказ' (r,i,o) =  descriptionOrdersRampIntervalDate(r,i,shipmentDateOrder(o));    
nameSupplierOrdersRampIntervalOrder 'Поставщик' (r,i,o) = nameSupplierOrder(orderRampIntervalDate(r,i,shipmentDateOrder(o)));
nameCustomerOrdersRampIntervalOrder 'Покупатель' (r,i,o) = nameCustomerOrder(orderRampIntervalDate(r,i,shipmentDateOrder(o)));

descriptionRampsOrder 'Рампы' (o)= 
    GROUP CONCAT (CONCAT ' ', [= FORMULA STRING[10] 'to_char($1,\'DD.MM.YYYY\')'](shipmentDateOrder(o)), timeInterval(i), '№ '+nameRamp(r)) IF inOrderRampInterval(o,r,i), ', ' 
    BY o
    ORDER shipmentDateOrder(o), nameRamp(r), timeInterval(i) 
    MINCHARWIDTH 20 PREFCHARWIDTH 30;

defaultShipmentTimeOrderDate (o)= GROUP MIN timeInterval(i) IF inOrderRampInterval(o,r,i) BY o;

backgroundOrdersRampIntervalOrder 'Цвет (свой заказ)' (r,i,o) = RGB(204,255,204) IF orderRampIntervalDate(r,i,shipmentDateOrder(o))==o;

changeOrderRampInterval = ACTION (o,r,i) {
    REQUEST BOOLEAN INPUT;
    IF requestedLogical() THEN {
        IF o IS Order AND NOT orderRampIntervalDate(r,i,shipmentDateOrder(o)) THEN {
            inOrderRampInterval(o,r,i) <- TRUE;
        }
    } ELSE {
        inOrderRampInterval(o,r,i) <- NULL;
    }        
}

inRampOrder (ramp, order) = GROUP SUM 1 IF (inRampSku(ramp, skuOrderDetail(detail)) OR (ramp IS Ramp AND NOT countSkuGroupsRamp(ramp) AND skuOrderDetail(detail)))BY ramp, orderOrderDetail(detail);

FORM orderRamp 'Рампы'
    OBJECTS o=Order FIXED PANEL 
    OBJECTS st=Stock FIXED PANEL 
            
    OBJECTS r=Ramp 
    PROPERTIES(r) READONLY nameRamp
    ORDER BY      nameRamp(r)
    FILTERS stockRamp(r) == st,
            inRampOrder(r,o)
    
    OBJECTS i= Interval
    PROPERTIES(i) READONLY BACKGROUND backgroundOrdersRampIntervalOrder(r,i,o) timeInterval
    ORDER BY timeInterval(i)
    FILTERS inRampInterval(r,i) 
       
    PROPERTIES(r,i,o) BACKGROUND backgroundOrdersRampIntervalOrder(r,i,o) READONLY descriptionOrdersRampIntervalOrder,
                      nameSupplierOrdersRampIntervalOrder, nameCustomerOrdersRampIntervalOrder 
    PROPERTIES    BACKGROUND backgroundOrdersRampIntervalOrder(r,i,o) inOrderRampInterval(o,r,i) ON CHANGE changeOrderRampInterval(o,r,i)           
                                                        
    FILTERGROUP filter
        FILTER 'Только свободное время' o IS Order AND NOT orderRampIntervalDate(r,i,shipmentDateOrder(o)) 'F10'                                                        
;
DESIGN orderRamp {
    main{ 
        preferredSize = (800, 800); 
        MOVE o.box;
        NEW row {
            type = SPLITV;
            fill = 1;
            MOVE r.box { fill = 1;}
            MOVE i.box { fill = 2;}
        }
        MOVE functions.box;
    } 
}

META  defineOrderRamp(stockProp)       
    
    toFillRampOrder 'Зарезервировать рампу' = ACTION (order) {
    
        FORM orderRamp OBJECTS o = order, st = stockProp##Order(order)  MODAL;
        IF formResult() == FormResult.ok THEN {
            shipmentTimeOrder(order) <- defaultShipmentTimeOrderDate(order);         
        }          
    } 
    
    EXTEND FORM userOrder
        PROPERTIES (o) descriptionRampsOrder READONLY, toFillRampOrder 
    ;
    DESIGN userOrder {
        headerExtraParams {                                 
            NEW documentRamp AFTER documentShipmentGroup {
                caption = 'Погрузка/отгрузка';
                type = COLUMNS;
                columns = 1;
                MOVE PROPERTY(descriptionRampsOrder(o));
                MOVE PROPERTY(toFillRampOrder(o));
            }
        }    
    }    
    

END 
