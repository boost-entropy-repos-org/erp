MODULE OrderRamp;

REQUIRE Ramp, Order, Time;

NAMESPACE Order;

TABLE orderRampInterval(Order, Ramp, Interval);
in 'Вкл.' = DATA BOOLEAN (Order, Ramp, Interval);

CONSTRAINT in(Order o,Ramp r,Interval i) AND NOT in(r,i)
    MESSAGE 'В заказ включены рампа и интервал, у которых нет связи';    
    
//orderRampIntervalDate (r,i,d)= GROUP NAGGR o BY r,i,shipmentDateOrder(o) WHERE inOrderRampInterval(o,r,i);   
order (r,i,d)= GROUP MAX Order o IF in(o,Ramp r,Interval i) BY r,i,shipmentDate(o);   
countSupplier (r,i,d,s)= GROUP SUM 1 IF in(Order o,Ramp r,Interval i) BY r,i,shipmentDate(o), supplier(o); 

countSuppliers (r,i,d)= GROUP SUM 1 IF countSupplier (Ramp r,Interval i,DATE d,LegalEntity s) BY r,i,d; 
  
banDiffSupplier'Запрет резервирования рамы на одно время для разных поставщиков (только заказы)'= DATA BOOLEAN (Ramp);
EXTEND FORM ramp
    PROPERTIES (r) banDiffSupplier
;
  
CONSTRAINT countSuppliers (Ramp r,Interval i,DATE d) >1 AND banDiffSupplier(r)
    MESSAGE 'Рампа не может быть зарезервирована на одно и то же время для разных поставщиков.';    

//descriptionOrdersRampIntervalDate 'Заказ' (r,i,d)= descriptionOrder(orderRampIntervalDate(r,i,d));   
descriptionOrders 'Заказы' (r,i,d)= 
    GROUP CONCAT description(Order o) IF in(o,Ramp r,Interval i), ', ' 
    BY r,i,shipmentDate(o)
    ORDER o 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;   

//nameSupplierOrdersRampIntervalDate 'Поставщик' (r,i,d) = nameSupplierOrder(orderRampIntervalDate(r,i,d));
nameSupplierOrders 'Поставщики' (r,i,d)= 
    GROUP CONCAT name(LegalEntity s) IF countSupplier (Ramp r,Interval i,DATE d,s), ', ' 
    BY r,i,d
    ORDER s 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;   
    
//nameCustomerOrdersRampIntervalDate 'Покупатель' (r,i,d) = nameCustomerOrder(orderRampIntervalDate(r,i,d)); 
countCustomer (r,i,d,c)= GROUP SUM 1 IF in(Order o,Ramp r,Interval i) BY r,i,shipmentDate(o), customer(o); 
nameCustomerOrders 'Покупатели' (r,i,d)= 
    GROUP CONCAT name(LegalEntity c) IF countCustomer (Ramp r,Interval i,DATE d,c), ', ' 
    BY r,i,d
    ORDER c 
    MINCHARWIDTH 30 PREFCHARWIDTH 50;   
descriptionOrders'Заказы' (Ramp r,Interval i,Order o) =  descriptionOrders(r,i,shipmentDate(o));    
nameSupplierOrders 'Поставщики' (Ramp r,Interval i,Order o) = nameSupplierOrders(r,i,shipmentDate(o));
nameCustomerOrders 'Покупатели' (Ramp r,Interval i,Order o) = nameCustomerOrders(r,i,shipmentDate(o));

descriptionRamps 'Рампы' (o)= 
    GROUP CONCAT (CONCAT ' ', [= FORMULA STRING[10] 'to_char($1,\'DD.MM.YYYY\')'](shipmentDate(Order o)), time(Interval i), '№ '+name(Ramp r)) IF in(o,r,i), ', ' 
    BY o
    ORDER shipmentDate(o), name(r), time(i) 
    MINCHARWIDTH 20 PREFCHARWIDTH 30;            

defaultShipmentTimeDate (o)= GROUP MIN time(Interval i) IF in(Order o,Ramp r,i) BY o;

backgroundOrders 'Цвет (свой заказ)' (r,i,o) = ABSTRACT CASE COLOR(Ramp,Interval,Order);

backgroundOrders(Ramp r,Interval i,Order o) +=  WHEN in(o,r,i) THEN  RGB(204,255,204);

change(Order o,Ramp r,Interval i) = ACTION {
    REQUEST BOOLEAN INPUT;
    IF requestedLogical() THEN {
//        IF o IS Order AND NOT orderRampIntervalDate(r,i,shipmentDateOrder(o)) THEN {
        in(o,r,i) <- TRUE;
//        }
    } ELSE {
        in(o,r,i) <- NULL;
    }        
}

in (ramp, order) = GROUP SUM 1 IF (in(Ramp ramp, sku(OrderDetail detail)) OR (ramp IS Ramp AND NOT countSkuGroups(ramp) AND sku(detail)))BY ramp, order(detail);

FORM orderRamp 'Рампы'
    OBJECTS o=Order FIXED PANEL 
    OBJECTS st=Stock FIXED PANEL 
            
    OBJECTS r=Ramp 
    PROPERTIES(r) READONLY name
    ORDER BY      name(r)
    FILTERS stock(r) == st,
            in(r,o)
    
    OBJECTS i= Interval
    PROPERTIES(i) READONLY BACKGROUND backgroundOrders(r,i,o) time
    ORDER BY time(i)
    FILTERS in(r,i) 
       
    PROPERTIES(r,i,o) BACKGROUND backgroundOrders(r,i,o) READONLY descriptionOrders,
                      nameSupplierOrders, nameCustomerOrders 
    PROPERTIES    BACKGROUND backgroundOrders(r,i,o) in(o,r,i) ON CHANGE change(o,r,i)           
                                                        
    FILTERGROUP filter
        FILTER 'Только свободное время' o IS Order AND NOT order(r,i,shipmentDate(o)) 'F10'   
                                                             
    FILTERGROUP filter2
        FILTER 'По заказу' in(o,r,i) 'F9'                                                                      
;
DESIGN orderRamp {
    main{ 
        preferredSize = (800, 800); 
        MOVE o.box;
        NEW row {
            type = SPLITV;
            fill = 1;
            MOVE r.box { fill = 1;}
            MOVE i.box { fill = 2;}
        }
        MOVE functions.box;
    } 
}

META  defineOrderRamp(stockProp)       
    
    toFillRamp 'Зарезервировать рампу'(Order order) = ACTION {
    
        FORM orderRamp OBJECTS o = order, st = stockProp(order)  MODAL;
        IF formResult() == FormResult.ok THEN {
            shipmentTime(order) <- defaultShipmentTimeDate(order);         
        }          
    } 
    
    EXTEND FORM userOrder
        PROPERTIES (o) descriptionRamps READONLY, toFillRamp 
    ;
    DESIGN userOrder {
        headerExtraParams {                                 
            NEW documentRamp AFTER documentShipmentGroup {
                caption = 'Погрузка/отгрузка';
                type = COLUMNS;
                columns = 1;
                MOVE PROPERTY(descriptionRamps(o));
                MOVE PROPERTY(toFillRamp(o));
            }
        }    
    }    
    

END 
