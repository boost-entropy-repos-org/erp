MODULE OrderRampForm;

REQUIRE OrderRamp;

NAMESPACE Order;

countOrders (ramp, date, dateFrom, dateTo) = GROUP SUM 1 IF 
    order(Ramp r,Interval i,DATE d) AND d >= (DATE dateFrom AS DATE) AND d <= (DATE dateTo AS DATE)
        BY r, d, dateFrom, dateTo;
        
countOrders (stock, date, dateFrom, dateTo) = GROUP SUM 1 IF 
    order(Ramp r,Interval i,DATE d) AND d >= (DATE dateFrom AS DATE) AND d <= (DATE dateTo AS DATE)
        BY stock(r), d, dateFrom, dateTo;     
countDates 'Кол-во дней' (stock, dateFrom, dateTo) = GROUP SUM 1 IF 
    countOrders (Stock stock, DATE date, DATE dateFrom, DATE dateTo)
        BY stock, dateFrom, dateTo; 
                   
countLoadInterval (ramp, date) = GROUP SUM 1 IF 
    order(Ramp r,Interval i,DATE d) BY r, d;  
countLoadInterval (stock, date) = GROUP SUM 1 IF
    order(Ramp r,Interval i,DATE d) BY stock(r), d;
           
percInterval '% загрузки рампы' (Ramp r,DATE d) = countLoadInterval (r, d) * 100.00 / countInterval(r);            
percInterval '% загрузки рампы' (Stock st,DATE d) = countLoadInterval (st, d)* 100.00 / countInterval(st) ;          
        

countLoadIntervalDate 'Кол-во загруженных интервалов' (st,dFrom,dTo) = GROUP SUM countLoadInterval (Stock st, DATE d) IF 
    countOrders(st,d,DATE dFrom,DATE dTo) BY st,dFrom,dTo;
    
percInterval '% загрузки рамп' (Stock st,DATE dateFrom,DATE dateTo) = countLoadIntervalDate(st,dateFrom,dateTo) *100.00 / 
    (countDates(st,dateFrom,dateTo) * countInterval(st));  

FORM rampStockDate 'Загрузка рамп'
    OBJECTS st=Stock PANEL 
    PROPERTIES(st) SELECTOR name
    FILTERS isCompany(st)
        
    OBJECTS params = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES objFrom = VALUE(dFrom), objTo = VALUE(dTo)       
      
    OBJECTS d=DATE 
    PROPERTIES READONLY objDate = VALUE(d)  
    PROPERTIES(st,d) READONLY percInterval           
        
    FILTERS countOrders(st,d,dFrom,dTo),
            iterate(d, dFrom, dTo)               
    
    PROPERTIES(st,dFrom,dTo) READONLY percInterval            
            
    OBJECTS r=Ramp 
    PROPERTIES(r) READONLY name
    PROPERTIES(r,d) READONLY percInterval   
    ORDER BY      name(r)
    FILTERS stock(r) == st,
            countOrders(r,d,dFrom,dTo)
                
    
    OBJECTS i= Interval
    PROPERTIES(i) READONLY time
    ORDER BY time(i)
    FILTERS in(r,i) 
       
    PROPERTIES(r,i,d) READONLY nameSupplierOrders, descriptionOrders, nameCustomerOrders 
    FILTERGROUP filter1
        FILTER 'Только свободные рампы' percInterval(r,d) < 100 'F9'                         
    FILTERGROUP filter
        FILTER 'Только свободное время' TRUE AND NOT order(r,i,d) 'F10'                          
;
DESIGN rampStockDate {
    BOX { 
        NEW top {
            type = CONTAINERH;
            MOVE BOX(st);
            MOVE BOX(params);
        }
        NEW columnTop {
            type = SPLITH;
            fill = 1;
            NEW column1 {
                fill = 1;
                type = SPLITH;
                fill = 1;
                MOVE BOX(d);
                MOVE BOX(r);                                                              
            }
            MOVE BOX(i) {fill = 2;}            
        }
        MOVE TOOLBARBOX;
    } 
}
@extendFormFilterStockAccess(st, rampStockDate, company);
percInterval '% загрузки рампы' (Ramp r,Order o) = percInterval(r, shipmentDate(o));
EXTEND FORM orderRamp
    PROPERTIES READONLY percInterval(r,o)         

    FILTERGROUP filter1
        FILTER 'Только свободные рампы' percInterval(r,o) < 100 'F9'    
;

NAVIGATOR {
    rampNavigator {
        ADD rampStockDate;       
    }
}