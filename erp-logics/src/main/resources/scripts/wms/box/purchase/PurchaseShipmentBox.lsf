MODULE PurchaseShipmentBox;

REQUIRE PurchaseBox, PurchaseShipment;

NAMESPACE Purchase;

@defineDocumentInterfaceDetailBox(shipment);
overCopy[UserShipmentDetail,UserShipmentDetail](UserShipmentDetail d, UserShipmentDetail detail) += {      
    box(d) <- box(detail);
}
@extendFormInterfaceDetailBox(shipment, s, , quantity);

@deriveDocumentOperationProperty(UserShipment, showBox);

//-- invoice
@defineInvoiceShipmentBox(customerStock);

@deriveDocumentOperationProperty(UserInvoice, showBox);


EXTEND CLASS Box : ShipmentDimension;
name(Box d) += Box.name(d);

shipmentDimension[UserShipmentDetail](UserShipmentDetail d) += box(d);
shipmentDimension[UserInvoiceDetail](UserInvoiceDetail d) += box(d);

//---------------------------------Новый функционал ----------------------------------//

@defineDocumentDetailBoxCustom(userShipment, current, ' текущий');
currentBox (UserShipmentDetail detail) = currentBox(userShipment(detail));

shipmentDimension(UserShipment s) += currentBox(s);

EXTEND FORM userShipment
    PROPERTIES(s) SHOWIF showBox(s) nameCurrentBox
;

DESIGN userShipment {
    headerBox {
        MOVE PROPERTY(nameCurrentBox(s)) AFTER PROPERTY(showBox(s));
    }
}

in (box, userShipment) = GROUP SUM 1 IF include(invoice(InvoiceDetail detail), UserShipment userShipment)
    BY box(detail), userShipment;
CONSTRAINT  currentBox(UserShipment userShipment) AND NOT in(currentBox(userShipment), userShipment)
    CHECKED BY currentBox[UserShipment] MESSAGE 'В качестве текущего короба выбран короб, которого нету в накладных';

WHEN SESSION FORMS userShipment
    SET(UserShipmentDetail detail IS UserShipmentDetail) AND currentBox(detail)
        DO ASSIGN box(detail) <- currentBox(detail) WHERE currentBox(detail);

WHEN SESSION FORMS userShipment
    CHANGED(invoiceDetail(UserShipmentDetail detail)) AND NOT box(detail)
        DO ASSIGN box(detail) <- box(invoiceDetail(detail));

WHEN SESSION FORMS userShipment
    CHANGED(sku(UserShipmentDetail detail)) AND box(detail)
        DO ASSIGN invoiceDetail(detail) <- defaultInvoiceDetail(sku(detail), box(detail), userShipment(detail))
                                                                WHERE defaultInvoiceDetail(sku(detail), box(detail), userShipment(detail));

WHEN SESSION FORMS userShipment
    CHANGED(sku(UserShipmentDetail detail)) AND NOT box(detail)
        DO ASSIGN invoiceDetail(detail) <- defaultInvoiceDetail(sku(detail), userShipment(detail)) 
                                                                WHERE defaultInvoiceDetail(sku(detail), userShipment(detail));
