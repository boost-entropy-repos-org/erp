MODULE PurchaseShipmentBox;

REQUIRE PurchaseBox, PurchaseShipment;

NAMESPACE Purchase;

@defineDocumentInterfaceDetailBox(shipment);
overCopyUserShipmentDetail(d, detail) += ACTION (d, detail) {      
    boxUserShipmentDetail(d) <- boxUserShipmentDetail(detail);
}
@extendFormInterfaceDetailBox(shipment, s, , quantity);

@deriveDocumentOperationProperty(UserShipment, showBox);

//-- invoice
@defineInvoiceShipmentBox(customerStock);

@deriveDocumentOperationProperty(UserInvoice, showBox);


EXTEND CLASS Box : ShipmentDimension;
nameShipmentDimension(d) += nameBox(d);

shipmentDimensionUserShipmentDetail(d) += boxUserShipmentDetail(d);
shipmentDimensionUserInvoiceDetail(d) += boxUserInvoiceDetail(d);

//---------------------------------Новый функционал ----------------------------------//

@defineDocumentDetailBoxCustom(userShipment, current, ' текущий');
currentBoxUserShipmentDetail (detail) = currentBoxUserShipment(userShipmentUserShipmentDetail(detail));

shipmentDimensionUserShipment(s) += currentBoxUserShipment(s);

EXTEND FORM userShipment
    PROPERTIES(s) SHOWIF showBoxUserShipment(s) nameCurrentBoxUserShipment
;

DESIGN userShipment {
    headerBox {
        ADD PROPERTY(nameCurrentBoxUserShipment(s)) AFTER PROPERTY(showBoxUserShipment(s));
    }
}

inBoxUserShipment (box, userShipment) = GROUP SUM 1 IF includeInvoiceUserShipment(invoiceInvoiceDetail(detail), userShipment)
    BY boxInvoiceDetail(detail), userShipment;
CONSTRAINT  currentBoxUserShipment(userShipment) AND NOT inBoxUserShipment(currentBoxUserShipment(userShipment), userShipment)
    CHECKED BY currentBoxUserShipment MESSAGE 'В качестве текущего короба выбран короб, которого нету в накладных';

WHEN SESSION FORMS userShipment
    SET(detail IS UserShipmentDetail) AND currentBoxUserShipmentDetail(detail)
        DO ASSIGN boxUserShipmentDetail(detail) <- currentBoxUserShipmentDetail(detail) WHERE currentBoxUserShipmentDetail(detail);

WHEN SESSION FORMS userShipment
    CHANGED(invoiceDetailUserShipmentDetail(detail)) AND NOT boxUserShipmentDetail(detail)
        DO ASSIGN boxUserShipmentDetail(detail) <- boxInvoiceDetail(invoiceDetailUserShipmentDetail(detail));

WHEN SESSION FORMS userShipment
    CHANGED(skuUserShipmentDetail(detail)) AND boxUserShipmentDetail(detail)
        DO ASSIGN invoiceDetailUserShipmentDetail(detail) <- defaultInvoiceDetailSkuDimensionUserShipment(skuUserShipmentDetail(detail), boxUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail))
                                                                WHERE defaultInvoiceDetailSkuDimensionUserShipment(skuUserShipmentDetail(detail), boxUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail));

WHEN SESSION FORMS userShipment
    CHANGED(skuUserShipmentDetail(detail)) AND NOT boxUserShipmentDetail(detail)
        DO ASSIGN invoiceDetailUserShipmentDetail(detail) <- defaultInvoiceDetailSkuUserShipment(skuUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail)) 
                                                                WHERE defaultInvoiceDetailSkuUserShipment(skuUserShipmentDetail(detail), userShipmentUserShipmentDetail(detail));
