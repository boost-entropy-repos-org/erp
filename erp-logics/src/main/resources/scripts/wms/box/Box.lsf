MODULE Box;

REQUIRE Stock, Barcode, WMS;

//------------------------- Короб -----------------------------//

CLASS Box 'Короб' ;
TABLE box (Box);

@defineExternalizable(box, VARSTRING[100]);
nameBox 'Наименование' = DATA VARISTRING[20](Box);


FORM box 'Короб'
    OBJECTS s = Box FIXED PANEL
    PROPERTIES(s) nameBox, idBox SHOWIF showIDs()
    EDIT Box OBJECT s
;

FORM boxes 'Короба'

    OBJECTS s = Box
    PROPERTIES(s) READONLY nameBox, idBox SHOWIF showIDs()
    PROPERTIES(s) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    ORDER BY nameBox

    DIALOG Box OBJECT s
;

DESIGN boxes FROM DEFAULT { preferredSize = (1024, 768); }

                                                               
//--------------------------------------------------Короб для Detail--------------------------------------//

META defineDocumentDetailBoxCustom(detail, prefix, caption)
    prefix###box###detail = DATA Box (###detail);
    name###prefix###box###detail 'Короб'###caption (detail) = nameBox(prefix###box###detail(detail)) MINCHARWIDTH 10 PREFCHARWIDTH 10;
END
META defineDocumentAbstractDetailBoxCustom(detail, prefix, caption)
    prefix###box###detail = ABSTRACT Box (###detail) PERSISTENT;
    name###prefix###box###detail 'Короб'###caption (detail) = nameBox(prefix###box###detail(detail)) MINCHARWIDTH 10 PREFCHARWIDTH 10;
END
META defineDocumentInterfaceDetailBoxCustom(detail, prefix, caption)
    @defineDocumentAbstractDetailBoxCustom(detail, prefix, caption);
    @defineDocumentDetailBoxCustom(user###detail, prefix, caption);
    prefix###box###detail(detail) += prefix###box###user###detail(detail);
END
META defineDocumentInterfaceDetailBoxCustom(detail)
    @defineDocumentInterfaceDetailBoxCustom(detail, , );
END

META defineDocumentAbstractDetailBox(object)
    @defineDocumentAbstractDetailBoxCustom(object###detail, , );
END

META defineDocumentInterfaceDetailBox(object, caption)
    @defineDocumentInterfaceDetailBoxCustom(object###detail, , caption);
END
META defineDocumentInterfaceDetailBox(object)
    @defineDocumentInterfaceDetailBoxCustom(object###detail, , );
END

META extendFormInterfaceDetailBox(object, concrete, prefix, befProp)
    @defineDocumentInterfaceProperty (object, showBox, 'Короб');

    EXTEND FORM user###object
        PROPERTIES(concrete) showBox###user###object
        PROPERTIES (d) SHOWIF showBox###user###object(concrete) name###prefix###box###user###object###detail BEFORE befProp###user###object###detail
    ;
    EXTEND DESIGN user###object {
        headerExtraParams {
            NEW headerBox {
                caption = 'Короб';
                type = CONTAINERH;
                ADD PROPERTY(showBox###user###object);
            }
        }
    }
    EXTEND FORM object##s
    PROPERTIES (d) READONLY SHOWIF showBox###object(concrete) name###prefix###box###object###detail BEFORE befProp###object###detail
    ;
END

//---------------- invoiceShipment --------------------//

META defineInvoiceShipmentBox(stockProp)

    @defineDocumentInterfaceDetailBox(invoice);
    boxShipmentDetail (detail) += boxInvoiceDetail(invoiceDetailInvoiceShipmentDetail(detail));

    @defineDocumentInterfaceProperty (invoice, showBox, 'Короб');

    EXTEND FORM userInvoice
        PROPERTIES(i) showBoxUserInvoice
        PROPERTIES(d) BEFORE name###stockProp###userInvoiceDetail SHOWIF showBoxUserInvoice(i) BACKGROUND backgroundShippedInvoice(i)
                      nameBoxUserInvoiceDetail
    ;
    EXTEND DESIGN userInvoice {
        headerExtraParams {
            NEW headerBox {
                caption = 'Короб';
                ADD PROPERTY(showBoxUserInvoice);
            }
        }
    }

    EXTEND FORM invoices
        PROPERTIES(d) READONLY BEFORE name###stockProp###invoiceDetail SHOWIF showBoxInvoice(i) BACKGROUND backgroundShippedInvoice(i)
                      nameBoxInvoiceDetail
    ;
    EXTEND FORM invoiceDetails
        PROPERTIES(d) READONLY BEFORE name###stockProp###invoiceDetail SHOWIF showBoxInvoiceDetail(d) BACKGROUND backgroundShippedInvoiceDetail(d)
                      nameBoxInvoiceDetail
    ;
    
    overCopyUserInvoiceDetail(d, detail) += ACTION (d, detail) {      
        boxUserInvoiceDetail(d) <- boxUserInvoiceDetail(detail);
    }     

END

NAVIGATOR {
    WMSNavigator {
        NEW boxNavigator 'Короба' {
            ADD boxes;
        }
    }
}



