MODULE OrderInvoicePickingOrder;

REQUIRE OrderPickingOrder, Invoice;

NAMESPACE Invoice;

inCreatePickingUserInvoicePostedOrder = DATA SESSION BOOLEAN (Order);
countInCreatePickingUserInvoicePostedOrder = GROUP SUM 1 IF inCreatePickingUserInvoicePostedOrder(o);

countInCreatePickingUserInvoicePostedOrderParams = GROUP SUM 1 IF inCreatePickingUserInvoicePostedOrder(o)
                                                         BY fromOrder(o), toOrder(o), fromStockOrder(o), toStockOrder(o);
countParamsInCreatePickingUserInvoicePostedOrder = GROUP SUM 1 IF countInCreatePickingUserInvoicePostedOrderParams(from, to, fromStock, toStock); 

createPickingUserInvoicePostedOrder 'Создать проведенную накладную' = ABSTRACT ACTION CASE (Order);

META defineOrderInvoicePickingOrder (sign)

    pickingBatchOrderDetail (batch, orderDetail) = PARTITION UNGROUP pickingQuantityOrderDetail
                                                               LIMIT STRICT currentBalanceBatchStock(batch, supplierStockOrderDetail(orderDetail)) IF skuBatch(batch) == skuOrderDetail(orderDetail)
                                                               BY orderDetail
                                                               ORDER orderBatch(batch);

    createPickingDetailUserInvoiceOrder = ACTION (userInvoice, order) {
        IF useExplicitBatchDetailUserInvoiceOrder(order) AND explicitBatchLedgerStock(fromStockOrder(order)) THEN {
            FOR orderOrderDetail(orderDetail) == order AND pickingBatchOrderDetail(batch, orderDetail) AND NOT batchOrderDetail(orderDetail)
                ORDER orderCreateUserInvoiceOrderDetail(orderDetail) 
                ADDOBJ d = UserInvoiceDetail DO {
                userInvoiceUserInvoiceDetail(d) <- userInvoice;
                orderUserInvoiceDetail(d) <- order;
                skuUserInvoiceDetail(d) <- skuOrderDetail(orderDetail);
                batchUserInvoiceDetail(d) <- batch;
                quantityUserInvoiceDetail (d) <- pickingBatchOrderDetail(batch, orderDetail);
                
                fillUserInvoiceDetailOrderDetail(d, orderDetail);
                overFillUserInvoiceDetailOrderDetail(d, orderDetail);
            }
            FOR orderOrderDetail(orderDetail) == order AND PREV(pickingQuantityOrderDetail(orderDetail)) AND batchOrderDetail(orderDetail)
                ORDER orderCreateUserInvoiceOrderDetail(orderDetail) 
                ADDOBJ d = UserInvoiceDetail DO {
                userInvoiceUserInvoiceDetail(d) <- userInvoice;
                orderUserInvoiceDetail(d) <- order;
                skuUserInvoiceDetail(d) <- skuOrderDetail(orderDetail);
                batchUserInvoiceDetail(d) <- batchOrderDetail(orderDetail);
                quantityUserInvoiceDetail (d) <- PREV(pickingQuantityOrderDetail(orderDetail));
    
                fillUserInvoiceDetailOrderDetail(d, orderDetail);
                overFillUserInvoiceDetailOrderDetail(d, orderDetail);
            }
        } ELSE {
            FOR orderOrderDetail(orderDetail) == order AND
                pickingQuantityOrderDetail(orderDetail) > 0 // возможно еще нужно фильтровать по складам
                AND inOrderDetailUserInvoice(orderDetail, userInvoice)
                ORDER orderCreateUserInvoiceOrderDetail(orderDetail) 
                ADDOBJ d = UserInvoiceDetail DO {
                    userInvoiceUserInvoiceDetail(d) <- userInvoice;
                    orderUserInvoiceDetail(d) <- order;
                    skuUserInvoiceDetail(d) <- skuOrderDetail(orderDetail);
                    batchUserInvoiceDetail(d) <- batchOrderDetail(orderDetail);
                    quantityUserInvoiceDetail (d) <- pickingQuantityOrderDetail(orderDetail);

                    fillUserInvoiceDetailOrderDetail(d, orderDetail);
                    overFillUserInvoiceDetailOrderDetail(d, orderDetail);                    
            }
        }

        FOR excessQuantityPickingPickingDetailOrderSku(order, sku) > 0 ADDOBJ d = UserInvoiceDetail DO {
            userInvoiceUserInvoiceDetail(d) <- userInvoice;
            orderUserInvoiceDetail(d) <- order;
            skuUserInvoiceDetail(d) <- sku;
            quantityUserInvoiceDetail (d) <- excessQuantityPickingPickingDetailOrderSku(order, sku);
        }
    }

    fillPickingUserInvoiceOrder = ACTION (userInvoice, order) {
        fillHeaderUserInvoiceOrder(userInvoice, order);
        createPickingDetailUserInvoiceOrder(userInvoice, order);
        fillPriceUserInvoiceOrder(userInvoice, order);
    }

    packQuantityInvoice 'Общее количество грузовых мест' = ABSTRACT NUMERIC[8,2] (Invoice);
    packQuantityUserInvoice 'Общее количество грузовых мест' = DATA NUMERIC[8,2] (UserInvoice);
    packQuantityInvoice(invoice) += packQuantityUserInvoice(invoice);

    createPickingUserInvoiceOrder 'Создать накладную по комплектации' = ACTION (order) {
        IF order IS Order THEN {
            userInvoiceOrder(order) <- NULL;
            FOR ADDOBJ i = UserInvoice DO {
                fillPickingUserInvoiceOrder(i, order);
                userInvoiceOrder(order) <- i;
                packQuantityUserInvoice(i) <- packQuantityOrder(order);
                createdUserInvoiceOrder(i, order) <- TRUE;
                includeOrderUserInvoice(order,i) <- TRUE;
            }
        }
    }

    createPickingUserInvoicePostedOrder 'Создать проведенную накладную по комплектации' = ACTION (order) {
        IF order IS Order THEN {
            userInvoiceOrder(order) <- NULL;
            IF countInCreatePickingUserInvoicePostedOrder() THEN {
                IF countParamsInCreatePickingUserInvoicePostedOrder() > 1 THEN {
                    MESSAGE 'По выбранным заказам не может быть создана одна накладная';
                } ELSE {
                    FOR ADDOBJ i = UserInvoice DO {
                        isPostedUserInvoice(i) <- TRUE;
                        FOR inCreatePickingUserInvoicePostedOrder(o) DO {
                            fillPickingUserInvoiceOrder(i, o);                        
                            createdUserInvoiceOrder(i, o) <- TRUE;
                            includeOrderUserInvoice(o,i) <- TRUE;
                        }
                        packQuantityUserInvoice(i) <- [=GROUP SUM packQuantityOrder(o) IF inCreatePickingUserInvoicePostedOrder(o)]();
                    }
                }
            } ELSE {
                FOR ADDOBJ i = UserInvoice DO {
                    isPostedUserInvoice(i) <- TRUE;
                    fillPickingUserInvoiceOrder(i, order);
                    packQuantityUserInvoice(i) <- packQuantityOrder(order);
                    createdUserInvoiceOrder(i, order) <- TRUE;
                    userInvoiceOrder(order) <- i;
                    includeOrderUserInvoice(order,i) <- TRUE;
                }
            }
        }
    }
    Invoice.createPickingUserInvoicePostedOrder(o) += createPickingUserInvoicePostedOrder(o); 

    addPickingUserInvoiceOrder 'Накладная по комплектации'###sign =  ACTION (order) NEWSESSION{
        IF order IS Order THEN {
            createPickingUserInvoiceOrder(order);
            IF excessQuantityPickingPickingDetailOrder(order) THEN {
                MESSAGE 'В накладной присутствуют товары, не указанные в заказе';
            }
            FORM userInvoice OBJECTS i = userInvoiceOrder(order) MANAGESESSION DOCKEDMODAL;
        }
    } TOOLBAR;

    EXTEND FORM orders
        PROPERTIES(o) SHOWIF calcPartOrder(o) addPickingUserInvoiceOrder
    ;
    EXTEND DESIGN orders {
        createdContainer{
            ADD PROPERTY(addPickingUserInvoiceOrder(o));
        }
    }

END