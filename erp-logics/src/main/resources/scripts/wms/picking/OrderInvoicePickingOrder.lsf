MODULE OrderInvoicePickingOrder;

REQUIRE OrderPickingOrder, OrderInvoice;

NAMESPACE Invoice;

inCreatePickingUserInvoicePosted = DATA LOCAL BOOLEAN (Order);
countInCreatePickingUserInvoicePostedOrder = GROUP SUM 1 IF inCreatePickingUserInvoicePosted(Order o);

countInCreatePickingUserInvoicePostedOrderParams = GROUP SUM 1 IF inCreatePickingUserInvoicePosted(Order o)
                                                         BY from(o), to(o), fromStock(o), toStock(o);
countParamsInCreatePickingUserInvoicePostedOrder = GROUP SUM 1 IF countInCreatePickingUserInvoicePostedOrderParams(LegalEntity from, LegalEntity to, Stock fromStock, Stock toStock); 

createPickingUserInvoicePosted 'Создать проведенную накладную' = ACTION ABSTRACT (Order);

META defineOrderInvoicePickingOrder (sign)

    picking (Batch batch, OrderDetail orderDetail) = PARTITION UNGROUP pickingQuantity
                                                               LIMIT STRICT currentBalance(batch, supplierStock(orderDetail)) IF sku(batch) == sku(orderDetail)
                                                               BY orderDetail
                                                               ORDER order(batch);

    createPickingDetail(UserInvoice userInvoice, Order.Order order) = ACTION {
        IF useExplicitBatchDetailUserInvoice(order) AND (explicitBatchLedger(fromStock(order)) OR forceExplicitBatchDetailUserInvoice(order)) THEN {
            FOR order(OrderDetail orderDetail) == order AND picking(Batch batch, orderDetail) AND NOT batch(orderDetail)
                ORDER orderCreateUserInvoice(orderDetail) 
                ADDOBJ d = UserInvoiceDetail DO {
                userInvoice(d) <- userInvoice;
                orderDetail(d) <- orderDetail;
                sku(d) <- sku(orderDetail);
                batch(d) <- batch;
                quantity (d) <- picking(batch, orderDetail);
                
                fill(d, orderDetail);
                overFill(d, orderDetail);
            }
            FOR order(OrderDetail orderDetail) == order AND PREV(pickingQuantity(orderDetail)) AND batch(orderDetail)
                ORDER orderCreateUserInvoice(orderDetail) 
                ADDOBJ d = UserInvoiceDetail DO {
                userInvoice(d) <- userInvoice;
                orderDetail(d) <- orderDetail;
                sku(d) <- sku(orderDetail);
                batch(d) <- batch(orderDetail);
                quantity (d) <- PREV(pickingQuantity(orderDetail));
    
                fill(d, orderDetail);
                overFill(d, orderDetail);
            }
        } ELSE {
            FOR order(OrderDetail orderDetail) == order AND
                pickingQuantity(orderDetail) > 0 // возможно еще нужно фильтровать по складам
                AND in(orderDetail, userInvoice)
                ORDER orderCreateUserInvoice(orderDetail) 
                ADDOBJ d = UserInvoiceDetail DO {
                    userInvoice(d) <- userInvoice;
                    orderDetail(d) <- orderDetail;
                    sku(d) <- sku(orderDetail);
                    batch(d) <- batch(orderDetail);
                    quantity (d) <- pickingQuantity(orderDetail);

                    fill(d, orderDetail);
                    overFill(d, orderDetail);                    
            }
        }

        FOR excessQuantityPickingPickingDetail(order, Sku sku) > 0 ADDOBJ d = UserInvoiceDetail DO {
            userInvoice(d) <- userInvoice;
            sku(d) <- sku;
            quantity (d) <- excessQuantityPickingPickingDetail(order, sku);
        }
    }

    fillPicking(UserInvoice userInvoice, Order.Order order) = ACTION {
        fillHeader(userInvoice, order);
        createPickingDetail(userInvoice, order);
        fillPrice(userInvoice, order);
    }

    packQuantity 'Общее количество грузовых мест' = ABSTRACT NUMERIC[8,2] (Invoice);
    packQuantity 'Общее количество грузовых мест' = DATA NUMERIC[8,2] (UserInvoice);
    packQuantity(UserInvoice invoice) += packQuantity(invoice);

    createPickingUserInvoice 'Создать накладную по комплектации'(Order order) = ACTION {
        IF order IS Order THEN {
            userInvoice(order) <- NULL;
            FOR ADDOBJ i = UserInvoice DO {
                fillPicking(i, order);
                userInvoice(order) <- i;
                packQuantity(i) <- packQuantity(order);
                createdUser(i, order) <- TRUE;
                include(order,i) <- TRUE;
            }
        }
    }

    createPickingUserInvoicePosted 'Создать проведенную накладную по комплектации'(Order order) = ACTION {
        IF order IS Order THEN {
            userInvoice(order) <- NULL;
            IF countInCreatePickingUserInvoicePostedOrder() THEN {
                IF countParamsInCreatePickingUserInvoicePostedOrder() > 1 THEN {
                    MESSAGE 'По выбранным заказам не может быть создана одна накладная';
                } ELSE {
                    FOR ADDOBJ i = UserInvoice DO {
                        isPosted(i) <- TRUE;
                        FOR inCreatePickingUserInvoicePosted(Order.Order o) DO {
                            fillPicking(i, o);                        
                            createdUser(i, o) <- TRUE;
                            include[Order,UserInvoice](o,i) <- TRUE;
                        }
                        packQuantity(i) <- [=GROUP SUM packQuantity(Order.Order o) IF inCreatePickingUserInvoicePosted(o)]();
                    }
                }
            } ELSE {
                FOR ADDOBJ i = UserInvoice DO {
                    isPosted(i) <- TRUE;
                    fillPicking(i, order);
                    packQuantity(i) <- packQuantity(order);
                    createdUser(i, order) <- TRUE;
                    userInvoice(order) <- i;
                    include(order,i) <- TRUE;
                }
            }
        }
    }
    createPickingUserInvoicePosted(Order o) += ACTION createPickingUserInvoicePosted(o); 

    addPickingUserInvoice 'Накладная по комплектации'###sign(Order order) =  ACTION NEWSESSION{
        IF order IS Order THEN {
            createPickingUserInvoice(order);
            IF excessQuantityPickingPickingDetail(order) THEN {
                MESSAGE 'В накладной присутствуют товары, не указанные в заказе';
            }
            FORM userInvoice OBJECTS i = userInvoice(order) MANAGESESSION DOCKED NOCANCEL;
        }
    } TOOLBAR;

    EXTEND FORM orders
        PROPERTIES(o) SHOWIF calcPart(o) addPickingUserInvoice
    ;
    DESIGN orders {
        createdContainer{
            MOVE PROPERTY(addPickingUserInvoice(o));
        }
    }

END

toInvoicePicked 'Не выписано' (OrderDetail d) = pickingQuantity(d) (-) invoiced(d);
toInvoicePicked 'Не выписано' (o) = GROUP SUM 1 IF toInvoicePicked(OrderDetail d) > 0 BY order(d) PERSISTENT;
