MODULE PickingOrder;

REQUIRE System,
        Utils,
        Historizable,
        Numerator,
        Document,
        Barcode,
        Stock,
        StockMovement,
        WMS,
        StockSkuDocument;

PRIORITY Utils, Stock;

NAMESPACE Picking;

//----------------------------------------------- Заказ на комплектацию ---------------------------------------------------//

GROUP pickingOrders 'Информация о заказе' : base;

CLASS ABSTRACT PickingOrder 'Заказ на комплектацию': Document ;
CLASS ABSTRACT PickingOrderDetail 'Строка заказа на комплектацию': DocumentDetail;

CLASS UserPickingOrder 'Заказ на комплектация (польз.)': PickingOrder;
CLASS UserPickingOrderDetail 'Строка заказа на комплектации (польз.)': PickingOrderDetail;

@defineDocumentInterface(pickingOrder);
@deriveDocumentHeaderTimePrefix(UserPickingOrder, );

@defineDocumentInterfaceNumber(pickingOrder);
@defineNumeratedDefault(UserPickingOrder, 'Заказы на комплектацию', 'ЗК');

@defineDocumentInterfaceStock(pickingOrder, stock, 'Склад ');

@defineDocumentInterfacePosted(pickingOrder);
@defineDocumentInterfaceClosed(pickingOrder);

@defineDocumentInterfaceDescription(pickingOrder, 'Заказ на комплектацию');

@defineDocumentInterfaceDetailSku(pickingOrder, sku);
@defineDocumentInterfaceDetailQuantity(pickingOrder);

@defineDocumentInterfaceDetailBatch(pickingOrder, batch);

@defineDocumentInterfaceHeaderQuantity(pickingOrder);

@defineDocumentHeaderSkuQuantity(pickingOrder, sku);
@defineDocumentHeaderSkuQuantity(userPickingOrder, sku);

@defineAddDetailDialogSkuStock(userPickingOrder, sku, stock, dialogSku);
@defineAddDetailDialogBarcode(userPickingOrder, sku);

@defineDocumentDetailGrossWeight(pickingOrder, sku);

@defineDocumentHeaderGrossWeight(pickingOrder);

// История по правой кнопке
@defineMovementSku(pickingOrderDetail, stock); //-- показываем по нажатию правой клавиши движение товара
@defineMovementSku(userPickingOrderDetail, stock); //-- показываем по нажатию правой клавиши движение товара
@defineBalancesSku(pickingOrderDetail); //-- показываем по нажатию правой клавиши остатки товара
@defineBalancesSku(userPickingOrderDetail); //-- показываем по нажатию правой клавиши остатки товара

@defineBarcodeSku(pickingOrderDetail); //-- показываем по нажатию правой клавиши все штрихкоды для товара
@defineBarcodeSku(userPickingOrderDetail); //-- показываем по нажатию правой клавиши все штрихкоды для товара 

@defineBalancesBatch(pickingOrderDetail); //-- показываем по нажатию правой клавиши остатки партии
@defineBalancesBatch(userPickingOrderDetail); //-- показываем по нажатию правой клавиши остатки партии
@defineMovementBatch(pickingOrderDetail, stock); //-- показываем по нажатию правой клавиши движение по партии
@defineMovementBatch(userPickingOrderDetail, stock); //-- показываем по нажатию правой клавиши движение по партии

//
//    @defineDocumentDialogSupplierCustomerStock(userOrder, supplierFilter, customerFilter);
//    @defineDocumentDialogSupplierCustomerStockDetail(userOrderDetail, supplierFilter, customerFilter);
//    @defineDocumentDialogSupplierCustomerLegalEntity(userOrder, supplierFilter, customerFilter);

changeBatch(UserPickingOrderDetail detail) = ACTION {
    FORM dialogBatchStockOut OBJECTS st = stock(detail),
                                  t = dateTime(detail),
                                  sk = sku(detail) DIALOG SHOWDROP;

    IF formResult() == FormResult.ok THEN {
        ASSIGN batch(detail) <- chosenObject('bt');
    } ELSE IF formResult() == FormResult.drop THEN {
        ASSIGN batch(detail) <- NULL;
    }
};


// --------------------------- Формы Заказа ---------------------------------

edit 'Редактировать' = ACTION ABSTRACT LIST (PickingOrder) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;

editSku 'Редактировать SKU' (UserPickingOrderDetail d) = ACTION edit(sku(d));
FORM userPickingOrder 'Заказ на комплектацию'
    OBJECTS o = UserPickingOrder FIXED PANEL
    PROPERTIES (o) isPosted, nameStock,// ON CHANGE changeCustomerStock###customerFilter###userOrder(o),
                   nameNumerator, number, series, date, time,
                   note, countUserPickingOrderDetail, quantityUserPickingOrderDetail


    OBJECTS d = UserPickingOrderDetail
    PROPERTIES (d) index
    PROPERTIES (d) ON EDIT editSku(d) idBarcodeSku, nameSku, shortNameUOMSku,
                   nameBatch ON CHANGE changeBatch(d)
    PROPERTIES (d) quantity, ADDOBJ, DELETESESSION

    PROPERTIES(o) TODRAW d addDetailDialogSkuStockUserPickingOrderDetail,
                           addDetailInputBarcodeUserPickingOrderDetail, deleteUserPickingOrderDetail
    FILTERS userPickingOrder(d) == o

    EDIT UserPickingOrder OBJECT o
;

DESIGN userPickingOrder {

    main {
        preferredSize = (1024, 768);
        NEW specification.box BEFORE functions.box{
            fill = 1;
            MOVE d.box {
                caption = 'Спецификация';
            }
        }

        NEW header.box BEFORE specification.box {
            type = CONTAINERH;

            NEW headerRow1 {
                MOVE o.documentHeader {
                    type = CONTAINERV;
                    NEW row1 {
                        type = CONTAINERH;
                        MOVE PROPERTY(isPosted(o));
                        MOVE PROPERTY(nameStock(o));
                    }
                    NEW row2 {
                        type = CONTAINERH;
                        MOVE PROPERTY(nameNumerator(o));
                        MOVE PROPERTY(number(o));
                        MOVE PROPERTY(series(o));
                        MOVE PROPERTY(date(o));
                        MOVE PROPERTY(time(o));
                    }

                }
                MOVE o.documentPrm {type = CONTAINERH;}
            }
            MOVE o.documentSum {
                columns = 1;
            }
        }
    }
}

//    @extendFormDocumentSkuStock(userOrder, userOrder, o, legalEntityProp, stockProp);



addUserPickingOrder 'Добавить' = ACTION ADDFORM UserPickingOrder;
edit 'Редактировать' (userOrder) = ACTION EDITFORM UserPickingOrder;
edit(UserPickingOrder order) += ACTION edit(order);


overCopy = ACTION ABSTRACT LIST (UserPickingOrderDetail, PickingOrderDetail);
copy 'Копировать'(PickingOrder order) = ACTION NEWSESSION {
    FOR ADDOBJ o = UserPickingOrder DO {

        stock(o) <- stock(order);
        note(o) <- note(order);

        FOR pickingOrder(PickingOrderDetail orderDetail) == order DO {
            FOR ADDOBJ d=UserPickingOrderDetail DO {
                userPickingOrder(d) <- o;
                sku(d) <- sku(orderDetail);
                quantity(d) <- quantity(orderDetail);
                batch(d) <- batch(orderDetail);
                overCopy(d, orderDetail);
            }
        }

        FORM userPickingOrder OBJECTS o = o MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;


FORM pickingOrders 'Заказы на комплектацию'
    OBJECTS o = PickingOrder
    PROPERTIES (o) READONLYIF isReadonly() isClosed, isPosted, number, series, 
                   date, time, nameStock

    PROPERTIES (o) READONLY countPickingOrderDetail, quantityPickingOrderDetail

    PROPERTIES (o) READONLY FORCE PANEL createdNameUser, createdTime, createdHostnameComputer

    PROPERTIES ()  addUserPickingOrder TODRAW o
    PROPERTIES (o) edit, copy
    PROPERTIES     deleteo=DELETE(o) FORCE PANEL TOOLBAR  SHOWIF is[UserPickingOrder](o)

    OBJECTS d=PickingOrderDetail
    PROPERTIES (d) READONLY index, nameSku, shortNameUOMSku,
                   nameBatch, quantity
    FILTERS pickingOrder(d) == o
    DIALOG PickingOrder OBJECT o
;

DESIGN pickingOrders {
    PROPERTY (deleteo) {
        askConfirm = TRUE;
    }

    NEW documentContainer BEFORE functions.box {
        type = SPLITV;
        fill = 1;

        MOVE o.box;

        NEW documentDetail {
            type = TABBED;
            fill = 1;

            MOVE d.box {
                caption = 'Спецификация';
            }
            NEW documentHistory {
                caption = 'История';
                MOVE o.created;

            }
            NEW printTab {
                caption = 'Печатные формы';
                NEW printContainer {
                    caption = 'Печать';
                }
            }
            NEW actionContainer {
                caption = 'Действия';
                type = CONTAINERH;
                NEW createdContainer {
                    caption = 'Создание на основе';
                    MOVE PROPERTY(copy(o)) { caption = 'Заказ на комплектацию';}
                }
            }
        }
    }
}
@extendFormEditable(pickingOrders);
@defineFilterIsOpened (pickingOrder, pickingOrders, o);
@defineDocumentLogForm(pickingOrders, o);
//----------------------------------------------- Комплектация ---------------------------------------------------//

NAVIGATOR {
    WMSNavigator {
        NEW pickingNavigator 'Комплектация' {
            ADD pickingOrders;
        }
    }
}