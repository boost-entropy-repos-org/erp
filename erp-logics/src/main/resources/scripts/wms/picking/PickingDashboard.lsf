MODULE PickingDashboard;

REQUIRE Dashboard, OrderPickingOrder;

NAMESPACE Picking;

skip 'Пропустить печать' = ABSTRACT BOOLEAN (PickingOrder);
overPrint 'Печать' = ACTION ABSTRACT LIST (PickingOrder);

acceptPrint 'Принять' (PickingOrder pickingOrder) = ACTION {
    accept(pickingOrder);       
    overPrint(pickingOrder);
    IF NOT skip(pickingOrder) THEN {
        print(pickingOrder);
    }
} TOOLBAR EDITKEY 'SPACE';


isParent(SkuGroup sk, PickingOrderDetail detail) = isParent(sk, sku(detail));

FORM pickingDashboard 'Комплектация' AUTOREFRESH 60

    OBJECTS d = DATE PANEL
    PROPERTIES(d) date = VALUE

    OBJECTS po=PickingOrder GRID
    PROPERTIES (po) READONLY BACKGROUND backgroundStatusPicking(po) statusPicking, namePerformer,
                    startDateTime, numberOrder, date, nameTo, nameToStock, addressToStock,
                    countPickingOrderDetail, quantityPickingOrderDetail, grossWeightPickingOrderDetail, note

    PROPERTIES (po) acceptPrint SHOWIF notAccepted(po),
                    unaccept SHOWIF accepted(po), fully = fullyAssembled SHOWIF accepted(po),
                    partially = partiallyAssembled SHOWIF accepted(po)
                    
    FILTERS isPosted(po) 
    FILTERGROUP pickingFilters
        FILTER 'Не скомплектованные' NOT (calcPart(po) OR calcFull(po)) 'F4' DEFAULT

    TREE skuTree sk = SkuGroup PARENT parent
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    ORDER BY order(sk), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS pd=PickingOrderDetail GRID
    PROPERTIES (pd) READONLY nameSku, shortNameUOMSku,
                    quantity

    FILTERS pickingOrder(pd)==po,
            isParent(sk, pd)

    OBJECTS p = Picking GRID
    PROPERTIES (p) READONLY BACKGROUND backgroundStatus(p) status, namePerformer, number, finishDateTime,
                   nameTo, nameToStock, addressToStock, packQuantity, quantityPickingDetail, 
                   grossWeightPickingDetail, notePickingOrder

    PROPERTIES (p) edit
    PROPERTIES     NEWSESSION deleteo=DELETE(p) SHOWIF is[UserPicking](p)

    FILTERGROUP filters FILTER 'Текущие заказы' date(po)<=d 'F5' DEFAULT
                        FILTER 'Будущие заказы' date(po)>d 'F6'
                        
    FILTERGROUP filters1 FILTER 'Предыдущие комплектации' date(p)<d 'F7'
                         FILTER 'Комплектации на дату' date(p)==d 'F8' DEFAULT
                         FILTER 'Будущие комплектации' date(p)>d 'F9'

;

DESIGN pickingDashboard {
    main{

        NEW dash BEFORE functions.box{
            fill = 1;
            type = SPLITV;
            NEW header {
                fill = 1;                         
                MOVE po.box;

            }
            NEW pickings {
                fill = 1;
                type = TABBED;
                MOVE p.box{
                    PROPERTY(number(p)){
                        caption = 'Номер комплектации';
                    }
                };
                NEW detailContainer {
                    type = SPLITH;
                    caption = 'Детализация заказа';
                    MOVE skuTree.tree.box{
                        caption = 'Группы';
                    }
                    MOVE pd.box{
                        fill = 3;
                    }                   
                }
            }
        }          
        NEW secondContainer BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            MOVE PROPERTY(date);
        }
    }
}

NAVIGATOR {
    saleDashboardNavigator {
        ADD pickingDashboard;
    }
}


