MODULE PickingDashboard;

REQUIRE Dashboard, OrderPickingOrder;

NAMESPACE Picking;

skipPickingOrder 'Пропустить печать' = ABSTRACT BOOLEAN (PickingOrder);
overPrintPickingOrder 'Печать' = ABSTRACT ACTION LIST (PickingOrder);

acceptPrintPickingOrder 'Принять' (pickingOrder) = ACTION (pickingOrder) {
    acceptPickingOrder(pickingOrder);       
    overPrintPickingOrder(pickingOrder);
    IF NOT skipPickingOrder(pickingOrder) THEN {
        printPickingOrder(pickingOrder);
    }
} TOOLBAR EDITKEY 'SPACE';

FORM pickingDashboard 'Комплектация' AUTOREFRESH 60

    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) date = OBJVALUE

    OBJECTS po=PickingOrder FIXED GRID
    PROPERTIES (po) READONLY BACKGROUND backgroundStatusPickingPickingOrder(po) statusPickingPickingOrder, namePerformerPickingOrder,
                    startDateTimePickingOrder, numberOrderPickingOrder, datePickingOrder, nameToPickingOrder, nameToStockPickingOrder, addressToStockPickingOrder,
                    countPickingOrderDetailPickingOrder, quantityPickingOrderDetailPickingOrder, grossWeightPickingOrderDetailPickingOrder, notePickingOrder

    PROPERTIES (po) acceptPrintPickingOrder SHOWIF notAcceptedPickingOrder(po),
                    unacceptPickingOrder SHOWIF acceptedPickingOrder(po), fully = fullyAssembledPickingOrder SHOWIF acceptedPickingOrder(po),
                    partially = partiallyAssembledPickingOrder SHOWIF acceptedPickingOrder(po)
                    
    FILTERS isPostedPickingOrder(po) 
    FILTERGROUP pickingFilters
        FILTER 'Не скомплектованные' NOT (calcPartPickingOrder(po) OR calcFullPickingOrder(po)) 'F4' DEFAULT

    OBJECTS pd=PickingOrderDetail FIXED GRID
    PROPERTIES (pd) READONLY nameSkuPickingOrderDetail, shortNameUOMSkuPickingOrderDetail,
                    nameBinPickingOrderDetail, quantityPickingOrderDetail

    FILTERS pickingOrderPickingOrderDetail(pd)==po

    OBJECTS p = Picking FIXED GRID
    PROPERTIES (p) READONLY BACKGROUND backgroundStatusPicking(p) statusPicking, namePerformerPicking, numberPicking, finishDateTimePicking,
                   nameToPicking, nameToStockPicking, addressToStockPicking, packQuantityPicking, quantityPickingDetailPicking, 
                   grossWeightPickingDetailPicking, notePickingOrderPicking

    PROPERTIES (p) editPicking
    PROPERTIES     deleteo=DELETE(p) FORCE PANEL TOOLBAR  SHOWIF isUserPicking(p)

    FILTERGROUP filters FILTER 'Текущие заказы' datePickingOrder(po)<=d 'F5' DEFAULT
                        FILTER 'Будущие заказы' datePickingOrder(po)>d 'F6'
                        
    FILTERGROUP filters1 FILTER 'Предыдущие комплектации' datePicking(p)<d 'F7'
                         FILTER 'Комплектации на дату' datePicking(p)==d 'F8' DEFAULT
                         FILTER 'Будущие комплектации' datePicking(p)>d 'F9'

;

DESIGN pickingDashboard {
    main{

        NEW dash BEFORE functions.box{
            fill = 1;
            type = SPLITV;
            NEW header {
                fill = 1;                         
                ADD po.box;

            }
            NEW pickings {
                fill = 1;
                type = TABBED;
                ADD p.box{
                    PROPERTY(numberPicking(p)){
                        caption = 'Номер комплектации';
                    }
                };
                ADD pd.box {
                    caption = 'Детализация заказа';
                }
            }
        }          
        NEW secondContainer BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            ADD PROPERTY(date);
        }
    }
}

NAVIGATOR {
    saleDashboardNavigator {
        ADD pickingDashboard;
    }
}


