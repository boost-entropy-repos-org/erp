MODULE BinOrderDemand;

REQUIRE BinOrderTransfer, PurchaseOrder;

NAMESPACE Bin;

binReserve 'Страховой запас (кол-во)' = ABSTRACT NUMERIC[16,5](Sku, Bin);

binReserve 'Страховой запас (кол-во)' (Sku sku, UserBinOrder order) = binReserve(sku, inBin(order));
 
calcRecQuantity  (Sku sku, Bin bin) = 
    IF availableQuantity(sku, bin) > 0.0
        THEN binReserve(sku, bin) (-) availableQuantity(sku, bin)
        ELSE binReserve(sku, bin);

recommendedQuantity 'Рекомендуемое к перемещению кол-во' (Sku sku, UserBinOrder order) = 
   IF availableQuantity(sku, outBin(order))> 0.0 AND calcRecQuantity(sku, inBin(order)) > 0.0 THEN
   MIN calcRecQuantity(sku, inBin(order)), availableQuantity(sku, outBin(order));

ceilCalcRecQuantity (Sku sku, Bin bin) =  IF Purchase.amountPack(sku) > 0 THEN ceil(calcRecQuantity(sku, bin), amountPack(sku)) IF amountPack(sku)!=0
    ELSE (IF split(sku) THEN calcRecQuantity(sku, bin) ELSE ceil(calcRecQuantity(sku, bin)));

ceilAvailableQuantity(Sku sku, Bin bin) = IF amountPack(sku) > 0 THEN floor(availableQuantity(sku, bin)/(amountPack(sku) IF amountPack(sku)!=0))*amountPack(sku)
    ELSE (IF split(sku) THEN availableQuantity(sku, bin) ELSE floor(availableQuantity(sku, bin)));
    
recommendedPackQuantity 'Рекомендуемое к перемещению кол-во' (Sku sku, UserBinOrder order) = 
    IF ceilAvailableQuantity(sku, outBin(order))> 0.0 AND ceilCalcRecQuantity(sku, inBin(order))> 0.0 THEN
    MIN ceilCalcRecQuantity(sku, inBin(order)), ceilAvailableQuantity(sku, outBin(order));

fillRecommendedQuantity 'Заполнить рекомендуемым' (UserBinOrder userOrder) = {
    FOR Sku sku IS Sku AND recommendedQuantity (sku, userOrder) > 0.0 NEW d = UserBinOrderDetail DO {
            userBinOrder(d) <- userOrder;
            sku(d) <- sku;
            IF recommendedPackQuantity(sku, userOrder) AND NOT recommendedPackQuantity(sku, userOrder) > binReserve(sku, inBin(userOrder)) THEN {
                packQuantity (d) <- recommendedPackQuantity(sku, userOrder)/amountPack(sku(d)) IF amountPack(sku(d)) > 0;
                quantity (d) <- recommendedPackQuantity(sku, userOrder);      
            } ELSE {
                quantity (d) <- recommendedQuantity(sku, userOrder);     
            }
        }
} TOOLBAR CONFIRM;

EXTEND FORM userBinOrder
    PROPERTIES (ks, o) READONLY BEFORE price(ks, st, o) recommendedQuantity ON SHORTCUT fillRecommendedQuantity(o), binReserve   
;