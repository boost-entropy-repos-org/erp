MODULE PurchaseDeclarationBin;

REQUIRE PurchaseDeclarationDetail, PurchaseShipmentBin;

NAMESPACE Purchase;

TABLE userInvoiceBin (UserInvoice, Bin);
TABLE declarationUserInvoiceBin (Declaration, UserInvoice, Bin);

countNotDeclarationUserInvoiceBin  (userInvoice, bin)= GROUP SUM 1 IF detail IS UserInvoiceDetail AND NOT declarationUserInvoiceDetail(detail)
    BY userInvoiceUserInvoiceDetail(detail), binUserInvoiceDetail(detail);

countDeclarationUserInvoiceBin  (declaration, userInvoice, bin)= GROUP SUM 1 IF detail IS UserInvoiceDetail
    BY declarationUserInvoiceDetail(detail), userInvoiceUserInvoiceDetail(detail), binUserInvoiceDetail(detail);

TABLE declarationBin(Declaration, Bin);
inDeclarationBin 'Вкл.' = DATA BOOLEAN (Declaration, Bin);

changeInDeclarationBin= ACTION (declaration, bin) {
    REQUEST BOOLEAN INPUT;
    IF requestedLogical() THEN {
        inDeclarationBin(declaration,bin) <- TRUE;
        FOR binUserInvoiceDetail(detail) == bin AND NOT dataDeclarationUserInvoiceDetail(detail) DO {
            dataDeclarationUserInvoiceDetail(detail) <- declaration;
        }
    } ELSE {
        inDeclarationBin(declaration,bin) <- NULL;
        FOR binUserInvoiceDetail(detail) == bin AND dataDeclarationUserInvoiceDetail(detail)== declaration DO {
            dataDeclarationUserInvoiceDetail(detail) <- NULL;
        };
    }
}

EXTEND FORM declaration
    PROPERTIES(id) READONLY nameBinUserInvoiceDetail BEFORE quantityUserInvoiceDetail(id), indexUserInvoiceDetail
;
EXTEND DESIGN declaration {
    PROPERTY(indexUserInvoiceDetail) {caption = 'Номер строки накладной';}
}

EXTEND FORM declarationInvoices

    PROPERTIES(id) READONLY nameBinUserInvoiceDetail BEFORE quantityUserInvoiceDetail

    OBJECTS b=Bin
    PROPERTIES(b) READONLY nameBin
    PROPERTIES(d,b) inDeclarationBin ON CHANGE changeInDeclarationBin(d,b)
    FILTERS countDeclarationUserInvoiceBin(d,i,b) OR d IS Declaration AND countNotDeclarationUserInvoiceBin(i,b)

    OBJECTS idd=UserInvoiceDetail
    PROPERTIES(d, idd) inDeclarationUserInvoiceDetail
    PROPERTIES(idd) READONLY indexUserInvoiceDetail, idBarcodeSkuUserInvoiceDetail, nameSkuUserInvoiceDetail, shortNameUOMSkuUserInvoiceDetail,
                   quantityUserInvoiceDetail, priceUserInvoiceDetail
    FILTERS invoiceInvoiceDetail(idd)==i,
            binUserInvoiceDetail(idd)==b,
            declarationUserInvoiceDetail(idd)==d OR d IS Declaration AND NOT declarationUserInvoiceDetail(idd)
;
EXTEND DESIGN declarationInvoices {
    pane {
        NEW binContainer {
            caption = 'Ячейки';
            ADD b.box;
            ADD idd.box;
        }
    }
}