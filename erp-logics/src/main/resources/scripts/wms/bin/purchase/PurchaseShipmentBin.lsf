MODULE PurchaseShipmentBin;

REQUIRE PurchaseBin, PurchaseShipment, Item;

NAMESPACE Purchase;

dontSetBinPurchase 'Не заполнять ячейки на приходе' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES dontSetBinPurchase()
;

DESIGN options {
    purchase {
        ADD PROPERTY(dontSetBinPurchase());
    }
}

@defineDocumentInterfaceDetailBin(shipment);
overCopyUserShipmentDetail(d, detail) += ACTION (d, detail) {      
    binUserShipmentDetail(d) <- binUserShipmentDetail(detail);
}
@extendFormInterfaceDetailBin(shipment, s, , quantity);
binUserShipmentDetail(detail) <- binStockSku(customerStockUserShipmentDetail(detail), skuUserShipmentDetail(detail)) IF NOT dontSetBinPurchase() 
    WHEN (CHANGED(customerStockUserShipmentDetail(detail)) OR
         CHANGED(skuUserShipmentDetail(detail))) AND NOT allowReplaceItem();;

@deriveDocumentOperationProperty(UserShipment, showBin);

//-- invoice
@defineInvoiceShipmentBin(customerStock);

binUserInvoiceDetail(detail) <- binStockSku(customerStockUserInvoiceDetail(detail), skuUserInvoiceDetail(detail)) IF NOT dontSetBinPurchase()
    WHEN (CHANGED(customerStockUserInvoiceDetail(detail)) OR
          CHANGED(skuUserInvoiceDetail(detail))) AND NOT allowReplaceItem();

@deriveDocumentOperationProperty(UserInvoice, showBin);

//-- Проводим по регистру (ячейка)
binBatch (ledger) += binShipmentDetail(shipmentDetailShipmentBatch(ledger));

//---------------------------------Новый функционал ----------------------------------//

@defineDocumentDetailBinCustom(userShipment, current, ' текущая');
currentBinUserShipmentDetail (detail) = currentBinUserShipment(userShipmentUserShipmentDetail(detail));

EXTEND FORM userShipment
    PROPERTIES(s) SHOWIF showBinUserShipment(s) nameCurrentBinUserShipment
;

DESIGN userShipment {
    headerBin {
        ADD PROPERTY(nameCurrentBinUserShipment(s)) AFTER PROPERTY(showBinUserShipment(s));
    }
}

WHEN SESSION FORMS userShipment
    SET(detail IS UserShipmentDetail) AND currentBinUserShipmentDetail(detail)
        DO ASSIGN binUserShipmentDetail(detail) <- currentBinUserShipmentDetail(detail) WHERE currentBinUserShipmentDetail(detail);

WHEN SESSION FORMS userShipment
    CHANGED(invoiceDetailUserShipmentDetail(detail)) AND NOT binUserShipmentDetail(detail)
        DO ASSIGN binUserShipmentDetail(detail) <- binInvoiceDetail(invoiceDetailUserShipmentDetail(detail));


