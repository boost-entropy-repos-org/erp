MODULE Collection;

REQUIRE CashOperationZReport;

NAMESPACE CashOperation;

CLASS Collection 'Инкассация' : OutcomeCashOrder;
TABLE collection (Collection);

is (Collection collection) = collection IS Collection;

@defineDocumentHeaderCreated(Collection);
@defineDocumentHeaderNumber(Collection);

@defineNumeratedDefault(Collection, 'Инкассации', 'ИН');

@defineDocumentHeaderTime(Collection);
@deriveDocumentHeaderTimePrefix(Collection, );
@defineDocumentHeaderDepartmentStore(collection);
@defineDocumentDialogStocks(collection, departmentStore, company, , );

@defineDocumentHeaderDescription (Collection, 'Инкассация');

bankAccount = DATA Bank.Account (Collection);
bankAccount(Collection collection) <- bankAccount(store(departmentStore(collection)))
    WHEN SET(departmentStore(collection));

sumCash 'Сумма инкассации' = DATA NUMERIC[18,4] (Collection) IN documentSum;
basis 'Основание' (Collection collection) =
    'Инкассация №' + seriesNumber(collection) + ' от ' + date(collection) IN documentPrm;

date(Collection collection) += date(collection);
time(Collection collection) += time(collection);
number(Collection collection) += seriesNumber(collection) IF collection IS Collection;
departmentStore(Collection collection) += departmentStore(collection);
sumCash(Collection collection) += sumCash(collection);
basis(Collection collection) += basis(collection);
isPosted(Collection collection) += collection IS Collection;

sumCashForCollection 'Сумма инкассации' = DATA LOCAL NUMERIC[18,4] (DepartmentStore);

FORM addCollection 'Инкассация'

    OBJECTS dt = DATE PANEL
    PROPERTIES(dt) date = OBJVALUE

    OBJECTS t = TIME PANEL
    PROPERTIES(t) time = OBJVALUE

    OBJECTS d = DepartmentStore PANEL
    PROPERTIES(d) name SELECTOR, sumCashForCollection
;

DESIGN addCollection {
    NEW topContainer {
        type = SPLITV;
        NEW firstCase {
            type = CONTAINERH;
            NEW dateTimeContainer{
                caption = 'Дата/время';
                MOVE PROPERTY(date);
                MOVE PROPERTY(time);
            }
            MOVE d.box;
        }
    }
    MOVE functions.box;
}

FORM collection 'Инкассация'
    OBJECTS c = Collection PANEL
    PROPERTIES(c) number, series, date, time, nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(c),
                  sumCash, basis

    EDIT Collection OBJECT c
;

DESIGN collection {
    NEW topContainer{
        type = CONTAINERH;
        NEW firstCase{
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(number(c));
            MOVE PROPERTY(series(c));
            MOVE PROPERTY(date(c));
            MOVE PROPERTY(time(c));
        }
        NEW secondCase{
            caption = 'Параметры документа';
            MOVE PROPERTY(nameDepartmentStore(c));
            MOVE PROPERTY(basis(c));
        }
        NEW thirdCase{
            caption = 'Суммы';
            MOVE PROPERTY(sumCash(c));
        }
    }
    MOVE functions.box;
}

addCollectionForm 'Инкассация' () = ACTION {
    DIALOG addCollection OBJECTS dt INPUT, t INPUT DO {
        FOR sumCashForCollection(DepartmentStore departmentStore) DO {
            FOR NEW c = Collection DO {
                departmentStore(c) <- departmentStore;
                date(c) <- dt;
                time(c) <- t;
                sumCash(c) <- sumCashForCollection(departmentStore);
                generateSeriesNumber(c);
            }
        }
        apply();
    }
} TOOLBAR;

edit 'Редактировать' (collection) = ACTION EDITFORM Collection;

FORM centralCashRegister 'Центральная касса'

    OBJECTS interval = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)

    OBJECTS d = DepartmentStore PANEL
    PROPERTIES(d) name SELECTOR
    PROPERTIES(d) READONLY curSumCash

    OBJECTS i = IncomeCashOrder
    PROPERTIES(i) READONLY objectClassName, number, date, time, nameDepartmentStore,
                           numberCashRegister, sumCash, basis, isPrinted
    PROPERTIES(i) READONLY isComplete 
    PROPERTIES addOutcomeCashOperationForm() TODRAW i TOOLBAR
    PROPERTIES eoc = edit(i) SHOWIF is(i)
    PROPERTIES(i) NEWSESSION DELETE 
    ORDER BY date(i), time(i)

    FILTERS departmentStore(i) == d,
            date(i) >= dFrom,
            date(i) <= dTo

    OBJECTS o = OutcomeCashOrder
    PROPERTIES(o) READONLY number, date, time, nameDepartmentStore,
                           numberCashRegister, sumCash, basis, isPrinted
    PROPERTIES addIncomeCashOperationForm() TODRAW o TOOLBAR
    PROPERTIES addCollectionForm() TODRAW o TOOLBAR
    PROPERTIES eic = edit[IncomeCashOperation](o) SHOWIF is[IncomeCashOperation](o)
    PROPERTIES edit[Collection](o) SHOWIF is[Collection](o)
    PROPERTIES(o) NEWSESSION DELETE 
    ORDER BY date(o), time(o)
    FILTERS departmentStore(o) == d,
            date(o) >= dFrom,
            date(o) <= dTo,
            isPosted(i),
            isPosted(o)
;
@extendFormFilterStockAccess(d, centralCashRegister);

DESIGN centralCashRegister {

    NEW top {
        type = CONTAINERH;
        MOVE interval.box {
            caption = 'Период';
            type = CONTAINERH;
            PROPERTY(objFrom) {
                caption = 'Дата (с)';
            }
            PROPERTY(objTo) {
                caption = 'Дата (по)';
            }
        }
        MOVE d.box;
    }

    NEW center {
        fill = 1;
        type = SPLITV;
        MOVE i.box{
            caption = 'ПКО';
        }
        MOVE o.box{
            caption = 'РКО';
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    retailDashboardNavigator {
        ADD centralCashRegister;
    }
}