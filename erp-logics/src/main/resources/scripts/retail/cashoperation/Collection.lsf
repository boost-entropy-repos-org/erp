MODULE Collection;

REQUIRE CashOperationZReport;

NAMESPACE CashOperation;

CLASS Collection 'Инкассация' : OutcomeCashOrder;
TABLE collection (Collection);

isCollection (collection) = collection IS Collection;

@defineDocumentHeaderCreated(Collection);
@defineDocumentHeaderNumber(Collection);

@defineNumeratedDefault(Collection, 'Инкассации', 'ИН');

@defineDocumentHeaderTime(Collection);
@deriveDocumentHeaderTimePrefix(Collection, );
@defineDocumentHeaderDepartmentStore(collection);
@defineDocumentDialogStocks(collection, departmentStore, company, , );

@defineDocumentHeaderDescription (Collection, 'Инкассация');

bankAccountCollection = DATA Bank.Account (Collection);
bankAccountCollection(collection) <- bankAccountStore(storeDepartmentStore(departmentStoreCollection(collection)))
    WHEN SET(departmentStoreCollection(collection));

sumCashCollection 'Сумма инкассации' = DATA NUMERIC[16,2] (Collection) IN documentSum;
basisCollection 'Основание' (collection) =
    'Инкассация №' + seriesNumberCollection(collection) + ' от ' + dateCollection(collection) IN documentPrm;

dateCashDocument(collection) += dateCollection(collection);
timeCashDocument(collection) += timeCollection(collection);
numberCashDocument(collection) += seriesNumberCollection(collection) IF collection IS Collection;
departmentStoreCashDocument(collection) += departmentStoreCollection(collection);
sumCashOutcomeCashOrder(collection) += sumCashCollection(collection);
basisCashDocument(collection) += basisCollection(collection);
isPostedCashDocument(collection) += collection IS Collection;

sumCashForCollection 'Сумма инкассации' = DATA LOCAL NUMERIC[16,2] (DepartmentStore);

FORM addCollection 'Инкассация'

    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES(dt) date = OBJVALUE

    OBJECTS t = TIME FIXED PANEL
    PROPERTIES(t) time = OBJVALUE

    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES(d) nameDepartmentStore SELECTOR, sumCashForCollection
;

DESIGN addCollection {
    NEW topContainer {
        type = SPLITV;
        NEW firstCase {
            type = CONTAINERH;
            NEW dateTimeContainer{
                caption = 'Дата/время';
                MOVE PROPERTY(date);
                MOVE PROPERTY(time);
            }
            MOVE d.box;
        }
    }
    MOVE functions.box;
}

FORM collection 'Инкассация'
    OBJECTS c = Collection FIXED PANEL
    PROPERTIES(c) numberCollection, seriesCollection, dateCollection, timeCollection, nameDepartmentStoreCollection ON CHANGE changeDepartmentStoreCompanyCollection(c),
                  sumCashCollection, basisCollection

    EDIT Collection OBJECT c
;

DESIGN collection {
    NEW topContainer{
        type = CONTAINERH;
        NEW firstCase{
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(numberCollection(c));
            MOVE PROPERTY(seriesCollection(c));
            MOVE PROPERTY(dateCollection(c));
            MOVE PROPERTY(timeCollection(c));
        }
        NEW secondCase{
            caption = 'Параметры документа';
            MOVE PROPERTY(nameDepartmentStoreCollection(c));
            MOVE PROPERTY(basisCollection(c));
        }
        NEW thirdCase{
            caption = 'Суммы';
            MOVE PROPERTY(sumCashCollection(c));
        }
    }
    MOVE functions.box;
}

addCollectionForm 'Инкассация' () = ACTION () {
    FORM addCollection MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR sumCashForCollection(departmentStore) DO {
            FOR ADDOBJ c = Collection DO {
                departmentStoreCollection(c) <- departmentStore;
                dateCollection(c) <- chosenDate('dt');
                timeCollection(c) <- chosenTime('t');
                sumCashCollection(c) <- sumCashForCollection(departmentStore);
                generateSeriesNumberCollection(c);
            }
        }
        EXEC apply();
    }
} TOOLBAR;

editCollection 'Редактировать' (collection) = ACTION EDITFORM Collection;

FORM centralCashRegister 'Центральная касса'

    OBJECTS interval = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)

    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES(d) nameDepartmentStore SELECTOR
    PROPERTIES(d) READONLY curSumCashDepartmentStore

    OBJECTS i = IncomeCashOrder
    PROPERTIES(i) READONLY objectClassName, numberCashDocument, dateCashDocument, timeCashDocument, nameDepartmentStoreCashDocument,
                           numberCashRegisterCashDocument, sumCashIncomeCashOrder, basisCashDocument, isPrintedIncomeCashOrder
    PROPERTIES(i) READONLY isCompleteCashOperation FORCE GRID
    PROPERTIES addOutcomeCashOperationForm() TODRAW i FORCE PANEL TOOLBAR
    PROPERTIES eoc = editOutcomeCashOperation(i) SHOWIF isOutcomeCashOperation(i)
    PROPERTIES(i) DELETE FORCE PANEL TOOLBAR
    ORDER BY dateCashDocument(i), timeCashDocument(i)

    FILTERS departmentStoreCashDocument(i) == d,
            dateCashDocument(i) >= dFrom,
            dateCashDocument(i) <= dTo

    OBJECTS o = OutcomeCashOrder
    PROPERTIES(o) READONLY numberCashDocument, dateCashDocument, timeCashDocument, nameDepartmentStoreCashDocument,
                           numberCashRegisterCashDocument, sumCashOutcomeCashOrder, basisCashDocument, isPrintedOutcomeCashOrder
    PROPERTIES addIncomeCashOperationForm() TODRAW o FORCE PANEL TOOLBAR
    PROPERTIES addCollectionForm() TODRAW o FORCE PANEL TOOLBAR
    PROPERTIES eic = editIncomeCashOperation(o) SHOWIF isIncomeCashOperation(o)
    PROPERTIES editCollection(o) SHOWIF isCollection(o)
    PROPERTIES(o) DELETE FORCE PANEL TOOLBAR
    ORDER BY dateCashDocument(o), timeCashDocument(o)
    FILTERS departmentStoreCashDocument(o) == d,
            dateCashDocument(o) >= dFrom,
            dateCashDocument(o) <= dTo,
            isPostedCashDocument(i),
            isPostedCashDocument(o)
;
@extendFormFilterStockAccess(d, centralCashRegister);

DESIGN centralCashRegister {

    NEW top {
        type = CONTAINERH;
        MOVE interval.box {
            caption = 'Период';
            type = CONTAINERH;
            PROPERTY(objFrom) {
                caption = 'Дата (с)';
            }
            PROPERTY(objTo) {
                caption = 'Дата (по)';
            }
        }
        MOVE d.box;
    }

    NEW center {
        fill = 1;
        type = SPLITV;
        MOVE i.box{
            caption = 'ПКО';
        }
        MOVE o.box{
            caption = 'РКО';
        }
    }
    MOVE functions.box;
}

NAVIGATOR {
    retailDashboardNavigator {
        ADD centralCashRegister;
    }
}