MODULE Range;

REQUIRE Store, Sku, Barcode, PriceListStore, EmployeeSku, PriceListDashboardStore, SaleLedger;

// Уровни ассортимента

CLASS RangeLevel 'Уровень ассортимента';
TABLE rangeLevel(RangeLevel);
nameRangeLevel 'Название' = DATA VARISTRING[10] (RangeLevel) IN recognize;

TABLE rangeRangeLevel(Range, RangeLevel);
inRangeRangeLevel 'Вкл' = DATA BOOLEAN (Range, RangeLevel);

FORM rangeLevel 'Уровень ассортимента'
    OBJECTS l = RangeLevel FIXED PANEL
    PROPERTIES(l) nameRangeLevel

    EDIT RangeLevel OBJECT l
;

FORM rangeLevels 'Уровни ассортимента'
    OBJECTS l = RangeLevel
    PROPERTIES(l) READONLY nameRangeLevel
    PROPERTIES(l) ADDFORM, EDITFORM, DELETE TOOLBAR FORCE PANEL
    
    DIALOG RangeLevel OBJECT l
;

NAVIGATOR {
    retailNavigator {
        NEW retailRangeNavigator 'Управление ассортиментом' {
            ADD rangeLevels;
        }
    }
}

// Ассортименты
CLASS Range 'Ассортимент';
TABLE range(Range);
nameRange 'Название' = DATA VARISTRING[100] (Range) IN recognize;

TABLE rangeSkuGroup(Range, SkuGroup);
inDataRangeSkuGroup 'Вкл' = DATA BOOLEAN (Range, SkuGroup);

countSkuGroupRange 'Кол-во групп' = GROUP SUM 1 IF inDataRangeSkuGroup(r, g) BY r PERSISTENT; 

inParentRangeSkuGroup = GROUP SUM 1 IF inDataRangeSkuGroup(range, child) AND isParentSkuGroupSkuGroup(child, parent)
                              BY range, parent PERSISTENT;

inChildRangeSkuGroup = GROUP SUM 1 IF inDataRangeSkuGroup(range, parent) AND isParentSkuGroupSkuGroup(child, parent)
                              BY range, child PERSISTENT;

FORM range 'Ассортиментная матрица'
    OBJECTS r = Range FIXED PANEL
    PROPERTIES(r) nameRange

    OBJECTS l = RangeLevel
    PROPERTIES(l) READONLY nameRangeLevel
    PROPERTIES(r, l) inRangeRangeLevel

    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuGroupName = nameSkuGroup(sg)
    PROPERTIES inDataRangeSkuGroup(r, sg)
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(sg) DEFAULT

    EDIT Range OBJECT r
;

DESIGN range FROM DEFAULT {
    NEW pane {
        fill = 1;
        ADD r.panel;
        NEW tabs {
            fill = 1;
            type = TABBED;
            NEW options {
                caption = 'Настройки';
                fill = 1;
                type = SPLITH;
                ADD l.box;
                ADD skuTree.tree.box {
                    fill = 4;
                }
            }
        }
    }
    ADD functions.box;
}

@extendFormFilterSkuGroupAccess(range, sg);

FORM ranges 'Ассортиментные матрицы'
    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) OBJVALUE

    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuGroupName = nameSkuGroup(sg)
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(sg) DEFAULT

    OBJECTS r = Range
    PROPERTIES(r) READONLY nameRange
    PROPERTIES(r) ADDFORM, EDITFORM, DELETE TOOLBAR FORCE PANEL
    FILTERS inParentRangeSkuGroup(r, sg) OR NOT countSkuGroupRange(r)
    
    DIALOG Range OBJECT r
;

DESIGN ranges FROM DEFAULT {
    ADD d.box;
    NEW pane {
        fill = 1;
        type = SPLITH;
        ADD skuTree.tree.box;
        ADD r.box {
            fill = 2;        
        }
    }
    ADD functions.box;
}

countSkuGroupEmployeeRange 'Кол-во групп' = GROUP SUM 1 IF inDataRangeSkuGroup(r, g) AND countAccessEmployeeSkuGroup(e, g) BY e, r; 
EXTEND FORM ranges FILTERS (r IS Range AND NOT limitAccessEmployee(currentUser())) OR countSkuGroupEmployeeRange(currentUser(), r);

NAVIGATOR {
    retailRangeNavigator {
        ADD ranges;
    }
}

// Версии

CLASS RangeRev 'Версия';
TABLE rangeRev(RangeRev);
dateTimeRangeRev 'Действует (с)' = DATA DATETIME (RangeRev) IN recognize;
dateTimeRangeRev(v) <- currentDateTime() WHEN SET(v IS RangeRev);

dateRangeRev 'Действует (с)' (v) = toDate(dateTimeRangeRev(v)) PERSISTENT;

rangeRangeRev = DATA Range(RangeRev) NOT NULL AUTOSET;  

rangeRevRangeDate 'Версия' = GROUP LAST rev
                                       BY rangeRangeRev(rev), date
                                       ORDER dateTimeRangeRev(rev), rev
                                       WHERE dateRangeRev(rev) <= (date AS DATE) COMPLEX;
                           

TABLE rangeRevSku(RangeRev, Sku);
levelRangeRevSku = DATA RangeLevel(RangeRev, Sku);
nameLevelRangeRevSku 'Уровень' (r, s)= nameRangeLevel(levelRangeRevSku(r, s));

levelRangeSkuDate(r, s, d) = levelRangeRevSku(rangeRevRangeDate(r, d), s);

CONSTRAINT levelRangeRevSku(r, s) AND NOT inRangeRangeLevel(rangeRangeRev(r), levelRangeRevSku(r, s))
    CHECKED BY levelRangeRevSku 
    MESSAGE 'Уровень ассортимента для товара не соответствует типу ассортимента';

countSkuRangeRev 'Кол-во товаров' = GROUP SUM 1 IF levelRangeRevSku(r, s) BY r; 
countSkuRangeRevRangeLevel 'Кол-во товаров' = GROUP SUM 1 BY r, levelRangeRevSku(r, s);

countRecSkuRangeRevRangeLevel 'Кол-во товаров' = GROUP SUM countSkuRangeRevRangeLevel(r, l) IF l <= (level AS RangeLevel) BY r, level;

countSkuRangeDate 'Кол-во товаров' (r, d) = countSkuRangeRev(rangeRevRangeDate(r, d));
countSkuRangeRangeLevelDate 'Кол-во товаров' (r, l, d)= countSkuRangeRevRangeLevel(rangeRevRangeDate(r, d), l);

filterSupplier = DATA SESSION LegalEntity();
nameFilterSupplier 'Поставщик' = nameLegalEntity(filterSupplier());

retailPriceASkuDepartmentStoreRangeRev 'Розничная цена (управленческая)' (s, d, v) =  
    retailPriceASkuDepartmentStoreDateTime(s, d, dateTimeRangeRev(v));

markupPriceASkuDepartmentStoreRangeRev 'Розничная цена (надбавка)' (s, d, v) =  
    markupPriceASkuDepartmentStoreDateTime(s, d, dateTimeRangeRev(v));

priceALedgerPriceListTypeSkuStockRangeRev 'Цена' (pt, s, st, v)=  
    priceALedgerPriceListTypeSkuStockDateTime(pt, s, st, dateTimeRangeRev(v));

nameCompanyALedgerPriceListTypeSkuStockRangeRev 'Поставщик' (pt, s, st, v)=  
    nameCompanyALedgerPriceListTypeSkuStockDateTime(pt, s, st, dateTimeRangeRev(v));

FORM rangeRev 'Версия ассортимента'
    OBJECTS v = RangeRev FIXED PANEL
    PROPERTIES(v) dateTimeRangeRev

    PROPERTIES() nameFilterSupplier
    
    OBJECTS pt = DataPriceListType FIXED PANEL
    PROPERTIES(pt) SELECTOR namePriceListType

    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES(d) SELECTOR nameDepartmentStore
    
    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, nameUOMSku
    PROPERTIES READONLY    nameCompanyALedgerPriceListTypeSkuStockRangeRev(pt, s, d, v),
                           priceALedgerPriceListTypeSkuStockRangeRev(pt, s, d, v),
                           retailPriceASkuDepartmentStoreRangeRev(s, d, v),
                           markupSkuDepartmentStore(s, d),
                           markupPriceASkuDepartmentStoreRangeRev(s, d, v)
    PROPERTIES(v, s) nameLevelRangeRevSku
    FILTERS inChildRangeSkuGroup(rangeRangeRev(v), skuGroupSku(s)),
            (filterSupplier() AND priceALedgerPriceListTypeSkuStockCompanyDateTime(pt, s, d, filterSupplier(), dateTimeRangeRev(v))) OR 
                (s IS Sku AND NOT filterSupplier())
    
    FILTERGROUP active 
        FILTER 'Активный' 'F11' activeSku(s) DEFAULT
    
    ORDER BY nameSku(s)
    
    FILTERGROUP filters
        FILTER 'Включен' 'F10' levelRangeRevSku(v, s) DEFAULT
         
    EDIT RangeRev OBJECT v
;
DESIGN rangeRev FROM DEFAULT {
    NEW top {
        type = CONTAINERH;
        ADD s.panel;
        NEW supplier {
            caption = 'Поставщик';
            ADD PROPERTY(nameFilterSupplier());
        }
        ADD d.box;
        ADD pt.box;
    }
    ADD s.box {
        PROPERTY(priceALedgerPriceListTypeSkuStockRangeRev(pt, s, d, v)) {
            caption = 'Цена закупки';
        }    
    }
    ADD functions.box;
}

seekDefaultPriceListType = ACTION SEEK rangeRev.pt defaultPriceListType();
EXTEND FORM rangeRev
    EVENTS ON INIT seekDefaultPriceListType()
;

copyRangeRev 'Копировать' = ACTION (pr) {
    FOR ADDOBJ r = RangeRev DO {
        rangeRangeRev(r) <- rangeRangeRev(pr);
        levelRangeRevSku(r, s) <- levelRangeRevSku(pr, s);
        FORM rangeRev OBJECTS v = r DOCKEDMODAL;
    }
} TOOLBAR;

EXTEND FORM range
    OBJECTS vl = RangeLevel FIXED GRID
    FILTERS inRangeRangeLevel(r, vl)
    
    OBJECTS v = RangeRev
    PROPERTIES(v) READONLY dateTimeRangeRev, countSkuRangeRev
    PROPERTIES READONLY countSkuRangeRevRangeLevel(v, vl) COLUMNS (vl) HEADER nameRangeLevel(vl) 
    PROPERTIES(v) ADDSESSIONFORM, EDITSESSIONFORM, copyRangeRev, DELETESESSION TOOLBAR FORCE PANEL
    FILTERS rangeRangeRev(v) == r
;

EXTEND DESIGN range {
    tabs {
        ADD v.box FIRST {
            caption = 'Версии';
        }
    }
}

EXTEND FORM ranges
    OBJECTS vl = RangeLevel FIXED GRID

    PROPERTIES READONLY countSkuRangeDate(r, d)
    PROPERTIES READONLY countSkuRangeRangeLevelDate(r, vl, d) TODRAW r COLUMNS (vl) HEADER nameRangeLevel(vl) 
;

@extendFormFilterSkuGroupAccess(ranges, sg);

// ----------------- Связь ассортиментов и магазинов --------------- //

useRangeStoreType 'Использовать ассортиментную матрицу' = DATA BOOLEAN (StoreType);
EXTEND FORM chainStores
    PROPERTIES(s) useRangeStoreType AFTER nameStoreType(s)    
;
useRangeStore 'Использовать ассортиментную матрицу' (s) = useRangeStoreType(storeTypeStore(s)) PERSISTENT; 
useRangeDepartmentStore 'Использовать ассортиментную матрицу' (s) = useRangeStore(storeDepartmentStore(s)) PERSISTENT; 

TABLE rangeStoreTypeDate(Range, StoreType, DATE);
dataLevelRangeStoreTypeDate = DATA RangeLevel (Range, StoreType, DATE);

CONSTRAINT dataLevelRangeStoreTypeDate(r, t, d) AND NOT inRangeRangeLevel(r, dataLevelRangeStoreTypeDate(r, t, d))
    CHECKED BY dataLevelRangeStoreTypeDate
    MESSAGE 'Уровень ассортимента для формата магазина не соответствует типу ассортимента';

lastLevelRangeStoreTypeDate (r, t, d) = GROUP LAST dataLevelRangeStoreTypeDate(range, storeType, dateIn)
                                              BY range, storeType, date
                                              ORDER dateIn
                                              WHERE dataLevelRangeStoreTypeDate(range, storeType, dateIn) AND dateIn <= (date AS DATE); 

levelRangeStoreTypeDate(r, t, d) = OVERRIDE lastLevelRangeStoreTypeDate(r, t, d), dataLevelRangeStoreTypeDate(r, t, d);  
nameLevelRangeStoreTypeDate 'Уровень' (r, t, d)= nameRangeLevel(levelRangeStoreTypeDate(r, t, d)); 

countRangeSkuStoreTypeDate 'В ассортименте' (s, t, d) = GROUP SUM 1 IF levelRangeStoreTypeDate(range, t, d) >= levelRangeSkuDate(range, s, d) BY s, t, d;
countRangeSkuStoreDate 'В ассортименте' (s, st, d) = countRangeSkuStoreTypeDate(s, storeTypeStore(st), d);  
countRangeSkuDepartmentStoreDate 'В ассортименте' (s, ds, d) = countRangeSkuStoreTypeDate(s, storeTypeDepartmentStore(ds), d); 

countSkuRangeStoreTypeDate 'Кол-во товаров' (r, t, d) = countRecSkuRangeRevRangeLevel(rangeRevRangeDate(r, d), levelRangeStoreTypeDate(r, t, d));

countSkuStoreDate 'Кол-во позиций в ассортименте' = DATA INTEGER (Store, DATE);     
calcCountSkuStoreDate (st, d) =  GROUP SUM 1 IF countRangeSkuStoreDate(s, st, d) BY  st, d;

countAverageSkuStoreDate 'Кол-во позиций с остатком меньше ср.дн. р-ии' = DATA INTEGER (Store, DATE); 
averageSoldSkuStore 'Продаж в день' (sku, store) = GROUP SUM averageSoldSkuStock(sku, stock) BY sku, storeDepartmentStore(stock);
calcCountAverageSkuStoreDate= GROUP SUM 1 IF countRangeSkuStoreDate(s, st, d) AND NOT
    (balanceBSkuStoreDate(s, st, d) >= averageSoldSkuStore(s, st)) AND averageSoldSkuStore(s, st)          
        BY  st, d;  
        
percAverageSkuStoreDate '% в ассортименте с остатком меньше ср.дн. р-ии' (store, date)= 
    countAverageSkuStoreDate(store, date) / 
    (countSkuStoreDate(store, date) IF countSkuStoreDate(store, date) != 0) *  
    100.00;       
        
countLessSkuStoreDate 'Кол-во позиций без остатка или  с остатком меньше либо равным 0' = DATA INTEGER (Store, DATE); 
calcLessAverageSkuStoreDate= GROUP SUM 1 IF countRangeSkuStoreDate(s, st, d) AND  NOT (balanceBSkuStoreDate(s, st, d) >0)          
        BY  st, d;   
percLessSkuStoreDate '% в ассортименте с остатком меньше либо равным 0' (store, date)= 
    countLessSkuStoreDate(store, date) / 
    (countSkuStoreDate(store, date) IF countSkuStoreDate(store, date) != 0) * 
    100.00;    
       
countNotSkuStoreDate 'Кол-во позиций с остатком без ассортимента' = DATA INTEGER (Store, DATE); 
calcCountNotSkuStoreDate= GROUP SUM 1 IF balanceBSkuStoreDate(s, st, d) >0 AND NOT countRangeSkuStoreDate(s, st, d)        
        BY  st, d;       
             
fillSkuStoreDate 'Заполнить данные по ассортименту' = ACTION (store, date) {         
    countSkuStoreDate(store, date) <-  calcCountSkuStoreDate(store, date);  
    countAverageSkuStoreDate(store, date) <-  calcCountAverageSkuStoreDate(store, date);
    countLessSkuStoreDate(store, date) <-  calcLessAverageSkuStoreDate(store, date);
    countNotSkuStoreDate(store, date) <-  calcCountNotSkuStoreDate(store, date);              
}  

fillSkuDateFromTo 'Заполнить данные по ассортименту' = ACTION (dateFrom, dateTo) {       
    LOCAL dateCur = DATE();               
      
    FOR useRangeStore(store) AND isCompanyStore(store) DO {   
        dateCur() <- dateFrom;
        WHILE dateCur() <= dateTo DO {      
            fillSkuStoreDate(store, dateCur()); 
            dateCur() <- sumDate(dateCur(), 1);
        }
    }
} 
// -- 
 
inSkuRangeStoreDate 'В ассортименте' (s, range, st, d) = levelRangeStoreTypeDate(range, storeTypeStore(st), d) >= levelRangeSkuDate(range, s, d);  

countSkuRangeStoreDate 'Кол-во позиций в ассортименте' = DATA INTEGER (Range, Store, DATE);    
calcCountSkuRangeStoreDate (range, store, date) =  GROUP SUM 1 IF inSkuRangeStoreDate(sku, range, store, date) BY  range, store, date;

countAverageSkuRangeStoreDate 'Кол-во позиций с остатком меньше ср.дн. р-ии' = DATA INTEGER (Range, Store, DATE); 
calcCountAverageSkuRangeStoreDate= GROUP SUM 1 IF inSkuRangeStoreDate(s, r, st, d) AND NOT
    (balanceBSkuStoreDate(s, st, d) >= averageSoldSkuStore(s, st)) AND averageSoldSkuStore(s, st)          
        BY  r, st, d;  
        
percAverageSkuRangeStoreDate '% в ассортименте с остатком меньше ср.дн. р-ии' (range, store, date)= 
    countAverageSkuRangeStoreDate(range, store, date) / 
    (countSkuRangeStoreDate(range, store, date) IF countSkuRangeStoreDate(range, store, date) != 0) *  
    100.00;       
        
countLessSkuRangeStoreDate 'Кол-во позиций без остатка или  с остатком меньше либо равным 0' = DATA INTEGER (Range, Store, DATE); 
calcCountLessSkuRangeStoreDate = GROUP SUM 1 IF inSkuRangeStoreDate(s, r, st, d) AND  NOT (balanceBSkuStoreDate(s, st, d) >0)          
        BY  r, st, d;   
percLessSkuRangeStoreDate '% в ассортименте с остатком меньше либо равным 0' (range, store, date)= 
    countLessSkuRangeStoreDate(range, store, date) / 
    (countSkuRangeStoreDate(range, store, date) IF countSkuRangeStoreDate(range, store, date) != 0) * 
    100.00;    
       
countNotSkuRangeStoreDate 'Кол-во позиций с остатком без ассортимента' = DATA INTEGER (Range, Store, DATE); 
calcCountNotRangeInRangeStoreDate= GROUP SUM 1 IF balanceBSkuStoreDate(s, st, d) >0 AND NOT inSkuRangeStoreDate(s, r, st, d)        
        BY  r, st, d;       
             
fillSkuRangeStoreDate 'Заполнить данные по ассортименту' = ACTION (range, store, date) {         
    countSkuRangeStoreDate(range, store, date) <-  calcCountSkuRangeStoreDate(range, store, date);  
    countAverageSkuRangeStoreDate(range, store, date) <-  calcCountAverageSkuRangeStoreDate(range, store, date);
    countLessSkuRangeStoreDate(range, store, date) <-  calcCountLessSkuRangeStoreDate(range, store, date);
    countNotSkuRangeStoreDate(range, store, date) <-  calcCountNotRangeInRangeStoreDate(range, store, date);              
}  

fillRangeDateFromTo 'Заполнить данные по ассортименту' = ACTION (dateFrom, dateTo) {       
    LOCAL dateCur = DATE();               
      
    FOR useRangeStore(store) AND isCompanyStore(store) AND range IS Range DO {   
        dateCur() <- dateFrom;
        WHILE dateCur() <= dateTo DO {      
            fillSkuRangeStoreDate(range, store, dateCur());
            dateCur() <- sumDate(dateCur(), 1);
        }
    }
}  
 
 
                        
FORM inStoreRange 'Исполнение ассортимента'

    OBJECTS params = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)            

    PROPERTIES(dFrom,dTo) fillSkuDateFromTo 
    
    TREE treeStore a=STRING[3], t=ChainStores, st=StoreType
    PROPERTIES READONLY OBJVALUE(a), nameChainStores(t), nameStoreType(st)

    FILTERS stringEqualsAll(a),
            inChainStoresStoreType (t, st)

    OBJECTS dts = (d = DATE, ts = Store) FIXED GRID
    PROPERTIES READONLY dataD = OBJVALUE(d)
    PROPERTIES(ts) SELECTOR nameStore
    PROPERTIES(ts) READONLY addressStore, nameLegalEntityStore
    FILTERS  d >= dFrom, d <= dTo,
             countSkuStoreDate(ts,d)
             
    ORDER BY dataD, nameStore(ts) 

    FILTERS inChainStoresStoreTypeStore(t, st, ts),
            isCompanyStore(ts),
            useRangeStore(ts)
        
 
    PROPERTIES(ts, d) countSkuStoreDate, countLessSkuStoreDate, percLessSkuStoreDate,
                    countAverageSkuStoreDate, percAverageSkuStoreDate, 
                    countNotSkuStoreDate      
    
    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuGroupName = nameSkuGroup(sg)
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(sg) DEFAULT
    
    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, nameUOMSku
    PROPERTIES READONLY balanceBSkuStoreDate(s, ts, d), averageSoldSkuStore(s, ts)
    
    FILTERGROUP rangeSku 
        FILTER 'Позиции в ассортименте' 'F11' countRangeSkuStoreDate(s, ts, d)  
        FILTER 'Позиции с остатком <= 0' 'F9' countRangeSkuStoreDate(s, ts, d) AND  NOT (balanceBSkuStoreDate(s, ts, d) >0) 
        FILTER 'Позиции с остатком < ср.дн. р-ии' 'F10' countRangeSkuStoreDate(s, ts, d) AND NOT (balanceBSkuStoreDate(s, ts, d) >= averageSoldSkuStore(s, ts)) AND averageSoldSkuStore(s, ts)               
        FILTER 'Позиции с остатком без ассортимента' 'F8' balanceBSkuStoreDate(s, ts, d) >0 AND NOT countRangeSkuStoreDate(s, ts, d) 
    FILTERS isParentSkuGroupSku(sg,s)
    
 
    PROPERTIES(dFrom,dTo) fillRangeDateFromTo     
    
    OBJECTS ddtst = (dd = DATE, tst = Store, r = Range) FIXED GRID
    PROPERTIES READONLY dataDe = OBJVALUE(dd)
    PROPERTIES(tst) SELECTOR nameStore
    PROPERTIES(tst) READONLY addressStore, nameLegalEntityStore
    PROPERTIES(r) SELECTOR nameRange
    FILTERS  dd >= dFrom, dd <= dTo,
             countSkuRangeStoreDate(r,tst,dd)
    FILTERS inParentRangeSkuGroup(r, sg) OR NOT countSkuGroupRange(r)         
    ORDER BY dataDe, nameStore(tst), nameRange(r)  

    FILTERS inChainStoresStoreTypeStore(t, st, tst),
            isCompanyStore(tst),
            useRangeStore(tst)
        

    PROPERTIES(r, ts, d) countSkuRangeStoreDate, countLessSkuRangeStoreDate, percLessSkuRangeStoreDate,
                    countAverageSkuRangeStoreDate, percAverageSkuRangeStoreDate, 
                    countNotSkuRangeStoreDate       
                    
    OBJECTS ss = Sku
    PROPERTIES(ss) READONLY idBarcodeSku, nameSku, nameUOMSku
    PROPERTIES READONLY balanceBSkuStoreDate(ss, tst, d), averageSoldSkuStore(ss, tst)
    
    FILTERGROUP rangeSku1 
        FILTER 'Позиции в ассортименте' 'F11' inSkuRangeStoreDate(ss, r, tst, dd)  
        FILTER 'Позиции с остатком <= 0' 'F9' inSkuRangeStoreDate(ss, r, tst, dd) AND  NOT (balanceBSkuStoreDate(ss, tst, dd) >0) 
        FILTER 'Позиции с остатком < ср.дн. р-ии' 'F10' inSkuRangeStoreDate(ss, r, tst, dd) AND NOT (balanceBSkuStoreDate(ss, tst, dd) >= averageSoldSkuStore(ss, tst)) AND averageSoldSkuStore(ss, tst)               
        FILTER 'Позиции с остатком без ассортимента' 'F8' balanceBSkuStoreDate(ss, tst, dd) >0 AND NOT inSkuRangeStoreDate(ss, r, tst, dd)
    FILTERS isParentSkuGroupSku(sg,ss)                    
    
;

DESIGN inStoreRange FROM DEFAULT {
    ADD params.box;
    NEW pane {
        fill = 1;
        type = SPLITH;
        NEW leftPane {
            fill = 1;
            type = SPLITV;
            ADD treeStore.tree.box;
            ADD skuTree.tree.box {
            }
        }
        NEW rightPane{
            type = TABBED;
            fill = 2;
            NEW rightPane1 {
                caption = 'Магазин';
                fill = 1;   
                NEW rightPane11{
                    ADD PROPERTY(fillSkuDateFromTo(dFrom,dTo));    
                };               
                NEW rightPane12{
                    fill = 1;
                    type = SPLITV;
                    ADD dts.box;                     
                    ADD s.box;
                }    
            } 
            NEW rightPane2 {
                caption = 'Магазин / ассортимент';
                fill = 1;   
                NEW rightPane21{
                    ADD PROPERTY(fillRangeDateFromTo(dFrom,dTo));    
                };               
                NEW rightPane22{
                    fill = 1;
                    type = SPLITV;
                    ADD ddtst.box;                     
                    ADD ss.box;
                }    
            }            
        }    
    }
    ADD functions.box;
}             
              
FORM storeRange 'Ассортимент магазинов'
    OBJECTS dc = (d = DATE, c = ChainStores) FIXED PANEL
    PROPERTIES(d) OBJVALUE
    PROPERTIES(c) SELECTOR nameChainStores
     
    OBJECTS t = StoreType FIXED GRID
    PROPERTIES(t) READONLY nameStoreType
    FILTERS chainStoresStoreType(t) == c
    
    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuGroupName = nameSkuGroup(sg)
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(sg) DEFAULT

    OBJECTS r = Range
    PROPERTIES(r) READONLY nameRange
    PROPERTIES(r, t, d) nameLevelRangeStoreTypeDate COLUMNS (t) HEADER nameStoreType(t) BACKGROUND dataLevelRangeStoreTypeDate(r, t, d),
                        countSkuRangeStoreTypeDate COLUMNS(t) HEADER nameStoreType(t)
    FILTERS inParentRangeSkuGroup(r, sg) OR NOT countSkuGroupRange(r)
    
    OBJECTS s = Sku
    PROPERTIES(s) READONLY idBarcodeSku, nameSku, nameUOMSku
    PROPERTIES(s, t, d) READONLY countRangeSkuStoreTypeDate COLUMNS (t) HEADER nameStoreType(t)
    FILTERS levelRangeSkuDate(r, s, d) 
;

DESIGN storeRange FROM DEFAULT {
    ADD dc.box;
    NEW pane {
        fill = 1;
        type = SPLITH;
        NEW leftPane {
            fill = 1;
            type = SPLITV;
            ADD t.box;
            ADD skuTree.tree.box {
                fill = 2;
            }
        }
        NEW rightPane {
            fill = 2;
            type = SPLITV;
            ADD r.box {
                fill = 2;
                PROPERTY(countSkuRangeStoreTypeDate(r, t, d)) {
                    background = #FFEEFF;
                }
            }
            ADD s.box;
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    retailRangeNavigator {
        ADD storeRange;
        ADD inStoreRange;
    }
}
