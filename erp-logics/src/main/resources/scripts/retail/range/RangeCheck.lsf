MODULE RangeCheck;

REQUIRE Range;
NAMESPACE Range;

// Упрощенная версия

TABLE rangeRevStoreSku (RangeRev, Store, Sku);
include 'Вкл.' = DATA BOOLEAN (RangeRev, Store, Sku);
include 'Вкл.' (RangeRev v, DepartmentStore ds, Sku s) = include (v, store(ds), s);

include 'В ассортименте' = GROUP SUM 1 IF include(rangeRev(Range r, DATE dt), Store store, Sku s) BY s,store,dt;
in (Sku s, Range r, Store st, DATE d) += include(rangeRev(r, d), st, s);

inFilter 'В ассортименте' = GROUP SUM 1 IF include(RangeRev v, Store st, Sku s) BY v,s;
EXTEND FORM rangeRev
    OBJECTS store1 = Store BEFORE s  
    PROPERTIES include(v,store1,s) COLUMNS (store1) HEADER name(store1)
    FILTERGROUP filters
        FILTER 'Включен' inFilter(v, s)  DEFAULT
        FILTER 'Без поставщиков' inFilter(v, s) AND NOT companyA(pt, s, d, dateTime(v))      
;

counInclude 'Кол-во товаров' = GROUP SUM 1 IF include(RangeRev v, Store st, Sku s) BY v,st;
counInclude 'Кол-во товаров' = GROUP SUM 1 IF include(RangeRev v, Store st, Sku s) BY v,s;
counInclude 'Кол-во товаров' = GROUP SUM 1 IF counInclude(RangeRev v, Sku s) BY v;
counInclude'Кол-во товаров' (Range r, DATE dt) = counInclude(rangeRev(r,dt));
counInclude'Кол-во товаров' (Range r, Sku s, DATE dt) = counInclude(rangeRev(r,dt),s);
counInclude 'Кол-во товаров' = GROUP SUM 1 IF include(RangeRev v, Store st, Sku s) BY range(v),s;
counInclude 'Кол-во товаров' = GROUP SUM 1 IF include(RangeRev v, Store st, Sku s) BY range(v),st,s;

EXTEND FORM range
    PROPERTIES (v)       READONLY counInclude
    OBJECTS store = Store
    PROPERTIES  READONLY name(store),counInclude(v,store)
    FILTERS counInclude(v,store)
;
DESIGN range {
    tabs {
        NEW first FIRST {
            fill = 1;
            caption = 'Версии';
            type = SPLITV;
            MOVE v.box;
            MOVE store.box;
        }     
    }
}
EXTEND FORM ranges
    PROPERTIES (r,d) READONLY counInclude BEFORE nameGroup(r)        
;
counInclude (Range r, Store st, DATE dt) = counInclude(rangeRev(r,dt), st);
include (Range r, Store store, Sku s, DATE dt)= include(rangeRev(r, dt), store, s);
include = GROUP SUM 1 IF include(Range r, Store store, Sku s, DATE dt) BY r,s,dt;

EXTEND FORM ranges      
    ORDER BY name(s)
    FILTERS include(r,s,d)
    
    OBJECTS store = Store
    PROPERTIES(store) READONLY name
    FILTERS include(r,store,s,d)
;

DESIGN ranges {
    range{
        type = SPLITV;
        
        NEW tabR {
            fill = 2;
            type = SPLITH;
            MOVE s.box {fill = 2;}
            MOVE store.box;
        }
    }
}

EXTEND FORM storeRange 
    FILTERS counInclude(sr, ss, d)
    FILTERS include(rn, ds, sk, d)
;

WHEN autoSetGroupAttribute(mainRole(currentUser())) AND CHANGED(itemGroup(Item item)) AND include(RangeRev v, Store store, Sku s) AND NOT inChild(range(v), s) DO {
    inData(Range r, SkuGroup g) <- TRUE WHERE r == range(v) AND g == skuGroup(s);
}

CONSTRAINT include(RangeRev v, Store store, Sku s) AND NOT inChild(range(v), s) MESSAGE 'Товар не соответствует выбранным группам для ассортимента';

