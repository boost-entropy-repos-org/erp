MODULE POSInvoice;

REQUIRE POS, SaleInvoice;

NAMESPACE Sale;


createInvoiceReceipt 'Создать' = ACTION (receipt) {       // todo: сделать необходимые округления
    FOR ADDOBJ i = UserInvoice DO {
        ASSIGN supplierStockUserInvoice(i) <- departmentStoreReceipt(receipt);
        ASSIGN supplierUserInvoice(i) <- legalEntityStock(departmentStoreReceipt(receipt));
        ASSIGN dateUserInvoice(i) <- dateReceipt(receipt);
        ASSIGN timeUserInvoice(i) <- timeReceipt(receipt);

        FOR  receiptReceiptDetail(detail) == receipt ADDOBJ d = UserInvoiceDetail DO {
            ASSIGN userInvoiceUserInvoiceDetail(d) <- i;
            ASSIGN skuUserInvoiceDetail(d) <- skuReceiptDetail(detail);
            ASSIGN quantityUserInvoiceDetail(d) <- quantityReceiptDetail(detail);
            ASSIGN invoiceSumUserInvoiceDetail(d) <- sumReceiptDetail(detail);
            ASSIGN invoicePriceUserInvoiceDetail(d) <- sumReceiptDetail(detail)/quantityReceiptDetail(detail);
            ASSIGN priceUserInvoiceDetail(d) <- invoicePriceUserInvoiceDetail (d) * 100/(100 + calcValueVATUserInvoiceDetail(d));

            DELETE detail;
        }
        DELETE receipt;

        FORM userInvoice OBJECTS i=i MANAGESESSION MODAL;
    }
    EXEC createCurrentReceipt();
}

EXTEND FORM POS
    PROPERTIES(r) SHOWIF currentZReport() createInvoiceReceipt
;
EXTEND DESIGN POS {
    misc {
        NEW invoice {
            alignment = STRETCH;
            caption = 'Накладная';
            ADD PROPERTY(createInvoiceReceipt) { fill = 1; focusable = FALSE; font = 'Tahoma bold 24';  }
        }
    }
}