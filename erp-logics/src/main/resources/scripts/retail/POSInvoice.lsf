MODULE POSInvoice;

REQUIRE POS, SaleInvoice;

NAMESPACE Sale;


createInvoiceReceipt 'Создать накладную' = ACTION (receipt) {       // todo: сделать необходимые округления
    FOR ADDOBJ i = UserInvoice DO {
        SET supplierStockUserInvoice(i) <- departmentStoreReceipt(receipt);
        SET supplierUserInvoice(i) <- legalEntityStock(departmentStoreReceipt(receipt));
        SET dateUserInvoice(i) <- dateReceipt(receipt);
        SET timeUserInvoice(i) <- timeReceipt(receipt);

        FOR  receiptReceiptDetail(detail) == receipt ADDOBJ d = UserInvoiceDetail DO {
            SET userInvoiceUserInvoiceDetail(d) <- i;
            SET skuUserInvoiceDetail(d) <- skuReceiptDetail(detail);
            SET quantityUserInvoiceDetail(d) <- quantityReceiptDetail(detail);
            SET invoiceSumUserInvoiceDetail(d) <- sumReceiptDetail(detail);
            SET invoicePriceUserInvoiceDetail(d) <- sumReceiptDetail(detail)/quantityReceiptDetail(detail);
            SET priceUserInvoiceDetail(d) <- invoicePriceUserInvoiceDetail (d) * 100/(100 + calcValueVATUserInvoiceDetail(d));

            DELETE detail;
        }
        DELETE receipt;

        FORM userInvoice OBJECTS i=i MANAGESESSION MODAL;
    }
    EXEC createCurrentReceipt();
}

EXTEND FORM POS
    PROPERTIES(r) SHOWIF currentZReport() createInvoiceReceipt
;
EXTEND DESIGN POS {
    secondFirstEastContainer{
        NEW invoiceContainer{
            title = 'Накладная';
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(createInvoiceReceipt) { focusable = FALSE; font = 'Tahoma bold 24';  }
        }
    }
}