MODULE BatchRepricing;

REQUIRE RepricingDashboard;

NAMESPACE Repricing;

//----------------- 

isSupplierRepricing 'Дооценка по партиям за счет цены поставщика' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES() isSupplierRepricing
;

DESIGN options {
    repricings  {
        MOVE PROPERTY(isSupplierRepricing());
    }
}


// По партиям

//-- поставщика
supplierPriceBSku 'Цена по товару' (Batch batch, Stock stock, DATETIME dateTime)= priceB(SystemLedgerPriceListType.supplierPricingPriceListType, sku(batch), stock, dateTime);

//-- розничная
priceB 'Цена партии' (Batch batch, Stock stock, DATETIME dateTime)= priceBBatch(SystemLedgerPriceListType.retailPricingPriceListType, batch, stock, dateTime);
priceBSku 'Цена по товару' (Batch batch, Stock stock, DATETIME dateTime)= priceB(SystemLedgerPriceListType.retailPricingPriceListType, sku(batch), stock, dateTime);

toRepricing (Batch batch, Stock stock, DATETIME dateTime) =  balanceB(batch, stock, dateTime) 
    IF NOT (priceB(batch, stock, dateTime) == priceBSku(batch, stock, dateTime));
toRepricing (stock, dateTime) = GROUP SUM 1 IF toRepricing (Batch batch, Stock stock, DATETIME dateTime) BY stock, dateTime;     

setUserRepricingStock 'Создать акт переоценки по партиям'(DepartmentStore stock, DATETIME dateTime, Operation operation)  = ACTION NEWSESSION {
    IF toRepricing (stock, dateTime) AND stock IS DepartmentStore THEN {
    
        FOR ADDOBJ p = UserRepricing DO {
            departmentStore(p) <- stock;
            date(p) <- toDate(dateTime);
            time(p) <- toTime(dateTime);
            operation(p) <- operation;
            
            FOR toRepricing(Batch batch, stock, dateTime) INLINE ADDOBJ d = UserRepricingDetail DO {
                userRepricing(d) <- p;    
                sku(d) <- sku(batch);
                batch(d) <- batch;
                quantity(d) <- balanceB(batch, stock, dateTime);
                curRetailPrice(d) <- priceB(batch, stock, dateTime);
                retailPrice(d) <- priceBSku(batch, stock, dateTime);
                
                price(d) <- supplierPriceBSku(batch, stock, dateTime) WHERE isSupplierRepricing();
            }  
            SHOW userRepricing OBJECTS p = p MANAGESESSION DOCKED NOCANCEL;
        }
    }     
} TOOLBAR;

documentNameSkuBatch 'Наименование (для документов)' = documentNameSku(batch(RepricingDetail d));
documentNameSkuBatch 'Наименование (для документов)' = documentNameSku(batch(UserRepricingDetail d));

EXTEND FORM userRepricing
    PROPERTIES documentNameSkuBatch(d) ON CHANGE changeBatch(d) AFTER nameBatch(d)
;
EXTEND FORM repricings
    PROPERTIES READONLY documentNameSkuBatch(d) AFTER nameBatch(d)
;

FORM repricingBatchDashboard 'Дооценка по партиям'
    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES (d) SELECTOR name
    FILTERS isCompany(d)
        
    OBJECTS o = Repricing.Operation FIXED PANEL
    PROPERTIES (o) SELECTOR name
    FILTERS in(currentUser(), o)

    OBJECTS t=DATETIME FIXED PANEL
    PROPERTIES(t) OBJVALUE        
     
    OBJECTS bt = Batch
    PROPERTIES (bt) READONLY date, name, nameSku, documentNameSku 
    PROPERTIES (bt,d,t) READONLY balanceB, priceB, priceBSku
    
    FILTERS toRepricing(bt,d,t)       
    PROPERTIES (d,t,o)  TODRAW bt FORCE PANEL setUserRepricingStock
                  
    OBJECTS p = Repricing
    PROPERTIES (p) READONLY isPosted, number, series, date, time, beforeDate,
                   nameDepartmentStore, countRepricingDetail, diffSumRepricingDetail,
                   diffMarkupSumRepricingDetail, diffVATSumRepricingDetail,
                   diffRetailSumRepricingDetail, numberDisposal, skipChangeLedger,
                   statusLabel BACKGROUND backgroundPrintLabelTransaction(p),
                   statusMachinery ON SHORTCUT createSnapshotMachineryPriceTransaction(p) BACKGROUND backgroundCreateMachineryPriceTransaction(p), 
                   note                      
               
    PROPERTIES (p) createLabelTransaction FORCE PANEL SHOWIF showPrintLabelTransaction(p), 
                   createSnapshotLabelTransaction FORCE PANEL
                   
    PROPERTIES(p)  createMachineryPriceTransaction FORCE PANEL SHOWIF showCreateMachineryPriceTransaction(p)
    PROPERTIES (p) print FORCE PANEL TOOLBAR                                           
    PROPERTIES (p) NEWSESSION edit SHOWIF showEditUser(p), NEW
    PROPERTIES (p) NEWSESSION deletep=DELETE FORCE PANEL TOOLBAR SHOWIF showDeleteUser(p)    
                       
    FILTERGROUP filters FILTER 'Переоценки на дату' date(p)==toDate(t) 'F5' DEFAULT                
                                               
;
DESIGN repricingBatchDashboard {
    main {
        type = CONTAINERV;

        NEW topContainer {
            type = CONTAINERH;
            MOVE d.box;
            MOVE o.box;
            MOVE t.box { caption = 'Время с';}
        }
        MOVE bt.box;
        MOVE p.box;
        MOVE functions.box;
    }
}
NAVIGATOR {
    retailDashboardNavigator {
        ADD repricingBatchDashboard;
    }
}


