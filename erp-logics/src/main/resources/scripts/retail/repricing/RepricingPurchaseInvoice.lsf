MODULE RepricingPurchaseInvoice;

REQUIRE PricingPurchase, Repricing, ZReport;

PRIORITY Purchase;

NAMESPACE Repricing;

//------------------- Дооценка с учетом проданного количества между приходом и закачкой прайса в оборудование -----------------------//  
   
FORM extraValuation 'Дооценка'
    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES (d) SELECTOR nameDepartmentStore

    OBJECTS tf=DATETIME FIXED PANEL
    PROPERTIES(tf) OBJVALUE

    OBJECTS tt=DATETIME FIXED PANEL
    PROPERTIES(tt) OBJVALUE
    
    OBJECTS o=Repricing.Operation FIXED PANEL
    PROPERTIES(o) SELECTOR Repricing.nameOperation    

;
DESIGN extraValuation {
    main {
        type = CONTAINERV;

        NEW topContainer {
            type = CONTAINERH;
            MOVE d.box { caption = 'Выберите отдел магазина';}
            MOVE tf.box { caption = 'Дата/время с'; }
            MOVE tt.box { caption = 'Дата/время по'; }
            MOVE o.box { caption = 'Выберите операцию переоценки'; }
        }
        MOVE functions.box;
    }
}     
       
repricingInvoice = DATA Repricing (Invoice);
repricingInvoiceDetail (d) = repricingInvoice(invoiceInvoiceDetail(d));

prevRetailPricingPriceBInvoiceDetail 'Цена розничная до' (detail) = prevRetailPricingPriceBSkuStockDateTime(skuInvoiceDetail(detail), customerStockInvoiceDetail(detail), dateTimeInvoiceDetail(detail));

calcQuantitySoldSkuStockPriceDateTime 'Кол-во продано по старой цене' (sku, stock, price, dateTime) = GROUP SUM 
    signedQuantityReceiptDetail(d) IF dateTimeReceiptDetail(d) >= (dateTime AS DATETIME) AND isPostedReceiptDetail(d)
        BY skuReceiptDetail(d), departmentStoreReceiptDetail(d), priceReceiptDetail(d), dateTime; 
        
toRepricingQuantitySkuStockPriceDateTime 'Кол-во к дооценке' (sku, stock, price, dateTime) = prevBalanceBSkuStockDateTime (sku, stock, dateTime) (-) calcQuantitySoldSkuStockPriceDateTime(sku, stock, price, dateTime);         
toRepricingQuantityInvoiceDetail 'Кол-во к дооценке' (detail) = toRepricingQuantitySkuStockPriceDateTime(skuInvoiceDetail(detail), customerStockInvoiceDetail(detail), prevRetailPricingPriceBInvoiceDetail(detail), shipmentDateTimeInvoiceDetail(detail));
               
filterToRepricingInvoiceStockDateTime 'Фильтр' (invoice, department, timeFrom)= 
    customerStockInvoice(invoice) == department AND         
    shipmentDateTimeInvoice(invoice) >= timeFrom AND NOT
    costLedgerDepartmentStore(department) AND
    isPostedInvoice(invoice) AND NOT
    repricingInvoice(invoice)
; 
 
calcQuantitySoldSkuStockPriceDateTimeFromTo 'Кол-во продано по старой цене' (sku, stock, price, fromDateTime, toDateTime) = GROUP SUM 
    signedQuantityReceiptDetail(d) IF dateTimeReceiptDetail(d) >= (fromDateTime AS DATETIME) AND (dateTimeReceiptDetail(d) <= (toDateTime AS DATETIME)) AND isPostedReceiptDetail(d)
        BY skuReceiptDetail(d), departmentStoreReceiptDetail(d), priceReceiptDetail(d), fromDateTime, toDateTime; 
        
toRepricingQuantitySkuStockPriceDateTimeFromTo 'Кол-во к дооценке' (sku, stock, price, fromDateTime, toDateTime) = prevBalanceBSkuStockDateTime (sku, stock, fromDateTime) (-) calcQuantitySoldSkuStockPriceDateTimeFromTo(sku, stock, price, fromDateTime, toDateTime);         
toRepricingQuantityInvoiceDetailToDateTime 'Кол-во к дооценке' (detail, toDateTime) = toRepricingQuantitySkuStockPriceDateTimeFromTo(skuInvoiceDetail(detail), customerStockInvoiceDetail(detail), prevRetailPricingPriceBInvoiceDetail(detail), shipmentDateTimeInvoiceDetail(detail), toDateTime); 
filterToRepricingInvoiceStockDateTimeFromTo 'Фильтр' (invoice, department, timeFrom, timeTo)= 
    customerStockInvoice(invoice) == department AND         
    shipmentDateTimeInvoice(invoice) >= timeFrom AND
    isPostedInvoice(invoice) AND 
    shipmentDateTimeInvoice(invoice) <= timeTo AND NOT
    costLedgerDepartmentStore(department) AND NOT
    explicitBatchLedgerDepartmentStore(department) AND NOT
    repricingInvoice(invoice)
;                
                  
overExtraValuation = ABSTRACT ACTION LIST (RepricingDetail, InvoiceDetail);  
overExtraRevertValuation = ABSTRACT ACTION LIST (RepricingDetail, InvoiceDetail);

skipExtraValuationSkuStock = ABSTRACT BOOLEAN (Sku, Stock);        
        
maxInvoiceDetailSkuInvoice = GROUP LAST detail 
    BY skuInvoiceDetail(detail), invoiceInvoiceDetail(detail) 
    ORDER quantityInvoiceDetailSkuInvoice(skuInvoiceDetail(detail), invoiceInvoiceDetail(detail)), detail;
       
repricingForm =  DATA LOCAL UserRepricing();                      

skipNegativeRepricing 'Не использовать обратную дооценку' = DATA BOOLEAN ();  

EXTEND FORM options
    PROPERTIES () skipNegativeRepricing
;

DESIGN options {
    repricings {
        MOVE PROPERTY (skipNegativeRepricing());            
    }
}  

overPositiveInvoiceDetail = ABSTRACT ACTION LIST (InvoiceDetail); 
overNegativeInvoiceDetail= ABSTRACT ACTION LIST (InvoiceDetail); 

positiveInvoiceDetail =  DATA LOCAL BOOLEAN (InvoiceDetail);
negativeInvoiceDetail =  DATA LOCAL BOOLEAN (InvoiceDetail);    

keepSupplierPriceInRepricingInvoiceDetailDepartmentStore = ABSTRACT BOOLEAN (InvoiceDetail, DepartmentStore);

toRepricingOperation  = DATA Repricing.Operation ();
nameToRepricingOperation 'Операция для автоматической дооценки' = nameOperation(toRepricingOperation());

toRepricingTime 'Время (с) для автоматической дооценки' = DATA TIME ();
toRepricingCountDays 'Количество дней анализа для автоматической дооценки (с)' = DATA INTEGER ();
toRepricingDateTime 'Дата/время документа (с)' () = dateTimeToDateTime(subtractDate(currentDate(), toRepricingCountDays()), toRepricingTime()); 
minQuantityToRepricing 'Минимальное количество к дооценке' = DATA NUMERIC[14,4] ();

toRepricingTimeTo 'Время (по) для автоматической дооценки' = DATA TIME ();
toRepricingCountDaysTo 'Количество дней анализа для автоматической дооценки (по)' = DATA INTEGER ();
toRepricingDateTimeTo 'Дата/время документа (по)' () = OVERRIDE 2030_01_01_00:00, dateTimeToDateTime(subtractDate(currentDate(), toRepricingCountDaysTo()), toRepricingTimeTo()); 
    
    
@defineGroupDefaultValue(skipRepricing, 'Не дооценивать на приходе', BOOLEAN, skuGroup); 
          
quantityChildWithDataSkipRepricingSkuGroup (skuGroup) = GROUP SUM 1 IF dataSkipRepricingSkuGroup(childSkuGroup) AND isParentSkuGroupSkuGroup(childSkuGroup, skuGroup) BY skuGroup PERSISTENT;
quantityParentWithDataSkipRepricingSkuGroup (skuGroup) = GROUP SUM 1 IF dataSkipRepricingSkuGroup(parentSkuGroup) AND isParentSkuGroupSkuGroup(skuGroup, parentSkuGroup) BY skuGroup PERSISTENT;

showDataSkipRepricingSkuGroup 'Не дооценивать на приходе' (skuGroup) = TRUE IF (quantityChildWithDataSkipRepricingSkuGroup (skuGroup) (+) quantityParentWithDataSkipRepricingSkuGroup (skuGroup)) > 0 PERSISTENT;
showAnyDataSkipRepricingSkuGroup 'Не дооценивать на приходе' (skuGroup) = showDataSkipRepricingSkuGroup(skuGroup) OR NOT skuGroup IS SkuGroup;

backgroundShowDataSkipRepricingSkuGroup (skuGroup) =
    IF dataSkipRepricingSkuGroup(skuGroup) THEN
        RGB(0,0,0) IF skuGroup IS SkuGroup
    ELSE
        RGB(203,203,206) IF quantityChildWithDataSkipRepricingSkuGroup (skuGroup) != descendantNumberSkuGroup(skuGroup)
                         AND NOT quantityParentWithDataSkipRepricingSkuGroup (skuGroup);

setNullShowDataSkipRepricingSkuGroup 'Снять признак для всех потомков' = ACTION (skuGroup) {
    FOR isParentSkuGroupSkuGroup (childGroup, skuGroup) DO {
        ASSIGN skipRepricingSkuGroup(childGroup) <- NULL;
    }
} SHORTCUT skipRepricingSkuGroup CONFIRM;
  
createExtraValuationStockTimeOperationNotFormUserRepricing= ABSTRACT ACTION LIST (UserRepricing);
createExtraValuationStockTimeOperationNotForm 'Создать дооценку' = ACTION (department, timeFrom, timeTo, operation) {
    LOCAL inInvoice = BOOLEAN (Invoice);
    inInvoice(invoice) <- filterToRepricingInvoiceStockDateTimeFromTo (invoice, department, timeFrom, timeTo);
    IF [ = GROUP SUM 1 IF inInvoice(invoice)] () THEN {
        
        positiveInvoiceDetail(detail) <- NULL;
        positiveInvoiceDetail(detail) <- inInvoice(invoiceInvoiceDetail(detail));
        positiveInvoiceDetail(detail) <- NULL WHERE positiveInvoiceDetail(detail) AND skipRepricingSkuGroup(skuGroupSku(skuInvoiceDetail(detail)));        
        positiveInvoiceDetail(detail) <- prevRetailPricingPriceBInvoiceDetail(detail) != retailPriceInvoiceDetail(detail) WHERE positiveInvoiceDetail(detail);
        positiveInvoiceDetail(detail) <- toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo) > 0 AND isStockSkuInvoiceDetail(detail) WHERE positiveInvoiceDetail(detail);
        positiveInvoiceDetail(detail) <- maxInvoiceDetailSkuInvoice(skuInvoiceDetail(detail), invoiceInvoiceDetail(detail)) == detail WHERE positiveInvoiceDetail(detail);
        positiveInvoiceDetail(detail) <- NULL WHERE skipExtraValuationSkuStock(skuInvoiceDetail(detail), department) AND positiveInvoiceDetail(detail);                     
        positiveInvoiceDetail(detail) <- NULL WHERE positiveInvoiceDetail(detail) AND NOT priceInvoiceDetail(detail);
        IF minQuantityToRepricing() THEN {
            positiveInvoiceDetail(detail) <- NULL WHERE toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo) < minQuantityToRepricing() AND positiveInvoiceDetail(detail);
        }
          
        FOR positiveInvoiceDetail(detail) DO {
            overPositiveInvoiceDetail(detail);
        }              
    
        IF NOT skipNegativeRepricing() THEN {
            negativeInvoiceDetail(detail) <- NULL;
            negativeInvoiceDetail(detail) <- inInvoice(invoiceInvoiceDetail(detail));
            negativeInvoiceDetail(detail) <- NULL WHERE negativeInvoiceDetail(detail) AND skipRepricingSkuGroup(skuGroupSku(skuInvoiceDetail(detail)));        
            negativeInvoiceDetail(detail) <- prevRetailPricingPriceBInvoiceDetail(detail) != retailPriceInvoiceDetail(detail) WHERE negativeInvoiceDetail(detail);
            negativeInvoiceDetail(detail) <- toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo) < 0 AND isStockSkuInvoiceDetail(detail) WHERE negativeInvoiceDetail(detail);
            negativeInvoiceDetail(detail) <- maxInvoiceDetailSkuInvoice(skuInvoiceDetail(detail), invoiceInvoiceDetail(detail))==detail WHERE negativeInvoiceDetail(detail);
            negativeInvoiceDetail(detail) <- NULL WHERE skipExtraValuationSkuStock(skuInvoiceDetail(detail), department) AND negativeInvoiceDetail(detail);         
            negativeInvoiceDetail(detail) <- NULL WHERE negativeInvoiceDetail(detail) AND NOT priceInvoiceDetail(detail); 
            IF minQuantityToRepricing() THEN {
                negativeInvoiceDetail(detail) <- NULL WHERE delta(toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo),0) < minQuantityToRepricing() AND negativeInvoiceDetail(detail);
            }
        
            FOR negativeInvoiceDetail(detail) DO {
                overNegativeInvoiceDetail(detail);
            }
        }
                
        IF [ = GROUP SUM 1 IF positiveInvoiceDetail(detail) OR negativeInvoiceDetail(detail) ]() THEN {
            FOR ADDOBJ r = UserRepricing DO {
                departmentStoreUserRepricing(r) <-  department;
                operationUserRepricing(r) <- operation;
                skipChangeLedgerUserRepricing(r) <- TRUE;
                isPostedUserRepricing(r) <- TRUE;
        
                // дооценка в прямую сторону
                FOR positiveInvoiceDetail(detail) ADDOBJ rd = UserRepricingDetail DO {
                        userRepricingUserRepricingDetail(rd) <- r;
                        skuUserRepricingDetail(rd) <- skuInvoiceDetail(detail);
                        quantityUserRepricingDetail(rd) <- toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo);
        
                        curPriceUserRepricingDetail(rd) <- prevSupplierPricingPriceBSkuStockDateTime(skuInvoiceDetail(detail), department, dateTimeInvoiceDetail(detail));
                        curRetailPriceUserRepricingDetail(rd) <- prevRetailPricingPriceBInvoiceDetail(detail);
        
                        priceUserRepricingDetail(rd) <- IF NOT keepSupplierPriceInRepricingInvoiceDetailDepartmentStore(detail, department) THEN priceInvoiceDetail(detail) ELSE curPriceUserRepricingDetail(rd);
                        retailPriceUserRepricingDetail(rd) <- retailPriceInvoiceDetail(detail);                          
        
                        overExtraValuation(rd, detail);
                }
        
                // дооценка в обратную сторону
                FOR negativeInvoiceDetail(detail) ADDOBJ rd = UserRepricingDetail DO {
                        userRepricingUserRepricingDetail(rd) <- r;
                        skuUserRepricingDetail(rd) <- skuInvoiceDetail(detail);
                        quantityUserRepricingDetail(rd) <- - toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo);
        
                        curPriceUserRepricingDetail(rd) <- priceInvoiceDetail(detail);
                        curRetailPriceUserRepricingDetail(rd) <- retailPriceInvoiceDetail(detail);
        
                        priceUserRepricingDetail(rd) <- priceInvoiceDetail(detail);
                        retailPriceUserRepricingDetail(rd) <- prevRetailPricingPriceBInvoiceDetail(detail);                          
        
                        overExtraRevertValuation(rd, detail);
                }
                repricingInvoice(invoice) <- r WHERE inInvoice(invoice);
                createExtraValuationStockTimeOperationNotFormUserRepricing(r);                
                repricingForm() <- r;            
            } 
        }
    }
}                  
                   
createExtraValuationStockTimeOperation 'Создать дооценку' = ACTION  (department, timeFrom, timeTo, operation) NEWSESSION {
    createExtraValuationStockTimeOperationNotForm(department, timeFrom, timeTo, operation);
    FORM userRepricing  OBJECTS p = repricingForm() MANAGESESSION DOCKEDMODAL;  
    repricingForm() <- NULL;      
}                 
                                                    
createExtraValuation 'Создать дооценку' = ACTION () NEWSESSION {
    FORM extraValuation  MODAL;
    IF formResult() == FormResult.ok THEN {        
        createExtraValuationStockTimeOperation(chosenObject('d'), chosenDateTime('tf'), chosenDateTime('tt'), chosenObject('o'));                               
    }
} TOOLBAR ;

recreateExtraValuation = ACTION(dateFrom, dateTo) {
    FOR iterateDate(d, dateFrom, dateTo) DO {
        DELETE u WHERE operationUserRepricing(u) == toRepricingOperation() AND dateUserRepricing(u) == d;
        apply();
        FOR department IS DepartmentStore AND isCompanyStock(department) DO {
            createExtraValuationStockTimeOperationNotForm(department, DATETIME(subtractDate(d,1)), DATETIME(d), toRepricingOperation());
            dateUserRepricing(r) <- d WHERE SET(r IS UserRepricing);
            apply();
        }
    }
}

EXTEND FORM repricings
    PROPERTIES ()  createExtraValuation TODRAW p
; 
  
createOverExtraValuationAllStocks = ABSTRACT ACTION LIST ();
isOverExtraValuationAllStocks = ABSTRACT BOOLEAN ();

createExtraValuationAllStocks 'Создать автоматическую дооценку' = ACTION () NEWSESSION {
    IF isOverExtraValuationAllStocks() THEN {
        createOverExtraValuationAllStocks();
    } ELSE {   
        FOR department IS DepartmentStore AND isCompanyStock(department) DO {
            createExtraValuationStockTimeOperationNotForm(department, toRepricingDateTime(), toRepricingDateTimeTo(), toRepricingOperation());                                     
        } 
    }
    apply();
}

EXTEND FORM options
    PROPERTIES() nameToRepricingOperation, toRepricingTime, toRepricingCountDays, minQuantityToRepricing, toRepricingTimeTo, toRepricingCountDaysTo, createExtraValuationAllStocks
    
    TREE skuTree g = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY orderSkuGroup(g), skuGroupName = nameSkuGroup(g), skipRepricingSkuGroup(g) EDITABLE BACKGROUND backgroundShowDataSkipRepricingSkuGroup(g)
    FILTERGROUP inactive FILTER 'Активные' activeSkuGroup(g) 'F5' DEFAULT
    ORDER BY orderSkuGroup(g), skuGroupName
;

DESIGN options {
    repricings {
        NEW repricing {
            caption = 'Автоматическая дооценка';
            MOVE PROPERTY(nameToRepricingOperation());
            MOVE PROPERTY(toRepricingTime());
            MOVE PROPERTY(toRepricingCountDays());
            MOVE PROPERTY(minQuantityToRepricing());
            MOVE PROPERTY(toRepricingTimeTo());
            MOVE PROPERTY(toRepricingCountDaysTo());                       
            MOVE PROPERTY(createExtraValuationAllStocks());
        }
        MOVE skuTree.tree.box;
    }
}

