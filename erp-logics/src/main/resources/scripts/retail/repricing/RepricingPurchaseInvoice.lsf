MODULE RepricingPurchaseInvoice;

REQUIRE PricingPurchase, Repricing, ZReport;

PRIORITY Purchase;

NAMESPACE Repricing;

//------------------- Дооценка с учетом проданного количества между приходом и закачкой прайса в оборудование -----------------------//  
   
FORM extraValuation 'Дооценка'
    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES (d) SELECTOR nameDepartmentStore

    OBJECTS tf=DATETIME FIXED PANEL
    PROPERTIES(tf) OBJVALUE

    OBJECTS tt=DATETIME FIXED PANEL
    PROPERTIES(tt) OBJVALUE
    
    OBJECTS o=Repricing.Operation FIXED PANEL
    PROPERTIES(o) SELECTOR Repricing.nameOperation    

;
DESIGN extraValuation {
    main {
        type = CONTAINERV;

        NEW topContainer {
            type = CONTAINERH;
            MOVE d.box { caption = 'Выберите отдел магазина';}
            MOVE tf.box { caption = 'Дата/время с'; }
            MOVE tt.box { caption = 'Дата/время по'; }
            MOVE o.box { caption = 'Выберите операцию переоценки'; }
        }
        MOVE functions.box;
    }
}     
       
repricingInvoice = DATA Repricing (Invoice);
repricingInvoiceDetail (d) = repricingInvoice(invoiceInvoiceDetail(d));

prevRetailPricingPriceBInvoiceDetail 'Цена розничная до' (detail) = prevRetailPricingPriceBSkuStockDateTime(skuInvoiceDetail(detail), customerStockInvoiceDetail(detail), dateTimeInvoiceDetail(detail));

calcQuantitySoldSkuStockPriceDateTime 'Кол-во продано по старой цене' (sku, stock, price, dateTime) = GROUP SUM 
    signedQuantityReceiptDetail(d) IF dateTimeReceiptDetail(d) >= (dateTime AS DATETIME) AND isPostedReceiptDetail(d)
        BY skuReceiptDetail(d), departmentStoreReceiptDetail(d), priceReceiptDetail(d), dateTime; 
        
toRepricingQuantitySkuStockPriceDateTime 'Кол-во к дооценке' (sku, stock, price, dateTime) = prevBalanceBSkuStockDateTime (sku, stock, dateTime) (-) calcQuantitySoldSkuStockPriceDateTime(sku, stock, price, dateTime);         
toRepricingQuantityInvoiceDetail 'Кол-во к дооценке' (detail) = toRepricingQuantitySkuStockPriceDateTime(skuInvoiceDetail(detail), customerStockInvoiceDetail(detail), prevRetailPricingPriceBInvoiceDetail(detail), shipmentDateTimeInvoiceDetail(detail));
               
filterToRepricingInvoiceStockDateTime 'Фильтр' (invoice, department, timeFrom)= 
    customerStockInvoice(invoice) == department AND         
    shipmentDateTimeInvoice(invoice) >= timeFrom AND NOT
    costLedgerDepartmentStore(department) AND
    isPostedInvoice(invoice) AND NOT
    repricingInvoice(invoice)
; 
 
calcQuantitySoldSkuStockPriceDateTimeFromTo 'Кол-во продано по старой цене' (sku, stock, price, fromDateTime, toDateTime) = GROUP SUM 
    signedQuantityReceiptDetail(d) IF dateTimeReceiptDetail(d) >= (fromDateTime AS DATETIME) AND (dateTimeReceiptDetail(d) <= (toDateTime AS DATETIME)) AND isPostedReceiptDetail(d)
        BY skuReceiptDetail(d), departmentStoreReceiptDetail(d), priceReceiptDetail(d), fromDateTime, toDateTime; 
        
toRepricingQuantitySkuStockPriceDateTimeFromTo 'Кол-во к дооценке' (sku, stock, price, fromDateTime, toDateTime) = prevBalanceBSkuStockDateTime (sku, stock, fromDateTime) (-) calcQuantitySoldSkuStockPriceDateTimeFromTo(sku, stock, price, fromDateTime, toDateTime);         
toRepricingQuantityInvoiceDetailToDateTime 'Кол-во к дооценке' (detail, toDateTime) = toRepricingQuantitySkuStockPriceDateTimeFromTo(skuInvoiceDetail(detail), customerStockInvoiceDetail(detail), prevRetailPricingPriceBInvoiceDetail(detail), shipmentDateTimeInvoiceDetail(detail), toDateTime); 
filterToRepricingInvoiceStockDateTimeFromTo 'Фильтр' (invoice, department, timeFrom, timeTo)= 
    customerStockInvoice(invoice) == department AND         
    shipmentDateTimeInvoice(invoice) >= timeFrom AND 
    shipmentDateTimeInvoice(invoice) <= timeTo AND NOT
    costLedgerDepartmentStore(department) AND
    isPostedInvoice(invoice) AND NOT
    repricingInvoice(invoice)
;                
                  
overExtraValuation = ABSTRACT ACTION LIST (RepricingDetail, InvoiceDetail);  
overExtraRevertValuation = ABSTRACT ACTION LIST (RepricingDetail, InvoiceDetail);

skipExtraValuationSkuStock = ABSTRACT BOOLEAN (Sku, Stock);        
        
maxInvoiceDetailSkuInvoice = GROUP LAST detail 
    BY skuInvoiceDetail(detail), invoiceInvoiceDetail(detail) 
    ORDER quantityInvoiceDetailSkuInvoice(skuInvoiceDetail(detail), invoiceInvoiceDetail(detail)), detail;
       
repricingForm =  DATA LOCAL UserRepricing();                      

skipNegativeRepricing 'Не использовать обратную дооценку' = DATA BOOLEAN ();  

EXTEND FORM options
    PROPERTIES () skipNegativeRepricing
;

DESIGN options {
    repricings {
        MOVE PROPERTY (skipNegativeRepricing());            
    }
}  

overPositiveInvoiceDetail = ABSTRACT ACTION LIST (InvoiceDetail); 
overNegativeInvoiceDetail= ABSTRACT ACTION LIST (InvoiceDetail); 

positiveInvoiceDetail =  DATA LOCAL BOOLEAN (InvoiceDetail);
negativeInvoiceDetail =  DATA LOCAL BOOLEAN (InvoiceDetail);    

keepSupplierPriceInRepricingInvoiceDetailDepartmentStore = ABSTRACT BOOLEAN (InvoiceDetail, DepartmentStore);

toRepricingOperation  = DATA Repricing.Operation ();
nameToRepricingOperation 'Операция для автоматической дооценки' = nameOperation(toRepricingOperation());

toRepricingTime 'Время (с) для автоматической дооценки' = DATA TIME ();
toRepricingCountDays 'Количество дней анализа для автоматической дооценки (с)' = DATA INTEGER ();
toRepricingDateTime 'Дата/время документа (с)' () = dateTimeToDateTime(subtractDate(currentDate(), toRepricingCountDays()), toRepricingTime()); 
minQuantityToRepricing 'Минимальное количество к дооценке' = DATA NUMERIC[14,4] ();

toRepricingTimeTo 'Время (по) для автоматической дооценки' = DATA TIME ();
toRepricingCountDaysTo 'Количество дней анализа для автоматической дооценки (по)' = DATA INTEGER ();
toRepricingDateTimeTo 'Дата/время документа (по)' () = OVERRIDE 2030_01_01_00:00, dateTimeToDateTime(subtractDate(currentDate(), toRepricingCountDaysTo()), toRepricingTimeTo()); 
    
createExtraValuationStockTimeOperationNotForm 'Создать дооценку' = ACTION (department, timeFrom, timeTo, operation) {   
    IF [ = GROUP SUM 1 IF filterToRepricingInvoiceStockDateTimeFromTo (invoice, department, timeFrom, timeTo) BY department, timeFrom, timeTo] (department, timeFrom, timeTo) THEN {
        
        positiveInvoiceDetail(detail) <- NULL;
        positiveInvoiceDetail(detail) <- TRUE WHERE filterToRepricingInvoiceStockDateTimeFromTo(invoiceInvoiceDetail(detail), department, timeFrom, timeTo);
        positiveInvoiceDetail(detail) <- prevRetailPricingPriceBInvoiceDetail(detail) != retailPriceInvoiceDetail(detail) WHERE positiveInvoiceDetail(detail);
        positiveInvoiceDetail(detail) <- toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo) > 0 AND isStockSkuInvoiceDetail(detail) WHERE positiveInvoiceDetail(detail);
        positiveInvoiceDetail(detail) <- maxInvoiceDetailSkuInvoice(skuInvoiceDetail(detail), invoiceInvoiceDetail(detail))==detail WHERE positiveInvoiceDetail(detail);
        positiveInvoiceDetail(detail) <- NULL WHERE skipExtraValuationSkuStock(skuInvoiceDetail(detail), department) AND positiveInvoiceDetail(detail);                     
        positiveInvoiceDetail(detail) <- NULL WHERE positiveInvoiceDetail(detail) AND NOT priceInvoiceDetail(detail);   
        positiveInvoiceDetail(detail) <- NULL WHERE toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo) < minQuantityToRepricing() AND positiveInvoiceDetail(detail); 
          
        FOR positiveInvoiceDetail(detail) DO {
            overPositiveInvoiceDetail(detail);
        }              
    
        negativeInvoiceDetail(detail) <- NULL;
        negativeInvoiceDetail(detail) <- TRUE WHERE filterToRepricingInvoiceStockDateTimeFromTo(invoiceInvoiceDetail(detail), department, timeFrom, timeTo);
        negativeInvoiceDetail(detail) <- prevRetailPricingPriceBInvoiceDetail(detail) != retailPriceInvoiceDetail(detail) WHERE negativeInvoiceDetail(detail);
        negativeInvoiceDetail(detail) <- toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo) < 0 AND isStockSkuInvoiceDetail(detail) WHERE negativeInvoiceDetail(detail);
        negativeInvoiceDetail(detail) <- maxInvoiceDetailSkuInvoice(skuInvoiceDetail(detail), invoiceInvoiceDetail(detail))==detail WHERE negativeInvoiceDetail(detail);
        negativeInvoiceDetail(detail) <- NULL WHERE skipExtraValuationSkuStock(skuInvoiceDetail(detail), department) AND negativeInvoiceDetail(detail);         
        negativeInvoiceDetail(detail) <- NULL WHERE negativeInvoiceDetail(detail) AND NOT priceInvoiceDetail(detail); 
        negativeInvoiceDetail(detail) <- NULL WHERE negativeInvoiceDetail(detail) AND skipNegativeRepricing();
        negativeInvoiceDetail(detail) <- NULL WHERE delta(toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo),0) < minQuantityToRepricing() AND negativeInvoiceDetail(detail);   
        
        FOR negativeInvoiceDetail(detail) DO {
            overNegativeInvoiceDetail(detail);
        }              
                
        IF [ = GROUP SUM 1 IF positiveInvoiceDetail(detail) OR negativeInvoiceDetail(detail) ]() THEN {
            FOR ADDOBJ r = UserRepricing DO {
                departmentStoreUserRepricing(r) <-  department;
                operationUserRepricing(r) <- operation;
                skipChangeLedgerUserRepricing(r) <- TRUE;
                isPostedUserRepricing(r) <- TRUE;
        
                // дооценка в прямую сторону
                FOR positiveInvoiceDetail(detail) ADDOBJ rd = UserRepricingDetail DO {
                        userRepricingUserRepricingDetail(rd) <- r;
                        skuUserRepricingDetail(rd) <- skuInvoiceDetail(detail);
                        quantityUserRepricingDetail(rd) <- toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo);
        
                        curPriceUserRepricingDetail(rd) <- prevSupplierPricingPriceBSkuStockDateTime(skuInvoiceDetail(detail), department, dateTimeInvoiceDetail(detail));
                        curRetailPriceUserRepricingDetail(rd) <- prevRetailPricingPriceBInvoiceDetail(detail);
        
                        priceUserRepricingDetail(rd) <- IF NOT keepSupplierPriceInRepricingInvoiceDetailDepartmentStore(detail, department) THEN priceInvoiceDetail(detail) ELSE curPriceUserRepricingDetail(rd);
                        retailPriceUserRepricingDetail(rd) <- retailPriceInvoiceDetail(detail);                          
        
                        overExtraValuation(rd, detail);
                }
        
                // дооценка в обратную сторону
                FOR negativeInvoiceDetail(detail) ADDOBJ rd = UserRepricingDetail DO {
                        userRepricingUserRepricingDetail(rd) <- r;
                        skuUserRepricingDetail(rd) <- skuInvoiceDetail(detail);
                        quantityUserRepricingDetail(rd) <- - toRepricingQuantityInvoiceDetailToDateTime(detail, timeTo);
        
                        curPriceUserRepricingDetail(rd) <- priceInvoiceDetail(detail);
                        curRetailPriceUserRepricingDetail(rd) <- retailPriceInvoiceDetail(detail);
        
                        priceUserRepricingDetail(rd) <- priceInvoiceDetail(detail);
                        retailPriceUserRepricingDetail(rd) <- prevRetailPricingPriceBInvoiceDetail(detail);                          
        
                        overExtraRevertValuation(rd, detail);
                }
                repricingInvoice(invoice) <- r WHERE filterToRepricingInvoiceStockDateTimeFromTo(invoice, department, timeFrom, timeTo);
                repricingForm() <- r;            
            } 
        }
    }
}                  
                   
createExtraValuationStockTimeOperation 'Создать дооценку' = ACTION  (department, timeFrom, timeTo, operation) NEWSESSION {
    createExtraValuationStockTimeOperationNotForm(department, timeFrom, timeTo, operation);
    FORM userRepricing  OBJECTS p = repricingForm() MANAGESESSION DOCKEDMODAL;  
    repricingForm() <- NULL;      
}                 
                                                    
createExtraValuation 'Создать дооценку' = ACTION () NEWSESSION {
    FORM extraValuation  MODAL;
    IF formResult() == FormResult.ok THEN {        
        createExtraValuationStockTimeOperation(chosenObject('d'), chosenDateTime('tf'), chosenDateTime('tt'), chosenObject('o'));                               
    }
} TOOLBAR ;

EXTEND FORM repricings
    PROPERTIES ()  createExtraValuation TODRAW p
; 
  
createOverExtraValuationAllStocks = ABSTRACT ACTION LIST ();
isOverExtraValuationAllStocks = ABSTRACT BOOLEAN ();

createExtraValuationAllStocks 'Создать автоматическую дооценку' = ACTION () NEWSESSION {
    IF isOverExtraValuationAllStocks() THEN {
        createOverExtraValuationAllStocks();
    } ELSE {   
        FOR department IS DepartmentStore AND isCompanyStock(department) DO {
            createExtraValuationStockTimeOperationNotForm(department, toRepricingDateTime(), toRepricingDateTimeTo(), toRepricingOperation());                                     
        } 
    }
    apply();
}

EXTEND FORM options
    PROPERTIES() nameToRepricingOperation, toRepricingTime, toRepricingCountDays, minQuantityToRepricing, toRepricingTimeTo, toRepricingCountDaysTo, createExtraValuationAllStocks
;

DESIGN options {
    repricings {
        NEW repricing {
            caption = 'Автоматическая дооценка';
            MOVE PROPERTY(nameToRepricingOperation());
            MOVE PROPERTY(toRepricingTime());
            MOVE PROPERTY(toRepricingCountDays());
            MOVE PROPERTY(minQuantityToRepricing());
            MOVE PROPERTY(toRepricingTimeTo());
            MOVE PROPERTY(toRepricingCountDaysTo());                       
            MOVE PROPERTY(createExtraValuationAllStocks());
        }
    }
}

