MODULE RepricingPurchaseInvoiceDashboard;

REQUIRE Dashboard, RepricingPurchaseInvoice, RepricingLabel, RepricingMachinery;

NAMESPACE Repricing;

createExtraValuationNewStockTimeOperation 'Создать дооценку' = ACTION (department, timeFrom, timeTo, operation) NEWSESSION {
    createExtraValuationStockTimeOperation(department, timeFrom, timeTo, operation);   
}

FORM repricingInvoiceDashboard 'Дооценка'
    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES (d) SELECTOR nameDepartmentStore
    FILTERS isCompanyStock(d)
        
    OBJECTS o = Repricing.Operation FIXED PANEL
    PROPERTIES (o) SELECTOR Repricing.nameOperation
    FILTERS inUserOperation(currentUser(), o)

    OBJECTS t=DATETIME FIXED PANEL
    PROPERTIES(t) OBJVALUE        
    
    OBJECTS tt=DATETIME FIXED PANEL
    PROPERTIES(tt) OBJVALUE            
     
    OBJECTS i = Purchase.Invoice
    PROPERTIES (i) READONLY Purchase.isClosedInvoice, Purchase.isPostedInvoice,
                            Purchase.numberInvoice, Purchase.seriesInvoice, Purchase.dateInvoice, Purchase.timeInvoice,
                            Purchase.nameSupplierInvoice, Purchase.nameSupplierStockInvoice, Purchase.nameCustomerInvoice, Purchase.nameCustomerStockInvoice,            
                            Purchase.countInvoiceDetailInvoice, Purchase.quantityInvoiceDetailInvoice, Purchase.sumInvoiceDetailInvoice,
                            Purchase.VATSumInvoiceDetailInvoice, Purchase.invoiceSumInvoiceDetailInvoice
    PROPERTIES (i) Purchase.editInvoice SHOWIF Purchase.isOpenedInvoice(i)
    FILTERGROUP filters FILTER 'Накладные, по которым не было дооценки' filterToRepricingInvoiceStockDateTimeFromTo(i,d,t,tt) 'F9' DEFAULT     
    
    OBJECTS p = Repricing
    PROPERTIES (p) READONLY isPostedRepricing, numberRepricing, seriesRepricing, dateRepricing, timeRepricing, beforeDateRepricing,
                   nameDepartmentStoreRepricing, countRepricingDetailRepricing, diffSumRepricingDetailRepricing,
                   diffMarkupSumRepricingDetailRepricing, diffVATSumRepricingDetailRepricing,
                   diffRetailSumRepricingDetailRepricing, numberDisposalRepricing, skipChangeLedgerRepricing,
                   statusLabelPriceTransactionDocument BACKGROUND backgroundPrintLabelTransactionRepricing(p),
                   statusMachineryPriceTransactionDocument ON SHORTCUT createSnapshotMachineryPriceTransactionRepricing(p) BACKGROUND backgroundCreateMachineryPriceTransactionRepricing(p), 
                   noteRepricing                      
               
    PROPERTIES (p) createRepricingLabelTransaction FORCE PANEL SHOWIF showPrintLabelTransactionRepricing(p), 
                   createSnapshotRepricingLabelTransaction FORCE PANEL
                   
    PROPERTIES(p)  createMachineryPriceTransactionRepricing FORCE PANEL SHOWIF showCreateMachineryPriceTransactionRepricing(p)
    PROPERTIES (p) printRepricing FORCE PANEL TOOLBAR                                           
    PROPERTIES (p) editRepricing SHOWIF isOpenedRepricing(p), ADDFORM
    PROPERTIES (p) deletep=DELETE FORCE PANEL TOOLBAR SHOWIF isUserOpenedRepricing(p)    
               
    PROPERTIES (d,t,tt,o)  createExtraValuationNewStockTimeOperation TODRAW i FORCE PANEL TOOLBAR     
                     
    FILTERGROUP filters2 FILTER 'Переоценки на текущую дату' dateRepricing(p)==currentDate() 'F5' DEFAULT                         
                                               
;
DESIGN repricingInvoiceDashboard {
    main {
        type = CONTAINERV;

        NEW topContainer {
            type = CONTAINERH;
            MOVE d.box;
            MOVE o.box;
            MOVE t.box { caption = 'Время с';}
            MOVE tt.box { caption = 'Время по';}
        }
        MOVE i.box;
        MOVE p.box;
        MOVE functions.box;
    }
}

NAVIGATOR {
    retailDashboardNavigator {
        ADD repricingInvoiceDashboard;
    }
}