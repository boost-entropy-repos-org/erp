MODULE RetailCRMArticle;

REQUIRE RetailCRM, ItemArticle, StockArticle;

PRIORITY Stock;

// article
TABLE promotionConditionArticle (PromotionCondition, Article);
inDataPromotionConditionArticle 'В условии' = DATA BOOLEAN (PromotionCondition, Article);

inPromotionConditionArticle 'В условии' (condition, article) = OVERRIDE
    TRUE IF inParentPromotionConditionSkuGroup(condition, itemGroupArticle(article)),                                                                            
    inDataPromotionConditionArticle(condition, article); 

promotionConditionsArticle 'В акциях' = GROUP CONCAT ',', namePromotionCondition(c) IF inPromotionConditionArticle(c, a)
                                              BY a
                                              ORDER c;
promotionConditionsPromotionConditionArticle 'В других акциях' =
                                        GROUP CONCAT namePromotionCondition(c) IF 
                                                     inDataPromotionConditionArticle(c, a) AND 
                                                     dateToPromotionCondition(c) >= dateFromPromotionCondition(pc) AND
                                                     dateToPromotionCondition(pc) >= dateFromPromotionCondition(c) AND 
                                                     c != pc AS PromotionCondition, 
                                              ','
                                              BY pc, a
                                              ORDER c MINCHARWIDTH 10 PREFCHARWIDTH 20;

overInPromotionConditionSku (condition, sku) += inDataPromotionConditionArticle(condition, articleItem(sku));

nameBalancerStock (stock)= nameStock(stock) + '(остаток)' MINCHARWIDTH 15 PREFCHARWIDTH 20;    

backgroundBalancePromotion (p)= RGB(204,204,255) IF p IS Promotion;

prevCurrentBalanceArticlePromotion 'Итого (остаток)' (ar, sh) = GROUP SUM prevCurrentBalanceArticleStock(ar,ddd) IF inPromotionStore(sh, storeDepartmentStore(ddd)) BY ar, sh;

copyPropertiesPromotionCondition(promotionConditionTo, promotionConditionFrom) += ACTION (promotionConditionTo, promotionConditionFrom) {
    inDataPromotionConditionArticle(promotionConditionTo, article) <- inDataPromotionConditionArticle(promotionConditionFrom, article);        
}

EXTEND FORM promotion
    OBJECTS params = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)

    OBJECTS ddd = Stock FIXED GRID
    FILTERGROUP inactiveStock FILTER 'Активный' 'ctrl F10' activeStock(ddd) DEFAULT
    FILTERS     inPromotionStore(sh, storeDepartmentStore(ddd))
    
    OBJECTS ar = Article
    PROPERTIES(ar)     READONLY idItemGroupArticle, idArticle, captionArticle, prevCurrentBalanceArticle, canonicalNameItemGroupArticle FORCE PANEL,
                       imageArticle FORCE PANEL
    PROPERTIES(sg, ar) READONLY promotionConditionsPromotionConditionArticle
    PROPERTIES(sg, ar)          inPromotionConditionArticle
    PROPERTIES READONLY prevCurrentBalanceArticleStock(ar,ddd) COLUMNS 'astock' (ddd) HEADER nameBalancerStock(ddd) BACKGROUND backgroundBalancePromotion(sh),
                       prevCurrentBalanceArticlePromotion(ar,sh)
    FILTERS                     isParentGroupArticle(skg, ar)

    FILTERGROUP filterArticle
        FILTER 'В акции' 'F10' inPromotionConditionArticle(sg, ar)                        
;

EXTEND DESIGN promotion {
    REMOVE ddd.box;
    row212 {
        type = SPLITH;
        NEW row213 {
            fill = 1;
            type = SPLITV;
            ADD skuTree.tree.box;            
        
            NEW imageBox {
                fill = 1;
                caption = 'Изображение';                     
                ADD PROPERTY(imageArticle(ar)) {
                    caption = '';
                    fill = 1;
                }
            }                        
        }
        NEW filterBox {
            type = CONTAINERV;
            fill = 3.5;
            ADD params.box {
                type = CONTAINERH;
                caption = 'Реализация за период'; 
            } 
            ADD skuPane {
                type = TABBED;
                ADD ar.box;
                ADD sk.box;
            } 
        }
        
    }         
    PROPERTY(prevCurrentBalanceArticleStock(ar,ddd)) { minimumCharWidth = 2; preferredCharWidth = 2;} 
    PROPERTY(prevCurrentBalanceArticlePromotion(ar,sh)) { minimumCharWidth = 4; preferredCharWidth = 4;}  
}
                