MODULE RetailCRMArticle;

REQUIRE RetailCRM, ItemArticle, StockArticle;

PRIORITY Stock;

// article
TABLE promotionConditionArticle (PromotionCondition, Article);
inData 'В условии' = DATA BOOLEAN (PromotionCondition, Article);

 
in 'В условии' (PromotionCondition condition, Article article) = OVERRIDE
    inData(condition, article),                                                                            
    TRUE IF inParent(condition, itemGroup(article)); 

useFullNamePromotionCondition 'Использовать полное имя акции' = DATA BOOLEAN ();

EXTEND FORM options PROPERTIES() useFullNamePromotionCondition;
DESIGN options {
    commons {
        MOVE PROPERTY(useFullNamePromotionCondition());
    }
}

fullName(PromotionCondition c) = IF useFullNamePromotionCondition() THEN CONCAT '/', namePromotion(c), name(c) ELSE namePromotion(c);
promotionConditions 'В акциях' = GROUP CONCAT ',', name(PromotionCondition c) IF in(c, Article a)
                                              BY a
                                              ORDER c;
promotionConditions 'В других акциях' =
                                        GROUP CONCAT fullName(PromotionCondition c) IF 
                                                     inData(c, Article a) AND 
                                                     dateTo(c) >= dateFrom(PromotionCondition pc) AND
                                                     dateTo(pc) >= dateFrom(c) AND 
                                                     c != pc AS PromotionCondition, 
                                              ','
                                              BY pc, a
                                              ORDER c MINCHARWIDTH 10 PREFCHARWIDTH 20;

overIn (PromotionCondition condition, Item sku) += inData(condition, article(sku));

nameBalance (Stock stock)= name(stock) + '(остаток)' MINCHARWIDTH 15 PREFCHARWIDTH 20;    

backgroundBalance (Promotion p)= RGB(204,204,255) IF p IS Promotion;

prevCurrentBalance 'Итого (остаток)' (ar, sh) = GROUP SUM prevCurrentBalance(Article ar,DepartmentStore ddd) IF in(Promotion sh, store(ddd)) BY ar, sh;

copyProperties(PromotionCondition promotionConditionTo, PromotionCondition promotionConditionFrom) += {
    IF copyItems() THEN {
        inData(promotionConditionTo, Article article) <- inData(promotionConditionFrom, article);
    }
}

// ---------------- Импорт из XLS ------------------ //

importArticleXLS 'Импорт (xlsx)' (PromotionCondition c) = {

    LOCAL article = VARSTRING[100] (INTEGER);
    LOCAL price = NUMERIC[16,4] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        IMPORT XLSX TO article = A, price = B FROM f AS EXCELFILE;
        FOR imported(INTEGER r) DO {
            in(c, Article a) <- TRUE WHERE id(a) == article(r);
            resultPrice(c, Sku s) <- price(r) WHERE id(article(s)) == article(r) AND price(r) > 0.0;
        }
        
        MESSAGE 'Импортированы ' + (OVERRIDE [= GROUP SUM 1 IF article(article(INTEGER r))](), 0) + ' записей';
    }
}
          
removeArticleXLS 'Исключить из акции (xlsx)' (PromotionCondition c) = {

    LOCAL article = VARSTRING[100] (INTEGER);
    LOCAL count = INTEGER(); 
    
    INPUT f = EXCELFILE DO {
        IMPORT XLSX TO article = A FROM f AS EXCELFILE;
        count() <- [= GROUP SUM 1 IF in(PromotionCondition c, article(article(INTEGER r))) AND imported(r) BY c](c);

        FOR imported(INTEGER r) DO {
            in(c, Article a) <- NULL WHERE id(a) == article(r);
        }
        
        MESSAGE 'Исключены ' + (OVERRIDE count(), 0) + ' записей';
    }
}                
                
EXTEND FORM promotion
    OBJECTS params = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES objFrom = VALUE(dFrom), objTo = VALUE(dTo)

    OBJECTS ddd = Stock GRID
    FILTERGROUP inactiveStock FILTER 'Активный' active(ddd) 'ctrl F10' DEFAULT
    FILTERS     in(sh, store(ddd))
    
    OBJECTS ar = Article
    PROPERTIES(ar)     READONLY idItemGroup, id, caption, prevCurrentBalance, canonicalNameItemGroup PANEL,
                       image PANEL
    PROPERTIES(sg, ar) READONLY promotionConditions
    PROPERTIES(sg, ar)          in
    PROPERTIES READONLY prevCurrentBalance(ar,ddd) COLUMNS 'astock' (ddd) HEADER nameBalance(ddd) BACKGROUND backgroundBalance(sh),
                       prevCurrentBalance(ar,sh)
    FILTERS                     isParent(skg, ar)
    
    PROPERTIES(sg) TODRAW ar TOOLBAR importArticleXLS, removeArticleXLS 

    FILTERGROUP filterArticle
        FILTER 'В акции' in(sg, ar) 'F10'                        
;

DESIGN promotion {
    REMOVE ddd.box;
    row212 {
        type = SPLITH;
        NEW row213 {
            fill = 1;
            type = SPLITV;
            MOVE skuTree.tree.box;            
        
            NEW imageBox {
                fill = 1;
                caption = 'Изображение';                     
                MOVE PROPERTY(image(ar)) {
                    caption = '';
                    fill = 1;
                }
            }                        
        }
        NEW filterBox {
            type = CONTAINERV;
            fill = 3.5;
            MOVE params.box {
                type = CONTAINERH;
                caption = 'Реализация за период'; 
            } 
            MOVE skuPane {
                type = TABBED;
                NEW articleTab {
                    caption = 'Артикул';
                    fill = 1;
                    type = SPLITV;
                    MOVE ar.box;                    
                }
                MOVE sk.box;
            } 
        }
        
    }         
    PROPERTY(prevCurrentBalance(ar,ddd)) { minimumCharWidth = 2; preferredCharWidth = 2;} 
    PROPERTY(prevCurrentBalance(ar,sh)) { minimumCharWidth = 4; preferredCharWidth = 4;}  
}
                