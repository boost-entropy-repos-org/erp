MODULE DiscountCardInput;

REQUIRE DiscountCard, ZReportDiscountCard;


defaultDiscountLegalEntityGroup  = DATA LegalEntityGroup ();
nameDefaultDiscountLegalEntityGroup 'Группа организация по умолчанию' = name(defaultDiscountLegalEntityGroup());

EXTEND FORM options 
    PROPERTIES nameDefaultDiscountLegalEntityGroup();
    
DESIGN options {
    discountCards {
        MOVE PROPERTY(nameDefaultDiscountLegalEntityGroup());    
    }
}

prevLegalEntity(DiscountCard d) = PREV (legalEntity(d));

add(LegalEntity l, DiscountCard disc) = ACTION {
    legalEntity(disc) <- l;
    ownership(l) <- Ownership.individual;
    legalEntityGroup(l) <- defaultDiscountLegalEntityGroup(); 
}

changeFirstName(DiscountCard disc) = ACTION {
    INPUTX s = VARSTRING[30] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        firstName(LegalEntity l) <- s WHERE l == legalEntity(disc);
    }
}

changeLastName(DiscountCard disc) = ACTION {
    INPUTX s = VARSTRING[30] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        lastName(LegalEntity l) <- s WHERE l == legalEntity(disc);
    }
}

changeAdress(DiscountCard disc) = ACTION {
    INPUTX s = VARSTRING[150] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        dataAddress(LegalEntity l, DATE d) <- s WHERE l == legalEntity(disc) AND d==currentDate();
    }
}

changeEmail(DiscountCard disc) = ACTION {
    INPUTX s = VARSTRING[100] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        email(LegalEntity l) <- s WHERE l == legalEntity(disc);
    }
}

changePhone(DiscountCard disc) = ACTION {
    INPUTX s = VARSTRING[100] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        dataPhone(LegalEntity l, DATE d) <- s WHERE l == legalEntity(disc) AND d==currentDate();
    }
}

changeBirthday(DiscountCard disc) = ACTION {
    INPUTX d = DATE DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        birthday(LegalEntity l) <- d WHERE l == legalEntity(disc);
    }
}

FORM discoundCardInput 'Ввод анкет дисконтных карт'

    OBJECTS d = DiscountCard
    PROPERTIES (d) number READONLY, series READONLY, id SHOWIF showIDs() READONLY, idLegalEntity
    PROPERTIES (d) firstNameContact ON CHANGE changeFirstName(d), lastNameContact ON CHANGE changeLastName(d), addressLegalEntity ON CHANGE changeAdress(d),
                   emailLegalEntity ON CHANGE changeEmail(d), phoneLegalEntity ON CHANGE changePhone(d), birthdayContact ON CHANGE changeBirthday(d)                  
    FILTERGROUP noneLE
        FILTER 'Нет держателя' NOT prevLegalEntity(d) DEFAULT
;

NAVIGATOR {
    retailDashboardNavigator {
        ADD discoundCardInput;
    }
}