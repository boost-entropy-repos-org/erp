MODULE DiscountCard;

REQUIRE Retail, LegalEntity, Numerator, IndividualLegalEntity;

NAMESPACE RetailCRM;

CLASS DiscountCard 'Дисконтная карта';
TABLE discountCard (DiscountCard);
@defineExternalizable(discountCard, VARSTRING[100]);

@defineNumbered(DiscountCard);
@defineNumeratedDefault(DiscountCard, 'Дисконтные карты', 'ДС');

discountSeriesNumber (discountCard) = GROUP AGGR DiscountCard discountCard BY seriesNumber (discountCard) WHERE discountCard IS DiscountCard;
discountNumber (discountCard) = GROUP MAX DiscountCard discountCard BY number(discountCard) IF discountCard IS DiscountCard;

discountString (VARSTRING[28] discountCard) = OVERRIDE discountSeriesNumber(discountCard), discountNumber(discountCard);  
toLong = FORMULA NUMERIC[18,0] 'to_number(REPLACE(($1),\',\',\'.\'), \'999999999999999999\')';
longNumber (DiscountCard d) = toLong(number(d));

// Держатель
legalEntity (discountCard) = DATA LegalEntity(DiscountCard) INDEXED;

overNameLegalEntity = ABSTRACT VARISTRING[150] (DiscountCard);
nameLegalEntity 'Держатель дисконтной карты' (DiscountCard dc) = OVERRIDE overNameLegalEntity(dc), name(legalEntity(dc)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

overFirstNameContact = ABSTRACT VARISTRING[100] (DiscountCard);
firstNameContact 'Имя' (DiscountCard dc) = OVERRIDE overFirstNameContact(dc), firstName(legalEntity(dc)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

overLastNameContact = ABSTRACT VARISTRING[100] (DiscountCard);
lastNameContact 'Фамилия' (DiscountCard dc) = OVERRIDE overLastNameContact(dc), lastName(legalEntity(dc)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

overBirthdayContact = ABSTRACT DATE (DiscountCard);
birthdayContact 'День рождения' (DiscountCard dc) = OVERRIDE overBirthdayContact(dc), birthday(legalEntity(dc)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

overPhoneLegalEntity = ABSTRACT VARISTRING[100] (DiscountCard);
phoneLegalEntity 'Телефон' (DiscountCard dc) = OVERRIDE overPhoneLegalEntity(dc), phone(legalEntity(dc)) MINCHARWIDTH 30 PREFCHARWIDTH 40;

overEmailLegalEntity = ABSTRACT VARISTRING[400] (DiscountCard);
emailLegalEntity 'E-mail' (DiscountCard dc) = OVERRIDE overEmailLegalEntity(dc), email(legalEntity(dc)) MINCHARWIDTH 30 PREFCHARWIDTH 40;

overAddressLegalEntity = ABSTRACT VARISTRING[150] (DiscountCard);
addressLegalEntity 'Адрес' (DiscountCard dc) = OVERRIDE overAddressLegalEntity(dc), address(legalEntity(dc)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;


middleNameHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
numberSexHttpServerContact = ABSTRACT INTEGER (DiscountCard);
skipLoad = ABSTRACT BOOLEAN (DiscountCard);

//тип карты
CLASS DiscountCardType 'Тип дисконтной карты';
TABLE discountCardType (DiscountCardType);

discountCardType = DATA DiscountCardType(DiscountCard);
nameDiscountCardType 'Тип карты' = staticCaption(discountCardType(DiscountCard d));

id 'Код' = DATA VARSTRING[10] (DiscountCardType);
idDiscountCardType 'Тип дисконтной карты' (DiscountCard d) = id(discountCardType(d));

FORM discountCardType 'Тип дисконтной карты'
    OBJECTS t = DiscountCardType PANEL 
    PROPERTIES(t)  staticCaption, id    
    EDIT DiscountCardType OBJECT t
;

FORM discountCardTypes 'Типы дисконтных карт'
    OBJECTS t = DiscountCardType
    PROPERTIES(t) READONLY staticCaption, id
    PROPERTIES(t) NEWSESSION NEW, EDIT, deletet = DELETE 
    
    LIST DiscountCardType OBJECT t
;

NAVIGATOR {
    retailMasterData {
        ADD discountCardTypes;
    }
}

// Сроки действия
GROUP discountCardDate 'Срок действия' : base;
date 'Дата выдачи' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;
date (DiscountCard discountCard) <- currentDate() WHEN SET(discountCard IS DiscountCard);

dateTo 'Дата окончания действия' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;

name 'Наименование' = DATA VARSTRING[100] (DiscountCard);
initialSum 'Начальная сумма' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

percent 'Процент скидки' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

// Генерация дисконтных карт
FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=Numerator  PANEL
    PROPERTIES(n) SELECTOR name

    OBJECTS quantityCards=INTEGER PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = VALUE;

DESIGN generationDiscountCards {
    main{
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
    }
}

overGenerateDiscountCard = ABSTRACT LIST (DiscountCard);
generateDiscountCards 'Сгенерировать дисконтные карты'() = {
    DIALOG generationDiscountCards OBJECTS n=defaultNumeratorDiscountCard(), n INPUT, quantityCards INPUT CHECK DO {
        LOCAL num = INTEGER();
        num() <- 0;
        WHILE num() < quantityCards DO {
            NEW d = DiscountCard {
                // todo : делается влоб, поскольку numerator сейчас не умеет генерировать сразу несколько объектов
                numerator(d) <- n;
                number(d) <- curStringValue(numerator(d));
                series(d) <- series(numerator(d));
                curValue(Numerator numerator) <- curValue(numerator) + 1 WHERE numerator == numerator(d);
                num() <- num() + 1;
                overGenerateDiscountCard(d);
            }
        }
    }
}

// Формы по работе с дисконтными картами
FORM discountCard 'Дисконтная карта'
    OBJECTS d=DiscountCard PANEL
    PROPERTIES(d) id SHOWIF showIDs(), name, initialSum, percent, nameNumerator, number, series, date, dateTo,
                  firstNameContact, lastNameContact, phoneLegalEntity, emailLegalEntity,          
                  addressLegalEntity, birthdayContact, nameDiscountCardType

    EDIT DiscountCard OBJECT d
;

DESIGN discountCard {
    main{
        preferredSize = (1024, 768);
        d.box {
            d.panel {
                type = CONTAINERV;
                NEW row1 {
                    NEW common {
                        type = CONTAINERH;
                        caption = 'Общие';
                        MOVE PROPERTY(id(d));
                        MOVE PROPERTY(name(d));
                        MOVE PROPERTY(initialSum(d));
                        MOVE PROPERTY(percent(d));
                        MOVE PROPERTY(nameDiscountCardType(d));
                    }
                }
                NEW row2 {
                    type = CONTAINERH;
                    MOVE d.numbered;
                    MOVE d.discountCardDate;
                }
                NEW row3 {
                    caption = 'Держатель';
                    type = COLUMNS;
                    columns = 3;
                    MOVE PROPERTY(firstNameContact(d));
                    MOVE PROPERTY(lastNameContact(d));
                    MOVE PROPERTY(phoneLegalEntity(d));
                    MOVE PROPERTY(emailLegalEntity(d));
                    MOVE PROPERTY(addressLegalEntity(d));
                    MOVE PROPERTY(birthdayContact(d));
                }
            }
        }
        NEW tabContainer {
            fill = 1;
            type = TABBED;
        }
        MOVE functions.box;
    }
}

FORM discountCardDialog 'Дисконтные карты'

    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY number, series, id SHOWIF showIDs(), nameDiscountCardType, nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity,  addressLegalEntity,
                           firstNameContact, lastNameContact, birthdayContact, 
                           name, initialSum, percent, date, dateTo

    LIST DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d=DiscountCard
    PROPERTIES(d) READONLYIF isReadonly() number, series, id SHOWIF showIDs(), nameDiscountCardType, nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity, addressLegalEntity, 
                           firstNameContact, lastNameContact, birthdayContact, 
                           date, dateTo, name, initialSum, percent
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE 

    ORDER BY number(d)

    PROPERTIES() generateDiscountCards TODRAW d TOOLBAR 
;
@extendFormEditable(discountCards);

DESIGN discountCards {
    main {
            MOVE d.box;
            NEW tabbed {
                fill = 1;
                type = TABBED;
            };
            MOVE functions.box;
        }
}

NAVIGATOR {
    retailMasterData {
        ADD discountCards;
    }
}

DESIGN options {
    pane {        
        NEW discountCards {
            caption = 'Дисконтные карты';
        }
    }
}