MODULE DiscountCard;

REQUIRE Retail, LegalEntity, Numerator, IndividualLegalEntity;

NAMESPACE RetailCRM;

CLASS DiscountCard 'Дисконтная карта';
TABLE discountCard (DiscountCard);
@defineExternalizable(discountCard, VARSTRING[100]);

@defineNumbered(DiscountCard);
@defineNumeratedDefault(DiscountCard, 'Дисконтные карты', 'ДС');

discountSeriesNumber (discountCard) = GROUP AGGR DiscountCard discountCard BY seriesNumber (discountCard) WHERE discountCard IS DiscountCard;
discountNumber (discountCard) = GROUP MAX DiscountCard discountCard BY number(discountCard) IF discountCard IS DiscountCard;

discountString (VARSTRING[28] discountCard) = OVERRIDE discountNumber(discountCard), discountSeriesNumber(discountCard);  
toLong = FORMULA NUMERIC[18,0] 'to_number(REPLACE(($1),\',\',\'.\'), \'999999999999999999\')';
longNumber (DiscountCard d) = toLong(number(d));

// Держатель
legalEntity (discountCard) = DATA LegalEntity(DiscountCard) INDEXED;
phoneLegalEntity 'Телефон' (DiscountCard discountCard) = phone(legalEntity(discountCard));
emailLegalEntity 'E-mail' (DiscountCard discountCard) = email(legalEntity(discountCard));
idLegalEntityGroup 'Группа держателя дисконтной карты' (DiscountCard discountCard) = id(legalEntityGroup(legalEntity(discountCard)));
idLegalEntity 'Держатель дисконтной карты' (DiscountCard discountCard) = id(legalEntity(discountCard));
nameLegalEntity 'Держатель дисконтной карты' (DiscountCard discountCard) = name(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
addressLegalEntity 'Адрес' (DiscountCard discountCard) = address(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
firstNameContact 'Имя' (DiscountCard discountCard) = firstName(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
lastNameContact 'Фамилия' (DiscountCard discountCard) = lastName(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
birthdayContact 'День рождения' (DiscountCard discountCard) = birthday(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
postAddressContact 'Почтовый адрес' (DiscountCard discountCard) = postAddress[Contact](legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

type 'Тип дисконтной карты' = ABSTRACT VARSTRING[10] (DiscountCard);

firstNameHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
lastNameHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
middleNameHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
birthdayHttpServerContact = ABSTRACT DATE (DiscountCard);
numberSexHttpServerContact = ABSTRACT INTEGER (DiscountCard);
cityHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
streetHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
phoneHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
emailHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
agreeSubscribeHttpServerContact = ABSTRACT BOOLEAN (DiscountCard);
skipLoad = ABSTRACT BOOLEAN (DiscountCard);

// Сроки действия
GROUP discountCardDate 'Срок действия' : base;
date 'Дата выдачи' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;
date (DiscountCard discountCard) <- currentDate() WHEN SET(discountCard IS DiscountCard);

dateTo 'Дата окончания действия' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;

name 'Наименование' = DATA VARSTRING[100] (DiscountCard);
initialSum 'Начальная сумма' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

percent 'Процент скидки' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

// Генерация дисконтных карт
FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=Numerator  FIXED PANEL
    PROPERTIES(n) SELECTOR name

    OBJECTS quantityCards=INTEGER FIXED PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = OBJVALUE;

DESIGN generationDiscountCards {
    main{
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
    }
}

overGenerateDiscountCard = ACTION ABSTRACT LIST (DiscountCard);
generateDiscountCards 'Сгенерировать дисконтные карты'() = ACTION {
    FORM generationDiscountCards OBJECTS n=defaultNumeratorDiscountCard() MODAL CHECK;
    IF formResult() == FormResult.ok THEN {
        LOCAL num = INTEGER();
        num() <- 0;
        WHILE num() < chosenInteger('quantityCards') DO {
            FOR ADDOBJ d = DiscountCard DO {
                // todo : делается влоб, поскольку numerator сейчас не умеет генерировать сразу несколько объектов
                numerator(d) <- chosenObject('n');
                number(d) <- curStringValue(numerator(d));
                series(d) <- series(numerator(d));
                curValue(Numerator numerator) <- curValue(numerator) + 1 WHERE numerator == numerator(d);
                num() <- num() + 1;
                overGenerateDiscountCard(d);
            }
        }
    }
}

// Формы по работе с дисконтными картами
FORM discountCard 'Дисконтная карта'
    OBJECTS d=DiscountCard FIXED PANEL
    PROPERTIES(d) id SHOWIF showIDs(), name, initialSum, percent,
                  nameNumerator, number, series,
                  date, dateTo,
                  firstNameContact, lastNameContact, phoneLegalEntity, emailLegalEntity,                  
                  addressLegalEntity, birthdayContact 

    EDIT DiscountCard OBJECT d
;

DESIGN discountCard {
    main{
        preferredSize = (1024, 768);
        d.box {
            d.panel {
                type = CONTAINERV;
                NEW row1 {
                    NEW common {
                        type = CONTAINERH;
                        caption = 'Общие';
                        MOVE PROPERTY(id(d));
                        MOVE PROPERTY(name(d));
                        MOVE PROPERTY(initialSum(d));
                        MOVE PROPERTY(percent(d));
                    }
                }
                NEW row2 {
                    type = CONTAINERH;
                    MOVE d.numbered;
                    MOVE d.discountCardDate;
                }
                NEW row3 {
                    caption = 'Держатель';
                    type = COLUMNS;
                    columns = 3;
                    MOVE PROPERTY(firstNameContact(d));
                    MOVE PROPERTY(lastNameContact(d));
                    MOVE PROPERTY(phoneLegalEntity(d));
                    MOVE PROPERTY(emailLegalEntity(d));
                    MOVE PROPERTY(addressLegalEntity(d));
                    MOVE PROPERTY(birthdayContact(d));
                }
            }
        }
        NEW tabContainer {
            fill = 1;
            type = TABBED;
        }
        MOVE functions.box;
    }
}

FORM discountCardDialog 'Дисконтные карты'

    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY number, series, id SHOWIF showIDs(), nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity,  addressLegalEntity,
                           firstNameContact, lastNameContact, birthdayContact, 
                           name, initialSum, percent, date, dateTo

    DIALOG DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d=DiscountCard
    PROPERTIES(d) READONLYIF isReadonly() number, series, id SHOWIF showIDs(), nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity, addressLegalEntity, 
                           firstNameContact, lastNameContact, birthdayContact, 
                           date, dateTo, name, initialSum, percent
    PROPERTIES(d) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    ORDER BY series(d), number(d)

    PROPERTIES() generateDiscountCards TODRAW d FORCE PANEL TOOLBAR 
;
@extendFormEditable(discountCards);

DESIGN discountCards {
    main {
            MOVE d.box;
            NEW tabbed {
                fill = 1;
                type = TABBED;
            };
            MOVE functions.box;
        }
}

NAVIGATOR {
    retailMasterData {
        ADD discountCards;
    }
}

DESIGN options {
    pane {        
        NEW discountCards {
            caption = 'Дисконтные карты';
        }
    }
}