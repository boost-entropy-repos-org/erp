MODULE DiscountCard;

REQUIRE Retail, LegalEntity, Numerator, IndividualLegalEntity;

NAMESPACE RetailCRM;

CLASS DiscountCard 'Дисконтная карта';
TABLE discountCard (DiscountCard);
@defineExternalizable(discountCard, VARSTRING[100]);

@defineNumbered(DiscountCard);
@defineNumeratedDefault(DiscountCard, 'Дисконтные карты', 'ДС');

discountSeriesNumber (discountCard) = GROUP AGGR DiscountCard discountCard BY seriesNumber (discountCard) WHERE discountCard IS DiscountCard;
discountNumber (discountCard) = GROUP MAX DiscountCard discountCard BY number(discountCard) IF discountCard IS DiscountCard;

discountString (VARSTRING[28] discountCard) = OVERRIDE discountNumber(discountCard), discountSeriesNumber(discountCard);  
intId (DiscountCard d) = toInteger(id(d));

// Держатель
legalEntity (discountCard) = DATA LegalEntity(DiscountCard) INDEXED;
phoneLegalEntity 'Телефон' (DiscountCard discountCard) = phone(legalEntity(discountCard));
emailLegalEntity 'E-mail' (DiscountCard discountCard) = email(legalEntity(discountCard));
idLegalEntityGroup 'Группа держателя дисконтной карты' (DiscountCard discountCard) = id(legalEntityGroup(legalEntity(discountCard)));
idLegalEntity 'Держатель дисконтной карты' (DiscountCard discountCard) = id(legalEntity(discountCard));
nameLegalEntity 'Держатель дисконтной карты' (DiscountCard discountCard) = name(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
addressLegalEntity 'Юридический адрес' (DiscountCard discountCard) = address(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
firstNameContact 'Имя' (DiscountCard discountCard) = firstName(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
lastNameContact 'Фамилия' (DiscountCard discountCard) = lastName(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
birthdayContact 'День рождения' (DiscountCard discountCard) = birthday(legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;
postAddressContact 'Почтовый адрес' (DiscountCard discountCard) = postAddress[Contact](legalEntity(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

// Сроки действия
GROUP discountCardDate 'Срок действия' : base;
date 'Дата выдачи' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;
date (DiscountCard discountCard) <- currentDate() WHEN SET(discountCard IS DiscountCard);

dateTo 'Дата окончания действия' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;

name 'Наименование' = DATA VARSTRING[100] (DiscountCard);
initialSum 'Начальная сумма' (discountCard) = DATA NUMERIC[16,2] (DiscountCard);

percent 'Процент скидки' (discountCard) = DATA NUMERIC[16,2] (DiscountCard);

// Генерация дисконтных карт
FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=Numerator  FIXED PANEL
    PROPERTIES(n) SELECTOR name

    OBJECTS quantityCards=INTEGER FIXED PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = OBJVALUE;

DESIGN generationDiscountCards {
    main{
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
    }
}

overGenerateDiscountCard = ACTION ABSTRACT LIST (DiscountCard);
generateDiscountCards 'Сгенерировать дисконтные карты'() = ACTION {
    FORM generationDiscountCards OBJECTS n=defaultNumeratorDiscountCard() MODAL CHECK;
    IF formResult() == FormResult.ok THEN {
        LOCAL num = INTEGER();
        num() <- 0;
        WHILE num() < chosenInteger('quantityCards') DO {
            FOR ADDOBJ d = DiscountCard DO {
                // todo : делается влоб, поскольку numerator сейчас не умеет генерировать сразу несколько объектов
                numerator(d) <- chosenObject('n');
                number(d) <- curStringValue(numerator(d));
                series(d) <- series(numerator(d));
                curValue(Numerator numerator) <- curValue(numerator) + 1 WHERE numerator == numerator(d);
                num() <- num() + 1;
                overGenerateDiscountCard(d);
            }
        }
    }
} TOOLBAR;

// Формы по работе с дисконтными картами
FORM discountCard 'Дисконтная карта'
    OBJECTS d=DiscountCard FIXED PANEL
    PROPERTIES(d) nameLegalEntity, phoneLegalEntity, emailLegalEntity, 
                  addressLegalEntity, firstNameContact, lastNameContact, 
                  birthdayContact, postAddressContact, id SHOWIF showIDs(), 
                  name, initialSum, percent, nameNumerator, number, series,
                  date, dateTo

    EDIT DiscountCard OBJECT d
;

DESIGN discountCard {
    main{
        preferredSize = (1024, 768);
        d.box {
            d.panel {
                type = CONTAINERV;
                NEW row1 {
                    type = CONTAINERH;
                    MOVE PROPERTY(nameLegalEntity(d)) {
                        font = '24';
                    }
                    MOVE PROPERTY(initialSum(d)) {
                        font = '24';
                    }                    
                }
                NEW row2 {
                    type = CONTAINERH;
                    NEW row21{
                        caption = 'Код';
                        MOVE PROPERTY(id(d));
                    }
                    MOVE d.numbered;
                    MOVE d.discountCardDate;
                }
            }
        }
        NEW tabContainer {
            fill = 1;
            type = TABBED;
        }
        MOVE functions.box;
    }
}

FORM discountCardDialog 'Дисконтные карты'

    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY number, series, id SHOWIF showIDs(), nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity,  addressLegalEntity,
                           firstNameContact, lastNameContact, birthdayContact, 
                           name, initialSum, percent, date, dateTo

    DIALOG DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d=DiscountCard
    PROPERTIES(d) READONLYIF isReadonly() number, series, id SHOWIF showIDs(), nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity, addressLegalEntity, 
                           firstNameContact, lastNameContact, birthdayContact, 
                           date, dateTo, name, initialSum, percent
    PROPERTIES(d) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    ORDER BY series(d), number(d)

    PROPERTIES() generateDiscountCards TODRAW d
;
@extendFormEditable(discountCards);

NAVIGATOR {
    retailMasterData {
        ADD discountCards;
    }
}

DESIGN options {
    pane {        
        NEW discountCards {
            caption = 'Дисконтные карты';
        }
    }
}