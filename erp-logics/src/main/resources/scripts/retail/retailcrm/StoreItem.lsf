MODULE StoreItem;

REQUIRE StoreSkuLedger, Item;

@defineItemGroupDefaultValue(notPromotion, 'Не использовать в акциях', BOOLEAN);

dataNotPromotionItem 'Не использовать в акциях' = DATA BOOLEAN (Item);
notPromotionItem 'Не использовать в акциях' (item) = OVERRIDE notPromotionItemGroup(itemGroupItem(item)), dataNotPromotionItem(item) IN itemBase;
notPromotionSku (sku) += notPromotionItem(sku);
notPromotionSkuBarcode (barcode) = notPromotionSku(skuBarcode(barcode));

quantityChildWithNotPromotionItemGroup (itemGroup) = GROUP SUM 1 IF dataNotPromotionItemGroup(childItemGroup) AND isParentItemGroupItemGroup(childItemGroup, itemGroup) BY itemGroup PERSISTENT;
quantityParentWithNotPromotionItemGroup (itemGroup) = GROUP SUM 1 IF dataNotPromotionItemGroup(parentItemGroup) AND isParentItemGroupItemGroup(itemGroup, parentItemGroup) BY itemGroup PERSISTENT;

showNotPromotionItemGroup 'Не использовать в акциях' (itemGroup) = TRUE IF (quantityChildWithNotPromotionItemGroup (itemGroup) (+) quantityParentWithNotPromotionItemGroup (itemGroup)) > 0 PERSISTENT;

backgroundShowNotPromotionItemGroup (itemGroup) =
    IF dataNotPromotionItemGroup(itemGroup) THEN
        RGB(0,0,0) IF itemGroup IS ItemGroup
    ELSE
        RGB(203,203,206) IF quantityChildWithNotPromotionItemGroup (itemGroup) != descendantNumberItemGroup(itemGroup)
                         AND NOT quantityParentWithNotPromotionItemGroup (itemGroup);
                             
setNullShowNotPromotionItemGroup 'Снять признак для всех потомков' = ACTION (itemGroup) {
    FOR isParentItemGroupItemGroup (childGroup, itemGroup) DO {
        ASSIGN dataNotPromotionItemGroup(childGroup) <- NULL;
    }
} SHORTCUT dataNotPromotionItemGroup CONFIRM;                                 

EXTEND FORM itemGroup PROPERTIES(g) dataNotPromotionItemGroup;
EXTEND FORM itemGroups PROPERTIES(g) READONLYIF  isReadonly() showNotPromotionItemGroup BACKGROUND backgroundShowNotPromotionItemGroup(g) BEFORE deleteg;
EXTEND FORM item PROPERTIES(i) notPromotionItem;
EXTEND FORM items PROPERTIES(i) READONLYIF isReadonly() BEFORE deletei notPromotionItem;


