MODULE RetailCRM;

REQUIRE System,
        Time,
        Retail,
        Stock,
        Barcode,
        Store,
        Numerator,
        LegalEntity,
        Document,
        PriceListType;

PRIORITY Stock;

//--------------------------------------Дисконтные карточки------------------------------------------------------------------//
CLASS DiscountCard 'Дисконтная карта';
TABLE discountCard (DiscountCard);
@defineExternalizable(discountCard, VARSTRING[100]);

@defineNumbered(DiscountCard);

@defineNumeratedDefault(DiscountCard, 'Дисконтные карты', 'ДС');

CLASS ABSTRACT DiscountSkuGroup 'Дисконтная группа товаров';

nameDiscountSkuGroup 'Наименование' = ABSTRACT VARISTRING[150](DiscountSkuGroup);

discountCardSeriesNumber (discountCard) = GROUP AGGR discountCard BY seriesNumberDiscountCard (discountCard) WHERE discountCard IS DiscountCard;
discountCardNumber (discountCard) = GROUP MAX discountCard BY numberDiscountCard(discountCard) IF discountCard IS DiscountCard;

legalEntityDiscountCard (discountCard) = DATA LegalEntity(DiscountCard);
nameLegalEntityDiscountCard 'Держатель дисконтной карты' (discountCard) = nameLegalEntity(legalEntityDiscountCard(discountCard)) IN recognize MINCHARWIDTH 30 PREFCHARWIDTH 40;

GROUP discountCardDate 'Срок действия' : base;
dateDiscountCard 'Дата выдачи' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;
dateDiscountCard (discountCard) <- currentDate() WHEN SET(discountCard IS DiscountCard);

dateToDiscountCard 'Дата окончания действия' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;

TABLE discountSkuGroupSku(DiscountSkuGroup, Sku);
inDiscountSkuGroupSku 'Вкл' = ABSTRACT BOOLEAN (DiscountSkuGroup, Sku) PERSISTENT;

initialSumDiscountCard 'Начальная сумма' (discountCard) = DATA NUMERIC[16,2] (DiscountCard);

priceListTypeDiscountCard = DATA PriceListType (DiscountCard);
namePriceListTypeDiscountCard 'Вид цен для дисконтных карт' (DiscountCard) = namePriceListType(priceListTypeDiscountCard(DiscountCard));

// накопительная сумма  : потом посчитать ее по карте

FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=Numerator  FIXED PANEL
    PROPERTIES(n) SELECTOR nameNumerator

    OBJECTS quantityCards=INTEGER FIXED PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = OBJVALUE;

DESIGN generationDiscountCards FROM DEFAULT {
    main{
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
    }
}

generateDiscountCards 'Сгенерировать дисконтные карты' = ACTION() {
    FORM generationDiscountCards OBJECTS n=defaultNumeratorDiscountCard() MODAL CHECK;
    IF formResult() == FormResult.ok THEN {
        LOCAL num = INTEGER();
        ASSIGN num() <- 0;
        WHILE num() < chosenInteger('quantityCards') DO {
            FOR ADDOBJ d = DiscountCard DO {
                // todo : делается влоб, поскольку numerator сейчас не умеет генерировать сразу несколько объектов
                ASSIGN numeratorDiscountCard(d) <- chosenObject('n');
                ASSIGN numberDiscountCard(d) <- curStringValueNumerator(numeratorDiscountCard(d));
                ASSIGN seriesDiscountCard(d) <- seriesNumerator(numeratorDiscountCard(d));
                ASSIGN curValueNumerator(numerator) <- curValueNumerator(numerator) + 1 WHERE numerator == numeratorDiscountCard(d);
                ASSIGN num() <- num() + 1;
            }
        }
    }
} TOOLBAR;

FORM discountCard 'Дисконтная карта'
    OBJECTS d=DiscountCard FIXED PANEL
    PROPERTIES(d) nameLegalEntityDiscountCard, idDiscountCard SHOWIF showIDs(), initialSumDiscountCard,
                  nameNumeratorDiscountCard, numberDiscountCard, seriesDiscountCard,
                  dateDiscountCard, dateToDiscountCard, namePriceListTypeDiscountCard

    EDIT DiscountCard OBJECT d
;

DESIGN discountCard FROM DEFAULT {
    main{
        preferredSize = (1024, 768);
        d.box {
            d.panel {
                type = CONTAINERV;
                NEW row1 {
                    type = CONTAINERH;
                    ADD PROPERTY(nameLegalEntityDiscountCard) {
                        font = '24';
                    }
                    ADD PROPERTY(initialSumDiscountCard) {
                        font = '24';
                    }                    
                    ADD PROPERTY(namePriceListTypeDiscountCard) {
                        font = '24';
                    }
                }
                NEW row2 {
                    type = CONTAINERH;
                    NEW row21{
                        caption = 'Код';
                        ADD PROPERTY(idDiscountCard);
                    }
                    ADD d.numbered;
                    ADD d.discountCardDate;
                }
            }
        }
    }
}

FORM discountCardDialog 'Дисконтные карты'

    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY numberDiscountCard, seriesDiscountCard, idDiscountCard SHOWIF showIDs(), nameLegalEntityDiscountCard, initialSumDiscountCard,
                         namePriceListTypeDiscountCard, dateDiscountCard, dateToDiscountCard

    DIALOG DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d=DiscountCard
    PROPERTIES(d) READONLY numberDiscountCard, seriesDiscountCard, idDiscountCard SHOWIF showIDs(), nameLegalEntityDiscountCard, 
                           namePriceListTypeDiscountCard, dateDiscountCard, dateToDiscountCard, initialSumDiscountCard
    PROPERTIES(d) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    ORDER BY seriesDiscountCard, numberDiscountCard

    PROPERTIES() generateDiscountCards TODRAW d
;

// todo: сделать генерацию штрих-кода для дисконтных карт

//-------------------------- Группы акций ----------------------------------------//

CLASS PromotionGroup 'Группа акций';
TABLE promotionGroup (PromotionGroup);

namePromotionGroup 'Наименование' = DATA VARISTRING[100](PromotionGroup) IN recognize;

//--------------------------Акции----------------------------------------//

CLASS Promotion 'Акция';
TABLE promotion (Promotion);

@defineCreated(Promotion);
@defineNumbered(Promotion);

@defineNumeratedDefault(Promotion, 'Акции', 'АК');

namePromotion 'Наименование' = DATA VARISTRING[100](Promotion) IN recognize;

promotionGroupPromotion (promotion) = DATA PromotionGroup(Promotion) AUTOSET NOT NULL;
namePromotionGroupPromotion 'Группа акций' (promotion) = namePromotionGroup(promotionGroupPromotion(promotion)) IN base;

userOrderPromotion 'Порядок' (promotion) = DATA INTEGER (Promotion);
orderPromotion 'Порядок' (promotion) = OVERRIDE 1 IF promotion IS Promotion, userOrderPromotion(promotion) PERSISTENT;

userSetPromotion 'Задается кассиром' (promotion) = DATA BOOLEAN (Promotion);
// ----------------------------- Условия акций ------------------------ //

CLASS PromotionCondition 'Условие акции';
TABLE promotionCondition (PromotionCondition);

GROUP retailCRM 'Реквизиты' : recognize;

@defineExternalizable(promotionCondition, VARSTRING[100]);

namePromotionCondition 'Наименование' = DATA VARISTRING[200](PromotionCondition);

dateFromPromotion 'ДАТА ОТ:' = DATA DATE (Promotion) IN retailCRM NOT NULL;
dateToPromotion 'ДАТА ДО:' = DATA DATE (Promotion) IN retailCRM NOT NULL;

timeOfFromPromotion 'ВРЕМЯ ОТ:' =  DATA TIME (Promotion) IN retailCRM;
timeOfToPromotion 'ВРЕМЯ ДО:' =  DATA TIME (Promotion) IN retailCRM;

TABLE promotionDOW (Promotion, DOW);
skipPromotionDOW 'Исключить' = DATA BOOLEAN (Promotion, DOW);

allStoresPromotion 'В акции' = DATA BOOLEAN (Promotion);
allStoresPromotion(promotion) <- TRUE WHEN SET(promotion IS Promotion);

TABLE promotionChainStores(Promotion, ChainStores);
dataInPromotionChainStores 'В акции' = DATA BOOLEAN (Promotion, ChainStores);

TABLE promotionStoreType(Promotion, StoreType);
dataInPromotionStoreType 'В акции' = DATA BOOLEAN (Promotion, StoreType);

TABLE promotionStore(Promotion, Store);
dataInPromotionStore 'В акции' = DATA BOOLEAN (Promotion, Store);

inPromotionChainStores 'В акции' (promotion, chainStores) = OVERRIDE
    allStoresPromotion(promotion) AND chainStores IS ChainStores,
    dataInPromotionChainStores(promotion, chainStores) PERSISTENT;

inPromotionStoreType 'В акции' (promotion, storeType) = OVERRIDE
    inPromotionChainStores(promotion, chainStoresStoreType(storeType)),
    dataInPromotionStoreType(promotion, storeType) PERSISTENT;

inPromotionStore 'В акции' (promotion, store) =
    (OVERRIDE inPromotionStoreType(promotion, storeTypeStore(store)),
              dataInPromotionStore(promotion, store))
    AND isCompanyStore(store);

storesPromotion 'Магазины' (promotion) = GROUP CONCAT nameStore(store) IF inPromotionStore(promotion, store), ', '
                                              BY promotion
                                              ORDER store PERSISTENT MINCHARWIDTH 20 PREFCHARWIDTH 40;

minSumBillPromotion 'Сумма чека (от)' (promotion) = DATA NUMERIC[14,2] (Promotion);
maxSumBillPromotion 'Сумма чека (до)' (promotion) = DATA NUMERIC[14,2] (Promotion);

hasDiscountCardPromotion 'Есть дисконтная карта' = DATA BOOLEAN (Promotion);
noDiscountCardPromotion 'Нет дисконтной карты' = DATA BOOLEAN (Promotion);
allDiscountCardPromotion 'Любая дисконтная карта' = DATA BOOLEAN (Promotion);

TABLE promotionDiscountCard (Promotion, DiscountCard);
inDataPromotionDiscountCard 'В акции' = DATA BOOLEAN (Promotion, DiscountCard);

inPromotionDiscountCard 'В акции' (promotion, discountCard) = OVERRIDE allDiscountCardPromotion(promotion) AND discountCard IS DiscountCard,
                                                                       inDataPromotionDiscountCard(promotion, discountCard);

minCumulativeSumPromotion 'Накоп. сумма (от)' (promotion) = DATA NUMERIC[14,2] (Promotion);
maxCumulativeSumPromotion 'Накоп. сумма (до)' (promotion) = DATA NUMERIC[14,2] (Promotion);

useCurrentReceiptPromotion 'Учитывать текущий чек' (promotion) = DATA BOOLEAN (Promotion);

minCumulativeSumPromotion(promotion) => hasDiscountCardPromotion (promotion) RESOLVE FALSE;
maxCumulativeSumPromotion(promotion) => hasDiscountCardPromotion (promotion) RESOLVE FALSE;
allDiscountCardPromotion(promotion) => hasDiscountCardPromotion (promotion) RESOLVE FALSE;

promotionPromotionCondition = DATA Promotion (PromotionCondition) NOT NULL DELETE;
namePromotionPromotionCondition 'Акция' (promotionCondition) = namePromotion(promotionPromotionCondition(promotionCondition));
dateFromPromotionCondition 'Действует (от)' (promotionCondition) = dateFromPromotion(promotionPromotionCondition(promotionCondition));
dateToPromotionCondition 'Действует (до)' (promotionCondition) = dateToPromotion(promotionPromotionCondition(promotionCondition));

countPromotionConditionPromotion 'Кол-во условий' (promotion) = GROUP SUM 1 IF promotionPromotionCondition(promotionCondition) == promotion BY promotion PERSISTENT;

minQuantityPromotionCondition 'Количество (от)' = DATA NUMERIC[14,3] (PromotionCondition);
CONSTRAINT minQuantityPromotionCondition(promotionCondition) <= 0.0 MESSAGE 'Количество в условии акции должно быть строго больше 0';

minSumPromotionCondition 'Сумма (от)' = DATA NUMERIC[16,2] (PromotionCondition);
CONSTRAINT minSumPromotionCondition(promotionCondition) <= 0.0 MESSAGE 'Сумма в условии акции должна быть строго больше 0';

quantityDiscountPromotionCondition 'Кол-во товаров со скидкой' = DATA NUMERIC[14,3] (PromotionCondition) NOT NULL;
percentPromotionCondition 'Процент скидки'  = DATA NUMERIC[8,3] (PromotionCondition);
resultPricePromotionCondition 'Цена со скидкой' = DATA NUMERIC[14,2] (PromotionCondition);
sumDiscountPromotionCondition 'Сумма скидки' (promotionCondition) = DATA NUMERIC[14,2] (PromotionCondition);
sumReceiptPromotionCondition 'Сумма чека' (promotionCondition) = DATA NUMERIC[14,2] (PromotionCondition);

priceListTypePromotionCondition 'Вид цены' (promotionCondition) = DATA PriceListType (PromotionCondition);
namePriceListTypePromotionCondition 'Вид цены' (promotionCondition) = namePriceListType(priceListTypePromotionCondition(promotionCondition));

userSetPromotionCondition 'Задается кассиром' (promotionCondition) = userSetPromotion(promotionPromotionCondition(promotionCondition));

// discountSkuGroup
TABLE promotionConditionDiscountSkuGroup (PromotionCondition, DiscountSkuGroup);
allDiscountSkuGroupPromotionCondition 'Все дисконтные группы товаров' = DATA BOOLEAN (PromotionCondition);
inDataPromotionConditionDiscountSkuGroup 'В условии' = DATA BOOLEAN (PromotionCondition, DiscountSkuGroup);
inPromotionConditionDiscountSkuGroup 'В условии' = OVERRIDE allDiscountSkuGroupPromotionCondition(promotionCondition) AND discountSkuGroup IS DiscountSkuGroup,
                                                            inDataPromotionConditionDiscountSkuGroup(promotionCondition, discountSkuGroup) PERSISTENT;
countPromotionConditionSku 'Кол-во дисконтных групп' = GROUP SUM 1 IF inPromotionConditionDiscountSkuGroup(promotionCondition, discountSkuGroup)
                                                                  AND inDiscountSkuGroupSku(discountSkuGroup, sku) BY promotionCondition, sku;


// skuGroup
TABLE promotionConditionSkuGroup (PromotionCondition, SkuGroup);
inDataPromotionConditionSkuGroup 'В условии' = DATA BOOLEAN (PromotionCondition, SkuGroup);
inParentPromotionConditionSkuGroup (promotionCondition, skuGroup) = GROUP SUM 1 IF inDataPromotionConditionSkuGroup(promotionCondition, parent) AND isParentSkuGroupSkuGroup(child, parent)
                                                                          BY promotionCondition, child PERSISTENT;
inPromotionConditionSkuGroup 'В условии' = inParentPromotionConditionSkuGroup(promotionCondition, skuGroup) OR inDataPromotionConditionSkuGroup(promotionCondition, skuGroup);

// sku
TABLE promotionConditionSku (PromotionCondition, Sku);
inDataPromotionConditionSku 'В условии' = DATA BOOLEAN (PromotionCondition, Sku);

inPromotionConditionSku 'В условии' (promotionCondition, sku) = OVERRIDE (countPromotionConditionSku(promotionCondition, sku) OR (allDiscountSkuGroupPromotionCondition(promotionCondition) AND sku IS Sku)) AND
                                                                          inParentPromotionConditionSkuGroup(promotionCondition, skuGroupSku(sku)),
                                                                          inDataPromotionConditionSku(promotionCondition, sku);

notHasDiscountCardPromotion (p) = NOT hasDiscountCardPromotion(p);
notNoDiscountCardPromotion (p) = NOT noDiscountCardPromotion(p);

FORM promotion 'Акция'
    OBJECTS sh=Promotion   FIXED PANEL
    PROPERTIES(sh)     namePromotion, namePromotionGroupPromotion, userSetPromotion,
                       nameNumeratorPromotion, numberPromotion, seriesPromotion,
                       dateFromPromotion, dateToPromotion, timeOfFromPromotion, timeOfToPromotion,
                       minSumBillPromotion, maxSumBillPromotion
    PROPERTIES(sh) SHOWIF notNoDiscountCardPromotion(sh)  hasDiscountCardPromotion 
    PROPERTIES(sh) SHOWIF notHasDiscountCardPromotion(sh) noDiscountCardPromotion 
    PROPERTIES(sh) SHOWIF hasDiscountCardPromotion(sh)    minCumulativeSumPromotion, maxCumulativeSumPromotion, useCurrentReceiptPromotion, allDiscountCardPromotion
    OBJECTS d=DOW
    PROPERTIES(d)     READONLY staticCaption
    PROPERTIES(sh, d)  skipPromotionDOW
    FILTERGROUP filterDOW
        FILTER 'Только отмеченные' 'F11' skipPromotionDOW(sh, d)

    OBJECTS sg=PromotionCondition
    PROPERTIES(sg)     idPromotionCondition SHOWIF showIDs(), namePromotionCondition, minQuantityPromotionCondition, minSumPromotionCondition, quantityDiscountPromotionCondition, percentPromotionCondition,
                       namePriceListTypePromotionCondition, resultPricePromotionCondition, sumDiscountPromotionCondition, sumReceiptPromotionCondition, ADDOBJ, DELETESESSION

    FILTERS            promotionPromotionCondition(sg)==sh

    TREE skuTree skg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = nameSkuGroup(skg)
    ORDER BY skuTreeName
    PROPERTIES(sg, skg) inPromotionConditionSkuGroup
    FILTERGROUP inactive FILTER 'Активные' 'F5' activeSkuGroup(skg) DEFAULT

    OBJECTS dg=DiscountSkuGroup
    PROPERTIES(sg) allDiscountSkuGroupPromotionCondition TODRAW dg FORCE PANEL
    PROPERTIES(dg) READONLY nameDiscountSkuGroup
    PROPERTIES(sg, dg) inPromotionConditionDiscountSkuGroup

    FILTERGROUP filterDiscountSkuGroup
        FILTER 'Только отмеченные' 'F11' inPromotionConditionDiscountSkuGroup(sg, dg)

    OBJECTS sk = Sku
    PROPERTIES(sk)    READONLY idBarcodeSku, nameSku
    FILTERS           isParentSkuGroupSku(skg, sk)
    PROPERTIES(sg, sk)   inPromotionConditionSku

    FILTERGROUP filterSku
        FILTER 'Только отмеченные' 'F10' inPromotionConditionSku(sg, sk)

    TREE treeStore a=STRING[3], t=ChainStores, st=StoreType
    PROPERTIES READONLY OBJVALUE(a), nameChainStores(t), nameStoreType(st)

    FILTERS stringEqualsAll(a),
            inChainStoresStoreType (t, st)

    OBJECTS s=Store
    PROPERTIES(s) READONLY nameStore, addressStore, nameLegalEntityStore

    FILTERS inChainStoresStoreTypeStore(t, st, s),
            isCompanyStore(s)

    PROPERTIES(sh) allStoresPromotion TODRAW a FORCE GRID
    PROPERTIES(sh, t) inPromotionChainStores
    PROPERTIES(sh, st) inPromotionStoreType
    PROPERTIES(sh, s) inPromotionStore
    FILTERGROUP filterStore
        FILTER 'Только отмеченные' 'F9' inPromotionStore(sh, s)

    OBJECTS dis= DiscountCard
    PROPERTIES(dis)    READONLY numberDiscountCard, seriesDiscountCard, nameLegalEntityDiscountCard
    PROPERTIES(sh, dis) inPromotionDiscountCard
    FILTERGROUP filterDiscountCard
        FILTER 'Только отмеченные' 'F8' inPromotionDiscountCard(sh, dis)

    EDIT Promotion OBJECT sh
;

DESIGN promotion FROM DEFAULT {
    main{
        preferredSize = (1024, 768);

        ADD sh.box {
            type = CONTAINERH;
            NEW row01 {
                caption = 'Название';
                type = COLUMNS;
                columns = 1;
                ADD PROPERTY(namePromotion(sh)) {
                    preferredCharWidth = 30;

                }
                ADD PROPERTY(namePromotionGroupPromotion(sh));
                ADD PROPERTY(userSetPromotion(sh));
            }
            ADD sh.numbered {
                type = COLUMNS;
                columns = 1;
            }
            NEW row11 {
                caption = 'Диапозон дат';
                type = COLUMNS;
                columns = 1;
                ADD PROPERTY(dateFromPromotion);
                ADD PROPERTY(dateToPromotion);
            }
            NEW row12 {
                caption = 'Часы';
                type = COLUMNS;
                columns = 1;
                ADD PROPERTY(timeOfFromPromotion);
                ADD PROPERTY(timeOfToPromotion);
            }
            NEW row13 {
                caption = 'Суммы';
                type = COLUMNS;
                columns = 1;
                ADD PROPERTY(minSumBillPromotion);
                ADD PROPERTY(maxSumBillPromotion);
            }
            NEW row14 {
                caption = 'Дисконтные карты';
                type = COLUMNS;
                columns = 1;
                ADD PROPERTY(hasDiscountCardPromotion);
                ADD PROPERTY(noDiscountCardPromotion);
                ADD PROPERTY(minCumulativeSumPromotion);
                ADD PROPERTY(maxCumulativeSumPromotion);
                ADD PROPERTY(useCurrentReceiptPromotion);
                ADD PROPERTY(allDiscountCardPromotion);
            }
        }

        NEW row2 {
            fill = 1;
            type = TABBED;

            NEW row21 {
                caption = 'Условия';
                fill = 1;
                type = SPLITV;

                ADD sg.box;

                NEW row212 {
                    fill = 3;
                    type = SPLITH;
                    caption = 'Товары';

                    NEW skuGroupContainer {
                        fill = 1;
                        type = SPLITV;
                        ADD skuTree.tree.box;
                        ADD dg.box {
                            ADD dg.panel BEFORE dg.grid.box;
                        }
                    }
                    ADD sk.box {
                        fill = 2;
                    }
                }
            }

            NEW row22 {
                fill = 1;
                type = SPLITH;
                caption = 'Магазины';

                ADD treeStore.tree.box;
                ADD s.box {
                    fill = 3;
                }
            }
            ADD dis.box;
            ADD d.box;
        }

        ADD functions.box;
    }
}

copyPromotion 'Копировать' = ACTION (promotion) NEWSESSION {
    FOR ADDOBJ p = Promotion DO {
        promotionGroupPromotion(p) <- promotionGroupPromotion(promotion);
        dateFromPromotion(p) <- dateFromPromotion(promotion);
        dateToPromotion(p) <- dateToPromotion(promotion);
        timeOfFromPromotion(p) <- timeOfFromPromotion(promotion);
        timeOfToPromotion(p) <- timeOfToPromotion(promotion);
        minSumBillPromotion(p) <- minSumBillPromotion(promotion);
        maxSumBillPromotion(p) <-  maxSumBillPromotion(promotion);
        hasDiscountCardPromotion(p) <- hasDiscountCardPromotion(promotion);
        noDiscountCardPromotion(p) <- noDiscountCardPromotion(promotion);
        minCumulativeSumPromotion(p) <- minCumulativeSumPromotion(promotion);
        maxCumulativeSumPromotion(p) <- maxCumulativeSumPromotion(promotion);
        useCurrentReceiptPromotion(p) <- useCurrentReceiptPromotion(promotion);
        allDiscountCardPromotion(p) <- allDiscountCardPromotion(promotion);

        FOR promotionPromotionCondition(promotionCondition) == promotion ADDOBJ pc = PromotionCondition DO {
            promotionPromotionCondition(pc) <- p;
            minQuantityPromotionCondition(pc) <- minQuantityPromotionCondition(promotionCondition);
            minSumPromotionCondition(pc) <- minSumPromotionCondition(promotionCondition);
            quantityDiscountPromotionCondition(pc) <- quantityDiscountPromotionCondition(promotionCondition);
            percentPromotionCondition(pc) <- percentPromotionCondition(promotionCondition);
            priceListTypePromotionCondition(pc) <- priceListTypePromotionCondition(promotionCondition);
            resultPricePromotionCondition(pc) <- resultPricePromotionCondition(promotionCondition);
            sumDiscountPromotionCondition(pc) <- sumDiscountPromotionCondition(promotionCondition);
            sumReceiptPromotionCondition(pc) <- sumReceiptPromotionCondition(promotionCondition);

            allDiscountSkuGroupPromotionCondition(pc) <- allDiscountSkuGroupPromotionCondition(promotionCondition);
            inDataPromotionConditionDiscountSkuGroup(pc, discountSkuGroup) <- inDataPromotionConditionDiscountSkuGroup(promotionCondition, discountSkuGroup);
            inDataPromotionConditionSkuGroup(pc, skuGroup) <- inDataPromotionConditionSkuGroup(promotionCondition, skuGroup);
            inDataPromotionConditionSku(pc, sku) <- inDataPromotionConditionSku(promotionCondition, sku);
        }

        FORM promotion OBJECTS sh = p MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

FORM promotions 'Акции'
    OBJECTS sh=Promotion
    PROPERTIES(sh)     READONLY namePromotion, numberPromotion, seriesPromotion, dateFromPromotion, dateToPromotion, storesPromotion,
                                timeOfFromPromotion, timeOfToPromotion, minSumBillPromotion, maxSumBillPromotion,
                                hasDiscountCardPromotion, noDiscountCardPromotion, minCumulativeSumPromotion, maxCumulativeSumPromotion,
                                useCurrentReceiptPromotion, allDiscountCardPromotion
    PROPERTIES(sh)    copyPromotion, ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    PROPERTIES (sh) READONLY FORCE PANEL createdNameUserPromotion, createdTimePromotion, createdHostnameComputerPromotion

;

FORM promotionGroup 'Группа акций'
    OBJECTS st=PromotionGroup FIXED PANEL
    PROPERTIES(st)     namePromotionGroup

    OBJECTS sh=Promotion
    PROPERTIES(sh)     userOrderPromotion
    PROPERTIES(sh)     READONLY namePromotion, seriesPromotion, numberPromotion, dateFromPromotion, dateToPromotion, storesPromotion,
                                timeOfFromPromotion, timeOfToPromotion, minSumBillPromotion, maxSumBillPromotion,
                                hasDiscountCardPromotion, noDiscountCardPromotion, minCumulativeSumPromotion, maxCumulativeSumPromotion,
                                useCurrentReceiptPromotion, allDiscountCardPromotion
    PROPERTIES(sh)     copyPromotion, ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    ORDER BY userOrderPromotion

    FILTERS promotionGroupPromotion(sh)==st

    EDIT PromotionGroup OBJECT st
;

FORM promotionGroups 'Группы акций'
    OBJECTS st=PromotionGroup
    PROPERTIES(st)     READONLY namePromotionGroup
    PROPERTIES(st)     ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    OBJECTS sh=Promotion
    PROPERTIES(sh)     READONLY orderPromotion
    PROPERTIES(sh)     READONLY namePromotion, seriesPromotion, numberPromotion, dateFromPromotion, dateToPromotion, storesPromotion,
                                timeOfFromPromotion, timeOfToPromotion, minSumBillPromotion, maxSumBillPromotion,
                                hasDiscountCardPromotion, noDiscountCardPromotion, minCumulativeSumPromotion, maxCumulativeSumPromotion,
                                useCurrentReceiptPromotion, allDiscountCardPromotion
    PROPERTIES(sh)     ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
    ORDER BY orderPromotion

    FILTERS promotionGroupPromotion(sh)==st

    DIALOG PromotionGroup OBJECT st
;

EXTEND FORM discountCard
    OBJECTS p=Promotion
    PROPERTIES(p, d) inPromotionDiscountCard
    PROPERTIES(p) READONLY namePromotion, numberPromotion, seriesPromotion, dateFromPromotion, dateToPromotion, storesPromotion,
                           timeOfFromPromotion, timeOfToPromotion, minSumBillPromotion, maxSumBillPromotion,
                           hasDiscountCardPromotion, noDiscountCardPromotion, minCumulativeSumPromotion, maxCumulativeSumPromotion,
                           useCurrentReceiptPromotion, allDiscountCardPromotion
    FILTERS hasDiscountCardPromotion(p)
;

EXTEND DESIGN discountCard {
    ADD p.box BEFORE functions.box;
}

NAVIGATOR {
    retailNavigator {
        NEW retailCRMNavigator 'CRM' BEFORE retailMasterData {
            ADD promotions;
            ADD promotionGroups;
            ADD discountCards;
        }
    }
}