MODULE ZReportDiscountCard;

REQUIRE ZReport, DiscountCard;

NAMESPACE ZReport;

discountCard (receipt) = DATA DiscountCard (Receipt) INDEXED;
countReceipt 'Кол-во чеков' = GROUP SUM 1 BY discountCard(Receipt r); 
prevCountReceipt 'Кол-во чеков' (DiscountCard dc) = PREV(countReceipt(dc));
seriesNumberDiscountCard (Receipt receipt) = seriesNumber(discountCard(receipt));
discountCard (ReceiptSaleDetail d) = discountCard(receipt(d));
discountCard (ReceiptReturnDetail d) = discountCard(receipt(d));
//discountCard (ReceiptReturnDetail d) = OVERRIDE discountCard(receiptSale(d)), discountCard(receipt(d));
discountCard (ReceiptDetail d) = discountCard(receipt(d));
idDiscountCard 'Код дисконтной карты' (ReceiptDetail d) = id(discountCard(d));

CONSTRAINT dateTo(discountCard(Receipt receipt)) < date(receipt) CHECKED BY discountCard[Receipt] MESSAGE 'Закончился срок действия карты';

numberDiscountCard 'Номер дисконтной карты' (Receipt receipt) = seriesNumber(discountCard(receipt)) IN receiptDiscount;
nameLegalEntityDiscountCard 'Держатель дисконтной карты' (Receipt receipt) = nameLegalEntity(discountCard(receipt)) IN receiptDiscount MINCHARWIDTH 30 PREFCHARWIDTH 40;
numberDiscountCard 'Номер дисконтной карты' (ReceiptDetail receiptDetail) = numberDiscountCard(receipt(receiptDetail));
nameLegalEntityDiscountCard 'Держатель дисконтной карты' (ReceiptDetail receiptDetail) = nameLegalEntityDiscountCard(receipt(receiptDetail)) MINCHARWIDTH 30 PREFCHARWIDTH 40;
nameDiscountCard 'Наименование дисконтной карты' (Receipt receipt) = name(discountCard(receipt));

countReceiptsDiscountCard 'Кол-во чеков по дисконтным картам в Z-отчете' (zReport) = GROUP SUM 1 IF discountCard(Receipt receipt)  
    BY zReport(receipt) PERSISTENT IN documentSum;
    
EXTEND FORM zReport
    PROPERTIES(b) AFTER nameEmployee(b) numberDiscountCard, nameLegalEntityDiscountCard
;
EXTEND FORM zReports
    PROPERTIES(b) READONLY AFTER nameEmployee(b) numberDiscountCard, nameLegalEntityDiscountCard 
;

//--Накопленные суммы

posSum 'Сумма продаж' (discountCard) = GROUP SUM sumReceiptDetail (Receipt receipt) BY discountCard(receipt) PERSISTENT @@denomination;
posReturnSum 'Сумма возвратов' (discountCard) = GROUP SUM sum(ReceiptReturnDetail d) IF NOT discountCard[ReceiptDetail](d) BY discountCard(receiptSale(d)) PERSISTENT @@denomination; 
signedTransferSum 'Сумма переводов' (discountCard) = ABSTRACT NUMERIC[18,4] (DiscountCard) PERSISTENT @@denomination;

cumulativeSum 'Накопленная сумма (основная)' (DiscountCard discountCard) = initialSum(discountCard) (+) posSum(discountCard) (-) posReturnSum(discountCard) (+) signedTransferSum(discountCard) PERSISTENT @@denomination;

externalSum 'Дополнительные накопления' (discountCard) = ABSTRACT NUMERIC[18,4] (DiscountCard) PERSISTENT @@denomination;
totalSum 'Накопленная сумма' (DiscountCard d) = cumulativeSum(d) (+) externalSum(d); 
prevTotalSum 'Накопленная сумма' (DiscountCard d) = PREV(totalSum(d)); 

dateFirstUse 'Дата первого использования'=  GROUP MIN date(Receipt receipt) BY discountCard (receipt);

//перевод накоплений по карте

discountCard = DATA DiscountCard (DiscountCard);
numberDiscountCard 'Номер дисконтной карты для перевода' (DiscountCard d) = number(discountCard(d)); 
transferSum 'Сумма перевода' = DATA NUMERIC[18,4] (DiscountCard) @@denomination;

totalTransferSum 'Сумма переводов' = GROUP SUM transferSum(DiscountCard d) BY discountCard(d);

signedTransferSum(DiscountCard d) += totalTransferSum(d) (-) transferSum(d);

EXTEND FORM discountCard
    PROPERTIES(d) READONLY totalTransferSum
    PROPERTIES(d) numberDiscountCard, transferSum
;

DESIGN discountCard {
    d.panel {
        NEW row4 {
            caption = 'Перевод накоплений';
            type = CONTAINERH;
            MOVE PROPERTY(totalTransferSum(d));
            MOVE PROPERTY(numberDiscountCard(d));
            MOVE PROPERTY(transferSum(d));
        }
    }       
}

EXTEND FORM discountCards
    PROPERTIES(d) READONLY AFTER initialSum(d) posSum, posReturnSum, cumulativeSum, externalSum, signedTransferSum, totalSum
    PROPERTIES(d) READONLY AFTER date(d) dateFirstUse
    PROPERTIES(d) READONLY totalTransferSum, numberDiscountCard, transferSum 
;

numberDiscountCardInput 'Штрихкод' = DATA LOCAL VARSTRING[15] () EVENTID 'SCANNER';
changeNumberDiscountCardInput() = ACTION {
    REQUEST VARSTRING[15] INPUT;
    numberDiscountCardInput() <- processedBarcode(requestedString());
    formOk();
}

FORM numberDiscountCardInput 'Ввод номера старой дисконтной карты'
    PROPERTIES() numberDiscountCardInput ON CHANGE changeNumberDiscountCardInput()
;

DESIGN numberDiscountCardInput {
    PROPERTY (numberDiscountCardInput()) {
        caption = 'Штрихкод';
        font = 'bold 32';
        panelCaptionAbove = TRUE;
        editKey = 'F4';
        focusable = TRUE;
    }
}

numberDiscountCardInputNew 'Штрихкод' = DATA LOCAL VARSTRING[15] () EVENTID 'SCANNER';
changeNumberDiscountCardInputNew() = ACTION {
    REQUEST VARSTRING[15] INPUT;
    numberDiscountCardInputNew() <- processedBarcode(requestedString());
    formOk();
}

FORM numberDiscountCardInputNew 'Ввод номера новой дисконтной карты'
    PROPERTIES() numberDiscountCardInputNew ON CHANGE changeNumberDiscountCardInputNew()
;

DESIGN numberDiscountCardInputNew {
    PROPERTY (numberDiscountCardInputNew()) {
        caption = 'Штрихкод';
        font = 'bold 32';
        panelCaptionAbove = TRUE;
        editKey = 'F4';
        focusable = TRUE;
    }
}

showTransferSum() = numberDiscountCardInput() AND NOT numberDiscountCardInputNew();
notShowTransferSum() = NOT showTransferSum();

changeTransferSum (DiscountCard dc) = ACTION {
    REQUEST NUMERIC[18,4] INPUT;
    IF NOT requestedNumeric() > prevTotalSum(dc) AND NOT requestedNumeric() < 0.0 THEN
        transferSum(dc) <- requestedNumeric()
    ELSE 
        IF requestedNumeric() < 0.0 THEN
            MESSAGE 'Сумма перевода не может быть меньше ноля'
    ELSE    
        MESSAGE 'Нельзя переводить сумму больше суммы накопленний';           
}

FORM transferDiscountCards 'Перевод накоплений'
    OBJECTS dc = DiscountCard FIXED PANEL
    PROPERTIES(dc) READONLY nameLegalEntity, seriesNumber, prevTotalSum
    PROPERTIES(dc) transferSum SHOWIF showTransferSum() ON CHANGE changeTransferSum(dc)
    PROPERTIES(dc) totalTransferSum SHOWIF notShowTransferSum()
;

DESIGN transferDiscountCards {
    NEW topContainer {
        type = CONTAINERV;
        MOVE PROPERTY(nameLegalEntity(dc)){
            font = 'bold 32';
            panelCaptionAbove = TRUE;
            preferredCharWidth = 35;
            
        }
        MOVE PROPERTY(seriesNumber(dc)){
            font = 'bold 32';
            panelCaptionAbove = TRUE;
            alignment = STRETCH;            
        }
        MOVE PROPERTY(prevTotalSum(dc)){
            font = 'bold 32';
            panelCaptionAbove = TRUE;
            alignment = STRETCH;              
        }
        MOVE PROPERTY(transferSum(dc)){
            font = 'bold 32';
            panelCaptionAbove = TRUE;
            alignment = STRETCH;              
        }
        MOVE PROPERTY(totalTransferSum(dc)){
            font = 'bold 32';
            panelCaptionAbove = TRUE;
            alignment = STRETCH;              
        }
    }
    MOVE functions.box;
}

overTransferDiscountCards = ACTION ABSTRACT LIST (DiscountCard, DiscountCard); 
transferDiscountCards 'Замена карты' () = ACTION NEWSESSION {
    FORM numberDiscountCardInput MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR DiscountCard card == discountString(numberDiscountCardInput()) DO { 
            transferSum(card) <- prevTotalSum(card);
            FORM transferDiscountCards OBJECTS dc = card MODAL;
            IF formResult() == FormResult.ok THEN {
                FORM numberDiscountCardInputNew MODAL;
                IF formResult() == FormResult.ok THEN {
                    FOR DiscountCard newCard == discountString(numberDiscountCardInputNew()) DO {
                        discountCard(card) <- newCard;
                        dateTo(card) <- currentDate();
                        legalEntity(newCard) <- legalEntity(card) WHERE NOT legalEntity(newCard);
                        overTransferDiscountCards(newCard, card);
                        FORM transferDiscountCards OBJECTS dc = newCard MODAL;
                        IF formResult() == FormResult.ok THEN {
                            apply();
                            MESSAGE 'Замена карты была успешно проведена';
                        } ELSE {
                            cancel();
                        }
                    }    
                }
            }
        }    
    }
}
