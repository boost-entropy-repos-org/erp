MODULE ZReportRetailBonus;

REQUIRE ZReportDiscountCard, RetailBonus;

NAMESPACE ZReport;

bonusSum 'Начислено бонусов' = DATA NUMERIC[16,2] (ReceiptSaleDetail);
bonusSum 'Начислено бонусов' = DATA NUMERIC[16,2] (ReceiptReturnDetail);

bonusSum 'Начислено бонусов' = ABSTRACT NUMERIC[16,2] (ReceiptDetail);
bonusSum(ReceiptSaleDetail d) += bonusSum(d);
bonusSum(ReceiptReturnDetail d) += bonusSum(d);

bonusSum 'Начислено бонусов' (Receipt r) = GROUP SUM bonusSum(ReceiptDetail d) BY receipt(d);

signedBonusSum 'Начислено бонусов' = ABSTRACT NUMERIC[16,2] (ReceiptDetail) PERSISTENT;
signedBonusSum(ReceiptSaleDetail d) += bonusSum(d);
signedBonusSum(ReceiptReturnDetail d) += -bonusSum(d);

signedBonusSum 'Начислено бонусов' (Receipt r) = GROUP SUM signedBonusSum(ReceiptDetail d) BY receipt(d);

bonusPaid 'Оплачено бонусами' = DATA NUMERIC[16,2] (ReceiptSaleDetail);
bonusPaid 'Оплачено бонусами' = DATA NUMERIC[16,2] (ReceiptReturnDetail);

bonusPaid 'Оплачено бонусами' = ABSTRACT NUMERIC[16,2] (ReceiptDetail);
bonusPaid(ReceiptSaleDetail d) += bonusPaid(d);
bonusPaid(ReceiptReturnDetail d) += bonusPaid(d);
extraDiscountSum(ReceiptSaleDetail d) += bonusPaid(d);

bonusPaid 'Оплачено бонусами' (Receipt r) = GROUP SUM bonusPaid(ReceiptDetail d) BY receipt(d);

signedBonusPaid 'Оплачено бонусами' = ABSTRACT NUMERIC[16,2] (ReceiptDetail) PERSISTENT;
signedBonusPaid(ReceiptSaleDetail d) += bonusPaid(d);
signedBonusPaid(ReceiptReturnDetail d) += -bonusPaid(d);

signedBonusPaid 'Оплачено бонусами' (Receipt r) = GROUP SUM signedBonusPaid(ReceiptDetail d) BY receipt(d);

signedBonusSum 'Начислено бонусов' = GROUP SUM signedBonusSum(ReceiptDetail d)
    BY zReport(receipt(d));

signedBonusPaid 'Оплачено бонусами' = GROUP SUM signedBonusPaid(ReceiptDetail d)
    BY zReport(receipt(d));
    
EXTEND FORM zReports
    PROPERTIES(z)  READONLY SHOWIF useBonus() signedBonusSum, signedBonusPaid
;

cumulativeBonuses 'Накоплено бонусов' (DiscountCard dc) = GROUP SUM signedBonusSum(ReceiptDetail rd) BY discountCard(rd) PERSISTENT;
blockedBonuses 'Заблокировано бонусов' (DiscountCard dc) = GROUP SUM signedBonusSum(ReceiptDetail rd) IF date(rd) > subtract(currentDate(), blockDaysBonus()) BY discountCard(rd);

paidBonuses 'Оплачено бонусами' = GROUP SUM signedBonusPaid(ReceiptDetail rd) BY discountCard(rd) PERSISTENT;

availableBonuses 'Доступно бонусов (основная)' (DiscountCard dc) = cumulativeBonuses(dc) (+) sumBonusLedger(dc) (-) blockedBonuses(dc) (-) paidBonuses(dc) PERSISTENT; 

externalBonuses 'Дополнительные бонусы' = ABSTRACT NUMERIC[16,2] (DiscountCard) PERSISTENT;
totalBonuses 'Доступно бонусов' (DiscountCard d) = availableBonuses(d) (+) externalBonuses(d);
prevTotalBonuses 'Доступно бонусов' (DiscountCard d) = PREV(totalBonuses(d));

totalBonuses 'Доступно бонусов' (Receipt r) = prevTotalBonuses(discountCard(r)) IN receiptDiscount;

//-- Скидки с учетом бонусов
discountBonusSumVAT 'Сумма НДС в скидке' (ReceiptDetail d) = round((discountSum(d) (+) bonusPaid(d))* valueVAT(d) / (100 + valueVAT(d)), currency(d));
 
discountBonusSumVATSale 'Сумма НДС в скидке (продажа)' (zReport)= GROUP SUM discountBonusSumVAT(ReceiptSaleDetail d) IF d IS ReceiptSaleDetail BY zReport[ReceiptDetail](d);
discountBonusSumVATReturn 'Сумма НДС в скидке (возврат)' (zReport)= GROUP SUM discountBonusSumVAT(ReceiptReturnDetail d) IF d IS ReceiptReturnDetail BY zReport[ReceiptDetail](d);

discountBonusSumVAT 'Сумма НДС в скидке' (ZReport zReport)= discountBonusSumVATSale(zReport) (-) discountBonusSumVATReturn(zReport);

discountBonusSumMarkup 'Сумма набавки в скидке' (ZReport z) = discountSum(z) (+) signedBonusPaid(z) (-) discountBonusSumVAT(z);

EXTEND FORM discountCard
    PROPERTIES(d) READONLY SHOWIF useBonus() cumulativeBonuses, blockedBonuses, paidBonuses, availableBonuses, externalBonuses, totalBonuses
;

DESIGN discountCard {
    d.panel {
        NEW row4 {
            caption = 'Бонусы';
            type = CONTAINERH;
            MOVE PROPERTY(cumulativeBonuses(d));
            MOVE PROPERTY(blockedBonuses(d));
            MOVE PROPERTY(paidBonuses(d));
            MOVE PROPERTY(sumBonusLedger(d));
            MOVE PROPERTY(availableBonuses(d));
            MOVE PROPERTY(externalBonuses(d));
            MOVE PROPERTY(totalBonuses(d));
        }
    }
}

EXTEND FORM discountCards
    PROPERTIES(d) READONLY SHOWIF useBonus() cumulativeBonuses, blockedBonuses, paidBonuses, availableBonuses, externalBonuses, totalBonuses
;