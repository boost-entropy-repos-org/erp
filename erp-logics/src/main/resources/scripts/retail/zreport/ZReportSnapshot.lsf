MODULE ZReportSnapshot;

REQUIRE ZReport, SaleSnapshot;

NAMESPACE Stock;

//discountSumSaleLedger 'Сумма скидки' (d) = MULTI discountSumReceiptSaleDetail(d), -discountSumReceiptReturnDetail(d);

//-- Sku
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot);

//-- Batch
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (Batch, Stock, Snapshot);

//-- По поставщикам
discountSumBatch 'Сумма скидки' (supplier, snapshot) =
    GROUP SUM discountSum (Batch batch, Stock stock, Snapshot snapshot) BY supplier(batch), snapshot;

//-- По складу поставщика
discountSumBatchSupplier 'Сумма скидки' (supplier, snapshot) =
    GROUP SUM discountSum (Batch batch, Stock stock, Snapshot snapshot) BY supplierStock(batch), snapshot;

//-- группа/склад       
discountSum 'Сумма скидки' (group, stock, snapshot) =
    GROUP SUM discountSum (Sku sku, Stock stock, Snapshot snapshot) IF isParent(SkuGroup group, sku)
        BY group, stock, snapshot MATERIALIZED;  
        
//-- группа         
discountSum 'Сумма скидки' (group, snapshot) =
    GROUP SUM discountSum (SkuGroup group, Stock stock, Snapshot snapshot)  BY group, snapshot;    
                
//--По складам
discountSum 'Сумма скидки' (stock, snapshot) =
    GROUP SUM discountSum (SkuGroup group, Stock stock, Snapshot snapshot)  BY stock, snapshot;  
                     
//-- Sku на дату
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot, DATE);        
             
//--По складам на дату     

discountSum 'Сумма скидки' (stock, snapshot, date) =
    GROUP SUM discountSum (Sku sku, Stock stock, Snapshot snapshot, DATE date)  BY stock, snapshot, date;    
           
nameDiscountSum (Stock st) = CONCAT ' ', name(st), ' (сумма скидки)';    
       
isDiscount 'Скидка' = DATA BOOLEAN (Snapshot) IN evidence;
hintDiscountBackground  = RGB(0, 214, 161) IF TRUE;  
      
isDiscount 'Скидка' = DATA BOOLEAN (SnapshotType);

EXTEND FORM snapshotType
    PROPERTIES (t) isDiscount
;
DESIGN snapshotType {
    row3 {
        MOVE PROPERTY (isDiscount(t));
    }
}
EXTEND FORM snapshotTypes
    PROPERTIES (t) READONLY  isDiscount
;
@deriveDocumentSnapshotProperty(isDiscount);      
       
//-- Расширяем ACTION    
discountSum 'Скидка (сумма)' (sku, stock, date) =  GROUP SUM discountSum(SaleLedger ledger) BY sku[SkuLedger](ledger), stock[SkuLedger](ledger), date[SkuLedger](ledger);
  
discountSum 'Скидка за интервал (сумма)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        discountSum(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY sku, stock, dateFrom, dateTo;  

                    
overTakeSkuFromTo(Snapshot snapshot, DATE dateFrom, DATE dateTo) += {
    IF isDiscount(snapshot) THEN {
        IF isDate(snapshot) THEN {
            IF isSum(snapshot) THEN {
                discountSum(Sku sku, Stock stock, snapshot, DATE date) <- NULL;
                discountSum(Sku sku, Stock stock, snapshot, DATE date) <- NUMERIC[16,2](discountSum(sku, stock, date)) 
                    WHERE include(snapshot, stock) AND include(snapshot, sku) AND date AS DATE AND date >= (dateFrom AS DATE) 
                        AND date <= (dateTo AS DATE) AND discountSum(sku, stock, date);  
                     
            }                               
        }  
        IF isSum(snapshot) THEN {  
            discountSum(Sku sku, Stock stock, snapshot) <- NULL;
            discountSum(Sku sku, Stock stock, snapshot) <- NUMERIC[16,2](discountSum(sku, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku) AND discountSum(sku, stock, dateFrom, dateTo);    
 
        }              
    }            
};                                                                                                
               
//-- Batch      
discountSum 'Скидка (сумма)' (batch, stock, date) =  GROUP SUM discountSum(SaleLedger ledger) BY batch[SkuLedger](ledger), stock[SkuLedger](ledger), date[SkuLedger](ledger);
  
discountSum 'Скидка за интервал (сумма)' (batch, stock, dateFrom, dateTo) = GROUP SUM
        discountSum(Batch batch, Stock stock, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo
        BY batch, stock, dateFrom, dateTo;  

overTakeBatchFromTo(Snapshot snapshot, DATE dateFrom, DATE dateTo) += {
    IF isDiscount(snapshot) THEN {
        IF isSum(snapshot) THEN { 
            discountSum(Batch batch, Stock stock, snapshot) <- NULL;
            discountSum(Batch batch, Stock stock, snapshot) <- NUMERIC[16,2](discountSum(batch, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku(batch)) AND discountSum(batch, stock, dateFrom, dateTo);   
       
        }          
    }      
};  
       
//-- SHOWIF

isSumDiscount= isSum(Snapshot snapshot) AND isDiscount(snapshot);

isSumDiscountDate= isSum(Snapshot snapshot) AND isDiscount(snapshot) AND isDate(snapshot);

isSumDiscountBatch= isSum(Snapshot snapshot) AND isDiscount(snapshot) AND isBatch(snapshot);
                                                    
                                                         
EXTEND FORM snapshot
    PROPERTIES(r)      BACKGROUND hintDiscountBackground() isDiscount
//--sku  
    PROPERTIES(s,st,r) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(s,st,r)                      
                       discountSum SHOWIF isSumDiscount(r)

//--sku на дату  
    PROPERTIES(s3,st3,r,d3) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(s3,st3,r,d3)
                       discountSum SHOWIF isSumDiscountDate(r)

//--По складам на дату 
    PROPERTIES(st4,r,d4) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(st4,r,d4)
                       discountSum COLUMNS 'g' (st4) HEADER nameDiscountSum(st4) SHOWIF isSumDiscountDate(r)
                                                                 
//-- batch
    PROPERTIES(bt,ss,r) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(bt,ss,r) 
                       discountSum SHOWIF isSumDiscountBatch(r)
        
//-- По группам                           
    PROPERTIES(sk1,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE balanceA(sk1,r)         
                       discountSum SHOWIF isSumDiscount(r)

//-- По складам                           
    PROPERTIES(ts1,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(ts1,r)                               
                       discountSum SHOWIF isSumDiscount(r)

//-- По поставщикам
    PROPERTIES(l,r)    READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityBatch(l,r)
                       discountSumBatch SHOWIF isSumDiscountBatch(r)
                       
//-- По складам поставщика
    PROPERTIES(lst,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityBatchSupplier(lst,r)
                       discountSumBatchSupplier SHOWIF isSumDiscountBatch(r) 
;  
DESIGN snapshot {
    row3 {
        MOVE PROPERTY (isDiscount(r));
    }
}


EXTEND FORM snapshots
    PROPERTIES(r)      READONLY  BACKGROUND hintDiscountBackground() isDiscount     
; 
