MODULE ZReportSnapshot;

REQUIRE ZReport, SaleSnapshot;

NAMESPACE Stock;

discountSumSaleLedger 'Сумма скидки' (d) = MULTI discountSumReceiptSaleDetail(d), -discountSumReceiptReturnDetail(d);

//-- Sku
discountSumSkuStockSnapshot 'Сумма скидки' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot);

//-- Batch
discountSumBatchStockSnapshot 'Сумма скидки' = DATA NUMERIC[16,2] (Batch, Stock, Snapshot);


//-- группа/склад       
discountSumSkuGroupStockSnapshot 'Сумма скидки' (group, stock, snapshot) =
    GROUP SUM discountSumSkuStockSnapshot (sku, stock, snapshot) IF isParentSkuGroupSku(group, sku)
        BY group, stock, snapshot PERSISTENT;  
        
//-- группа         
discountSumSkuGroupSnapshot 'Сумма скидки' (group, snapshot) =
    GROUP SUM discountSumSkuGroupStockSnapshot (group, stock, snapshot)  BY group, snapshot;    
                
//--По складам
discountSumStockSnapshot 'Сумма скидки' (stock, snapshot) =
    GROUP SUM discountSumSkuGroupStockSnapshot (group, stock, snapshot)  BY stock, snapshot;  
                     
//-- Sku на дату
discountSumSkuStockSnapshotDate 'Сумма скидки' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot, DATE);        
             
//--По складам на дату     

discountSumStockSnapshotDate 'Сумма скидки' (stock, snapshot, date) =
    GROUP SUM discountSumSkuStockSnapshotDate (sku, stock, snapshot, date)  BY stock, snapshot, date;    
           
nameDiscountSumStock (st) = CONCAT ' ', nameStock(st), ' (сумма скидки)';    
       
isDiscountSnapshot 'Скидка' = DATA BOOLEAN (Snapshot) IN evidence;
hintDiscountBackground  = RGB(0, 214, 161) IF TRUE;        
       
//-- Расширяем ACTION    
discountSumSkuStockDate 'Скидка (сумма)' (sku, stock, date) =  GROUP SUM discountSumSaleLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), dateSkuLedger(ledger);
  
discountSumSkuStockDateFromTo 'Скидка за интервал (сумма)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        discountSumSkuStockDate(sku, stock, date) IF date >= dateFrom AND date <= dateTo
        BY sku, stock, dateFrom, dateTo;  

                    
overTakeSkuSnapshotDateFromTo(snapshot, dateFrom, dateTo) += ACTION (snapshot, dateFrom, dateTo) {
    IF isDiscountSnapshot(snapshot) THEN {
        IF isDateSnapshot(snapshot) THEN {
            IF isSumSnapshot(snapshot) THEN {
                discountSumSkuStockSnapshotDate(sku, stock, snapshot, date) <- NUMERIC[16,2](discountSumSkuStockDate(sku, stock, date)) 
                    WHERE includeSnapshotStock(snapshot, stock) AND includeSnapshotSku(snapshot, sku) AND date AS DATE AND date >= (dateFrom AS DATE) 
                        AND date <= (dateTo AS DATE) AND discountSumSkuStockDate(sku, stock, date);  
                     
            }                               
        }  
        IF isSumSnapshot(snapshot) THEN {    
            discountSumSkuStockSnapshot(sku, stock, snapshot) <- NUMERIC[16,2](discountSumSkuStockDateFromTo(sku, stock, dateFrom, dateTo)) 
                WHERE includeSnapshotStock(snapshot, stock) AND includeSnapshotSku(snapshot, sku);    
 
        }              
    }            
};                                                                                                
               
//-- Batch      
discountSumBatchStockDate 'Скидка (сумма)' (batch, stock, date) =  GROUP SUM discountSumSaleLedger(ledger) BY batchSkuLedger(ledger), stockSkuLedger(ledger), dateSkuLedger(ledger);
  
discountSumBatchStockDateFromTo 'Скидка за интервал (сумма)' (batch, stock, dateFrom, dateTo) = GROUP SUM
        discountSumBatchStockDate(batch, stock, date) IF date >= dateFrom AND date <= dateTo
        BY batch, stock, dateFrom, dateTo;  

overTakeBatchSnapshotDateFromTo(snapshot, dateFrom, dateTo) += ACTION (snapshot, dateFrom, dateTo) {
    IF isDiscountSnapshot(snapshot) THEN {
        IF isSumSnapshot(snapshot) THEN { 
            discountSumBatchStockSnapshot(batch, stock, snapshot) <- NUMERIC[16,2](discountSumBatchStockDateFromTo(batch, stock, dateFrom, dateTo)) 
                WHERE includeSnapshotStock(snapshot, stock) AND includeSnapshotSku(snapshot, skuBatch(batch));   
       
        }          
    }      
};  
       
//-- SHOWIF

isSumDiscountSnapshot= isSumSnapshot(snapshot) AND isDiscountSnapshot(snapshot);

isSumDiscountDateSnapshot= isSumSnapshot(snapshot) AND isDiscountSnapshot(snapshot) AND isDateSnapshot(snapshot);

isSumDiscountBatchSnapshot= isSumSnapshot(snapshot) AND isDiscountSnapshot(snapshot) AND isBatchSnapshot(snapshot);
                                                    
                                                         
EXTEND FORM snapshot
    PROPERTIES(r)      BACKGROUND hintDiscountBackground() isDiscountSnapshot
//--sku  
    PROPERTIES(s,st,r) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantitySkuStockSnapshot(s,st,r)                      
                       discountSumSkuStockSnapshot SHOWIF isSumDiscountSnapshot(r)

//--sku на дату  
    PROPERTIES(s3,st3,r,d3) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantitySkuStockSnapshotDate(s3,st3,r,d3)
                       discountSumSkuStockSnapshotDate SHOWIF isSumDiscountDateSnapshot(r)

//--По складам на дату 
    PROPERTIES(st4,r,d4) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityStockSnapshotDate(st4,r,d4)
                       discountSumStockSnapshotDate COLUMNS 'g' (st4) HEADER nameDiscountSumStock(st4) SHOWIF isSumDiscountDateSnapshot(r)
                                                                 
//-- batch
    PROPERTIES(bt,ss,r) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityBatchStockSnapshot(bt,ss,r) 
                       discountSumBatchStockSnapshot SHOWIF isSumDiscountBatchSnapshot(r)
        
//-- По группам                           
    PROPERTIES(sk1,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE balanceASkuGroupSnapshot(sk1,r)         
                       discountSumSkuGroupSnapshot SHOWIF isSumDiscountSnapshot(r)

//-- По складам                           
    PROPERTIES(ts1,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityStockSnapshot(ts1,r)                               
                       discountSumStockSnapshot SHOWIF isSumDiscountSnapshot(r)

;  
EXTEND DESIGN snapshot {
    row3 {
        ADD PROPERTY (isDiscountSnapshot(r));
    }
}


EXTEND FORM snapshots
    PROPERTIES(r)      READONLY  BACKGROUND hintDiscountBackground() isDiscountSnapshot     
; 
