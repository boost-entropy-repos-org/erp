MODULE ReceiptDetailReport;

REQUIRE ZReport, ZReportDiscountCard;

NAMESPACE ZReport;

filterDepartmentStore  = DATA LOCAL DepartmentStore();
nameFilterDepartmentStore 'Магазин' = name(filterDepartmentStore());
filterDepartmentStore(ReceiptDetail d) = departmentStore(d) == filterDepartmentStore() OR NOT filterDepartmentStore();

saleDiscountCard  = DATA LOCAL DiscountCard ();
numberDiscountCard 'Дисконтная карта' = seriesNumber(saleDiscountCard());
nameDiscountCard 'Наименование дисконтной карты' (ReceiptDetail receiptDetail) = name(discountCard(receipt(receiptDetail)));

FORM receiptDetailReport 'Регистр строк чека'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)       
     
    PROPERTIES() nameFilterDepartmentStore, numberDiscountCard
    
    OBJECTS rd = ReceiptDetail
    PROPERTIES(rd) READONLY nameDepartmentStore, numberZReport, numberReceipt, numberDiscountCard, nameLegalEntityDiscountCard, nameDiscountCard, date, time,  
                            id, number, nameSku, idBarcode, signedQuantity, price, signedSum, signedDiscountSum
    FILTERS date(rd) >= dFrom AND date(rd)<= dTo,
            filterDepartmentStore(rd),
            numberDiscountCard(rd) == numberDiscountCard() OR NOT numberDiscountCard()
;

DESIGN receiptDetailReport {
    NEW dates {
        type = CONTAINERH;
        caption = 'Период';
        MOVE PROPERTY(valFrom){caption = 'Дата с';}
        MOVE PROPERTY(valTo){caption = 'Дата по';}
    }
    NEW top{
        type = CONTAINERH;       
        NEW operationContainer{
            type = CONTAINERH;
            caption = 'Фильтры';
            MOVE PROPERTY(nameFilterDepartmentStore());
            MOVE PROPERTY(numberDiscountCard());
        }
    }
    MOVE BOX(rd);
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    retailNavigator {
        retailReports BEFORE retailMasterData {
            ADD receiptDetailReport;
        }
    }
}
