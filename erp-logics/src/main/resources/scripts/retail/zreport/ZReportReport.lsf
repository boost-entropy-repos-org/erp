MODULE ZReportReport;

REQUIRE POS, GiftCard;

NAMESPACE POS;

inReportStock 'Отм.' = DATA SESSION BOOLEAN (Stock);

sumReceiptDetailZReportDateDateFromTo 'Сумма Z-отчета' (date, dateFrom, dateTo)= GROUP SUM sumReceiptDetailZReport(zReport) IF 
    dateZReport(zReport) >= (dateFrom AS DATE) AND dateZReport(zReport) <= (dateTo AS DATE) AND isPostedZReport(zReport) AND
    inReportStock(departmentStoreZReport(zReport))
        BY dateZReport(zReport), dateFrom, dateTo;
    
sumReceiptDetailZReportStockDate 'Сумма Z-отчета' (stock, date)= GROUP SUM sumReceiptDetailZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);

sumPositiveCashPaymentZReportStockDate 'Сумма продажа (наличные)' (stock, date)= GROUP SUM sumPositiveCashPaymentZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);
sumPositiveCardPaymentZReportStockDate 'Сумма продажа (карточка)' (stock, date)= GROUP SUM sumPositiveCardPaymentZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);
sumGiftCardPaymentZReportStockDate 'Сумма продаж (сертификат)' (stock, date)= GROUP SUM sumGiftCardPaymentZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);
sumReceiptDetailGiftCardZReportStockDate 'Сумма проданных сертификатов' (stock, date)= GROUP SUM sumReceiptDetailGiftCardZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);                                              
discountSumReceiptDetailZReportStockDate 'Сумма скидок Z-отчета' (stock, date)= GROUP SUM discountSumReceiptDetailZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);        
VATSumSaleZReportStockDate 'Сумма НДС по продажам' (stock, date)= GROUP SUM VATSumSaleZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);    
              
sumNegativeCashPaymentZReportStockDate 'Сумма возврата (наличные)' (stock, date)= GROUP SUM sumNegativeCashPaymentZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport); 
sumNegativeCardPaymentZReportStockDate 'Сумма возврата (карточка)' (stock, date)= GROUP SUM sumNegativeCardPaymentZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);  
VATSumReturnZReportStockDate 'Сумма НДС по возвратам' (stock, date)= GROUP SUM VATSumReturnZReport(zReport) IF isPostedZReport(zReport)
        BY departmentStoreZReport(zReport), dateZReport(zReport);

nameReceiptDetailZReportStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма Z-отчета)\''](nameStock(stock));    
namePositiveCashStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма продажа (наличные))\''](nameStock(stock));    
namePositiveCardStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма продажа (карточка))\''](nameStock(stock));    
nameGiftCardStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма продаж (сертификат))\''](nameStock(stock));    
nameReceiptDetailGiftCardStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма проданных сертификатов)\''](nameStock(stock));    
nameDiscountSumStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма скидок)\''](nameStock(stock));    
nameVATSumSaleStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма НДС по продажам)\''](nameStock(stock));        
                                  
nameNegativeCashStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма возврата (наличные))\''](nameStock(stock));    
nameNegativeCardStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма возврата (карточка))\''](nameStock(stock));    
nameVATSumReturnStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(сумма НДС по возвратам)\''](nameStock(stock));                                     
                                  
countReceiptDepartmentStoreDate 'Кол-во чеков в магазине за день' (departmentStore, date) = 
    GROUP SUM countReceiptZReport(zReport) IF isPostedZReport(zReport) BY departmentStoreZReport(zReport), dateZReport(zReport);
averageSumReceiptDepartmentStoreDate 'Средняя сумма чека в магазине за день' (departmentStore, date) = 
    sumReceiptDetailZReportStockDate(departmentStore, date) / countReceiptDepartmentStoreDate(departmentStore, date);

countReceiptDetailZReport (zReport) = GROUP SUM countReceiptDetailReceipt(receipt) BY zReportReceipt(receipt);
countReceiptDetailDepartmentStoreDate 'Кол-во позиций чеков в магазине за период' (departmentStore, date) =
    GROUP SUM countReceiptDetailZReport(zReport) IF isPostedZReport(zReport) BY departmentStoreZReport(zReport), dateZReport(zReport);
averageCountReceiptDetailDepartmentStoreDate 'Среднее кол-во позиций в чеке в магазине за париод' (departmentStore, date) = 
    NUMERIC[14,2](countReceiptDetailDepartmentStoreDate(departmentStore, date)) / NUMERIC[14,2](countReceiptDepartmentStoreDate(departmentStore, date));
    
countReceiptStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(кол-во чеков)\''](nameStock(stock));        
averageSumReceiptStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(cредняя сумма чека)\''](nameStock(stock));        
countReceiptDetailStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(кол-во позиций чеков)\''](nameStock(stock));        
averageCountReceiptDetailStock (stock)= [= FORMULA VARSTRING[50] 'CAST($1 AS TEXT) || \' \' || \'(среднее кол-во позиций в чеке)\''](nameStock(stock));        
        
FORM saleZreportStock 'Отчет по магазинам'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)        
     
    OBJECTS ts = DepartmentStore FIXED GRID
    PROPERTIES nameStock(ts) READONLY, nameLegalEntityStock(ts) READONLY, inReportStock(ts)
    ORDER BY nameStock(ts)
    FILTERS isCompanyStock(ts)     
    FILTERGROUP inactiveStock FILTER 'Активный' 'ctrl F10' activeStock(ts) DEFAULT  
    
    OBJECTS st = DepartmentStore FIXED GRID     
    FILTERS isCompanyStock(st),
            inReportStock(st)   
         
    OBJECTS dt = DATE FIXED GRID
    PROPERTIES READONLY valDt = OBJVALUE(dt)
    PROPERTIES(dt) READONLY extractMonthName, extractDOWName 
    
    FILTERS    sumReceiptDetailZReportDateDateFromTo(dt,dFrom,dTo)
                         
    PROPERTIES READONLY sumReceiptDetailZReportStockDate(st,dt)  COLUMNS (st) HEADER nameReceiptDetailZReportStock(st)
    PROPERTIES READONLY sumPositiveCashPaymentZReportStockDate(st,dt)  COLUMNS (st) HEADER namePositiveCashStock(st)
    PROPERTIES READONLY sumPositiveCardPaymentZReportStockDate(st,dt)  COLUMNS (st) HEADER namePositiveCardStock(st)                                    
    PROPERTIES READONLY sumGiftCardPaymentZReportStockDate(st,dt)  COLUMNS (st) HEADER nameGiftCardStock(st)
    PROPERTIES READONLY sumReceiptDetailGiftCardZReportStockDate(st,dt)  COLUMNS (st) HEADER nameReceiptDetailGiftCardStock(st)                                 
    PROPERTIES READONLY discountSumReceiptDetailZReportStockDate(st,dt)  COLUMNS (st) HEADER nameDiscountSumStock(st)                                 
    PROPERTIES READONLY VATSumSaleZReportStockDate(st,dt)  COLUMNS (st) HEADER nameVATSumSaleStock(st)   
                                  
    PROPERTIES READONLY sumNegativeCashPaymentZReportStockDate(st,dt)  COLUMNS (st) HEADER nameNegativeCashStock(st)                                 
    PROPERTIES READONLY sumNegativeCardPaymentZReportStockDate(st,dt)  COLUMNS (st) HEADER nameNegativeCardStock(st)
    PROPERTIES READONLY VATSumReturnZReportStockDate(st,dt)  COLUMNS (st) HEADER nameVATSumReturnStock(st)                                 
    
    PROPERTIES READONLY countReceiptDepartmentStoreDate(st,dt) COLUMNS (st) HEADER countReceiptStock(st)                                 
    PROPERTIES READONLY averageSumReceiptDepartmentStoreDate(st,dt) COLUMNS (st) HEADER averageSumReceiptStock(st)                                 
    PROPERTIES READONLY countReceiptDetailDepartmentStoreDate(st,dt) COLUMNS (st) HEADER countReceiptDetailStock(st)                                 
    PROPERTIES READONLY averageCountReceiptDetailDepartmentStoreDate(st,dt) COLUMNS (st) HEADER averageCountReceiptDetailStock(st)                                 
;


DESIGN saleZreportStock FROM DEFAULT {

    ADD dates.box {
        caption = 'Период';
        type = CONTAINERH;
    }

    REMOVE st.box;
    NEW detail {
        type = SPLITH;
        fill = 1;
        ADD ts.box;
        ADD dt.box {
            fill = 3;
            caption = 'Дата/склад';
        }
        PROPERTY(sumPositiveCashPaymentZReportStockDate(st,dt)) { background = #FFEEEE; } 
        PROPERTY(sumPositiveCardPaymentZReportStockDate(st,dt)) { background = #F4FFBD; }    
        PROPERTY(sumGiftCardPaymentZReportStockDate(st,dt)) { background =  #FFBBFF; }  
        
        PROPERTY(sumReceiptDetailGiftCardZReportStockDate(st,dt)) { background = #EEEEEE; }              
        PROPERTY(discountSumReceiptDetailZReportStockDate(st,dt)) { background = #EEFFEE; }  
        PROPERTY(VATSumSaleZReportStockDate(st,dt)) { background = #E4FFBD; }             
        
        PROPERTY(sumNegativeCashPaymentZReportStockDate(st,dt)) { background = #D4FFFF; }              
        PROPERTY(sumNegativeCardPaymentZReportStockDate(st,dt)) { background = #C4FFBD; }  
        PROPERTY(VATSumReturnZReportStockDate(st,dt)) { background = #EEFFFF; }              
                 
    }                                                                                
    ADD functions.box;
}

NAVIGATOR {
    retailReports  {
        ADD saleZreportStock;
    }
}
