MODULE ReceiptProcessing;

REQUIRE ZReport;
  
diffsumPayment 'Отклонение' = sumReceiptDetail(Receipt b) (-) sumPayment(b);
prevDiffsumPayment 'Отклонение' = PREV(diffsumPayment(Receipt b));                                                                        
   
filterProcessingStock  = DATA LOCAL DepartmentStore ();
nameFilterProcessingStock 'Склад' = name[Stock](filterProcessingStock()) MINCHARWIDTH 15 PREFCHARWIDTH 20;          
filterStockProcessing (Receipt b) = departmentStore(b) == filterProcessingStock() OR (b IS Receipt AND NOT filterProcessingStock());                                                                        
                                                                      
FORM receiptProcessing 'Обработка чеков'    
    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)
    OBJECTS b=Receipt
    PROPERTIES(b) READONLY nameDepartmentStore, number, dateTime, nameEmployee, overNumberCashRegister,
                  sumReceiptDetail, discountSumReceiptDetail,
                  discountSum, sumVATReceiptDetail, countReceiptDetail, quantityReceiptDetail,
                  sumPayment, prevDiffsumPayment
    ORDER BY   dateTime(b)           
    PROPERTIES  nameFilterProcessingStock()            
    FILTERS filterStockProcessing (b),
            date(b) >= dFrom,
            date(b) <= dTo

    OBJECTS d = ReceiptDetail FIXED GRID
    PROPERTIES(d) READONLY FORCE GRID type, idBarcode
    PROPERTIES(d) READONLY FORCE GRID idSku, nameSku 
    PROPERTIES(d) FORCE GRID descriptionSale READONLY, quantity, quantityReturned,
                  price, sum, discountPercent, discountSum, numberVAT READONLY, 
                  valueVAT READONLY                  
    PROPERTIES(d) DELETE FORCE PANEL TOOLBAR               
    
    OBJECTS p=Payment
    PROPERTIES(p) namePaymentType, namePaymentMeans, sum, NEW, DELETE

    FILTERS receipt(p)==b
               
    FILTERS receipt(d) == b          
    FILTERGROUP receiptFilter
        FILTER  'Нет платежа' NOT sumPayment(b)
        FILTER  'Сумма платежей меньше суммы чека' 
            ((sumReceiptDetail(b) > 0 AND sumReceiptDetail(b) > sumPayment(b)) OR (sumReceiptDetail(b) < 0 AND sumReceiptDetail(b) != sumPayment(b)))
        FILTER  'Сумма по безналу больше суммы чека' 
            ((sumReceiptDetail(b) > 0 AND sumReceiptDetail(b) < sumCardPayment(b)) OR (sumReceiptDetail(b) <0 AND sumReceiptDetail(b) > sumCardPayment(b)))   
        FILTER  'С отклонениями'  prevDiffsumPayment(b)  DEFAULT              
                                                           
;
DESIGN receiptProcessing {

    NEW filter {
        caption = 'Фильтры';
        type = CONTAINERH;
        MOVE PROPERTY (objFrom) {caption = 'Дата с';};
        MOVE PROPERTY (objTo) {caption = 'Дата по';};
        MOVE PROPERTY (nameFilterProcessingStock());
    }
    MOVE b.box;
    NEW secondCase{
        fill = 1;
        type = SPLITH;

        MOVE d.box {
            fill = 3;
            caption = 'Строка чека';            
        }
        MOVE p.box;
    }           
    
    MOVE functions.box;
}

NAVIGATOR {
    retailDashboardNavigator {
        ADD receiptProcessing;
    }
}