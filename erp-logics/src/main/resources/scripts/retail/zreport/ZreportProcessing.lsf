MODULE ZReportProcessing;

REQUIRE ZReport, Pricing;

retailPriceReceiptDetail 'Розничная цена' (d)= IF batchReceiptDetail(d) 
    THEN priceAPriceListTypeBatchStockDateTime(SystemLedgerPriceListType.retailPricingPriceListType, batchReceiptDetail(d), departmentStoreReceiptDetail(d), dateTimeReceiptDetail(d)) 
    ELSE priceAPriceListTypeSkuStockDateTime(SystemLedgerPriceListType.retailPricingPriceListType, skuReceiptDetail(d), departmentStoreReceiptDetail(d), dateTimeReceiptDetail(d));

FORM ZReportProcessing 'Обработка Z-отчетов'
    OBJECTS ds = DepartmentStore FIXED PANEL
    PROPERTIES(ds) SELECTOR nameDepartmentStore 
    
    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)         

    FILTERS isCompanyStock(ds)

    OBJECTS d = ReceiptDetail FIXED GRID
    PROPERTIES(d) READONLY FORCE GRID nameDepartmentStoreReceiptDetail, dateTimeReceiptDetail, dateReceiptDetail, timeReceiptDetail, numberCashRegisterReceiptDetail, numberZReportReceiptDetail, numberReceiptReceiptDetail, 
                                      nameEmployeeReceiptDetail, typeReceiptDetail, idBarcodeReceiptDetail
    PROPERTIES(d) FORCE GRID idSkuReceiptDetail READONLY, nameSkuReceiptDetail ON CHANGE changeDialogSkuReceiptDetail(d)
    PROPERTIES(d) READONLY FORCE GRID descriptionSaleReceiptReturnDetail, quantityReceiptDetail, quantityReturnedReceiptSaleDetail,
                  priceReceiptDetail, sumReceiptDetail, discountPercentReceiptSaleDetail, discountSumReceiptDetail, numberVATReceiptDetail, 
                  valueVATReceiptDetail, nameSkuGroup1SkuReceiptDetail, nameSkuGroup2SkuReceiptDetail, nameSkuGroup3SkuReceiptDetail, nameSkuGroup4SkuReceiptDetail, nameSkuGroup5SkuReceiptDetail, nameSkuGroup6SkuReceiptDetail
    ORDER BY dateReceiptDetail(d)              
    FILTERGROUP department
        FILTER 'По магазину' departmentStoreReceiptDetail(d) == ds DEFAULT                  
    FILTERGROUP priceFilter
        FILTER  'Без расценки' skuReceiptDetail(d) AND NOT retailPriceReceiptDetail(d) 'F11'
        FILTER  'Разные цены' retailPriceReceiptDetail(d) != priceReceiptDetail(d) 'F10'
        
    FILTERS dateReceiptDetail(d) >= dFrom,  dateReceiptDetail(d) <= dTo  
;
DESIGN ZReportProcessing {
    NEW top {
        type = CONTAINERH;
        MOVE dates.box {caption = 'Период';}
        MOVE ds.box;
    }
    MOVE d.box;
    MOVE functions.box;
}

fillSkuReceiptDetails 'Привязать штрихкоды' = ACTION () { 
    CONFIRM 'Вы хотите перепривязать товары в соответствии со штрихкодом?';
    IF (confirmed()) THEN {
        FOR [= FILTER ZReportProcessing.d](detail) AND idBarcodeReceiptDetail(detail) AND skuReceiptDetail(detail) 
            AND NOT (skuBarcode(barcodeIdDate(idBarcodeReceiptDetail(detail), dateReceiptDetail(detail)))==skuReceiptDetail(detail))  DO {
            
            skuReceiptDetail(detail) <-  skuBarcode(barcodeIdDate(idBarcodeReceiptDetail(detail),dateReceiptDetail(detail)));               
        }
    } 
    FOR [= FILTER ZReportProcessing.d](detail) AND idBarcodeReceiptDetail(detail) AND NOT skuReceiptDetail(detail)  DO {
        skuReceiptDetail(detail) <-  skuBarcode(barcodeIdDate(idBarcodeReceiptDetail(detail),dateReceiptDetail(detail)));   
    }
    apply();
}
EXTEND FORM ZReportProcessing PROPERTIES fillSkuReceiptDetails() FORCE PANEL TOOLBAR TODRAW d;

@extendFormFilterStockAccess(ds, ZReportProcessing);

NAVIGATOR {
    retailDashboardNavigator {
        ADD ZReportProcessing;
    }
}

