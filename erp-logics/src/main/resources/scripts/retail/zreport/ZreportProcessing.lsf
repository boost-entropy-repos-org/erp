MODULE ZReportProcessing;

REQUIRE ZReport, Pricing;

retailPrice 'Розничная цена' (ReceiptDetail d)= IF batch(d) 
    THEN priceA[PriceListType,Batch,Stock,DATETIME](SystemLedgerPriceListType.retailPricingPriceListType, batch(d), departmentStore(d), dateTime(d)) 
    ELSE priceA[PriceListType,Sku,Stock,DATETIME](SystemLedgerPriceListType.retailPricingPriceListType, sku(d), departmentStore(d), dateTime(d));

FORM ZReportProcessing 'Обработка Z-отчетов'
    OBJECTS ds = DepartmentStore PANEL
    PROPERTIES(ds) SELECTOR name 
    
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)         

    FILTERS isCompany(ds)

    OBJECTS d = ReceiptDetail GRID
    PROPERTIES(d) READONLY  nameDepartmentStore, dateTime, date, time, numberCashRegister, numberZReport, numberReceipt, 
                                      nameEmployee, type, idBarcode
    PROPERTIES(d)  idSku READONLY, nameSku ON CHANGE changeDialogSku(d)
    PROPERTIES(d) READONLY  descriptionSale, quantity, quantityReturned,
                  price, sum, discountPercent, discountSum, numberVAT, 
                  valueVAT, nameSkuGroup1Sku, nameSkuGroup2Sku, nameSkuGroup3Sku, nameSkuGroup4Sku, nameSkuGroup5Sku, nameSkuGroup6Sku
    ORDER BY date(d)              
    FILTERGROUP department
        FILTER 'По магазину' departmentStore(d) == ds DEFAULT                  
    FILTERGROUP priceFilter
        FILTER  'Без расценки' sku(d) AND NOT retailPrice(d) 'F11'
        FILTER  'Разные цены' retailPrice(d) != price(d) 'F10'
        FILTER  'Без товара' NOT sku(d) 'F9'
        
    FILTERS date(d) >= dFrom,  date(d) <= dTo  
;
DESIGN ZReportProcessing {
    NEW top {
        type = CONTAINERH;
        MOVE BOX(dates) {caption = 'Период';}
        MOVE BOX(ds);
    }
    MOVE BOX(d);
    MOVE TOOLBARBOX;
}

fillSkuReceiptDetails 'Привязать штрихкоды'() = { 
    ASK 'Вы хотите перепривязать товары в соответствии со штрихкодом?' DO {
        FOR [= FILTER ZReportProcessing.d](ReceiptDetail detail) AND idBarcode(detail) AND sku(detail) 
            AND NOT (sku(barcode(idBarcode(detail), date(detail)))==sku(detail))  DO {
            
            sku(detail) <-  sku(barcode(idBarcode(detail),date(detail)));               
        }
    } 
    FOR [= FILTER ZReportProcessing.d](ReceiptDetail detail) AND idBarcode(detail) AND NOT sku(detail)  DO {
        sku(detail) <-  sku(barcode(idBarcode(detail),date(detail)));   
    }
    apply();
}
EXTEND FORM ZReportProcessing PROPERTIES fillSkuReceiptDetails() TOOLBAR TODRAW d;

@extendFormFilterStockAccess(ds, ZReportProcessing);

NAVIGATOR {
    retailDashboardNavigator {
        ADD ZReportProcessing;
    }
}

