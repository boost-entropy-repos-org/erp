MODULE ReceiptSkuLedger;

REQUIRE ZReport,SkuLedger;
   
//---------------------- Продажа ----------------//
CLASS ReceiptSaleSkuLedger 'Продажа за день (общ.)' : OutFIFOSkuLedger, SaleLedger; //-- агрегированный объект
TABLE ReceiptSaleSkuLedger (ReceiptSaleSkuLedger);
TABLE departmentStoreSkuDatePrice(DepartmentStore, Sku, DATE, NUMERIC[18,6]);

quantityReceiptSaleDetail 'Кол-во' (department, sku, date, price) = GROUP SUM quantity(ReceiptSaleDetail d) IF isPosted(d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT;
sumReceiptSaleDetail 'Сумма' (department, sku, date, price) = GROUP SUM sum(ReceiptSaleDetail d) IF isPosted(d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT @@denomination;
discountSumReceiptSaleDetail 'Сумма скидки' (department, sku, date, price) = GROUP SUM discountSum(ReceiptSaleDetail d) IF isPosted(d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT;
countReceiptSaleDetail (department, sku, date, price) = GROUP SUM 1 IF isPosted(ReceiptSaleDetail d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT;

VATReceiptSaleDetail = DATA Range (DepartmentStore, Sku, DATE, NUMERIC[18,6]);
WHEN SET(quantityReceiptSaleDetail(DepartmentStore department, Sku sku, DATE date, NUMERIC[16,4] price)) DO {
    VATReceiptSaleDetail (department, sku, date, price) <- VAT (sku, country(department));
}

departmentStore = DATA DepartmentStore (ReceiptSaleSkuLedger) INDEXED;
sku = DATA Sku (ReceiptSaleSkuLedger);
date = DATA DATE (ReceiptSaleSkuLedger);
price = DATA NUMERIC[16,4] (ReceiptSaleSkuLedger) @@denomination;

@defineAggregationCustom(departmentStoreReceiptSaleSkuLedger, skuReceiptSaleSkuLedger, dateReceiptSaleSkuLedger, priceReceiptSaleSkuLedger, departmentStore, DepartmentStore, sku, Sku, date, DATE, price, NUMERIC[16,4], receiptSaleSkuLedger, ReceiptSaleSkuLedger, countReceiptSaleDetail);

INDEX sku(ReceiptSaleSkuLedger o), departmentStore(o);
INDEX price(ReceiptSaleSkuLedger o), departmentStore(o), date(o);
INDEX date(ReceiptSaleSkuLedger o), departmentStore(o), sku(o), price(o);

dateTime[DataSkuLedger] (ReceiptSaleSkuLedger ledger) += dateTimeToDateTime(date(ledger), 23:59);
isPosted[DataSkuLedger] (ReceiptSaleSkuLedger ledger) += ledger IS ReceiptSaleSkuLedger;
isClosed[DataSkuLedger] (ReceiptSaleSkuLedger ledger) += ledger IS ReceiptSaleSkuLedger;
sku[DataSkuLedger] (ReceiptSaleSkuLedger ledger) += sku(ledger);
stock[DataSkuLedger] (ReceiptSaleSkuLedger ledger) += departmentStore(ledger);
description[DataSkuLedger] (ReceiptSaleSkuLedger ledger) += VARSTRING[200]('Продажа за день')IF ledger IS ReceiptSaleSkuLedger;

skip[SkuLedger] (ReceiptSaleSkuLedger ledger) += date(ledger) < startDateGroupCashRegister(departmentStore(ledger)) OR
                           skuType(sku(ledger)) == SkuType.skuTypeCharge;

quantity[OutFIFOSkuLedger] (ReceiptSaleSkuLedger ledger) += NUMERIC[14,3](quantityReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
sum[OutSkuLedger] (ReceiptSaleSkuLedger ledger) += NUMERIC[18,4](sumReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger))
    (+) discountSumReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));

//---------------------- SaleLedger ----------------//

dateTime[SaleLedger] (ReceiptSaleSkuLedger ledger) += toDateTime(date(ledger));
isPosted[SaleLedger] (ReceiptSaleSkuLedger ledger) += ledger IS ReceiptSaleSkuLedger;
isClosed[SaleLedger] (ReceiptSaleSkuLedger ledger) += ledger IS ReceiptSaleSkuLedger;
sku[SaleLedger] (ReceiptSaleSkuLedger ledger) += sku(ledger);
stock[SaleLedger] (ReceiptSaleSkuLedger ledger) += departmentStore(ledger);
description[SaleLedger] (ReceiptSaleSkuLedger ledger) += VARSTRING[200]('Продажа за день')IF ledger IS ReceiptSaleSkuLedger;

cost(ReceiptSaleSkuLedger ledger, Batch batch) +=  cost[SkuLedger,Batch](ledger, batch) IF ledger IS ReceiptSaleSkuLedger;
quantity[SaleLedger] (ReceiptSaleSkuLedger ledger) += NUMERIC[14,3](quantityReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
VAT (ReceiptSaleSkuLedger ledger) += VATReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger));
sum[SaleLedger] (ReceiptSaleSkuLedger ledger) += NUMERIC[18,4](sumReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
price (ReceiptSaleSkuLedger ledger) += price(ledger);

discountSum (ReceiptSaleSkuLedger ledger) += NUMERIC[18,4](discountSumReceiptSaleDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));

costSum (ReceiptSaleSkuLedger ledger) += WHEN ledger IS ReceiptSaleSkuLedger THEN costSum[SkuLedger](ledger);

//---------------------- Возврат ----------------//

CLASS ReceiptReturnSkuLedger 'Возврат за день (общ.)' : InLIFOSkuLedger, SaleLedger; //-- агрегированный объект
TABLE ReceiptReturnSkuLedger (ReceiptReturnSkuLedger);

quantityReceiptReturnDetail 'Кол-во' (department, sku, date, price) = GROUP SUM quantity(ReceiptReturnDetail d) IF isPosted(d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT;
sumReceiptReturnDetail 'Сумма' (department, sku, date, price) = GROUP SUM sum(ReceiptReturnDetail d) IF isPosted(d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT @@denomination;
discountSumReceiptReturnDetail 'Сумма скидки' (department, sku, date, price) = GROUP SUM discountSum(ReceiptReturnDetail d) IF isPosted(d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT;
countReceiptReturnDetail  (department, sku, date, price) = GROUP SUM 1 IF isPosted(ReceiptReturnDetail d)
    BY departmentStore(d), sku(d), dateZReport(d), NUMERIC[18,6](price(d)) PERSISTENT;

VATReceiptReturnDetail = DATA Range (DepartmentStore, Sku, DATE, NUMERIC[18,6]);
WHEN SET(quantityReceiptReturnDetail(DepartmentStore department, Sku sku, DATE date, NUMERIC[16,4] price)) DO {
    VATReceiptReturnDetail (department, sku, date, price) <- VAT (sku, country(department));
}

departmentStore = DATA DepartmentStore (ReceiptReturnSkuLedger) INDEXED;
sku = DATA Sku (ReceiptReturnSkuLedger);
date = DATA DATE (ReceiptReturnSkuLedger);
price = DATA NUMERIC[16,4] (ReceiptReturnSkuLedger) @@denomination;

@defineAggregationCustom(departmentStoreReceiptReturnSkuLedger, skuReceiptReturnSkuLedger, dateReceiptReturnSkuLedger, priceReceiptReturnSkuLedger, departmentStore, DepartmentStore, sku, Sku, date, DATE, price, NUMERIC[16,4], receiptReturnSkuLedger, ReceiptReturnSkuLedger, countReceiptReturnDetail);

INDEX sku(ReceiptReturnSkuLedger o), departmentStore(o);
INDEX price(ReceiptReturnSkuLedger o), departmentStore(o), date(o);
INDEX date(ReceiptReturnSkuLedger o), departmentStore(o), sku(o), price(o);

dateTime[DataSkuLedger] (ReceiptReturnSkuLedger ledger) += dateTimeToDateTime(date(ledger), 23:59);
isPosted[DataSkuLedger] (ReceiptReturnSkuLedger ledger) += ledger IS ReceiptReturnSkuLedger;
isClosed[DataSkuLedger] (ReceiptReturnSkuLedger ledger) += ledger IS ReceiptReturnSkuLedger;
sku[DataSkuLedger] (ReceiptReturnSkuLedger ledger) += sku(ledger);
stock[DataSkuLedger] (ReceiptReturnSkuLedger ledger) += departmentStore(ledger);
description[DataSkuLedger] (ReceiptReturnSkuLedger ledger) += VARSTRING[200]('Возврат за день')IF ledger IS ReceiptReturnSkuLedger;

skip[SkuLedger] (ReceiptReturnSkuLedger ledger) += date(ledger) < startDateGroupCashRegister(departmentStore(ledger));

quantity[InLIFOSkuLedger] (ReceiptReturnSkuLedger ledger) += NUMERIC[14,3](quantityReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
sum[InSkuLedger] (ReceiptReturnSkuLedger ledger) += NUMERIC[18,4](sumReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger))
    (+) discountSumReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));

//---------------------- SaleLedger ----------------//

dateTime[SaleLedger] (ReceiptReturnSkuLedger ledger) += toDateTime(date(ledger));
isPosted[SaleLedger] (ReceiptReturnSkuLedger ledger) += ledger IS ReceiptReturnSkuLedger;
isClosed[SaleLedger] (ReceiptReturnSkuLedger ledger) += ledger IS ReceiptReturnSkuLedger;
sku[SaleLedger] (ReceiptReturnSkuLedger ledger) += sku(ledger);
stock[SaleLedger] (ReceiptReturnSkuLedger ledger) += departmentStore(ledger);
description[SaleLedger] (ReceiptReturnSkuLedger ledger) += VARSTRING[200]('Возврат за день')IF ledger IS ReceiptReturnSkuLedger;

cost(ReceiptReturnSkuLedger ledger, Batch batch) += -cost[SkuLedger,Batch](ledger, batch) IF ledger IS ReceiptReturnSkuLedger;
quantity[SaleLedger] (ReceiptReturnSkuLedger ledger) += -NUMERIC[14,3](quantityReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
VAT (ReceiptReturnSkuLedger ledger) += VATReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger));
price (ReceiptReturnSkuLedger ledger) += price(ledger);
sum[SaleLedger] (ReceiptReturnSkuLedger ledger) += -NUMERIC[18,4](sumReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
discountSum (ReceiptReturnSkuLedger ledger) += -NUMERIC[18,4](discountSumReceiptReturnDetail(departmentStore(ledger), sku(ledger), date(ledger), price(ledger)));
costSum (ReceiptReturnSkuLedger ledger) += WHEN ledger IS ReceiptReturnSkuLedger THEN -costSum[SkuLedger](ledger);

isReceiptDetail (SaleLedger d) = d IS ReceiptSaleSkuLedger OR d IS ReceiptReturnSkuLedger;