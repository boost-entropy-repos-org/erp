MODULE ZReportPayment;

REQUIRE ZReport, Payment;

typePayment = DATA INTEGER (Payment.Payment);
nameTypePayment 'Тип платежа' = CASE WHEN typePayment(payment) == 0 THEN 'Наличные'
                                                      WHEN typePayment(payment) == 1 THEN 'Карточка';
zReportPayment = DATA ZReport (Payment.Payment);
numberZReportPayment 'Номер Z-отчета' = numberZReport(zReportPayment(payment));

extraSumCashZReport 'Дополнительная сумма Z-отчета(наличные)' = 
    GROUP SUM Payment.sumPayment(payment) IF typePayment(payment) == 0 AND NOT isReturnPayment(payment) BY zReportPayment(payment);  
extraNegativeSumCashZReport 'Дополнительная сумма возврата Z-отчета(наличные)' = 
    GROUP SUM Payment.sumPayment(payment) IF typePayment(payment) == 0 AND isReturnPayment(payment) BY zReportPayment(payment);      
     
extraSumCardZReport 'Дополнительная сумма Z-отчета(карточка)' = 
    GROUP SUM Payment.sumPayment(payment) IF typePayment(payment) == 1 AND NOT isReturnPayment(payment) BY zReportPayment(payment);   
extraNegativSumCardZReport 'Дополнительная сумма возврата Z-отчета(карточка)' = 
    GROUP SUM Payment.sumPayment(payment) IF typePayment(payment) == 1 AND isReturnPayment(payment) BY zReportPayment(payment);   
        
extraSumZReport 'Дополнительная сумма Z-отчета' = 
    GROUP SUM Payment.sumPayment(payment) BY zReportPayment(payment);   

totalSumCashZReport 'Общая сумма Z-отчета(наличные)' (ZReport) = sumCashZReport(ZReport) (+) extraSumCashZReport(ZReport);
totalSumCardZReport 'Общая сумма Z-отчета(карточка)' (ZReport) = sumCardZReport(ZReport) (+) extraSumCardZReport(ZReport);
totalSumZReport 'Общая сумма Z-отчета' (ZReport) = sumReceiptDetailZReport(ZReport) (+) extraSumZReport(ZReport);

EXTEND FORM zReports
    PROPERTIES(z) READONLY BEFORE sumReceiptDetailZReport(z) totalSumZReport, totalSumCashZReport, totalSumCardZReport
    PROPERTIES(z) READONLY AFTER sumReceiptDetailZReport(z) extraSumZReport, extraSumCashZReport, extraSumCardZReport, 
                                 extraNegativeSumCashZReport, extraNegativSumCardZReport
;

EXTEND FORM payments
    PROPERTIES(p) READONLY numberZReportPayment, nameTypePayment
;
