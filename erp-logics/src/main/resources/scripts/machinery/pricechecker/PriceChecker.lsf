MODULE PriceChecker;

REQUIRE System, Machinery;

// Группы
CLASS GroupPriceChecker 'Группы прайс чекеров' : GroupMachinery;

stockGroupPriceChecker = DATA Stock (GroupPriceChecker);
stockGroupMachinery (groupMachinery) += stockGroupPriceChecker(groupMachinery);

// Модели
CLASS PriceCheckerModel 'Модель прайс чекеров' : Model;

namePriceCheckerModel 'Наименование' = DATA VARISTRING[110](PriceCheckerModel);

nameModel(model) += namePriceCheckerModel(model) IF model IS PriceCheckerModel;

FORM priceCheckerModel 'Модель прайс чекера'
    OBJECTS ch=PriceCheckerModel FIXED PANEL
    PROPERTIES(ch) namePriceCheckerModel, noteModel, handlerModel, maxProductModel
    EDIT PriceCheckerModel OBJECT ch
;

FORM priceCheckerModels 'Модели прайс чекеров'
    OBJECTS m=PriceCheckerModel
    PROPERTIES(m) READONLY namePriceCheckerModel, maxProductModel, handlerModel, noteModel
    PROPERTIES(m) ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
;

// Прайс-чекеры
CLASS PriceChecker 'Прайс чекер' : Machinery;
isGroupPriceChecker (machinery) = machinery IS GroupPriceChecker;

groupPriceCheckerPriceChecker = DATA GroupPriceChecker (PriceChecker);
groupMachineryMachinery(machinery) += groupPriceCheckerPriceChecker(machinery);

priceCheckerModelPriceChecker = DATA PriceCheckerModel (PriceChecker);
modelMachinery(machinery) += priceCheckerModelPriceChecker(machinery);

FORM priceChecker 'Прайс чекер'
    OBJECTS ch=PriceChecker FIXED PANEL
    PROPERTIES(ch) nameGroupMachineryMachinery, nppMachinery, descriptionMachinery, portMachinery
    EDIT PriceChecker OBJECT ch
;

FORM groupPriceChecker 'Группа прайс чекеров'
    OBJECTS grch=GroupPriceChecker FIXED PANEL
    PROPERTIES(grch) nppGroupMachinery, nameStockGroupMachinery, nameGroupMachinery,
                     filterSkuGroupMachinery, showFilterSkuGroupMachinery SHOWIF filterSkuGroupMachinery(grch),
                     namePriceListTypeGroupMachinery

    OBJECTS ch=PriceChecker
    PROPERTIES(ch)  nameGroupMachineryMachinery, nppMachinery, descriptionMachinery, portMachinery, ADDOBJ, DELETESESSION
    FILTERGROUP filters3
        FILTER 'Показывать только для данной группы' 'F10' groupPriceCheckerPriceChecker(ch) == grch DEFAULT
    EDIT GroupPriceChecker OBJECT grch
;

FORM groupsPriceChecker 'Группы прайс чекеров'
    OBJECTS grch=GroupPriceChecker
    PROPERTIES(grch) READONLY nppGroupMachinery, nameStockGroupMachinery, nameGroupMachinery,
                              filterSkuGroupMachinery, showFilterSkuGroupMachinery SHOWIF filterSkuGroupMachinery(grch)
    PROPERTIES(grch)          ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    OBJECTS ch=PriceChecker
    PROPERTIES(ch)   READONLY nppMachinery, descriptionMachinery, portMachinery
    FILTERS groupPriceCheckerPriceChecker(ch) == grch
;

DESIGN groupsPriceChecker FROM DEFAULT {
    NEW topContainer{
        fill = 1;
        type = SPLITV;

        ADD grch.box;
        ADD ch.box;
    }
    ADD functions.box;
}

// ------------------------------------------------ Стандартные значения ------------------------------------ //

loadDefaultPriceCheckerModel 'Добавить модель прайс-чекера' = ACTION (stringOne, stringTwo)  {
    FOR ADDOBJ pcm = PriceCheckerModel DO {
         ASSIGN namePriceCheckerModel(pcm) <- stringOne;
         ASSIGN handlerModel(pcm) <- stringTwo;
    }
}

loadDefaultPriceCheckerModels 'Загрузить стандартные модели прайс-чекеров' = ACTION () {
    EXEC loadDefaultPriceCheckerModel('Прайс-чекеры EasyCSV', 'equ.clt.handler.easy.EasyCSVHandler');
} IN loadDefault;

@implementLoadDefaultData(loadDefaultPriceCheckerModels);

// --------------------------------------- Генерация групп прайс-чекеров -------------------------------- //
loadDefaultGroupPriceChecker 'Сгенерировать группу прайс-чекеров' = ACTION(stock, model, num) {
    FOR ADDOBJ g = GroupPriceChecker DO {
        stockGroupPriceChecker(g) <- stock;
        nameGroupMachinery(g) <- 'Группа прайс-чекеров по умолчанию';

        LOCAL numPriceChecker = INTEGER();
        numPriceChecker() <- 0;
        WHILE numPriceChecker() < num DO {
            FOR ADDOBJ pc = PriceChecker DO {
                groupPriceCheckerPriceChecker(pc) <- g;
                numPriceChecker() <- numPriceChecker() + 1;
                nppMachinery(pc) <- numPriceChecker();
                priceCheckerModelPriceChecker(pc) <- model;
            }
        }
    }
} IN loadDefault;

EXTEND FORM defaultData
    OBJECTS         pm=PriceCheckerModel FIXED PANEL
    PROPERTIES(pm)  SELECTOR namePriceCheckerModel

    OBJECTS         cPriceChecker=INTEGER FIXED PANEL
    PROPERTIES(cPriceChecker)   intValuePriceChecker = OBJVALUE
    PROPERTIES (s, pm, cPriceChecker)  loadDefaultGroupPriceChecker
;

EXTEND DESIGN defaultData {
    PROPERTY(namePriceCheckerModel(pm)) {
        caption = 'Модель прайс-чекеров';
    }
    PROPERTY(intValuePriceChecker) {
        caption = 'Количество прайс-чекеров';
    }
    machinery {
        NEW priceChecker {
            caption = 'Прайс-чекеры';
            ADD PROPERTY(namePriceCheckerModel(pm));
            ADD PROPERTY(intValuePriceChecker);
            ADD PROPERTY(loadDefaultGroupPriceChecker(s,pm,cPriceChecker));
        }
    }
}

NAVIGATOR {
    machineryNavigator {
        NEW priceCheckerNavigator 'Прайс-чекеры' {
            ADD groupsPriceChecker;
            ADD priceCheckerModels;
        }
    }
}