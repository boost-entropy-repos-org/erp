MODULE PaymentTerminal;

REQUIRE System, Machinery, Store, LegalEntity, CashRegister;

// Модели
CLASS PaymentTerminalModel 'Модель платёжных терминалов' : Model;

name 'Наименование' = DATA VARISTRING[110](PaymentTerminalModel);

name(PaymentTerminalModel model) += name(model) IF model IS PaymentTerminalModel;

FORM paymentTerminalModel 'Модель платёжных терминалов'
    OBJECTS p=PaymentTerminalModel FIXED PANEL
    PROPERTIES(p) name, sid, note, handler
    EDIT PaymentTerminalModel OBJECT p
;

FORM paymentTerminalsModels 'Модели платёжных терминалов'
    OBJECTS p=PaymentTerminalModel
    PROPERTIES(p) READONLY name, sid, handler, note
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE FORCE PANEL TOOLBAR
    LIST PaymentTerminalModel OBJECT p
;

NAVIGATOR {
    cashRegisterNavigator {
        ADD paymentTerminalsModels; 
    }
}

// Привязываем кассу к платежным терминалам 
paymentTerminalModel 'Модель платёжного терминала' = DATA PaymentTerminalModel(CashRegister) IN base;
namePaymentTerminalModel 'Модель платёжного терминала' (CashRegister cashRegister) = name[Model](paymentTerminalModel(cashRegister));

comPortPaymentTerminalModel 'Порт платёжного терминала' = DATA INTEGER (CashRegister) IN base;
comPortCurrentPaymentTerminalModelCashRegister 'Порт платёжного терминала' = comPortPaymentTerminalModel(currentCashRegister());

EXTEND FORM groupCashRegister 
    PROPERTIES(c) namePaymentTerminalModel, comPortPaymentTerminalModel
;

// ------------------------------------------------ Стандартные значения ------------------------------------ //

loadDefaultPaymentTerminalModel 'Добавить модель платёжных терминалов'(VARISTRING[110] iname, VARSTRING[20] isid, VARSTRING[200] ihandler) = ACTION  {
    FOR NEW ptm = PaymentTerminalModel DO {
         ASSIGN name(ptm) <- iname;
         ASSIGN sid(ptm) <- isid;
         ASSIGN handler(ptm) <- ihandler;
    }
}

loadDefaultPaymentTerminalModels 'Загрузить стандартные модели платёжных терминалов'() = ACTION {
    EXEC loadDefaultPaymentTerminalModel('Платёжные терминалы JadeEKO', 'JadeEKO', '');
} IN loadDefault;

@implementLoadDefaultData(loadDefaultPaymentTerminalModels);