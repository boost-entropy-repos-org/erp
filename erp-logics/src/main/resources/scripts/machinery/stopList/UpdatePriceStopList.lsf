MODULE UpdatePriceStopList;

REQUIRE UpdatePriceDashboard, StopList;

NAMESPACE Pricing;

statusStopList 'Запрет продаж' (Sku sku, Stock stock)= IF inStopList(sku, stock, currentDateTime())
    THEN 'В стоп-листе'
    ELSE 'Нет запрета' MINCHARWIDTH 10 PREFCHARWIDTH 12;
backgroundStopList 'Цвет' (Sku sku, Stock stock) = RGB (255,0,0) IF inStopList(sku, stock, currentDateTime());

notResetStoplistSelectSku 'Не сбрасывать отмеченные товары при включении/исключении из стоп-листа' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() notResetStoplistSelectSku
;
DESIGN options {
    stock1 {
        MOVE PROPERTY(notResetStoplistSelectSku());
    }
}

createReversStopListStock 'Исключить из стоп-листа'(department)  = {
    IF countSelectSkus() THEN {
        NEWSESSION {   
            FOR NEW sl = StopList DO {    
                in(Stock st, sl) <- TRUE WHERE st == department;  
                exclude(sl) <-TRUE;
                isPosted(sl) <-TRUE;
                       
                FOR select(Sku sku) NEW d = StopListDetail DO {
                    
                    stopList(d) <- sl;
                    sku(d) <- sku;            
                }   
                SHOW stopList OBJECTS sl =sl MANAGESESSION DOCKED;
            }        
        }
        IF NOT notResetStoplistSelectSku() THEN {
            select(Sku sku) <- NULL;
        }
    } ELSE {
        MESSAGE 'Не отмечены товары.';
    }
};
createStopListStock 'Включить в стоп-лист'(department)  = {
    IF countSelectSkus() THEN {
        NEWSESSION {   
            FOR NEW sl = StopList DO {    
                in(Stock st, sl) <- TRUE WHERE st == department;  
                exclude(sl) <-NULL;
                isPosted(sl) <-TRUE;
                       
                FOR select(Sku sku) NEW d = StopListDetail DO {
                    
                    stopList(d) <- sl;
                    sku(d) <- sku;            
                }   
                SHOW stopList OBJECTS sl =sl MANAGESESSION DOCKED;
            }        
        }
        IF NOT notResetStoplistSelectSku() THEN {
            select(Sku sku) <- NULL;
        }
    } ELSE {
        MESSAGE 'Не отмечены товары.';
    }
};


EXTEND FORM updatePriceDashboard
    PROPERTIES (d) TOOLBAR SHOWIF countSelectSkus()  TODRAW sk  createReversStopListStock, createStopListStock
    PROPERTIES (sk,d) READONLY statusStopList BACKGROUND backgroundStopList(sk,d) ON SHORTCUT showHistoryStopList(sk, d)
    FILTERGROUP stop
        FILTER 'Разрешен к продаже' NOT inStopList(sk, d, currentDateTime()) 'F8' DEFAULT
;

DESIGN updatePriceDashboard {

    sk.box {
        NEW skuTopPane FIRST{
            type = CONTAINERH;
            NEW stoplist {
                type = CONTAINERH;
                caption = 'Стоп-листы';
                MOVE filters.stop;
                MOVE PROPERTY(createReversStopListStock(d)); 
                MOVE PROPERTY(createStopListStock(d)); 
            }
            NEW filters {
                type = CONTAINERH;
                caption = 'Фильтры';
                MOVE filters.inactive;
                MOVE filters.error;
            }
        
        }
    }

}
