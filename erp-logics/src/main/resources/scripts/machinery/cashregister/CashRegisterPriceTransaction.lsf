MODULE CashRegisterPriceTransaction;

REQUIRE CashRegister, MachineryPriceTransaction;

NAMESPACE CashRegister;

// -------------------------- Загрузка прайса в оборудование --------------------- //
CLASS CashRegisterPriceTransaction 'Загрузка прайса в кассы' : MachineryPriceTransaction;
groupCashRegisterCashRegisterPriceTransaction 'Группа касс' = DATA GroupCashRegister (CashRegisterPriceTransaction);
groupMachineryMachineryPriceTransaction (transaction) += groupCashRegisterCashRegisterPriceTransaction(transaction);

EXTEND FORM groupMachineryInput
    PROPERTIES(m) READONLY FORCE GRID SHOWIF isGroupCashRegister(g) comPortCashRegister, baudRateCashRegister, nameModelMachinery,
                                                                    dataDirectoryCashRegister, dateCashRegister
;

createMachineryPriceTransactionGroupMachinery (groupMachinery) += ACTION (groupMachinery) {
    IF groupMachinery IS GroupCashRegister THEN
        ADDOBJ CashRegisterPriceTransaction;
}

succeededCashRegisterCashRegisterPriceTransaction 'Загружена' = DATA BOOLEAN(CashRegister, CashRegisterPriceTransaction);
dateTimeSucceededCashRegisterCashRegisterPriceTransaction 'Время загрузки' = DATA DATETIME(CashRegister, CashRegisterPriceTransaction);
showCashRegister(t) = t IS CashRegisterPriceTransaction;

countSucceededCashRegistersPriceTransactionDocument'Кол-во загруженных касс' (priceTransactionDocument) = GROUP SUM 1 IF succeededCashRegisterCashRegisterPriceTransaction(cr,t) 
    BY priceTransactionDocumentMachineryPriceTransaction(t);

countCashRegistersGroupCashRegister 'Кол-во касс в группе' (gcr) =GROUP SUM 1 BY groupCashRegisterCashRegister(cr);
countCashRegistersPriceTransactionDocument'Кол-во касс в группе' (priceTransactionDocument) = GROUP SUM countCashRegistersGroupCashRegister(groupMachineryMachineryPriceTransaction(t)) BY priceTransactionDocumentMachineryPriceTransaction(t);

concatSucceededCashRegistersCashRegisterPriceTransaction 'Кол-во касс' (t)  = CONCAT '/', countSucceededCashRegistersPriceTransactionDocument(t), countCashRegistersPriceTransactionDocument(t); 

isPartlyPriceTransactionDocument (t) += t IS PriceTransactionDocument AND (countSucceededCashRegistersPriceTransactionDocument(t) != countCashRegistersPriceTransactionDocument(t));
partlyStatusPriceTransactionDocument (t) += STRING[30](CONCAT ' ', 'Частично загружен', concatSucceededCashRegistersCashRegisterPriceTransaction(t));


EXTEND FORM machineryPriceTransactions
    OBJECTS c = CashRegister FIXED GRID
    PROPERTIES(c) SHOWIF showCashRegister(t) nppMachinery, descriptionMachinery, dataDirectoryCashRegister 
    PROPERTIES(c, t) SHOWIF showCashRegister(t) succeededCashRegisterCashRegisterPriceTransaction, dateTimeSucceededCashRegisterCashRegisterPriceTransaction 
;

EXTEND DESIGN machineryPriceTransactions {
    specContainer {
        NEW cashRegisterContainer {
            fill = 1;
            caption = 'Кассы';
            ADD c.box;
        }     
    }
}
