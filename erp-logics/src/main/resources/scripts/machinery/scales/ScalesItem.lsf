MODULE ScalesItem;

REQUIRE Item, Machinery, Scales;

NAMESPACE Item;

createAttributeMachineryPriceTransactionGroupMachinery(transaction, groupMachinery) += ACTION (transaction, groupMachinery) {
    IF groupMachinery IS GroupScales THEN
        ASSIGN nameMachineryPriceTransactionBarcode(transaction, barcode) <- titleScalesBarcode(barcode) WHERE inMachineryPriceTransactionBarcode(transaction, barcode);
}

GROUP scales 'Весы' : public;

// Фильтрация для передачи на весы
passScalesItem 'Передавать на весы' = DATA BOOLEAN (Item) IN scales;
overInGroupMachinerySku(groupMachinery, sku) += groupMachinery IS GroupScales AND sku IS Item AND NOT passScalesItem(sku);

// Формат этикетки
labelFormatPackItem = DATA LabelFormat(Item);
idLabelFormatPackItem 'Код формата этикетки' (item) = idLabelFormat(labelFormatPackItem(item)) IN scales;
labelFormatBarcode 'Формат этикетки' = idLabelFormatPackItem(skuBarcode(barcode));

@defineMachineryPriceTransactionAttribute(labelFormat, 'Формат этикетки', INTEGER);

// Сроки годности
daysExpiryPackItem 'Срок годности в днях с момента фасовки' = DATA NUMERIC[8,2] (Item) IN scales;
hoursExpiryPackItem 'Срок годности в часах с момента фасовки' = DATA INTEGER (Item) IN scales;

daysExpiryBarcode 'Срок годности в днях с момента фасовки' = daysExpiryPackItem(skuBarcode(barcode));
hoursExpiryBarcode 'Срок годности в часах с момента фасовки' = hoursExpiryPackItem(skuBarcode(barcode));

@defineMachineryPriceTransactionAttribute(daysExpiry, 'Срок годности в днях с момента фасовки', NUMERIC[8,2]);
@defineMachineryPriceTransactionAttribute(hoursExpiry, 'Срок годности в часах с момента фасовки', INTEGER);

// Состав товара
compositionScalesItem 'Состав товара (ингредиенты) для весов' = DATA TEXT (Item) IN scales;
compositionBarcode 'Состав товара' = compositionScalesItem(skuBarcode(barcode));

@defineMachineryPriceTransactionAttribute(composition, 'Состав товара', TEXT);

synchronizeCompositionMachineryPriceTransactionGroupScales = ACTION (transaction, groupMachinery) {
    FOR compositionMachineryPriceTransactionBarcode(transaction, barcode) IF groupMachinery IS GroupScales
        AND NOT cellScalesGroupScalesComposition(groupMachinery, compositionMachineryPriceTransactionBarcode(transaction, barcode))
        ADDOBJ c = CellScales DO {
            ASSIGN groupScalesCellScales(c) <- groupMachinery;
            ASSIGN compositionCellScales(c) <- compositionMachineryPriceTransactionBarcode (transaction, barcode);
        }
}

snapshotAttributeMachineryPriceTransactionGroupMachinery(transaction, groupMachinery) += synchronizeCompositionMachineryPriceTransactionGroupScales(transaction, groupMachinery);
createAttributeMachineryPriceTransactionGroupMachinery(transaction, groupMachinery) += synchronizeCompositionMachineryPriceTransactionGroupScales(transaction, groupMachinery);

EXTEND FORM item
    PROPERTIES(i) passScalesItem
    PROPERTIES(i) SHOWIF passScalesItem(i) idLabelFormatPackItem, daysExpiryPackItem, hoursExpiryPackItem, compositionScalesItem
;
EXTEND DESIGN item {
    itemPrimaryColumn2 {
        ADD i.scales {
            columns = 1;
        }
    }
}

overCopyItem(s, d) += ACTION (s, d) {
    ASSIGN passScalesItem(d) <- passScalesItem(s);
    ASSIGN labelFormatPackItem(d) <- labelFormatPackItem(s);
    ASSIGN daysExpiryPackItem(d) <- daysExpiryPackItem(s);
    ASSIGN hoursExpiryPackItem(d) <- hoursExpiryPackItem(s);
    ASSIGN compositionScalesItem(d) <- compositionScalesItem(s);
}
