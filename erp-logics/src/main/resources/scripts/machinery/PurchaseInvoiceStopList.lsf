MODULE  PurchaseInvoiceStopList;

REQUIRE PurchaseStopList, PurchaseInvoiceMachinery;

NAMESPACE StopList;

statusStopList 'Запрет продаж' (Purchase.InvoiceDetail d)= IF skipMachineryPriceTransaction(sku(d), customerStock(d), currentDateTime())
    THEN 'В стоп-листе'
    ELSE 'Нет запрета' MINCHARWIDTH 10 PREFCHARWIDTH 12;
backgroundStopList 'Цвет' (Purchase.InvoiceDetail d) = RGB (255,0,0) IF skipMachineryPriceTransaction(sku(d), customerStock(d), currentDateTime());

countInStopList 'Количество товаров в стоп-листе ' (i,s)= GROUP SUM 1 
    IF skipMachineryPriceTransaction(sku(Purchase.InvoiceDetail d), customerStock(d), currentDateTime()) 
          BY invoice(d), sku(d);

countSkuInStopList 'Количество товаров в стоп-листе ' = GROUP SUM 1 
    IF countInStopList(Purchase.Invoice i,Sku s) BY i;
            
createReversStopList 'Исключить из стоп-листа'(Purchase.Invoice invoice)  = ACTION {

    NESTEDSESSION {   
        FOR ADDOBJ sl = StopList DO {    
            in(Stock st, sl) <- TRUE WHERE st == customerStock(invoice);  
            exclude(sl) <-TRUE;
            isPosted(sl) <-TRUE;
                   
            FOR countInStopList(invoice, Sku sku) ADDOBJ d = StopListDetail DO {
                
                stopList(d) <- sl;
                sku(d) <- sku;            
            }   
            FORM stopList OBJECTS sl =sl MANAGESESSION DOCKEDMODAL;
        }        
    }
};

EXTEND FORM userInvoice
    PROPERTIES (i) READONLY FORCE PANEL SHOWIF countSkuInStopList(i) TODRAW d countSkuInStopList,
                   createReversStopList EDITABLE 
    PROPERTIES (d) READONLY statusStopList BACKGROUND backgroundStopList(d)
    
;
DESIGN userInvoice {
    d.panel {
        NEW stopList {
            type = CONTAINERH;
            caption = 'Стоп-лист';            
            MOVE PROPERTY (countSkuInStopList(i)) {background = #FF0000;}
            MOVE PROPERTY (createReversStopList(i));
        }        
    }    
}  
EXTEND FORM invoices
    PROPERTIES (d) READONLY statusStopList BACKGROUND backgroundStopList(d)
;