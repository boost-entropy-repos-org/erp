MODULE  PurchaseStopList;

REQUIRE StopList,
        PurchaseLedger
        ;

NAMESPACE StopList;

//-------------------- Создание стоп-листа для склада, если нет движения и остатков----------------------//
     
ff 'Дата с' = subtract(currentDate(), LONG int);
FORM stockDays 'Выбор склада и количества дней'

    OBJECTS s=Stock PANEL

    PROPERTIES(s) SELECTOR name
                   
    OBJECTS i=INTEGER PANEL
    PROPERTIES    val =OBJVALUE(i)     
    PROPERTIES(i) ff
    FILTERS isCompany(s)                               

;
DESIGN stockDays {
    main {
        NEW top {
            type = CONTAINERH;
            
            MOVE s.box {
                caption = 'Выбор склада';
            }
            MOVE i.box {
                caption = 'Выбор количества дней';
                PROPERTY (val) {caption = 'Количество дней без поставок';}
            } 
        } 
        MOVE functions.box;
    }     
}


quantityPurchase 'Закуплено за интервал (кол-во)' (sku, stock, dateFrom) = GROUP SUM
        PurchaseLedger.quantityPurchase(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom 
        BY sku, stock, dateFrom;
        
notPurchase (stock, dateFrom, dateTime)= GROUP SUM 1 IF NOT (quantityPurchase(Sku sku, Stock stock, DATE dateFrom) OR currentBalance(sku, stock) OR lastStopListDetail(sku, stock, DATETIME dateTime)) 
    AND sku IS Sku AND stock IS Stock AND dateFrom IS DATE AND dateTime IS DATETIME 
        BY stock, dateFrom, dateTime; 
     
createStopList 'Создать по товарам без прихода и остатка'()  = ACTION {
    DIALOG stockDays OBJECTS s INPUT, i INPUT DO {
        
        IF notPurchase(s, subtract(currentDate(), i), currentDateTime()) THEN {
            FOR NEW sl = StopList DO {
            
                in(s, sl) <- TRUE;  
               
                
                FOR Sku sku IS Sku IF NOT (quantityPurchase(sku, s, subtract(currentDate(), i)) OR currentBalance(sku, s) OR lastStopListDetail(sku, s, currentDateTime()))   
                    NEW d = StopListDetail DO {
                    
                    stopList(d) <- sl;
                    sku(d) <- sku;
                }   
                SHOW stopList OBJECTS sl = sl MANAGESESSION DOCKED NOCANCEL;
            }
        } ELSE {
            MESSAGE 'Для данного склада не требуется создание "Запрета продаж по кассе".';
        }
         
    }
} TOOLBAR;

EXTEND FORM stopLists
    PROPERTIES () createStopList TODRAW sl 
;
