MODULE  PurchaseStopList;

REQUIRE StopList,
        PurchaseLedger
        ;

NAMESPACE StopList;

//-------------------- Создание стоп-листа для склада, если нет движения и остатков----------------------//
     
ff 'Дата с' = subtract(currentDate(), LONG int);
FORM stockDays 'Выбор склада и количества дней'

    OBJECTS s=Stock FIXED PANEL

    PROPERTIES(s) SELECTOR name
                   
    OBJECTS i=INTEGER FIXED PANEL
    PROPERTIES    val =OBJVALUE(i)     
    PROPERTIES(i) ff
    FILTERS isCompany(s)                               

;
DESIGN stockDays {
    main {
        NEW top {
            type = CONTAINERH;
            
            MOVE s.box {
                caption = 'Выбор склада';
            }
            MOVE i.box {
                caption = 'Выбор количества дней';
                PROPERTY (val) {caption = 'Количество дней без поставок';}
            } 
        } 
        MOVE functions.box;
    }     
}


quantityPurchase 'Закуплено за интервал (кол-во)' (sku, stock, dateFrom) = GROUP SUM
        PurchaseLedger.quantityPurchase(Sku sku, Stock stock, DATE date) IF date >= DATE dateFrom 
        BY sku, stock, dateFrom;
        
notPurchase (stock, dateFrom, dateTime)= GROUP SUM 1 IF NOT (quantityPurchase(Sku sku, Stock stock, DATE dateFrom) OR currentBalance(sku, stock) OR lastStopListDetail(sku, stock, DATETIME dateTime)) 
    AND sku IS Sku AND stock IS Stock AND dateFrom IS DATE AND dateTime IS DATETIME 
        BY stock, dateFrom, dateTime; 
     
createStopList 'Создать по товарам без прихода и остатка'()  = ACTION {
    FORM stockDays ; 
    IF formResult() == FormResult.ok THEN{
        
        IF notPurchase(chosenObject('s'), subtract(currentDate(), chosenInteger('i')), currentDateTime()) THEN {
            FOR ADDOBJ sl = StopList DO {
            
                in(Stock st, sl) <- TRUE WHERE st == chosenObject('s');  
               
                
                FOR Sku sku IS Sku IF NOT (quantityPurchase(sku, chosenObject('s'), subtract(currentDate(), chosenInteger('i'))) OR currentBalance(sku, chosenObject('s')) OR lastStopListDetail(sku, chosenObject('s'), currentDateTime()))   
                    ADDOBJ d = StopListDetail DO {
                    
                    stopList(d) <- sl;
                    sku(d) <- sku;
                }   
                FORM stopList OBJECTS sl =sl MANAGESESSION DOCKED;
            }
        } ELSE {
            MESSAGE 'Для данного склада не требуется создание "Запрета продаж по кассе".';
        }
         
    }
} TOOLBAR;

EXTEND FORM stopLists
    PROPERTIES () createStopList TODRAW sl 
;
