MODULE Machinery;

REQUIRE System, SkuLedger, Barcode, PriceListType, DefaultData;

//---------------------------- группы оборудования ----------------------------------------//

CLASS ABSTRACT GroupMachinery 'Группы оборудования';
TABLE groupMachinery (GroupMachinery);

nppGroupMachinery 'Порядковый номер' = DATA INTEGER (GroupMachinery) IN recognize NOT NULL; 
nameGroupMachinery 'Наименование' = DATA VARSTRING[200] (GroupMachinery) IN recognize MINCHARWIDTH 10 PREFCHARWIDTH 20;
directoryGroupMachinery = ABSTRACT VARSTRING[200] (GroupMachinery);

inactiveGroupMachinery 'Неактивный' = ABSTRACT BOOLEAN (GroupMachinery);
activeGroupMachinery 'Активный' (group) = group IS GroupMachinery AND NOT inactiveGroupMachinery(group);

// -------------------------------- Склады --------------------------------------------- //

stockGroupMachinery = ABSTRACT Stock (GroupMachinery) PERSISTENT;
idStockGroupMachinery 'Склад' (groupMachinery) = idStock(stockGroupMachinery(groupMachinery));
nameStockGroupMachinery 'Склад' (groupMachinery) = nameStock(stockGroupMachinery(groupMachinery));

// фильтрация по группам товаров

filterSkuGroupMachinery 'Фильтровать по классификатору' = DATA BOOLEAN (GroupMachinery);

TABLE groupMachinerySkuGroup (GroupMachinery, SkuGroup);
inGroupMachinerySkuGroup 'Вкл' = DATA BOOLEAN (GroupMachinery, SkuGroup);

FORM filterSkuGroupMachinery 'Фильтрация по классификатору'
    OBJECTS gm = GroupMachinery FIXED PANEL
    PROPERTIES(gm) READONLY nppGroupMachinery, nameStockGroupMachinery, nameGroupMachinery

    TREE treeGroups g=SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY orderSkuGroup(g), nameSkuGroup(g)
    ORDER BY orderSkuGroup(g), nameSkuGroup(g)
    FILTERGROUP inactive FILTER 'Активные' activeSkuGroup(g) 'F5' DEFAULT

    OBJECTS cg=SkuGroup
    PROPERTIES(cg) READONLY canonicalNameSkuGroup
    PROPERTIES(gm, cg)      inGroupMachinerySkuGroup
    ORDER BY canonicalNameSkuGroup(cg)
    FILTERS isParentLeafSkuGroupSkuGroup(cg, g)
    FILTERGROUP inactive1 FILTER 'Активные' activeSkuGroup(cg) 'F5' DEFAULT

    FILTERGROUP filters
        FILTER 'Только выбранные группы' inGroupMachinerySkuGroup(gm, cg) 'F10'
;

DESIGN filterSkuGroupMachinery FROM DEFAULT {
    main {
        ADD gm.box;
        NEW row {
            fill = 1;
            type = SPLITH;

            ADD treeGroups.tree.box;
            ADD cg.box {fill = 2;}
        }
        ADD functions.box;
    }

}

showFilterSkuGroupMachinery 'Выбрать группы' (groupMachinery) = ACTION FORM filterSkuGroupMachinery OBJECTS gm = groupMachinery MODAL;

// --------------------------------- Вид цены ----------------------------- //

priceListTypeGroupMachinery = DATA PriceListType (GroupMachinery);
namePriceListTypeGroupMachinery 'Вид цен' (groupMachinery) = namePriceListType(priceListTypeGroupMachinery(groupMachinery));
idPriceListTypeGroupMachinery 'Вид цен' (groupMachinery) = idPriceListType(priceListTypeGroupMachinery(groupMachinery));

// ---------------------------- Модели оборудования ----------------------------------------//
CLASS ABSTRACT Model 'Модель';
TABLE model (Model);

nameModel 'Наименование' = ABSTRACT VARISTRING[110](Model);

sidModel 'Код' = DATA VARSTRING[20] (Model) IN base;
noteModel 'Примечание' = DATA VARSTRING[200] (Model) IN base;
handlerModel 'Обработчик' = DATA VARSTRING[200] (Model) IN base;
maxProductModel 'MAX допустимое колич. товаров' = DATA INTEGER (Model) IN base;

//---------------------------- типы оборудования  ----------------------------------------//
CLASS ABSTRACT Machinery 'Оборудование';
TABLE machinery(Machinery);

groupMachineryMachinery = ABSTRACT GroupMachinery (Machinery) PERSISTENT;
dataDirectoryMachinery = ABSTRACT VARSTRING[100] (Machinery);
overDirectoryMachinery (machinery) = OVERRIDE directoryGroupMachinery(groupMachineryMachinery(machinery)), dataDirectoryMachinery(machinery);
nppGroupMachineryMachinery 'Номер группы' (machinery) = nppGroupMachinery(groupMachineryMachinery(machinery));
nameGroupMachineryMachinery 'Группа' (machinery) = nameGroupMachinery(groupMachineryMachinery(machinery));
stockMachinery (machinery) = stockGroupMachinery(groupMachineryMachinery(machinery));
idStockMachinery (machinery) = idStock(stockMachinery(machinery));

modelGroupMachinery = ABSTRACT Model(GroupMachinery) PERSISTENT;
sidModelGroupMachinery 'Код' (groupMachinery) = sidModel(modelGroupMachinery(groupMachinery)) IN base;
nameModelGroupMachinery 'Модель' (groupMachinery) = nameModel(modelGroupMachinery(groupMachinery)) IN base;
handlerModelGroupMachinery 'Обработчик' (machinery) = handlerModel(modelGroupMachinery(machinery)) IN base;
handlerModelMachinery (machinery) = handlerModelGroupMachinery(groupMachineryMachinery(machinery));

nppMachinery 'Порядковый номер' = DATA INTEGER (Machinery) IN base NOT NULL;
descriptionMachinery 'Описание' = DATA VARSTRING[200] (Machinery) IN base;
portMachinery 'Адрес/порт' = DATA VARSTRING[100] (Machinery) IN base;

// ------------------------------------------------ Стандартные значения ------------------------------------ //

EXTEND FORM defaultData
    OBJECTS         pt=PriceListType FIXED PANEL
    PROPERTIES(pt)  SELECTOR namePriceListType

    OBJECTS         s=Stock FIXED PANEL
    PROPERTIES(s)   SELECTOR nameStock
;

EXTEND DESIGN defaultData {
    pane {
        NEW machinery {
            fill = 1;
            caption = 'Оборудование';

            ADD PROPERTY(namePriceListType(pt)) {
                caption = 'Вид цен';
            }
            ADD PROPERTY(nameStock(s)) {
                caption = 'Склад';
            }
        }
    }
}

NAVIGATOR {
    NEW machineryNavigator 'Оборудование' BEFORE administration WINDOW toolbar IMAGE '/images/cogwheel.png' {
       NEW machineryExport 'Экспорт';
       NEW machineryMasterData 'Справочники';
    }
}