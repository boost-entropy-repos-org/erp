MODULE LabelPriceList;

REQUIRE PriceListOperation, Label;

//--------------- Акционные ценники -------------//

labelTypePriceList = ABSTRACT LabelType (PriceList);
nameLabelTypePriceList 'Тип ценника' = nameLabelType(labelTypePriceList(p));
labelTypePriceListDetail (d) = labelTypePriceList(priceListPriceListDetail(d));

labelTypeUserPriceList = DATA LabelType (UserPriceList);
nameLabelTypeUserPriceList 'Тип ценника' = nameLabelType(labelTypeUserPriceList(p)) IN documentPrm;
labelTypeUserPriceListDetail (d) = labelTypeUserPriceList(userPriceListUserPriceListDetail(d));

labelTypePriceList(p) += labelTypeUserPriceList(p);

EXTEND FORM  userPriceList  
    PROPERTIES (p) nameLabelTypeUserPriceList 

;

EXTEND FORM  priceLists  
    PROPERTIES (p) READONLY nameLabelTypeUserPriceList 

;

ledgerPriceListTypeLabelType = DATA DataPriceListType (LabelType);
nameLedgerPriceListTypeLabelType 'Управленческий вид цен' (l) = nameLedgerPriceListType(ledgerPriceListTypeLabelType(l));

EXTEND FORM labelType PROPERTIES nameLedgerPriceListTypeLabelType(l);

DESIGN labelType {
    wor2{
        MOVE PROPERTY (nameLedgerPriceListTypeLabelType(l) );
    }
    
}

inOverPriceListTypeSkuStock (type, sku, stock)= priceListTypeLabelType(labelTypePriceListDetail(activePriceListLedgerALedgerPriceListTypeSkuStockDateTime(type, sku, stock, currentDateTime())))==type;

overLabelTypeSkuDepartmentStore(labelType, sku, departmentStore) += 
    departmentStore IS DepartmentStore AND labelTypePriceListDetail(activePriceListLedgerALedgerPriceListTypeSkuStockDateTime(ledgerPriceListTypeLabelType(labelType), sku, departmentStore, currentDateTime())) == labelType; 
skipLabelTypeSkuDepartmentStore(labelType, sku, departmentStore) += 
    departmentStore IS DepartmentStore AND labelTypePriceListDetail(activePriceListLedgerALedgerPriceListTypeSkuStockDateTime(ledgerPriceListTypeLabelType(labelType), sku, departmentStore, currentDateTime())) != labelType; 
        
labelTypeOperation = DATA LabelType (PriceList.Operation);   
nameLabelTypeOperation 'Тип ценника' = nameLabelType(labelTypeOperation(o));

EXTEND FORM PriceList.operation
    PROPERTIES (o) nameLabelTypeOperation 
;
DESIGN PriceList.operation {
    showContainer {
        MOVE PROPERTY(nameLabelTypeOperation(o));
    }    
}
@deriveDocumentOperationProperty(UserPriceList, labelType);