MODULE LabelPriceList;

REQUIRE PriceListOperation, Label;

//--------------- Акционные ценники -------------//

labelType = ABSTRACT LabelType (PriceList);
nameLabelType 'Тип ценника' = name(labelType(PriceList p));
labelType (PriceListDetail d) = labelType(priceList(d));

labelType = DATA LabelType (UserPriceList);
nameLabelType 'Тип ценника' = name(labelType(UserPriceList p)) IN documentPrm;
labelType (UserPriceListDetail d) = labelType(userPriceList(d));

labelType(UserPriceList p) += labelType(p);

EXTEND FORM  userPriceList  
    PROPERTIES (p) nameLabelType 

;

EXTEND FORM  priceLists  
    PROPERTIES (p) READONLY nameLabelType[UserPriceList] PANEL 

;

ledgerPriceListType = DATA DataPriceListType (LabelType);
nameLedgerPriceListType 'Управленческий вид цен' (LabelType l) = name[LedgerPriceListType](ledgerPriceListType(l));

EXTEND FORM labelType PROPERTIES nameLedgerPriceListType(l);

DESIGN labelType {
    wor2{
        MOVE PROPERTY (nameLedgerPriceListType(l) );
    }
    
}

inOver (LedgerPriceListType type, Sku sku, Stock stock)= priceListType(labelType[PriceListDetail](activePriceListLedgerA(type, sku, stock, currentDateTime())))==type;

over(LabelType labelType, Sku sku, DepartmentStore departmentStore) += 
    departmentStore IS DepartmentStore AND labelType[PriceListDetail](activePriceListLedgerA(ledgerPriceListType(labelType), sku, departmentStore, currentDateTime())) == labelType; 
skip(LabelType labelType, Sku sku, DepartmentStore departmentStore) += 
    departmentStore IS DepartmentStore AND labelType[PriceListDetail](activePriceListLedgerA(ledgerPriceListType(labelType), sku, departmentStore, currentDateTime())) != labelType; 
        
labelType = DATA LabelType (PriceList.Operation);   
nameLabelType 'Тип ценника' = name(labelType(PriceList.Operation o));

EXTEND FORM PriceList.operation
    PROPERTIES (o) nameLabelType 
;
DESIGN PriceList.operation {
    showContainer {
        MOVE PROPERTY(nameLabelType(o));
    }    
}
@deriveDocumentOperationProperty(UserPriceList, labelType);