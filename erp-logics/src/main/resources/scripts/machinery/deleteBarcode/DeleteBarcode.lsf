MODULE DeleteBarcode;

REQUIRE System, CashRegister;

CLASS DeleteBarcode 'Запрос удаления штрихкода';
TABLE deleteBarcode(DeleteBarcode);

groupMachinery = DATA GroupMachinery(DeleteBarcode) NOT NULL;
nppGroupMachinery (DeleteBarcode d) = npp(groupMachinery(d)); 
overDepartmentNumberGroupMachinery(DeleteBarcode d) = overDepartmentNumberCashRegister(groupMachinery(d));
nameGroupMachinery 'Группа оборудования' (DeleteBarcode d) = name(groupMachinery(d)); 
nameModelGroupMachinery 'Модель оборудования' (DeleteBarcode d) = nameModel(groupMachinery(d)); 
handlerModelGroupMachinery (DeleteBarcode d) = handlerModel(groupMachinery(d)); 
directoryGroupMachinery (DeleteBarcode d) = directory(groupMachinery(d)); 
idDepartmentStoreGroupMachinery(DeleteBarcode d) = idDepartmentStore(groupMachinery(d));

succeeded 'Выполнен' = DATA BOOLEAN (DeleteBarcode); 
dateTimeSucceeded 'Дата выполнения' = DATA DATETIME (DeleteBarcode);
barcode 'Штрихкод' = DATA VARSTRING[100] (DeleteBarcode) NOT NULL;
idSku 'Код товара' = DATA VARSTRING[100] (DeleteBarcode) NOT NULL;
nameSku 'Наименование товара' = DATA VARISTRING[255] (DeleteBarcode) NOT NULL;
idUOMSku 'Код единицы измерения товара' = DATA VARSTRING[100] (DeleteBarcode);
shortNameUOMSku 'Ед. изм. товара' = DATA VARSTRING[100] (DeleteBarcode);

succeedDeleteBarcode(INTEGER nppGroupMachinery) = ACTION {
    succeeded(DeleteBarcode d) <- TRUE WHERE NOT succeeded(d) AND groupMachinery(d) == groupMachineryNpp(nppGroupMachinery);
    dateTimeSucceeded(DeleteBarcode d) <- currentDateTime() WHERE NOT dateTimeSucceeded(d) AND groupMachinery(d) == groupMachineryNpp(nppGroupMachinery);
}

CLASS DeleteBarcodeError 'Ошибка';
TABLE deleteBarcodeError (DeleteBarcodeError);

data 'Сообщение об ошибке' = DATA VARSTRING[200] (DeleteBarcodeError) IN base;
date 'Время возникновения' = DATA DATETIME (DeleteBarcodeError) IN base;
errorTrace 'След исключения' = DATA TEXT (DeleteBarcodeError) IN base;

disableDeleteBarcode 'Отключить создание заданий' = DATA BOOLEAN ();

FORM deleteBarcodes 'Запросы удаления штрихкодов'
    PROPERTIES disableDeleteBarcode()

    OBJECTS d = DeleteBarcode
    PROPERTIES (d) nameGroupMachinery, nameModelGroupMachinery
    PROPERTIES (d) READONLY succeeded, dateTimeSucceeded
    PROPERTIES (d) barcode, idSku, nameSku, idUOMSku, shortNameUOMSku, NEW
    
    OBJECTS e = DeleteBarcodeError
    PROPERTIES(e) READONLY data, date, errorTrace
;

NAVIGATOR {
    machineryExport {
        ADD deleteBarcodes;
    }
}

WHEN DROPPED (Barcode b IS Barcode) AND PREV(id(b)) AND NOT disableDeleteBarcode() DO {
    FOR GroupMachinery gm == [=GROUP MIN GroupMachinery gm IF active(gm) BY directory(gm)](VARSTRING[200] dir) NEW d = DeleteBarcode DO {
        barcode(d) <- PREV (id(b));
        idSku(d) <- PREV(idSku(b));
        nameSku(d) <- PREV(nameSku(b));
        idUOMSku(d) <- PREV(idUOM(b));
        shortNameUOMSku(d) <- PREV(shortNameUOM(b));
        groupMachinery(d) <- gm;
    } 
}