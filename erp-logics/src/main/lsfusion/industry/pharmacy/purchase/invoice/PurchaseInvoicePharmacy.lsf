MODULE PurchaseInvoicePharmacy;

REQUIRE System, PurchaseInvoice, StockPharmacy;

NAMESPACE Purchase;

// серия лекарства
seriesPharmacy 'Серия лекарственного средства' = ABSTRACT STRING[20] (InvoiceDetail) CHARWIDTH 7;
seriesPharmacy 'Серия лекарственного средства' = DATA STRING[20] (UserInvoiceDetail) CHARWIDTH 7;
seriesPharmacy(UserInvoiceDetail detail) += seriesPharmacy(detail);

// контрактная цена и валюта
contractPrice 'Контрактная цена' = ABSTRACT STRING[20] (InvoiceDetail) CHARWIDTH 7;
contractPrice 'Контрактная цена' = DATA STRING[20] (UserInvoiceDetail) CHARWIDTH 7;
contractPrice(UserInvoiceDetail detail) += contractPrice(detail);

@defineDocumentInterfaceHeaderProperty (invoice, showContractPrice, 'Контрактная цена');

@defineOperationProperty(showContractPrice, 'Контрактная цена', commonContainer);

@deriveDocumentOperationProperty(UserInvoice, showContractPrice);

// страна ввоза
importCountry 'Страна ввоза' = ABSTRACT Country (InvoiceDetail);
nameImportCountry 'Страна ввоза' = name(importCountry(InvoiceDetail invoiceDetail)) CHARWIDTH 15;

importCountry 'Страна ввоза' = DATA Country (UserInvoiceDetail);
nameImportCountry 'Страна ввоза' = name(importCountry(UserInvoiceDetail invoiceDetail)) CHARWIDTH 15;

importCountry(UserInvoiceDetail detail) += importCountry(detail);

overReplace(Country s, Country d) + { importCountry(UserInvoiceDetail detail) <- s WHERE importCountry(detail) == d;}

EXTEND FORM userInvoice
    PROPERTIES(i) showContractPrice
    PROPERTIES(d) BACKGROUND backgroundSku(d) seriesPharmacy SHOWIF showSeriesPharmacy() BEFORE nameBatch(d) ,
                  contractPrice SHOWIF showContractPrice[Invoice](i) BEFORE nameBatch(d),
                  nameImportCountry SHOWIF showImportCountry() BEFORE nameBatch(d)
;

DESIGN userInvoice {
    headerExtraParams {
        NEW headerContractPrice {
            caption = 'Контрактная цена';
            MOVE PROPERTY(showContractPrice(i));
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(d) READONLY seriesPharmacy SHOWIF showSeriesPharmacy() BEFORE nameBatch(d),
                           contractPrice SHOWIF showContractPrice[UserInvoice](i) BEFORE nameBatch(d),
                           nameImportCountry SHOWIF showImportCountry() BEFORE nameBatch(d)
;


WHEN LOCAL FORMS userInvoice (CHANGED (batch(UserInvoiceDetail detail)) OR
                               CHANGED (customerStock(detail))) AND batch(detail) AND NOT CHANGED(seriesPharmacy(detail)) DO {
    seriesPharmacy(detail) <- seriesPharmacy(batch(detail));
}

WHEN LOCAL FORMS userInvoice (CHANGED (batch(UserInvoiceDetail detail)) OR
                               CHANGED (customerStock(detail))) AND batch(detail) AND NOT CHANGED(contractPrice(detail)) DO {
    contractPrice(detail) <- contractPrice(batch(detail));
}

blisterAmount 'Кол-во блистеров (в ед.)' = ABSTRACT INTEGER (Batch) MATERIALIZED;

EXTEND FORM batches
    PROPERTIES(bt) READONLY blisterAmount
;

EXTEND FORM currentBalanceSkuStock
    PROPERTIES(bt) READONLY blisterAmount AFTER name(bt)
;

EXTEND FORM currentBalanceBatchStock
    PROPERTIES(bt) READONLY blisterAmount AFTER nameSku(bt)
;