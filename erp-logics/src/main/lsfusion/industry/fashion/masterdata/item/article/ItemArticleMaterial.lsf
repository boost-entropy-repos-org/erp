MODULE ItemArticleMaterial;

REQUIRE ItemMaterial, ItemArticle;

NAMESPACE Item;

//-- Article

TABLE materialArticle(Material, Article);
percent 'Процент' = DATA INTEGER (Material, Article);

percentMaterials 'Процент' (Article i) = GROUP SUM percent(Material m,i);

calcComposition 'Состав' (Article i) = STRING[255](
                       (GROUP CONCAT (percent(Material m,i)+'% '+name(m)) IF percent(m,i), ', ' ORDER DESC percent(m,i)))
                       CHARWIDTH 50 MATERIALIZED;

WHEN LOCAL FORMS article CHANGED(calcComposition(Article i)) DO {
    composition(i) <- calcComposition(i);            
} 
backgroundMaterials (Article i) = RGB(255,0,0) IF i IS Article AND (percentMaterials(i) != 100.0);

FORM articleMaterial 'Материалы'
    OBJECTS i=Article PANEL 
    PROPERTIES(i) READONLY percentMaterials BACKGROUND  backgroundMaterials(i)
    
    OBJECTS t=Material
    PROPERTIES(t) NEWSESSION name READONLY, DELETE 
    PROPERTIES(t,i) percent
    
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER name(t)
;
DESIGN articleMaterial { BOX { size = (600, 400); } }     

createComposition 'Выбрать состав'(Article article)  {          
    SHOW articleMaterial OBJECTS i = article ; 
} ASON CONTEXTMENU composition[Article];  