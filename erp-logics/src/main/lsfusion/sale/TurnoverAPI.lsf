MODULE TurnoverAPI;

REQUIRE PurchaseLedger, SaleLedger, Pricing, Item, API;

NAMESPACE Stock;

EXTEND CLASS Category {
    turnover 'Товарооборот'
}

TABLE stockLegalEntitySkuGroupDate(Stock, LegalEntity, SkuGroup, DATE);
costSumSold 'Себестоимость' = DATA NUMERIC[18,4](Stock, LegalEntity, SkuGroup, DATE);
markupSumSold  'Надбавка' = DATA NUMERIC[18,4](Stock, LegalEntity, SkuGroup, DATE);
sumSold 'Сумма продажи' = DATA NUMERIC[18,4](Stock, LegalEntity, SkuGroup, DATE);
costSumBalanceA 'Остаток в с/с без НДС' = DATA NUMERIC[18,4](Stock, LegalEntity, SkuGroup, DATE);

fillTurnover (DATE df, DATE dt) {
    
    LOCAL balance = NUMERIC[18,4](Batch, Stock);
    balance(Batch b, Stock st) <- balanceA(b, st, dt);
    
    LOCAL date = DATE ();
    date() <- dt;
    
    WHILE date() >= df DO {
        FOR DATE d = date() DO {
        
            costSumSold(Stock st, LegalEntity le, SkuGroup g, d) <- GROUP SUM costSumSold(Batch b, st, d) IF isParent(g, sku(b)) AND supplier(b) == le;
            markupSumSold(Stock st, LegalEntity le, SkuGroup g, d) <- GROUP SUM markupSumSold(Batch b, st, d) IF isParent(g, sku(b)) AND supplier(b) == le;
            sumSold(Stock st, LegalEntity le, SkuGroup g, d) <- GROUP SUM sumSold(Batch b, st, d) IF isParent(g, sku(b)) AND supplier(b) == le;
    
            costSumBalanceA(Stock st, LegalEntity le, SkuGroup g, d) <- GROUP SUM balance(Batch b, st) * cost(b) IF isParent(g, sku(b)) AND supplier(b) == le;
            balance(Batch b, Stock st) <- balance(b, st) (-) signedQuantity(b, st, date()) WHERE signedQuantity(b, st, date()); 
        }
        date() <- subtract(date(), 1);
        APPLY NESTED LOCAL;
    }
}

filter = DATA LOCAL BOOLEAN (Stock);
filter = DATA LOCAL BOOLEAN (LegalEntity);
filter = DATA LOCAL BOOLEAN (SkuGroup);
allStores = DATA LOCAL BOOLEAN () COMPLEX;
allSuppliers = DATA LOCAL BOOLEAN () COMPLEX;
allGroups = DATA LOCAL BOOLEAN () COMPLEX;

access (Stock s) = (filter(s) OR allStores()) AND (accessCompany(currentUser(), s) OR NOT limitAccess());

costSumSold (Stock s, DATE d) = GROUP SUM costSumSold(s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());
markupSumSold (Stock s, DATE d) = GROUP SUM markupSumSold(s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());
sumSold (Stock s, DATE d) = GROUP SUM sumSold(s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());
costSumBalanceA (Stock s, DATE d) = GROUP SUM costSumBalanceA(s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());

costSumSold (LegalEntity le, DATE d) = GROUP SUM costSumSold(Stock s, le, SkuGroup g, d) IF access(s) AND (filter(g) OR allGroups());
markupSumSold (LegalEntity le, DATE d) = GROUP SUM markupSumSold(Stock s, le, SkuGroup g, d) IF access(s) AND (filter(g) OR allGroups());
sumSold (LegalEntity le, DATE d) = GROUP SUM sumSold(Stock s, le, SkuGroup g, d) IF access(s) AND (filter(g) OR allGroups());
costSumBalanceA (LegalEntity le, DATE d) = GROUP SUM costSumBalanceA(Stock s, le, SkuGroup g, d) IF access(s) AND (filter(g) OR allGroups());

costSumSold (SkuGroup g, DATE d) = GROUP SUM costSumSold(Stock s, LegalEntity le, g, d) IF (filter(le) OR allSuppliers()) AND access(s);
markupSumSold (SkuGroup g, DATE d) = GROUP SUM markupSumSold(Stock s, LegalEntity le, g, d) IF (filter(le) OR allSuppliers()) AND access(s);
sumSold (SkuGroup g, DATE d) = GROUP SUM sumSold(Stock s, LegalEntity le, g, d) IF (filter(le) OR allSuppliers()) AND access(s);
costSumBalanceA (SkuGroup g, DATE d) = GROUP SUM costSumBalanceA(Stock s, LegalEntity le, g, d) IF (filter(le) OR allSuppliers()) AND access(s);

costSumSold (Stock s, DATE df, DATE dt) = GROUP SUM costSumSold(s, LegalEntity le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());
markupSumSold (Stock s, DATE df, DATE dt) = GROUP SUM markupSumSold(s, LegalEntity le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());
sumSold (Stock s, DATE df, DATE dt) = GROUP SUM sumSold(s, LegalEntity le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());
costSumBalanceA (Stock s, DATE df, DATE dt) = GROUP SUM costSumBalanceA(s, LegalEntity le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups());

costSumSold (LegalEntity le, DATE df, DATE dt) = GROUP SUM costSumSold(Stock s, le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND access(s) AND (filter(g) OR allGroups());
markupSumSold (LegalEntity le, DATE df, DATE dt) = GROUP SUM markupSumSold(Stock s, le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND access(s) AND (filter(g) OR allGroups());
sumSold (LegalEntity le, DATE df, DATE dt) = GROUP SUM sumSold(Stock s, le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND access(s) AND (filter(g) OR allGroups());
costSumBalanceA (LegalEntity le, DATE df, DATE dt) = GROUP SUM costSumBalanceA(Stock s, le, SkuGroup g, DATE d) IF iterate(d, df, dt) AND access(s) AND (filter(g) OR allGroups());

costSumSold (SkuGroup g, DATE df, DATE dt) = GROUP SUM costSumSold(Stock s, LegalEntity le, g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND access(s);
markupSumSold (SkuGroup g, DATE df, DATE dt) = GROUP SUM markupSumSold(Stock s, LegalEntity le, g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND access(s);
sumSold (SkuGroup g, DATE df, DATE dt) = GROUP SUM sumSold(Stock s, LegalEntity le, g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND access(s);
costSumBalanceA (SkuGroup g, DATE df, DATE dt) = GROUP SUM costSumBalanceA(Stock s, LegalEntity le, g, DATE d) IF iterate(d, df, dt) AND (filter(le) OR allSuppliers()) AND access(s);

costSumSold (Stock s, INTEGER w, INTEGER y) = GROUP SUM costSumSold(s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);
markupSumSold (Stock s, INTEGER w, INTEGER y) = GROUP SUM markupSumSold(s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);
sumSold (Stock s, INTEGER w, INTEGER y) = GROUP SUM sumSold(s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);
costSumBalanceA (Stock s, INTEGER w, INTEGER y) = GROUP SUM costSumBalanceA(s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);

costSumSold (LegalEntity le, INTEGER w, INTEGER y) = GROUP SUM costSumSold(Stock s, le, SkuGroup g, DATE d) IF access(s) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);
markupSumSold (LegalEntity le, INTEGER w, INTEGER y) = GROUP SUM markupSumSold(Stock s, le, SkuGroup g, DATE d) IF access(s) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);
sumSold (LegalEntity le, INTEGER w, INTEGER y) = GROUP SUM sumSold(Stock s, le, SkuGroup g, DATE d) IF access(s) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);
costSumBalanceA (LegalEntity le, INTEGER w, INTEGER y) = GROUP SUM costSumBalanceA(Stock s, le, SkuGroup g, DATE d) IF access(s) AND (filter(g) OR allGroups()) BY extractWeek(d), extractYear(d);

costSumSold (SkuGroup g, INTEGER w, INTEGER y) = GROUP SUM costSumSold(Stock s, LegalEntity le, g, DATE d) IF (filter(le) OR allSuppliers()) AND access(s) BY extractWeek(d), extractYear(d);
markupSumSold (SkuGroup g, INTEGER w, INTEGER y) = GROUP SUM markupSumSold(Stock s, LegalEntity le, g, DATE d) IF (filter(le) OR allSuppliers()) AND access(s) BY extractWeek(d), extractYear(d);
sumSold (SkuGroup g, INTEGER w, INTEGER y) = GROUP SUM sumSold(Stock s, LegalEntity le, g, DATE d) IF (filter(le) OR allSuppliers()) AND access(s) BY extractWeek(d), extractYear(d);
costSumBalanceA (SkuGroup g, INTEGER w, INTEGER y) = GROUP SUM costSumBalanceA(Stock s, LegalEntity le, g, DATE d) IF (filter(le) OR allSuppliers()) AND access(s) BY extractWeek(d), extractYear(d);

costSumSold (INTEGER w, INTEGER y) = GROUP SUM costSumSold(Stock s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s) BY extractWeek(d), extractYear(d);
markupSumSold (INTEGER w, INTEGER y) = GROUP SUM markupSumSold(Stock s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s) BY extractWeek(d), extractYear(d);
sumSold (INTEGER w, INTEGER y) = GROUP SUM sumSold(Stock s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s) BY extractWeek(d), extractYear(d);
costSumBalanceA (INTEGER w, INTEGER y) = GROUP SUM costSumBalanceA(Stock s, LegalEntity le, SkuGroup g, DATE d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s) BY extractWeek(d), extractYear(d);

costSumSold (DATE d) = GROUP SUM costSumSold(Stock s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s);
markupSumSold (DATE d) = GROUP SUM markupSumSold(Stock s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s);
sumSold (DATE d) = GROUP SUM sumSold(Stock s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s);
costSumBalanceA (DATE d) = GROUP SUM costSumBalanceA(Stock s, LegalEntity le, SkuGroup g, d) IF (filter(le) OR allSuppliers()) AND (filter(g) OR allGroups()) AND access(s);

importFilters (FILE stockList, FILE supplierList, FILE groupList) {

    IMPORT JSON FROM stockList AS FILE FIELDS LONG id DO {
        filter(Stock s) <- TRUE WHERE LONG (s AS Stock) == id;
    }
    allStores() <- TRUE IF NOT (GROUP SUM 1 IF filter(Stock s)); 
     
    IMPORT JSON FROM supplierList AS FILE FIELDS LONG id DO {
        filter(LegalEntity s) <- TRUE WHERE LONG (s AS LegalEntity) == id;
    }
    allSuppliers() <- TRUE IF NOT (GROUP SUM 1 IF filter(LegalEntity s)); 
    
    IMPORT JSON FROM groupList AS FILE FIELDS LONG id DO {
        filter(SkuGroup s) <- TRUE WHERE LONG (s AS SkuGroup) == id;
    }
    allGroups() <- TRUE IF NOT (GROUP SUM 1 IF filter(SkuGroup s)); 
}

FORM turnoverByStockDate
    OBJECTS (df = DATE, dt = DATE) PANEL
    OBJECTS st = DepartmentStore EXTID 'value'
    PROPERTIES id = LONG (st), name(st), costSumSold(st, df, dt), markupSumSold(st, df, dt), sumSold(st, df, dt), costSumBalanceA(st, df, dt)
    FILTERS active(st), access(st), sumSold(st, df, dt) OR costSumBalanceA(st, df, dt)
    
    OBJECTS d = DATE
    PROPERTIES date = VALUE(d), costSumSold(st, d), markupSumSold(st, d), sumSold(st, d), costSumBalanceA(st, d)
    FILTERS iterate(d, df, dt), sumSold(st, d) OR costSumBalanceA(st, d)
;

getTurnoverByStockDate (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
    
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT turnoverByStockDate OBJECTS df = df, dt = dt JSON;
} @@api;

FORM turnoverBySupplierDate
    OBJECTS (df = DATE, dt = DATE) PANEL
    OBJECTS le = LegalEntity EXTID 'value'
    PROPERTIES id = LONG (le), name(le), costSumSold(le, df, dt), markupSumSold(le, df, dt), sumSold(le, df, dt), costSumBalanceA(le, df, dt)
    FILTERS active(le), filter(le) OR allSuppliers(), sumSold(le, df, dt) OR costSumBalanceA(le, df, dt)
    
    OBJECTS d = DATE
    PROPERTIES date = VALUE(d), costSumSold(le, d), markupSumSold(le, d), sumSold(le, d), costSumBalanceA(le, d)
    FILTERS iterate(d, df, dt), sumSold(le, d) OR costSumBalanceA(le, d)
;

getTurnoverBySupplierDate (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
                             
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
     
    EXPORT turnoverBySupplierDate OBJECTS df = df, dt = dt JSON;
 } @@api;

FORM turnoverByGroupDate
    OBJECTS (df = DATE, dt = DATE, level = INTEGER) PANEL
    OBJECTS g = SkuGroup EXTID 'value'
    PROPERTIES id = LONG (g), name(g), costSumSold(g, df, dt), markupSumSold(g, df, dt), sumSold(g, df, dt), costSumBalanceA(g, df, dt)
    FILTERS active(g), filter(g) OR allGroups(), sumSold(g, df, dt) OR costSumBalanceA(g, df, dt), skuGroupRoot(g, level (+) 1) == g
    
    OBJECTS d = DATE
    PROPERTIES date = VALUE(d), costSumSold(g, d), markupSumSold(g, d), sumSold(g, d), costSumBalanceA(g, d)
    FILTERS iterate(d, df, dt), sumSold(g, d) OR costSumBalanceA(g, d)
;

getTurnoverByGroupDate (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt, INTEGER level) {
                                                   
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT turnoverByGroupDate OBJECTS df = df, dt = dt, level = level JSON;
} @@api;

getTurnoverByStock (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
                      
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT JSON FROM stock = LONG (Stock st), name(st), costSumSold = costSumSold(st, df, dt), markupSumSold = markupSumSold(st, df, dt), 
        sumSold = sumSold(st, df, dt), costSumBalanceA = costSumBalanceA(st, df, dt)
        WHERE access(st) AND (sumSold(st, df, dt) OR costSumBalanceA(st, df, dt));
} @@api;

getTurnoverBySupplier (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
                                            
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT JSON FROM supplier = LONG (LegalEntity le), name(le), costSumSold = costSumSold(le, df, dt), markupSumSold = markupSumSold(le, df, dt), 
        sumSold = sumSold(le, df, dt), costSumBalanceA = costSumBalanceA(le, df, dt)
        WHERE (filter(le) OR allSuppliers()) AND (sumSold(le, df, dt) OR costSumBalanceA(le, df, dt));
} @@api;


getTurnoverByGroup (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt, INTEGER level) {
                                                                   
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT JSON FROM group = LONG (SkuGroup g), name(g), costSumSold = costSumSold(g, df, dt), markupSumSold = markupSumSold(g, df, dt), 
        sumSold = sumSold(g, df, dt), costSumBalanceA = costSumBalanceA(g, df, dt)
        WHERE (filter(g) OR allGroups()) AND skuGroupRoot(g, level (+) 1) == g AND (sumSold(g, df, dt) OR costSumBalanceA(g, df, dt));
} @@api;

filterWeek(INTEGER y, INTEGER w, DATE df, DATE dt) = iterate(w, 1, 53) AND (CASE 
    WHEN extractYear(df) == extractYear(dt) THEN (w >= extractWeek(df) AND w <= extractWeek(dt) AND extractYear(df) == y)
    WHEN extractYear(df) < extractYear(dt) THEN ((w >= extractWeek(df) AND y == extractYear(df)) OR 
                                                (w <= extractWeek(dt) AND y == extractYear(dt)) OR 
                                                (y > extractYear(df) AND y < extractYear(dt))));

FORM turnoverByStockWeek
    OBJECTS (df = DATE, dt = DATE) PANEL
    OBJECTS st = DepartmentStore EXTID 'value'
    PROPERTIES id = LONG (st), name(st)
    PROPERTIES (st, df, dt) costSumSold, markupSumSold, sumSold, costSumBalanceA
    FILTERS active(st), access(st), sumSold(st, df, dt) OR costSumBalanceA(st, df, dt)
    
    OBJECTS d = (y = INTEGER, w = INTEGER)
    PROPERTIES date = y + '_' + lpad(TEXT (w), 2, '0')
    PROPERTIES (st, w, y) costSumSold, markupSumSold, sumSold, costSumBalanceA
    FILTERS filterWeek(y, w, df, dt), costSumSold(st, w, y) OR costSumBalanceA(st, w, y)
;

getTurnoverByStockWeek (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
    
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT turnoverByStockWeek OBJECTS df = df, dt = dt JSON;
} @@api;

FORM turnoverBySupplierWeek
    OBJECTS (df = DATE, dt = DATE) PANEL
    OBJECTS le = LegalEntity EXTID 'value'
    PROPERTIES id = LONG (le), name(le)
    PROPERTIES (le, df, dt) costSumSold, markupSumSold, sumSold, costSumBalanceA
    FILTERS active(le), filter(le) OR allSuppliers(), sumSold(le, df, dt) OR costSumBalanceA(le, df, dt)
    
    OBJECTS d = (y = INTEGER, w = INTEGER)
    PROPERTIES date = y + '_' + lpad(TEXT (w), 2, '0')
    PROPERTIES (le, w, y) costSumSold, markupSumSold, sumSold, costSumBalanceA
    FILTERS filterWeek(y, w, df, dt), costSumSold(le, w, y) OR costSumBalanceA(le, w, y)
;

getTurnoverBySupplierWeek (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
                             
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
     
    EXPORT turnoverBySupplierWeek OBJECTS df = df, dt = dt JSON;
 } @@api;

FORM turnoverByGroupWeek
    OBJECTS (df = DATE, dt = DATE, level = INTEGER) PANEL
    OBJECTS g = SkuGroup EXTID 'value'
    PROPERTIES id = LONG (g), name(g)
    PROPERTIES (g, df, dt) costSumSold, markupSumSold, sumSold, costSumBalanceA
    FILTERS active(g), filter(g) OR allGroups(), sumSold(g, df, dt) OR costSumBalanceA(g, df, dt), skuGroupRoot(g, level (+) 1) == g
    
    OBJECTS d = (y = INTEGER, w = INTEGER)
    PROPERTIES date = y + '_' + lpad(TEXT (w), 2, '0')
    PROPERTIES (g, w, y) costSumSold, markupSumSold, sumSold, costSumBalanceA
    FILTERS filterWeek(y, w, df, dt), costSumSold(g, w, y) OR costSumBalanceA(g, w, y)
;

getTurnoverByGroupWeek (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt, INTEGER level) {
                                                   
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT turnoverByGroupWeek OBJECTS df = df, dt = dt, level = level JSON;
} @@api;

getTurnoverByWeek (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
                                            
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT JSON FROM date = INTEGER y + '_' + lpad(TEXT (INTEGER w), 2, '0'), costSumSold = costSumSold(w, y), markupSumSold = markupSumSold(w, y), sumSold = sumSold(w, y), costSumBalanceA = costSumBalanceA(w, y)
        WHERE filterWeek(y, w, df, dt) AND (sumSold(w, y) OR costSumBalanceA(w, y));
} @@api;

getTurnoverByDate (FILE stockList, FILE supplierList, FILE groupList, DATE df, DATE dt) {
                                            
    IF limitAccess() AND NOT access(currentUser(), Category.turnover) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    importFilters(stockList, supplierList, groupList);
    
    EXPORT JSON FROM date = DATE d, costSumSold = costSumSold(d), markupSumSold = markupSumSold(d), sumSold = sumSold(d), costSumBalanceA = costSumBalanceA(d)
        WHERE iterate(d, df, dt) AND (sumSold(d) OR costSumBalanceA(d));
} @@api;