MODULE SaleShipmentLot;

REQUIRE SaleShipment, Lot, SkuLedgerLot;

NAMESPACE Sale;

@defineOperationProperty(useLot, 'Использовать марки', showContainer);

@defineDocumentLot(userInvoice);

changeQuantityLot (UserInvoiceDetail d) {
    quantity(Lot l) <- quantity(d, l);
    DIALOG dialogOutLots DO {
        quantity(d, Lot l) <- quantity(l);
    }    
}

EXTEND FORM userInvoice
    PROPERTIES(d) quantityLot AFTER shipmentQuantity(d) ON CHANGE changeQuantityLot(d) 
                  BACKGROUND backgroundQuantityLot(d) 
                  SHOWIF useLot(operation(i))
                  READONLYIF NOT lotType(sku(d))
;

@defineDocumentLotBarcodeOut(userInvoice);

// shipment detail
// пока упрощенная схема без поддержки UserShipmentDetail
quantity = ABSTRACT NUMERIC[16,5] (InvoiceDetail, Lot);
quantity(UserInvoiceDetail d, Lot l) += quantity(d, l);

quantity = ABSTRACT NUMERIC[16,5] (ShipmentDetail, Lot);
quantity(InvoiceShipmentDetail d, Lot l) += quantity(invoiceDetail(d), l);

quantity[OutFIFOSkuLedger, Lot](ShipmentDetail sd, Lot l) += quantity(sd, l);
