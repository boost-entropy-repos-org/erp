MODULE Competitor;

REQUIRE MasterData, Authentication, Sku, PriceListType;

NAMESPACE Competitor;

CLASS Competitor 'Конкурент';
TABLE competitor (Competitor);

name 'Наименование' = DATA VARISTRING[100] (Competitor) CHARWIDTH 15;
competitor = GROUP MAX Competitor c BY name(c);

FORM competitor 'Конкурент'
    OBJECTS c = Competitor PANEL
    PROPERTIES(c) name
    
    EDIT Competitor OBJECT c
;

FORM competitors 'Конкуренты'
    OBJECTS c = Competitor
    PROPERTIES(c) READONLY name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogCompetitors 'Конкуренты'
    OBJECTS c = Competitor
    PROPERTIES(c) READONLY name
    
    LIST Competitor OBJECT c
;

CLASS PriceList 'Прайс';
TABLE priceList (PriceList);

note 'Примечание' = DATA VARISTRING[100] (PriceList) CHARWIDTH 20;
date 'Дата' = DATA DATE (PriceList);
dateTimeCreated 'Дата/время создания' = DATA DATETIME (PriceList);

in 'Вкл.' = DATA BOOLEAN (PriceList, Competitor);
competitors 'Конкуренты' (PriceList p) = GROUP CONCAT name(Competitor c) IF in(p, c), ', ' ORDER name(c) CHARWIDTH 20;

createdUser = DATA User(PriceList);
nameCreatedUser 'Создан пользователем' (PriceList p) = name(createdUser(p));

CLASS PriceListDetail 'Строка прайса';
TABLE priceListDetail (PriceListDetail);

priceList = DATA PriceList(PriceListDetail) NONULL DELETE;

date 'Дата' (PriceListDetail d) = date(priceList(d));

in 'Вкл.' (PriceListDetail d, Competitor c) = in(priceList(d), c);
competitors (PriceListDetail d) = competitors(priceList(d));

sku = DATA Sku(PriceListDetail);
nameSku 'Товар' (PriceListDetail d) = name(sku(d));
idBarcodeSku 'Штрихкод' (PriceListDetail d) = idBarcode(sku(d));
idSku 'Код товара' (PriceListDetail d) = id(sku(d));

price 'Цена' = DATA NUMERIC[16,5](PriceListDetail, Competitor);
promotionPrice 'Цена (акция)' = DATA NUMERIC[16,5](PriceListDetail, Competitor);

price 'Цена конкурента' (Competitor c, Sku s, DATE d) = 
    GROUP LAST price(PriceListDetail dd, c) ORDER date(dd), dd WHERE date(dd) <= d AND in(dd, c) BY sku(dd);

createdUser(p) <- currentUser() WHEN SET (p IS PriceList);
dateTimeCreated(p) <- currentDateTime() WHEN SET (p IS PriceList);

FORM priceList 'Прайс'
    OBJECTS p = PriceList PANEL
    PROPERTIES(p) date, note, dateTimeCreated READONLY, nameCreatedUser READONLY 
    ORDER date(p)
    
    OBJECTS c = Competitor
    PROPERTIES in(p, c), name(c) READONLY 
    FILTERGROUP in FILTER 'Отмеченные' in(p, c)
    
    OBJECTS cc = Competitor
    PROPERTIES (cc) READONLY name SHOWIF NULL 
    FILTERS in(p, cc)
    
    OBJECTS d = PriceListDetail
    PROPERTIES (d) idSku SHOWIF showIDs(), idBarcodeSku, nameSku, NEW, DELETE 
    PROPERTIES promotionPrice(d, cc) COLUMNS (cc) HEADER (name(cc) + ' (акция)') AFTER nameSku(d)
    PROPERTIES price(d, cc) COLUMNS (cc) HEADER name(cc) AFTER nameSku(d)
    FILTERS priceList(d) == p
    
    EDIT PriceList OBJECT p
;

DESIGN priceList {
    OBJECTS {
        NEW header FIRST {
            caption = 'Параметры';
            type = CONTAINERH;
            MOVE PROPERTY (date(p));
            MOVE PROPERTY (note(p)) { charWidth = 50; }
        }
        NEW created AFTER header {
            caption = 'Создан';
            type = CONTAINERH;
            MOVE PROPERTY (dateTimeCreated(p));
            MOVE PROPERTY (nameCreatedUser(p)) { charWidth = 50; }
        }
        NEW bottom {
            type = CONTAINERH;
            fill = 1;
            MOVE BOX (c) { fill = 1; }
            MOVE BOX (d) { fill = 4; }
        }
    }
}

FORM priceLists 'Прайсы'
    OBJECTS p = PriceList
    PROPERTIES(p) READONLY date, competitors, note, dateTimeCreated, nameCreatedUser
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE
    ORDER date(p)

    OBJECTS cc = Competitor
    PROPERTIES (cc) READONLY name SHOWIF NULL 
    FILTERS in(p, cc)
    
    OBJECTS d = PriceListDetail
    PROPERTIES (d) READONLY idSku SHOWIF showIDs(), idBarcodeSku, nameSku, NEW, DELETE 
    PROPERTIES READONLY promotionPrice(d, cc) COLUMNS (cc) HEADER (name(cc) + ' (акция)') AFTER nameSku(d)
    PROPERTIES READONLY price(d, cc) COLUMNS (cc) HEADER name(cc) AFTER nameSku(d)
    FILTERS priceList(d) == p
;

FORM dialogPriceLists 'Прайсы'
    OBJECTS p = PriceList
    PROPERTIES(p) READONLY date, competitors, note, dateTimeCreated, nameCreatedUser
    ORDER date(p)
    
    LIST PriceList OBJECT p
;

NAVIGATOR {
    priceListNavigator {
        NEW FOLDER competitorPrices 'Цены конкурентов' { 
            NEW competitors;
            NEW priceLists;
        }
    }
}