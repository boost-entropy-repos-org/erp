MODULE SaleCompliance;

REQUIRE Compliance, SaleCertificate;

NAMESPACE Sale;

CLASS ABSTRACT PrintPageCompliance 'Страница сертификата соответствия';
compliance = ABSTRACT Compliance (PrintPageCompliance);
image = ABSTRACT IMAGEFILE (PrintPageCompliance);

EXTEND CLASS Compliance : PrintPageCompliance;
compliance(Compliance compliance) += compliance IF compliance IS Compliance;
image(Compliance compliance) += Compliance.image(compliance);

EXTEND CLASS ExtraPageComplianceDetail : PrintPageCompliance;
compliance(ExtraPageComplianceDetail detail) += Compliance.compliance(detail);
image(ExtraPageComplianceDetail detail) += Compliance.image(detail);

@defineDocumentCertificate(compliance, invoice, i, 'Сертификат соответствия');
@defineDocumentCertificateConstraint(compliance, invoice, 'Сертификат соответствия');
@deriveDocumentSaleCertificate(compliance, invoice, supplierStock, data);

count (compliance, invoice) =
    GROUP SUM 1
    BY compliance(InvoiceDetail detail), invoice(detail);

seriesCompliance(UserInvoiceDetail d) = series(compliance(d));
numberCompliance(UserInvoiceDetail d) = number(compliance(d));

image 'Файл сертификата' (InvoiceDetail invoiceDetail) = Compliance.image(compliance(invoiceDetail));
openImage 'Просмотреть сертификат' (InvoiceDetail invoiceDetail)  { open(image(invoiceDetail)); }

// todo : при попытке распечатать выдет сообщение "Форма не подходит для данных параметров"
FORM imageComplianceInvoice 'Файл сертификата'
    OBJECTS i = Invoice PANEL SUBREPORT

    OBJECTS c = Compliance
    FILTERS count(c, i)
    
    OBJECTS p = PrintPageCompliance
    PROPERTIES(p) image
    FILTERS compliance(p) == c    
;
printAllImage 'Печать сертификатов'(Invoice invoice)  { 
    PRINT imageComplianceInvoice OBJECTS i = invoice ;
}
printAllImageAuto 'Печать сертификатов'(Invoice invoice)  { 
    PRINT imageComplianceInvoice OBJECTS i = invoice NOPREVIEW;
}

EXTEND FORM userInvoice
    PROPERTIES(i) DRAW dc TOOLBAR printAllImage
    PROPERTIES(dc) TOOLBAR openImage
;

EXTEND FORM invoices
    PROPERTIES(i) DRAW dc TOOLBAR printAllImage
    PROPERTIES(dc) TOOLBAR openImage
;

@defineDocumentCertificate(compliance, shipment, s, 'сертификат соответствия');
compliance(InvoiceShipmentDetail detail) += compliance(invoiceDetail(detail));

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) + { 
    dataCompliance(s) <- compliance(i);
}
@defineDocumentCertificateConstraint(compliance, shipment, 'сертификат соответствия');
@deriveDocumentSaleCertificate(compliance, shipment, supplierStock, data);