MODULE QualityCertificate;

REQUIRE Certificate;

CLASS QualityCertificate 'Сертификат качества' : Certificate;
TABLE qualityCertificate(QualityCertificate);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(QualityCertificate, ,' документа');
@deriveDocumentHeaderTimePrefix(QualityCertificate, );
date(QualityCertificate certificate) += date(certificate);
time(QualityCertificate certificate) += time(certificate);

fromDate 'Дата с' = DATA DATE (QualityCertificate) IN documentHeader;
fromDate(QualityCertificate certificate) += fromDate(certificate);

toDate 'Дата по' = DATA DATE (QualityCertificate) IN documentHeader;
toDate(QualityCertificate certificate) += toDate(certificate);

// Файл
image 'Файл сертификата'  = DATA IMAGEFILE (QualityCertificate);
saveImage 'Загрузить сертификат' (QualityCertificate qualityCertificate)  { INPUT =image(qualityCertificate) CHANGE; }
openImage 'Просмотреть сертификат' (QualityCertificate qualityCertificate)  { open(image(qualityCertificate)); }

CLASS ExtraPageQualityCertificateDetail 'Дополнительная страница сертификата';
TABLE extraPageQualityCertificateDetail (ExtraPageQualityCertificateDetail);

qualityCertificate = DATA QualityCertificate (ExtraPageQualityCertificateDetail);

image 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageQualityCertificateDetail);
saveImage 'Загрузить сертификат' (ExtraPageQualityCertificateDetail extraPageQualityCertificateDetail)  { INPUT =image(extraPageQualityCertificateDetail) CHANGE; }
openImage 'Просмотреть сертификат' (ExtraPageQualityCertificateDetail extraPageQualityCertificateDetail)  { open(image(extraPageQualityCertificateDetail)); }

@defineDocumentDetailIndex(qualityCertificate, ExtraPageQualityCertificateDetail);

extraPageQualityCertificateDetail (index, qualityCertificate) = GROUP AGGR ExtraPageQualityCertificateDetail extraPageQualityCertificateDetail WHERE extraPageQualityCertificateDetail IS ExtraPageQualityCertificateDetail BY index (extraPageQualityCertificateDetail), qualityCertificate(extraPageQualityCertificateDetail);

@defineDocumentDetailNote(ExtraPageQualityCertificateDetail);

addExtraPageDetail 'Добавить'(QualityCertificate qualityCertificate)  { 
    NEW d = ExtraPageQualityCertificateDetail {
        qualityCertificate(d) <- qualityCertificate;
        saveImage(d);
        apply();
    }    
} IMAGE 'add.png';

// ------ Номер ----------- //

number 'Номер' = DATA ISTRING[100] (QualityCertificate) NONULL IN numbered CHARWIDTH 10;
series 'Серия' = DATA BPSTRING[2] (QualityCertificate) IN numbered CHARWIDTH 3 NOFLEX; 

seriesNumber 'Серия/Номер' (QualityCertificate o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 MATERIALIZED;

series(QualityCertificate certificate) += series(certificate);
number(QualityCertificate certificate) += number(certificate);

qualityCertificate = GROUP AGGR QualityCertificate s BY number (s);

// Описание
description 'Сертификат качества' (QualityCertificate qualityCertificate) = CONCAT ' ', seriesNumber(qualityCertificate),
                                                 'от ' + fromDate(qualityCertificate), 'по ' + toDate(qualityCertificate) CHARWIDTH 20;
// История                                                 
@defineDocumentHeaderCreated(QualityCertificate);                                                 


@defineBatchCertificate(qualityCertificate, QualityCertificate, 'Сертификат качества');
fromDateQualityCertificate 'Дата с' (Batch batch) = fromDate(qualityCertificate(batch));
toDateQualityCertificate 'Дата по' (Batch batch) = toDate(qualityCertificate(batch));

FORM qualityCertificate 'Сертификат качества'
    OBJECTS c = QualityCertificate PANEL
    PROPERTIES(c) number, series, date, time,
                  fromDate, toDate, saveImage, openImage       
                  
    OBJECTS d = ExtraPageQualityCertificateDetail
    PROPERTIES(d) index, note
    PROPERTIES(c) addExtraPageDetail DRAW d TOOLBAR
    PROPERTIES(d) NEWSESSION TOOLBAR openImage, saveImage, DELETE 
    FILTERS qualityCertificate(d) == c                              

    EDIT QualityCertificate OBJECT c
;

DESIGN qualityCertificate {
    BOX {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                MOVE PROPERTY (number(c)) { charWidth = 70;};
                MOVE PROPERTY (series(c));
                MOVE PROPERTY (date(c));
                MOVE PROPERTY (time(c));

            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDate(c));
                    MOVE PROPERTY (toDate(c));
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл свидетельства';
                MOVE PROPERTY(openImage(c));
                MOVE PROPERTY(saveImage(c));
            }    
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                MOVE BOX(d){
                    caption = 'Дополнительные страницы свидетельства';
                    PROPERTY(index(d)){
                        caption = 'Номер файла';
                    }
                }
            }                    
        }    
        MOVE TOOLBARBOX;
    }
}

overCopy  ABSTRACT LIST ( QualityCertificate, QualityCertificate);
    
copy 'Копировать'(QualityCertificate qualityCertificate)  { 
	NEWSESSION {
	    NEW c = QualityCertificate {
	        fromDate(c) <- fromDate(qualityCertificate);
	        toDate(c) <- toDate(qualityCertificate);
	
	        overCopy(c, qualityCertificate);
	        
	        SHOW qualityCertificate OBJECTS c = c DOCKED;
	    }
	}
} TOOLBAR;

skipShowEdit = ABSTRACT BOOLEAN (QualityCertificate);
showEdit = QualityCertificate c IS QualityCertificate AND NOT skipShowEdit(c);

FORM qualityCertificates 'Сертификаты качества'
    OBJECTS c = QualityCertificate
    PROPERTIES(c) READONLY number, series, date, time, fromDate, toDate
    PROPERTIES(c) copy

    PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(c) NEWSESSION NEW, EDIT SHOWIF showEdit(c), DELETE SHOWIF showEdit(c) 
    
    LIST QualityCertificate OBJECT c
;

DESIGN qualityCertificates {
    BOX {
        size = (1024, 768);
        NEW header {
            fill = 1;
            type = SPLITV;
            MOVE BOX(c) { fill = 2; }
            NEW documentDetail {
                fill = 1; 
                NEW documentHistory {
                    caption = 'История';
                    fill = 1; 
                    type = CONTAINERV;
        
                    MOVE GROUP(created,c);
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    customsDocuments {
        NEW qualityCertificates;
    }
}
