MODULE TerminalAPI;

REQUIRE Terminal, API;

NAMESPACE Terminal;

EXTEND CLASS Category {
    terminal 'ТСД'
}

getTerminalDocumentTypes () {
    EXPORT FROM id = TerminalDocumentType t, name(t) WHERE name(t) AND (access(currentUser(), Category.terminal) OR NOT limitAccess());
} @@api;

type = DATA LOCAL LONG ();
stock = DATA LOCAL LONG ();
quantity = DATA LOCAL NUMERIC[14,3] (INTEGER);
id = DATA LOCAL LONG (INTEGER);
barcode = DATA LOCAL VARSTRING[15] (INTEGER);
price = DATA LOCAL NUMERIC[16,4] (INTEGER);

GROUP item;

FORM addTerminalDocument
    PROPERTIES type = type(), stock = stock()
    OBJECTS details = INTEGER 

    PROPERTIES id = id(details) IN item, barcode = barcode(details) IN item, quantity = quantity(details), price = price(details) IN item
;

addTerminalDocument (FILE f) {

    IF limitAccess() AND NOT access(currentUser(), Category.terminal) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    
    IMPORT addTerminalDocument JSON FROM f AS FILE;
    
    IF (GROUP SUM 1 IF quantity(INTEGER i) AND LONG (Sku s AS Sku) = id(i)) THEN NEW t = TerminalDocument {
    
        terminalDocumentType(t) <- GROUP MAX TerminalDocumentType td IF LONG (td AS TerminalDocumentType) == type();
        stock(t) <- GROUP MAX Stock st IF LONG (st AS Stock) == stock();
        
        FOR quantity(INTEGER i) AND LONG (Sku s AS Sku) = id(i) DO NEW d = TerminalDocumentDetail {
            terminalDocument(d) <- t;
            sku(d) <- s;
            quantity(d) <- quantity(i);
            barcode(d) <- barcode(i);
            price(d) <- price(i); 
        }
    }
    APPLY;
} @@api;