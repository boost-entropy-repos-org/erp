MODULE LabelPriceList;

REQUIRE PriceListOperation, Label;

//--------------- Акционные ценники -------------//

labelType = ABSTRACT LabelType (PriceList);
labelType = ABSTRACT LabelType (PriceListDetail) MATERIALIZED ;
nameLabelType 'Тип ценника' = name(labelType(PriceList p));
nameLabelType 'Тип ценника' = name(labelType(PriceListDetail p));

labelType = DATA LabelType (UserPriceList);
nameLabelType 'Тип ценника' = name(labelType(UserPriceList p)) IN documentPrm;
dataLabelType = DATA LabelType (UserPriceListDetail);
labelType  (UserPriceListDetail d) = OVERRIDE dataLabelType(d),labelType(userPriceList(d));
nameLabelType 'Тип ценника' (UserPriceListDetail d) = name(labelType(d));

labelType(UserPriceList p) += labelType(p);
labelType(UserPriceListDetail d) += labelType(d);


EXTEND FORM  userPriceList  
    PROPERTIES (p) SHOWIF showLabelType(p) nameLabelType 
    PROPERTIES (d) SHOWIF showLabelType(p) nameLabelType 
;

EXTEND FORM  priceLists  
    PROPERTIES (p) READONLYIF isReadonly() nameLabelType
    PROPERTIES (d) READONLYIF isReadonly() nameLabelType SHOWIF showLabelType(p)
;

ledgerPriceListType = DATA DataPriceListType (LabelType);
nameLedgerPriceListType 'Управленческий вид цен' (LabelType l) = name[LedgerPriceListType](ledgerPriceListType(l));

EXTEND FORM labelType PROPERTIES nameLedgerPriceListType(l);

DESIGN labelType {
    wor2{
        MOVE PROPERTY (nameLedgerPriceListType(l) );
    }
    
}

over(LabelType labelType, Sku sku, DepartmentStore departmentStore) += 
    departmentStore IS DepartmentStore AND labelType[PriceListDetail](prevActivePriceListLedgerA(ledgerPriceListType(labelType), sku, departmentStore, currentDateTime())) == labelType; 
skip(LabelType labelType, Sku sku, DepartmentStore departmentStore) += 
    departmentStore IS DepartmentStore AND labelType[PriceListDetail](prevActivePriceListLedgerA(ledgerPriceListType(labelType), sku, departmentStore, currentDateTime())) != labelType; 
        
labelType = DATA LabelType (PriceList.Operation);   
nameLabelType 'Тип ценника' = name(labelType(PriceList.Operation o));

EXTEND FORM PriceList.operation
    PROPERTIES (o) nameLabelType 
;
DESIGN PriceList.operation {
    showContainer {
        MOVE PROPERTY(nameLabelType(o));
    }    
}
@deriveDocumentOperationProperty(UserPriceList, labelType);