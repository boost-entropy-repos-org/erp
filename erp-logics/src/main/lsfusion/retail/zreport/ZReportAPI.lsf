MODULE ZReportAPI;

REQUIRE ZReportReport, API;

NAMESPACE ZReport;

EXTEND CLASS Category {
    zReport 'Z-отчёт'
}

filter = DATA LOCAL BOOLEAN (DepartmentStore);
access (DepartmentStore s) = filter(s) AND (accessCompany(currentUser(), s) OR NOT limitAccess());

getSalesStores (DATE df, DATE dt, FILE f) {

    IF limitAccess() AND NOT access(currentUser(), Category.zReport) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }
    
    IMPORT JSON FROM f AS FILE FIELDS LONG id DO {
        filter(DepartmentStore s) <- TRUE WHERE LONG (s AS DepartmentStore) == id;
    }
    
    EXPORT FROM name(DepartmentStore s), 
        sum = sumReceiptDetailZReport(s, df, dt), 
        discountSum = discountSumZReport(s, df, dt),
        receipt = countReceipt(s, df, dt),
        averageReceipt = averageSumReceipt(s, df, dt)
        WHERE sumReceiptDetailZReport(s, df, dt) AND access(s);
} @@api;

getSalesDays (DATE df, DATE dt, FILE f) {

    IF limitAccess() AND NOT access(currentUser(), Category.zReport) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }

    IMPORT JSON FROM f AS FILE FIELDS LONG id DO {
        filter(DepartmentStore s) <- TRUE WHERE LONG (s AS DepartmentStore) == id;
    }
    
    EXPORT FROM date = DATE d, 
        sum = (GROUP SUM sumReceiptDetailZReport(DepartmentStore s, d) IF access(s)), 
        discountSum = (GROUP SUM discountSumZReport(DepartmentStore s, d) IF access(s)),
        receipt = (GROUP SUM countReceipt(DepartmentStore s, d) IF access(s)),
        averageReceipt = (GROUP SUM sumReceiptDetailZReport(DepartmentStore s, d) IF access(s)) / (GROUP SUM countReceipt(DepartmentStore s, d) IF access(s))
        WHERE (GROUP SUM sumReceiptDetailZReport(DepartmentStore s, d) IF access(s)) AND d >= df AND d <= dt;
} @@api;

FORM salesDepartmentStores
    OBJECTS (dFrom = DATE, dTo = DATE) PANEL
    OBJECTS s = DepartmentStore EXTID 'value'
    PROPERTIES(s) name
    FILTERS active(s), access(s)
    
    OBJECTS d = DATE
    PROPERTIES date = VALUE(d), sum = sumReceiptDetailZReport(s, d), receipt = countReceipt(s, d), averageReceipt = averageSumReceipt(s, d)
    FILTERS sumReceiptDetailZReport(s, d), d >= dFrom AND d <= dTo,
            (GROUP SUM sumReceiptDetailZReport(s, DATE dd) IF dd >= dFrom AND dd <= dTo)
;

getSalesDaysStores (DATE df, DATE dt, FILE f) {

    IF limitAccess() AND NOT access(currentUser(), Category.zReport) THEN {
        EXPORT FROM 'Нет доступа к API';
        RETURN;
    }

    IMPORT JSON FROM f AS FILE FIELDS LONG id DO {
        filter(DepartmentStore s) <- TRUE WHERE LONG (s AS DepartmentStore) == id;
    }
    
    EXPORT salesDepartmentStores OBJECTS dFrom = df, dTo = dt JSON;
} @@api;