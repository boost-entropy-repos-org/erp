MODULE ReceiptDetailReport;

REQUIRE ZReport, ZReportDiscountCard;

NAMESPACE ZReport;

filterDepartmentStoreReceiptDetail 'Просмотр только по одному магазину' = DATA BOOLEAN () COMPLEX;
filterSkuReceiptDetail 'Просмотр только по одному товару' = DATA BOOLEAN () COMPLEX;

EXTEND FORM options
    PROPERTIES() filterDepartmentStoreReceiptDetail, filterSkuReceiptDetail
;

DESIGN options {
    zReport {
        NEW receiptDetail {
            caption = 'Регистр строк чека';
            MOVE PROPERTY (filterDepartmentStoreReceiptDetail());
            MOVE PROPERTY (filterSkuReceiptDetail());
        }
    }
}

filterDepartmentStore  = DATA LOCAL DepartmentStore();
nameFilterDepartmentStore 'Магазин' = name(filterDepartmentStore());

saleDiscountCard  = DATA LOCAL DiscountCard ();
numberDiscountCard 'Дисконтная карта' = seriesNumber(saleDiscountCard());
nameDiscountCard 'Наименование дисконтной карты' (ReceiptDetail receiptDetail) = name(discountCard(receipt(receiptDetail)));

filterSku = DATA LOCAL Sku();
nameFilterSku 'Товар' = name(filterSku());

FORM receiptDetailReport 'Регистр строк чека'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)       
     
    PROPERTIES() nameFilterDepartmentStore, numberDiscountCard, nameFilterSku
    
    OBJECTS rd = ReceiptDetail
    PROPERTIES(rd) READONLY nameDepartmentStore, numberZReport, numberReceipt, numberCashRegister, nameEmployee, numberDiscountCard, nameLegalEntityDiscountCard, nameDiscountCard, dateZReport, time,  
                            id, number, nameSku, idBarcode, signedQuantity, price, signedSum, signedDiscountSum, show CHANGEABLE
    FILTERS dateZReport(rd) >= dFrom AND dateZReport(rd)<= dTo,
            departmentStore(rd) == filterDepartmentStore() OR (NOT filterDepartmentStore() AND NOT filterDepartmentStoreReceiptDetail()),
            numberDiscountCard(rd) == numberDiscountCard() OR NOT numberDiscountCard(),
            sku(rd) == filterSku() OR (NOT filterSku() AND NOT filterSkuReceiptDetail())
;

DESIGN receiptDetailReport {
    NEW dates {
        type = CONTAINERH;
        caption = 'Период';
        MOVE PROPERTY(valFrom){caption = 'Дата с';}
        MOVE PROPERTY(valTo){caption = 'Дата по';}
    }
    NEW top{
        type = CONTAINERH;       
        NEW operationContainer{
            type = CONTAINERH;
            caption = 'Фильтры';
            MOVE PROPERTY(nameFilterDepartmentStore());
            MOVE PROPERTY(numberDiscountCard());
            MOVE PROPERTY(nameFilterSku());
        }
    }
    MOVE BOX(rd);
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    retailNavigator {
        MOVE retailReports BEFORE retailMasterData {
            NEW receiptDetailReport;
        }
    }
}

showReceiptDetailReport 'Показать чеки товара по складу' (Sku sku, Stock stock) = {
    filterDepartmentStore() <- stock;
    filterSku() <- sku;
    SHOW receiptDetailReport DOCKED;
}

EXTEND FORM currentBalanceSkuStock
    PROPERTIES showReceiptDetailReport(s, st)
;

DESIGN currentBalanceSkuStock {
    actionContainer {
        NEW receipt {
            caption = 'Чеки';
            MOVE PROPERTY (showReceiptDetailReport(s, st));
        }
    }
}