MODULE DiscountCard;

REQUIRE Retail, LegalEntity, Numerator, IndividualLegalEntity;

NAMESPACE RetailCRM;

CLASS DiscountCard 'Дисконтная карта';
TABLE discountCard (DiscountCard);
@defineExternalizable(discountCard, VARSTRING[100]);

@defineNumbered(DiscountCard);
@defineNumeratedDefault(DiscountCard, 'Дисконтные карты', 'ДС');

discountSeriesNumber (discountCard) = GROUP AGGR DiscountCard ddiscountCard WHERE ddiscountCard IS DiscountCard BY seriesNumber (ddiscountCard);
discountNumber (discountCard) = GROUP MAX DiscountCard ddiscountCard BY number(ddiscountCard) IF ddiscountCard IS DiscountCard;

discountString (VARSTRING[28] discountCard) = OVERRIDE discountSeriesNumber(discountCard), discountNumber(discountCard);  
toLong = FORMULA NUMERIC[18,0] 'to_number(REPLACE(($1),\',\',\'.\'), \'999999999999999999\')';
longNumber (DiscountCard d) = toLong(number(d));

// Держатель
legalEntity (discountCard) = DATA LegalEntity(DiscountCard) INDEXED;

overNameLegalEntity = ABSTRACT VARISTRING[150] (DiscountCard);
nameLegalEntity 'Держатель дисконтной карты' (DiscountCard dc) = OVERRIDE overNameLegalEntity(dc), name(legalEntity(dc)) IN recognize CHARWIDTH 30;

overFirstNameContact = ABSTRACT VARISTRING[100] (DiscountCard);
firstNameContact 'Имя' (DiscountCard dc) = OVERRIDE overFirstNameContact(dc), firstName(legalEntity(dc)) IN recognize CHARWIDTH 15;

overLastNameContact = ABSTRACT VARISTRING[100] (DiscountCard);
lastNameContact 'Фамилия' (DiscountCard dc) = OVERRIDE overLastNameContact(dc), lastName(legalEntity(dc)) IN recognize CHARWIDTH 15;

overBirthdayContact = ABSTRACT DATE (DiscountCard);
birthdayContact 'День рождения' (DiscountCard dc) = OVERRIDE overBirthdayContact(dc), birthday(legalEntity(dc)) IN recognize;

overPhoneLegalEntity = ABSTRACT VARISTRING[100] (DiscountCard);
phoneLegalEntity 'Телефон' (DiscountCard dc) = OVERRIDE overPhoneLegalEntity(dc), phone(legalEntity(dc)) CHARWIDTH 15;

overEmailLegalEntity = ABSTRACT VARISTRING[400] (DiscountCard);
emailLegalEntity 'E-mail' (DiscountCard dc) = OVERRIDE overEmailLegalEntity(dc), email(legalEntity(dc)) CHARWIDTH 15;

overAddressLegalEntity = ABSTRACT VARISTRING[150] (DiscountCard);
addressLegalEntity 'Адрес' (DiscountCard dc) = OVERRIDE overAddressLegalEntity(dc), address(legalEntity(dc)) IN recognize CHARWIDTH 30;


middleNameHttpServerContact = ABSTRACT VARISTRING[100] (DiscountCard);
numberSexHttpServerContact = ABSTRACT INTEGER (DiscountCard);
skipLoad = ABSTRACT BOOLEAN (DiscountCard);

//тип карты
CLASS DiscountCardType 'Тип дисконтной карты';
TABLE discountCardType (DiscountCardType);

discountCardType = DATA DiscountCardType(DiscountCard) INDEXED;
nameDiscountCardType 'Тип карты' = staticCaption(discountCardType(DiscountCard d));

@defineExternalizable(discountCardType, VARSTRING[10]);
idDiscountCardType 'Тип дисконтной карты' (DiscountCard d) = id(discountCardType(d));

FORM discountCardType 'Тип дисконтной карты'
    OBJECTS t = DiscountCardType PANEL 
    PROPERTIES(t)  staticCaption, id    
    EDIT DiscountCardType OBJECT t
;

FORM discountCardTypes 'Типы дисконтных карт'
    OBJECTS t = DiscountCardType
    PROPERTIES(t) READONLY staticCaption, id
    PROPERTIES(t) NEWSESSION NEW, EDIT, deletet = DELETE 
    
    LIST DiscountCardType OBJECT t
;

NAVIGATOR {
    retailMasterData {
        NEW discountCardTypes;
    }
}

// Сроки действия
notFillDate 'Не заполнять дату выдачи' = DATA BOOLEAN ();

GROUP discountCardDate 'Срок действия' : base;
date 'Дата выдачи' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;
date (DiscountCard discountCard) <- currentDate() WHEN SET(discountCard IS DiscountCard) AND NOT notFillDate();

dateTo 'Дата окончания действия' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;

name 'Наименование' = DATA VARSTRING[100] (DiscountCard);
initialSum 'Начальная сумма' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

percent 'Процент скидки' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

// Генерация дисконтных карт
FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=Numerator  PANEL
    PROPERTIES(n) SELECTOR name

    OBJECTS quantityCards=INTEGER PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = VALUE

    OBJECTS t=DiscountCardType PANEL
    PROPERTIES(t) SELECTOR staticCaption
    
    OBJECTS p=NUMERIC[18,4] PANEL
    PROPERTIES(p) percent = VALUE
;

DESIGN generationDiscountCards {
    BOX {
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
        PROPERTY(staticCaption(t)) {
            caption = 'Наименование';
        }
        PROPERTY(percent) {
            caption = 'Процент скидки';
        }
    }
}

iterateLong(LONG i, LONG from, LONG to) = RECURSION i==from AND from IS LONG AND to IS LONG STEP i==$i+1 AND i<=to CYCLES IMPOSSIBLE;

overGenerateDiscountCard = ABSTRACT LIST (DiscountCard);
generateDiscountCards 'Сгенерировать дисконтные карты'() = {
    DIALOG generationDiscountCards OBJECTS n=defaultNumeratorDiscountCard(), n INPUT, quantityCards INPUT, t INPUT, p INPUT CHECK DO {
        IF   quantityCards <= 100000 THEN { 
            FOR iterateLong(LONG i, curValue(n), curValue(n) + quantityCards - 1) AND NOT discountSeriesNumber(CONCAT '', series(n), VARISTRING[28](i)) NEW d = DiscountCard DO {
                numerator(d) <- n;
                number(d) <- VARISTRING[28](i);
                series(d) <- series(n);
                discountCardType(d) <- t;
                percent(d) <- p;
                overGenerateDiscountCard(d);
            }
        
            curValue(Numerator numerator) <- curValue(numerator) + quantityCards WHERE numerator == n;
            APPLY;
        } ELSE {
            MESSAGE 'Слишком большой интервал';
        }
    }
}

// Формы по работе с дисконтными картами
FORM discountCard 'Дисконтная карта'
    OBJECTS d=DiscountCard PANEL
    PROPERTIES(d) id SHOWIF showIDs(), name, initialSum, percent, nameNumerator, number, series, date, dateTo,
                  firstNameContact, lastNameContact, phoneLegalEntity, emailLegalEntity,          
                  addressLegalEntity, birthdayContact, nameDiscountCardType

    EDIT DiscountCard OBJECT d
;

DESIGN discountCard {
    BOX {
        size = (1024, 768);
        BOX(d) {
            PANEL(d) {
                type = CONTAINERV;
                NEW row1 {
                    NEW common {
                        type = CONTAINERH;
                        caption = 'Общие';
                        MOVE PROPERTY(id(d));
                        MOVE PROPERTY(name(d));
                        MOVE PROPERTY(initialSum(d));
                        MOVE PROPERTY(percent(d));
                        MOVE PROPERTY(nameDiscountCardType(d));
                    }
                }
                NEW row2 {
                    type = CONTAINERH;
                    MOVE GROUP(numbered,d);
                    MOVE GROUP(discountCardDate,d);
                }
                NEW row3 {
                    caption = 'Держатель';
                    type = COLUMNS;
                    columns = 3;
                    MOVE PROPERTY(firstNameContact(d));
                    MOVE PROPERTY(lastNameContact(d));
                    MOVE PROPERTY(phoneLegalEntity(d));
                    MOVE PROPERTY(emailLegalEntity(d));
                    MOVE PROPERTY(addressLegalEntity(d));
                    MOVE PROPERTY(birthdayContact(d)) {
                        pattern='dd.MM.yyyy';
                    }
                }
            }
        }
        NEW tabContainer {
            fill = 1;
            type = TABBED;
        }
        MOVE TOOLBARBOX;
    }
}

FORM discountCardDialog 'Дисконтные карты'

    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY number, series, id SHOWIF showIDs(), nameDiscountCardType, nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity,  addressLegalEntity,
                           firstNameContact, lastNameContact, birthdayContact, 
                           name, initialSum, percent, date, dateTo

    LIST DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d=DiscountCard
    PROPERTIES(d) READONLYIF isReadonly() number, series, id SHOWIF showIDs(), nameDiscountCardType, nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity, addressLegalEntity, 
                           firstNameContact, lastNameContact, birthdayContact, 
                           date, dateTo, name, initialSum, percent
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE 

    ORDER number(d)

    PROPERTIES() generateDiscountCards TODRAW d TOOLBAR 
;
@extendFormEditable(discountCards);

DESIGN discountCards {
    BOX {
            MOVE BOX(d) {
                PROPERTY(birthdayContact(d)) {
                    pattern='dd.MM.yyyy';
                }
            }
            NEW tabbed {
                fill = 1;
                type = TABBED;
            };
            MOVE TOOLBARBOX;
        }
}

NAVIGATOR {
    retailMasterData {
        NEW discountCards;
    }
}
EXTEND FORM options
    PROPERTIES notFillDate()
;
DESIGN options {
    pane {        
        NEW discountCards {
            caption = 'Дисконтные карты';
            MOVE PROPERTY (notFillDate());
        }
    }
}