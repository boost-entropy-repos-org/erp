MODULE SaleBlanketPickingOrder;

REQUIRE SalePickingOrder, SaleBlanketOrder;

NAMESPACE Sale;


createPickingOrder 'Заказ на комплектацию' = DATA BOOLEAN (BlanketOrder, Stock);

createPickingOrder (BlanketOrder o, Stock st) <- createPickingOrder(operation(o, st)) IF in(o,st)  
    WHEN CHANGED(createPickingOrder(operation(o, st))) OR CHANGED(in(o,st));
 
EXTEND FORM blanketOrder
    PROPERTIES(o,c) createPickingOrder AFTER nameOperation(o,c)
;
createPickingOrder(BlanketOrderOrder o) += createPickingOrder(blanketOrder(o), stock(o));
pickingNumber(BlanketOrderOrderDetail d) +=  1 IF d IS BlanketOrderOrderDetail;

countOrders 'К-во заказов' (blanketOrder) = GROUP BY blanketOrder(BlanketOrderOrder order) SUM 1;
count 'К-во заказов' (blanketOrder, BlanketOrderOrder order) = GROUP BY blanketOrder(order) SUM 1;

namePerformer 'Комплектовщики' (BlanketOrder blanketOrder) = GROUP CONCAT namePerformer(BlanketOrderOrder o) IF count(blanketOrder, o), ',' CHARWIDTH 30;

countFull 'К-во полностью собранных' (blanketOrder) = GROUP BY blanketOrder(BlanketOrderOrder order) SUM 1 IF calcFull(order);
countPart 'К-во частично собранных' (blanketOrder) = GROUP BY blanketOrder(BlanketOrderOrder order) SUM 1 IF calcPart(order);
countPartFull 'К-во частично/полностью собранных' (blanketOrder) = GROUP  BY blanketOrder(BlanketOrderOrder order) SUM 1 IF (calcPart(order) OR calcFull(order));
countAccepted 'К-во в комплектации' (blanketOrder) = GROUP BY blanketOrder(BlanketOrderOrder order) SUM 1 IF accepted(order);
countPicking 'К-во ожидает комплектации' (blanketOrder) = GROUP BY blanketOrder(BlanketOrderOrder order) SUM 1 IF countPickingOrder(order);

packQuantity 'Кол-во собранных мест' (order) = GROUP BY blanketOrder(BlanketOrderOrder border) SUM packQuantity(border);

statusPicking 'Статус комплектации' (BlanketOrder order) = CASE
    WHEN countOrders(order) == countFull(order) THEN 'Полностью собран'
    WHEN countOrders(order) == countPartFull(order) THEN 'Частично собран'
    WHEN countAccepted(order)  THEN 'В комплектации'
    WHEN countPicking(order) THEN 'Ожидает комплектацию' 
    WHEN order IS BlanketOrder THEN 'Без комплектации';

backgroundStatusPicking 'Цвет' (BlanketOrder order) = CASE
    WHEN countOrders(order) == countFull(order) THEN RGB(212,255,212)
    WHEN countOrders(order) == countPartFull(order) THEN RGB(255,238,165)
    WHEN countAccepted(order) THEN RGB(255,238,165)
    WHEN countPicking(order) THEN RGB(212,212,255) 
    WHEN order IS BlanketOrder THEN RGB(255,200,216);

EXTEND FORM blanketOrders
    PROPERTIES(o) READONLY PANEL countOrders, namePerformer, packQuantity                  
    PROPERTIES(o) READONLY statusPicking BACKGROUND backgroundStatusPicking(o) BEFORE countBlanketOrderDetail(o)            
;
DESIGN blanketOrders {
    documentHistory {
        NEW picking {
            caption = 'Комплектация';
            type = CONTAINERV;
            MOVE PROPERTY(countOrders(o));
            MOVE PROPERTY(packQuantity(o));
            MOVE PROPERTY(namePerformer(o));
        }
    }
}
