MODULE PurchaseShipmentLot;

REQUIRE PurchaseShipment, 
        Lot, SkuLedgerLot;

NAMESPACE Purchase;

@defineOperationProperty(useLot, 'Марки', showContainer);

@defineDocumentLot(userInvoice, shipmentQuantity);

changeQuantityLot (UserInvoiceDetail d) {
    quantity(Lot l) <- quantity(d, l);
    SHOW dialogInLots;    
}

EXTEND FORM userInvoice
    PROPERTIES(d) quantityLot AFTER shipmentQuantity(d) ON CHANGE changeQuantityLot(d) 
                  BACKGROUND backgroundQuantityLot(d) 
                  SHOWIF useLot(operation(i))
                  READONLYIF NOT lotType(sku(d))
;

@defineDocumentLotBarcodeIn(userInvoice);

// shipment detail
// пока упрощенная схема без поддержки UserShipmentDetail
quantity = ABSTRACT NUMERIC[16,5] (InvoiceDetail, Lot);
quantity(UserInvoiceDetail d, Lot l) += quantity(d, l);

quantity = ABSTRACT NUMERIC[16,5] (ShipmentDetail, Lot);
quantity(InvoiceShipmentDetail d, Lot l) += quantity(invoiceDetail(d), l);

// ledger

quantity(ShipmentBatch b, Lot l) += quantity(shipmentDetail(b), l);
quantity(ShipmentSkuLedger sl, Lot l) += quantity(shipmentDetail(sl), l);