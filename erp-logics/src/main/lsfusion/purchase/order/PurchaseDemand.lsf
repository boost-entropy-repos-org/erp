MODULE PurchaseDemand;

REQUIRE PurchaseOrder;

NAMESPACE Purchase;

//-----------------------------------------------------------------------------------------------------------//

@defineOption(defaultDemandOrderPeriod, 'Период (дней) для заказов по потребностям', INTEGER, purchase);

dateFromSoldPeriod 'Дата с' = DATA DATE (UserOrder);
dateToSoldPeriod 'Дата по' = DATA DATE (UserOrder);

fromShipmentDate 'Начало периода' (UserOrder userOrder) = subtract(date(userOrder), (OVERRIDE quantityDaysNextShipment(userOrder), defaultDemandOrderPeriod()));   

WHEN LOCAL CHANGED (fromShipmentDate(UserOrder o)) AND NOT CHANGED(dateFromSoldPeriod(o)) DO {
    dateFromSoldPeriod(o) <- fromShipmentDate(o);   
}
WHEN LOCAL CHANGED (date(UserOrder o)) AND NOT CHANGED(dateToSoldPeriod(o)) DO {
    dateToSoldPeriod(o) <- subtract(date(o),1);   
}

quantitySold 'Продано' (Sku sku, UserOrder userOrder) = quantitySold(sku, customerStock(userOrder), dateFromSoldPeriod(userOrder), dateToSoldPeriod(userOrder));

purchaseReserve 'Страховой запас (кол-во)' = ABSTRACT NUMERIC[16,5](Stock, Sku, DATE);

recommendedQuantity 'Рекомендуемое к закупке кол-во' = ABSTRACT NUMERIC[16,5] (Sku, UserOrder);    

purchaseReserve 'Страховой запас (кол-во)' (Sku sku, Stock stock, UserOrder userOrder) = purchaseReserve(stock, sku, date(userOrder));

calcDiffPurchaseReserve 'Потребность' (Stock stock, Sku sku, INTEGER int, DATE date) =
    averageSold(sku, stock) * (int AS INTEGER) (+) purchaseReserve(stock, sku, date) (-) currentReservePurchase(sku, stock) (-) (currentBalance(sku, stock) IF currentBalance(sku, stock) > 0.0);
calcDiffPurchaseReserve 'Потребность' (Stock stock, Sku sku, INTEGER int, DATETIME dateTime) = calcDiffPurchaseReserve(stock, sku, int, toDate(dateTime));

//diffPurchaseReserveStockSkuIntegerDate 'Потребность' (stock, sku, int, date) = calcDiffPurchaseReserveStockSkuIntegerDate(stock, sku, int, date) IF calcDiffPurchaseReserveStockSkuIntegerDate(stock, sku, int, date) > 0; 
diffPurchaseReserve 'Потребность' (Stock stock, Sku sku, INTEGER int, DATE date) = 
    calcDiffPurchaseReserve(stock, sku, int, date) IF calcDiffPurchaseReserve(stock, sku, int, date) > 0; 
diffPurchaseReserve 'Потребность' (Stock stock, Sku sku, INTEGER int, DATETIME dateTime) = diffPurchaseReserve(stock, sku, int, toDate(dateTime));

overPurchaseReserve 'Страховой запас (кол-во)'(Stock stock, Sku sku, DATETIME dateTime) = purchaseReserve(stock, sku, toDate(dateTime));

numberSkuPurchaseReserve 'Кол-во товаров' (supplier, Stock stock, LedgerPriceListType priceListType, INTEGER int, DATETIME dateTime) =
    GROUP SUM 1 IF calcDiffPurchaseReserve(stock, Sku sku, int, dateTime) > 0
          BY companyA(priceListType, sku, stock, dateTime);

limitRecommendedQuantity = ABSTRACT VALUE BOOLEAN (Sku, UserOrder);

MOQ 'MOQ' = ABSTRACT NUMERIC[16,5] (Sku);
overRecommendedQuantity = ABSTRACT LIST (UserOrderDetail,Sku);

ceilRecommendedQuantity(Sku sku, NUMERIC[16,5] r) = IF (MIN MOQ(sku), amountPack(sku)) > 0 THEN ceil(r, (MIN MOQ(sku), amountPack(sku))) ELSE (IF split(sku) THEN r ELSE ceil(r));

fillRecommendedQuantityStock 'Заполнить рекомендуемым'(UserOrder userOrder) = {
    LOCAL in = BOOLEAN (Sku);
    in(Sku sku) <- prevPriceA(
                              ledgerPriceListType(priceListType(userOrder, sku)),
                              sku, customerStock(userOrder), supplier(userOrder), dateTime(userOrder))
                          AND NOT limitRecommendedQuantity(sku, userOrder); 
    FOR in(Sku sku) AND NUMERIC[16,5] r == recommendedQuantity (sku, userOrder)
        NEW d = UserOrderDetail DO {
            userOrder(d) <- userOrder;
            sku(d) <- sku;
            quantity (d) <- ceilRecommendedQuantity(sku, r);
            overRecommendedQuantity(d,sku);
        }
} TOOLBAR CONFIRM;
                              
EXTEND FORM userOrder
    PROPERTIES(o) TODRAW sts PANEL dateFromSoldPeriod, dateToSoldPeriod

    PROPERTIES READONLY AFTER prevCurrentBalance(ks,st) purchaseReserve(ks,st,o), quantitySold(ks, o)
    PROPERTIES READONLY AFTER quantityCustomer(ks, o, st)
        recommendedQuantity(ks, o) ON CONTEXTMENU fillRecommendedQuantityStock(o)
;

DESIGN userOrder {
    BOX(sts) {           
        NEW filterDate FIRST {
            caption= 'Период для расчета продаж';
            type = CONTAINERH;
            MOVE PROPERTY (dateFromSoldPeriod(o));
            MOVE PROPERTY (dateToSoldPeriod(o));
      
        }
    }
}