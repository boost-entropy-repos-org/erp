MODULE CryptoPro;

REQUIRE SystemEvents, Utils;

signature = DATA LOCAL RAWFILE ();
encodedSignature = replace(encode(signature(), 'base64'), '\n', '');

sign INTERNAL 'lsfusion.erp.region.ru.utils.cryptopro.SignAction' (RAWFILE, BOOLEAN, STRING, STRING, STRING, STRING);

keyAlias 'Имя ключа УКЭП' = DATA STRING();
keyPassword 'Пароль ключа УКЭП' = DATA STRING() ECHO;

sign (RAWFILE f, BOOLEAN detached) { 
    sign(f, detached, NULL, NULL, keyAlias(), keyPassword()); 
}
sign (FILE f, BOOLEAN detached) { 
    sign(RAWFILE(f), detached);
}

EXTEND FORM integrationData
    PROPERTIES() keyAlias, keyPassword
;

DESIGN integrationData {
    pane {
        NEW cryptopro {
            caption = 'УКЭП';
            MOVE PROPERTY(keyAlias());            
            MOVE PROPERTY(keyPassword());            
        }
    }
}

// нужно сделать так, чтобы JCP добавился как провайдер раньше чем первый раз пошло обращение к BouncyCastle.
initCryptoPro INTERNAL <{ new lsfusion.erp.region.ru.utils.cryptopro.CryptoPro(); }>;
onStarted() + {
    initCryptoPro();
}