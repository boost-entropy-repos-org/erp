MODULE GISMTSaleReturnShipment;

REQUIRE GISMTDocument, GISMTLegalEntity, GISMTIncomingDocument, 
        SaleReturnShipmentLot;

PRIORITY GISMT, SaleReturn;

NAMESPACE GISMTSaleReturn;

createDocument (Invoice i) = isPosted(i) AND createShipment(i) AND supplier(i) != customer(i);
@defineDocumentGISMT(invoice, createDocument, invoices, i, supplier, 'LP_ACCEPT_GOODS');

// release number
releaseNumber = ABSTRACT STRING[100] (Invoice, LotType);

@defineDocumentIncoming(userInvoice, releaseNumber, headerCreateDetail);
releaseNumber (UserInvoice i, LotType t) += releaseNumber(i, t);

// export

FORM exportInvoice
    OBJECTS i = Invoice, t = LotType PANEL
    
    PROPERTIES acceptance_date = replace(toChar(shipmentDateTime(i), 'yyyy-MM-dd T HH24:MI:SS.MI0Z'),' ',''), 
               document_number = seriesNumber(i),
               document_date = replace(toChar(dateTime(i), 'yyyy-MM-dd T HH24:MI:SS.MI0Z'),' ',''),
               request_type = 'ACCEPTANCE', 
               turnover_type = 'SELLING',
               trade_sender_inn = inn(customer(i)),
               trade_recipient_inn = inn(supplier(i)),
               release_order_number = releaseNumber(i, t)
    
    OBJECTS products = (d = InvoiceDetail, l = Lot)
    PROPERTIES accepted = TRUE IF d IS InvoiceDetail AND l IS Lot, uit_code = id(l), product_description = nameSku(d)
    FILTERS invoice(d) = i, quantity(d, l), lotType(sku(l)) = t
;

export (InvoiceDocument i) + {
    IF NOT releaseNumber(invoice(i), lotType(i)) THEN {
        MESSAGE 'Не задан документ на отгрузку товара. Отсылка в ГИС МТ не будет завершена.';
        stopCreate() <- TRUE;
        RETURN;
    }
    EXPORT exportInvoice OBJECTS i = invoice(i), t = lotType(i) JSON;
}
