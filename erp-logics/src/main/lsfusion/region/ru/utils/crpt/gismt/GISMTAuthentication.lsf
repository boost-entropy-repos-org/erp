MODULE GISMTAuthentication;

REQUIRE System, Utils, Time,
        CryptoPro;

NAMESPACE GISMT;

url 'Url к стенду ГИС МТ' = DATA STRING ();
inn 'ИНН участника' = DATA STRING ();

EXTEND FORM integrationData
    PROPERTIES() gismtUrl = url, gismtInn = inn
;

DESIGN integrationData {
    pane {
        NEW GISMT {
            caption = 'ГИС МТ';
            MOVE PROPERTY(gismtUrl);
            MOVE PROPERTY(gismtInn);
        }
    }
}

token = DATA STRING ();
tokenDateTime = DATA DATETIME();

getToken () {
    IF tokenDateTime() > sumMinutes(currentDateTime(), 480) THEN RETURN; 
    
    NEWSESSION {
    
        LOCAL result = JSONFILE ();
        EXTERNAL HTTP GET url() + '/auth/cert/key' TO result;
        
        LOCAL uuid = STRING();
        LOCAL data = STRING();
        IMPORT JSON FROM result() TO() uuid, data;
        
        stringToFile(data());
        
        sign(resultFile(), NULL);
    
        EXPORT JSON FROM uuid = uuid(), data = encodedSignature();
        
        EXTERNAL HTTP POST url() + '/auth/cert/' PARAMS exportFile() TO result;
        IMPORT JSON FROM result() TO() token;
        tokenDateTime() <- currentDateTime();
        
        APPLY;
    }
}