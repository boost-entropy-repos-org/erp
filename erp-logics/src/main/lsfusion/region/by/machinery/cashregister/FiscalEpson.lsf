MODULE FiscalEpson;

REQUIRE POS, POSSection;

disableLoadLibrary 'Отключить загрузку библиотеки Epson' = DATA BOOLEAN (Computer);
EXTEND FORM computer
    PROPERTIES(c) disableLoadLibrary
;

needDLL() = GROUP SUM 1 IF computer(CashRegister c) == currentComputer() AND sidModel(groupMachinery(c)) == 'Epson';
onClientStarted() + { 
	IF needDLL() THEN {
	    IF NOT is64Java(currentComputer()) AND NOT disableLoadLibrary(currentComputer()) THEN
	        loadLibrary('lib/win32/uniFiscalOcx.dll');
	}
};

fiscalEpsonComment 'Дополнительный текст для чека Epson' = ABSTRACT TEXT (Receipt);
fiscalEpsonComment 'Дополнительный текст для строки чека Epson' = ABSTRACT TEXT (ReceiptDetail);

loadDefaultCashRegisterModels() + {  EXEC loadDefaultCashRegisterModel('Фискальный регистратор Epson', 'Epson', NULL, NULL, TRUE, NULL); };

documentNumber 'Сквозной номер чека' (receipt) = DATA INTEGER (Receipt);

fiscalEpsonPrint 'Напечатать фискальный чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonPrintReceiptActionProperty' (Receipt);
postPrint(Receipt receipt) + {  IF sidCashRegisterModel(receipt) == 'Epson' THEN fiscalEpsonPrint(receipt); }

fiscalEpsonElectronicJournalReadOffset = DATA LOCAL NESTED INTEGER ();
fiscalEpsonElectronicJournalReadSize = DATA LOCAL NESTED INTEGER ();
fiscalEpsonSessionNumber = DATA LOCAL NESTED INTEGER ();

fiscalEpsonPrintCopyReceipt 'Копия чека'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonPrintCopyReceiptActionProperty' ();
fiscalPrintCopyReceipt() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonPrintCopyReceipt(); }

fiscalEpsonXReport 'X-отчёт'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonXReportActionProperty' ();
fiscalXReport() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonXReport(); }

fiscalEpsonZReport 'Z-отчёт'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonZReportActionProperty' ();
fiscalZReport() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonZReport(); }

fiscalEpsonElectronicJournal 'Печать контрольной ленты'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonElectronicJournalActionProperty' ();
fiscalElectronicJournal() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonElectronicJournal(); }

fiscalEpsonElectronicJournalToFile 'Печать контрольной ленты в файл'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonElectronicJournalToFileActionProperty' ();
fiscalElectronicJournalToFile() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonElectronicJournalToFile(); }

fiscalEpsonService 'Перемещение денег'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonServiceInOutActionProperty' (CashOperation);
fiscalService(CashOperation cashOperation) + {  IF sidCashRegisterModel(cashOperation) == 'Epson' THEN fiscalEpsonService(cashOperation); }

fiscalEpsonCancel 'Отменить чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCancelReceiptActionProperty' (Receipt);
fiscalCancel(Receipt receipt) + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonCancel(receipt); }

fiscalEpsonReset 'Аннулировать чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonResetReceiptActionProperty' (Receipt);
fiscalResetReceiptCashRegister(Receipt receipt) + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonReset(receipt); }

blockDesync 'Не открывать Z-отчёт при рассинхронизации времени (Epson)' = DATA BOOLEAN ();
maxDesync 'Максимальное время рассинхронизации (c) (Epson)' = DATA LONG ();

fiscalEpsonElectronicJournalReadOffset = DATA INTEGER (ZReport);
fiscalEpsonElectronicJournalReadOffsetCurrentZReport() = fiscalEpsonElectronicJournalReadOffset(currentZReport());

fiscalEpsonCheckOpenZReport 'Проверка перед открытием Z-отчета'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCheckOpenZReportActionProperty' (ZReport);
fiscalCheckOpenZReport(ZReport zReport) + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonCheckOpenZReport(zReport); }

fiscalEpsonCheckSKNO 'Проверить связь СКНО'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCheckSKNOActionProperty' ();

EXTEND FORM options PROPERTIES() blockDesync, maxDesync;
DESIGN options {
    machinery {
        NEW epson {
            caption = 'Epson';
            MOVE PROPERTY(blockDesync());
            MOVE PROPERTY(maxDesync());
        }
    }
}

EXTEND FORM operationCashRegister
    PROPERTIES() fiscalEpsonCheckSKNO 
;

DESIGN operationCashRegister{
    secondRowActions {
        MOVE PROPERTY(fiscalEpsonCheckSKNO()) BEFORE PROPERTY(numberCurrentZReport()) { font = '24';} 
    }
}

// --- Типы платежей

cardType 'Код платежа картой' = DATA INTEGER (CashRegister) IN base;
giftCardType 'Код платежа подарочным сертификатом' = DATA INTEGER (CashRegister) IN base;

EXTEND FORM groupCashRegister
    PROPERTIES(c) cardType, giftCardType
;

cardTypeCurrentCashRegister 'Код платежа картой' = cardType(currentCashRegister());
giftCardTypeCurrentCashRegister 'Код платежа подарочным сертификатом' = giftCardType(currentCashRegister());
