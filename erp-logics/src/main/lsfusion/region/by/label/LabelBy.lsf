MODULE LabelBy;

REQUIRE Label, Item;

NAMESPACE Label;

//цена за литр\кг

@defineItemGroupValue(printFullPrice, 'Печатать цену за килограмм', BOOLEAN);

@defineItemGroupValue(volumeMinPrice, 'Минимальный объем/вес для не печати цены за килограмм', NUMERIC[16,5]);

@defineOption(minVolumeLabelBy, 'Объем/Вес для цены за 100г (0,05 = 50г)', NUMERIC[10,3], label);

overMinVolumeLabelBy() = OVERRIDE minVolumeLabelBy(), 0.25;

labelBaseCoeff = ABSTRACT NUMERIC[9,3](Sku);
labelBaseRetailCoeff = ABSTRACT NUMERIC[9,3](Sku);
labelBaseName = ABSTRACT VARSTRING[20](Sku);
volumeMinPrice (LabelTransactionDetail d) = OVERRIDE volume(sku(d)) < volumeMinPrice(skuGroup(sku(d))), netWeight(sku(d)) < volumeMinPrice(skuGroup(sku(d)));

skipFullPrice 'Не печатать цену за килограмм' = ABSTRACT BOOLEAN (Sku);
skipFullPrice 'Не печатать цену за килограмм'  = DATA BOOLEAN (Item) IN itemBase;
skipFullPrice(Item i) += skipFullPrice(i);

EXTEND FORM items
    PROPERTIES (i) READONLYIF isReadonly() skipFullPrice
;
EXTEND FORM item
    PROPERTIES (i) skipFullPrice
;


basePrice 'Цена за базовую единицу' (LabelTransactionDetail d) = IF printFullPrice(skuGroup(sku(d))) AND NOT (volumeMinPrice(d) OR skipFullPrice(sku(d)))  THEN CASE 
                                                                                                          WHEN labelBaseCoeff(sku(d)) THEN round2(price(d) / labelBaseCoeff(sku(d)))
                                                                                                          WHEN volume(sku(d)) <= overMinVolumeLabelBy() THEN round2(price(d) / volume(sku(d)) / 10.0 )
                                                                                                          WHEN volume(sku(d)) != 1.0 THEN round2(price(d) / volume(sku(d)))
                                                                                                          WHEN netWeight(sku(d)) <= overMinVolumeLabelBy() THEN round2(price(d) / netWeight(sku(d)) / 10.0 )
                                                                                                          WHEN netWeight(sku(d)) != 1.0 THEN round2(price(d) / netWeight(sku(d))) ;
                                                                                                                                                                                                                  
basePriceName 'Базовая единица' (LabelTransactionDetail d) = IF printFullPrice(skuGroup(sku(d))) AND NOT (volumeMinPrice(d) OR skipFullPrice(sku(d))) THEN CASE 
                                                                                                          WHEN labelBaseName(sku(d)) THEN labelBaseName(sku(d))
                                                                                                          WHEN volume(sku(d)) <= overMinVolumeLabelBy() THEN '100мл'
                                                                                                          WHEN volume(sku(d)) != 1.0 THEN '1л'
                                                                                                          WHEN netWeight(sku(d)) <= overMinVolumeLabelBy() THEN '100г'
                                                                                                          WHEN netWeight(sku(d)) != 1.0 THEN '1кг' ;
                                                                                                          
basePriceRub 'Рубли (за б.е.)' (LabelTransactionDetail d) = trunc(basePrice(d));
basePriceKop 'Копейки (за б.е.)' (LabelTransactionDetail d) = OVERRIDE round0((basePrice(d) (-) basePriceRub(d)) * 100.0), 0.00 IF basePrice(d);

baseRetailPrice 'Цена за базовую единицу до' (LabelTransactionDetail d) = IF printFullPrice(skuGroup(sku(d))) AND NOT (volumeMinPrice(d) OR skipFullPrice(sku(d))) THEN CASE 
                                                                                                          WHEN labelBaseRetailCoeff(sku(d)) THEN round2(retailPrice(d) / labelBaseRetailCoeff(sku(d)))
                                                                                                          WHEN volume(sku(d)) <= overMinVolumeLabelBy() THEN round2(retailPrice(d) / volume(sku(d)) / 10.0 )
                                                                                                          WHEN volume(sku(d)) != 1.0 THEN round2(retailPrice(d) / volume(sku(d)))
                                                                                                          WHEN netWeight(sku(d)) <= overMinVolumeLabelBy() THEN round2(retailPrice(d) / netWeight(sku(d)) / 10.0 )
                                                                                                          WHEN netWeight(sku(d)) != 1.0 THEN round2(retailPrice(d) / netWeight(sku(d))) ;

EXTEND FORM printLabelTransaction PROPERTIES(d) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice;
EXTEND FORM customLabelTransaction PROPERTIES(d) basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice;
EXTEND FORM labelTransactions 
    PROPERTIES(dt) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice 
    PROPERTIES(dts) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, baseRetailPrice
;

//----------------------- Переход на новый основной классификатор ---------------------------//
overReplaceToItemGroup(CustomGroup g, ItemGroup ng) + {
    dataPrintFullPrice(ng) <- prevPrintFullPrice(itemGroup(g));
    dataVolumeMinPrice(ng) <- prevVolumeMinPrice(itemGroup(g));
}