MODULE PurchaseManufacturingPricePharmacyBy;

REQUIRE PurchaseManufacturingPrice, Pharmacy;

NAMESPACE Purchase;

maxSellingPrice 'Предельная отпускная цена' = ABSTRACT NUMERIC[16,4](InvoiceDetail);
maxSellingPrice 'Предельная отпускная цена' = DATA NUMERIC[16,4](UserInvoiceDetail);
maxSellingPrice (UserInvoiceDetail d) += maxSellingPrice(d);

@defineOperationProperty(showMaxSellingPrice, 'Предельная отпускная цена', showContainer);

EXTEND FORM userInvoice PROPERTIES maxSellingPrice(d) SHOWIF showMaxSellingPrice(operation(d));
EXTEND FORM invoices PROPERTIES maxSellingPrice(d) SHOWIF showMaxSellingPrice(operation(d));

maxSellingPriceListType = DATA PriceListType();
nameMaxSellingPriceListType 'Вид цен для предельной отпускной цены' () = name(maxSellingPriceListType());

EXTEND FORM options PROPERTIES nameMaxSellingPriceListType();

DESIGN options {
    pharmacy {
        MOVE PROPERTY (nameMaxSellingPriceListType());
    }
}


WHEN LOCAL CHANGED (sku(UserInvoiceDetail d)) OR CHANGED (customerStock(d)) OR CHANGED (dateTime(d)) DO {
    maxSellingPrice(d) <- priceA(maxSellingPriceListType(), sku(d), customerStock(d), dateTime(d));    
}
    
dataCostManufacuringPrice (UserInvoiceDetail d) += NUMERIC[20,8] 
    (min(incomeManufacturingPrice(d), maxSellingPrice(d)) (+) extraCostPrice(d) (+) customCostPrice(d) (+) certificateCostPrice(d));
