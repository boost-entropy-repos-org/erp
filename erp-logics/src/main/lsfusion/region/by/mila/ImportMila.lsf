MODULE ImportMila;

REQUIRE System, Barcode, PriceListOperation;

importMilaFile = DATA LOCAL FILE ();
importMila 'Импортировать'  INTERNAL  'lsfusion.erp.region.by.mila.ImportMilaActionProperty' (BOOLEAN);

importMilaLight 'Импорт списка товаров' () { importMila(NULL); open(importMilaFile()); }
importMilaFull 'Импорт всей информации по товарам' () { importMila(TRUE); open(importMilaFile()); }

FORM importMilaEuroopt 'Импорт Mila'
    PROPERTIES() importMilaLight, importMilaFull
;

EXTEND FORM integrationData
    PROPERTIES() importMilaLight, importMilaFull   
;
DESIGN integrationData {
    pane {
        NEW mila {
            caption = 'Мила';
            MOVE PROPERTY(importMilaLight());          
            MOVE PROPERTY(importMilaFull());          
        }
    }
}

gname = DATA LOCAL VARSTRING[100] (INTEGER);

group = DATA LOCAL INTEGER (INTEGER);
tname = DATA LOCAL VARSTRING[100] (INTEGER);
tcode = DATA LOCAL VARSTRING[15] (INTEGER);
price1 = DATA LOCAL NUMERIC[10,3](INTEGER);
price3 = DATA LOCAL NUMERIC[10,3](INTEGER);

FORM importMila
    OBJECTS groups = INTEGER
    PROPERTIES(groups) gname=gname
    
    OBJECTS goods = INTEGER
    PROPERTIES(goods) tcode=tcode, price1=price1, price3=price3
    FILTERS group(goods) = groups
;;

importPriceList () {
    importMilaFull();
    open(importMilaFile());
    importFile() <- importMilaFile();
    
    IMPORT importMila JSON;
    
    NEW p = UserPriceList {
        operation(p) <- GROUP LAST PriceList.Operation o WHERE id(o) == 'mila';
        FOR skuBarcode(trim(tcode(INTEGER i))) NEW d = UserPriceListDetail DO {
            
            priceList(d) <- p;
            sku(d) <- skuBarcode(trim(tcode(i)));
            price(d, DataPriceListType t) <- OVERRIDE (price3(i) IF price3(i) != 0), price1(i) WHERE id(t) = 'mila';
            price(d, DataPriceListType t) <- price1(i) IF price3(i) != 0 WHERE id(t) = 'mila_p';
        }
        isPosted(p) <- TRUE;
        APPLY;
    }
}