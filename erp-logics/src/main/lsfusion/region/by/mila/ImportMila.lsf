MODULE ImportMila;

REQUIRE System, Barcode, PriceListOperation, Competitor;

PRIORITY Competitor;

importMilaFile = DATA LOCAL FILE ();
addLog = DATA LOCAL BOOLEAN ();
importMila 'Импортировать'  INTERNAL  'lsfusion.erp.region.by.mila.ImportMilaAction' (BOOLEAN, BOOLEAN );

importMilaLight 'Импорт списка товаров' () { importMila(NULL, addLog()); open(importMilaFile()); }
importMilaFull 'Импорт всей информации по товарам' () { importMila(TRUE, addLog()); open(importMilaFile()); }

FORM importMilaEuroopt 'Импорт Mila'
    PROPERTIES() importMilaLight, importMilaFull
;

EXTEND FORM integrationData
    PROPERTIES() importMilaLight, importMilaFull   
;
DESIGN integrationData {
    pane {
        NEW mila {
            caption = 'Мила';
            MOVE PROPERTY(importMilaLight());          
            MOVE PROPERTY(importMilaFull());          
        }
    }
}

gname = DATA LOCAL STRING[100] (INTEGER);

group = DATA LOCAL INTEGER (INTEGER);
tcode = DATA LOCAL STRING[25] (INTEGER);
price2 = DATA LOCAL NUMERIC[10,3](INTEGER);
price3 = DATA LOCAL NUMERIC[10,3](INTEGER);

FORM importMila
    OBJECTS groups = INTEGER
    PROPERTIES(groups) gname=gname
    
    OBJECTS goods = INTEGER
    PROPERTIES(goods) tcode=tcode, price2=price2, price3=price3
    FILTERS group(goods) = groups
;

importPriceList () {
    importMila(TRUE, addLog());
    
    IMPORT importMila JSON FROM importMilaFile();
    
    NEW p = Competitor.PriceList {
        
        IF NOT competitor('Мила') THEN NEW c = Competitor { name(c) <- 'Мила'; }
        
        in(p, Competitor c) <- name(c) == 'Мила';
        date(p) <- currentDate();
        note(p) <- 'Цены Мила';
         
        FOR Sku s = skuBarcode(trim(replace(tcode(INTEGER i),'Код: ',''))) AND Competitor c == competitor('Мила') DO NEW d = Competitor.PriceListDetail {
            
            priceList(d) <- p;
            sku(d) <- s;
            price(d, c) <- OVERRIDE (price3(i) IF price3(i) != 0), price2(i);
            promotionPrice(d, c) <- price2(i) IF price3(i) != 0;
        }
        APPLY;
    }
}