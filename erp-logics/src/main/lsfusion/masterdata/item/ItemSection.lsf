MODULE ItemSection;

REQUIRE Item, Section;

NAMESPACE Item;

TABLE SectionItem (Section, Item);
TABLE sessionSectionItem (Session, Section, Item); 
dataIn 'Отм' = DATA BOOLEAN (Section, Item);

section (Item i) = GROUP MIN Section s IF dataIn(s, i);
idSection 'Секция' (Item item) = id(section(item)) CHARWIDTH 5;

allowMultiSection 'Ререшить несколько секций' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES allowMultiSection()
;

DESIGN options{
    items{
        MOVE PROPERTY(allowMultiSection());
    }
}

CONSTRAINT [=GROUP SUM 1 IF dataIn(Section s, Item i) BY i](Item i) > 1 AND NOT allowMultiSection()
    MESSAGE 'Для товара может быть задана только одна секция';

changeSection(Section s, Item i) = {
    INPUT b = BOOLEAN DO{
        dataIn(Section sec, i) <- NULL WHERE dataIn(sec, i) AND NOT allowMultiSection() ;
        dataIn(s, i) <- b;    
    }
}

dataIn (Section s, Item sku) += dataIn(s, sku);

EXTEND FORM item
    OBJECTS sec = Section GRID 
    PROPERTIES dataIn(sec, i) ON CHANGE changeSection(sec, i), id(sec) READONLY, name(sec) READONLY
;

DESIGN item {
    itemDetail {
        NEW itemSection {
            caption = 'Секция';
            fill = 1;
            MOVE BOX(sec); 
        }
    }
}

FORM changeSection 'Секции'
    OBJECTS sec = Section GRID 
    PROPERTIES (sec) id READONLY, name READONLY
    FILTERGROUP active
        FILTER 'Активные' active(sec) DEFAULT
;

changeSection (Item item) = {
    DIALOG changeSection OBJECTS sec = section(item) INPUT DO {
        dataIn(Section section, item) <- NULL;
        dataIn(sec, item) <- TRUE;
    }
}

EXTEND FORM items
    PROPERTIES (i) READONLYIF isReadonly() idSection ON CHANGE changeSection(i) 
;
overCopy(Item old, Item new) += {
    dataIn(Section s, new) <- dataIn(s, old);
}