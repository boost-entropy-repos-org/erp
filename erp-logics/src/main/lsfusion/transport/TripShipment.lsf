MODULE TripShipment;

REQUIRE TripOrder, OrderShipment;

NAMESPACE Trip;

// поставки
trip = DATA Trip(Shipment);
in 'Включен' (Trip trip, Shipment shipment) = trip(shipment)==trip;

filter(Trip trip, Shipment shipment) = trip(shipment)==trip OR (trip IS Trip AND shipment IS Shipment AND NOT trip(shipment));

grossWeightShipped 'Суммарный вес поставок' (trip) = GROUP BY trip(Shipment shipment) SUM grossWeightShipmentDetail(shipment);

quantityShipmentDetail (trip, Shipment shipment) = GROUP BY trip(Order order) SUM quantityShipmentDetail (order, shipment);

quantityShipped 'Кол-во в поставках' (trip, sku) = GROUP BY trip(shipment(ShipmentDetail shipmentDetail)), sku(shipmentDetail) SUM quantity(shipmentDetail);

EXTEND FORM trip
    PROPERTIES (t) READONLY grossWeightShipped
    
    OBJECTS s=Shipment
    PROPERTIES (t, s) in
    PROPERTIES(s) READONLY number, series, date, nameSupplier, nameFromStock,
                  nameCustomer, nameToStock, grossWeightShipmentDetail
    PROPERTIES (s) NEWSESSION EDIT

    FILTERS isPosted(s)

    FILTERS customer(s) == customer(t) OR (s IS Shipment AND NOT customer(t)),
            supplier(s) == supplier(t) OR (s IS Shipment AND NOT supplier(t))

    FILTERGROUP filterDateShipment
        FILTER 'Текущие' date(s) <= date(t) 'F9' DEFAULT
    FILTERGROUP filterShipment
        FILTER 'Не расписанные или в текущем рейсе' filter(t, s) 'F11'
        FILTER 'В текущем рейсе' in(t, s) 'F10' DEFAULT

    OBJECTS sd=ShipmentDetail
    PROPERTIES(sd) READONLY idBarcodeSku, nameSku, shortNameUOMSku, quantity, grossWeight, nameFromStock, nameToStock
    FILTERS shipment(sd)==s
;

DESIGN trip {
    headerSum {
        MOVE PROPERTY(grossWeightShipped(t));
    }
    
    pane {
        NEW shipments {
            fill = 1;
            caption = 'Поставки';
            MOVE BOX(s);
            MOVE BOX(sd);
        }
    }
}

EXTEND FORM trips
    PROPERTIES(t) READONLY grossWeightShipped

    OBJECTS s=Shipment
    PROPERTIES(s) READONLY number, series, date, nameSupplier, nameFromStock, nameCustomer, nameToStock, grossWeightShipmentDetail
    FILTERS in(t, s)
;

DESIGN trips {
    pane {
        MOVE BOX(s) {
            caption = 'Поставки';
        }
    }
}