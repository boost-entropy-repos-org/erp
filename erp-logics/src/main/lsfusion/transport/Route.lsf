MODULE Route;

REQUIRE Transport, Numerator;

// ------------------------------- Route ----------------------------------- //
CLASS Route 'Маршрут';
TABLE route(Route);

@defineNumbered(Route);
@defineNumeratedDefault(Route, 'Маршруты', 'МР');

truck = DATA Truck(Route);
sidTruck 'Машина' (Route route) = sid(truck(route));

FORM route 'Маршрут'
    OBJECTS r=Route PANEL
    PROPERTIES(r) nameNumerator, number, series, sidTruck

    EDIT Route OBJECT r
;

FORM routes 'Маршруты'
    OBJECTS r=Route
    PROPERTIES(r) READONLY nameNumerator, number, series, sidTruck
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE 
;


// ------------------------------- Node ------------------------------------ //
CLASS ABSTRACT Node 'Пункт';

name 'Наименование' = ABSTRACT ISTRING[150](Node);
address 'Адрес' = ABSTRACT ISTRING[150](Node);
legalEntity = ABSTRACT LegalEntity(Node);

nameLegalEntity 'Юр. лицо' (Node node) = name(legalEntity(node));

TABLE nodeRoute(Node, Route);
in 'Включен' = DATA BOOLEAN(Node, Route);
number 'Порядковый номер' = DATA INTEGER(Node, Route);

EXTEND CLASS Stock : Node;
//EXTEND CLASS supplier : node;

name(Stock stock) += Stock.name(stock) IF stock IS Stock;

address(Stock stock) += Stock.address(stock) IF stock IS Stock;

legalEntity(Stock stock) += Stock.legalEntity(stock);

skipShowNode = ABSTRACT BOOLEAN (Route);
showNode (Route r) = r IS Route AND NOT skipShowNode(r);

//nameNode(supplier) += name(supplier) IF supplier IS Supplier;

EXTEND FORM route

    OBJECTS n=Node
    PROPERTIES (n) READONLY SHOWIF showNode(r) name, address
    PROPERTIES (n, r) SHOWIF showNode(r) in, number
;

EXTEND FORM routes
    OBJECTS n=Node
    PROPERTIES (n) READONLY SHOWIF showNode(r) name, address
    PROPERTIES(n, r) READONLY SHOWIF showNode(r) number 
    FILTERS in(n, r)
;

NAVIGATOR {
    transportDocuments {
        NEW routes;
    }
}