MODULE SkuList;

REQUIRE Sku, Item, Barcode;

NAMESPACE Sku;

CLASS List 'Список SKU';
TABLE list (List);

@defineExternalizable(list, STRING[100]);

order 'Порядок' = DATA INTEGER (List) CHARWIDTH 2;
name 'Наименование' = DATA ISTRING[250](List) IN base;

@defineHierarchy(list);

@defineHierarchyFilter(List, List, name, 'По наименованию');

in 'Отм.' = DATA LOCAL BOOLEAN (List);

moveList 'Перенести отмеченные группы в текущую'(list)  { 

    FOR in(List i) DO{
        IF i != list THEN {
            parent(i) <- list;
            in(i) <- NULL;
        } ELSE {
            MESSAGE 'Выделенная товарная группа не может совпадать с объединяемой';
        }
    }
}

CLASS ListDetail 'Строка списка SKU';
TABLE listDetail(ListDetail);

list = DATA List(ListDetail) NONULL DELETE;

sku = DATA Sku(ListDetail);
idSku 'Код товара' (ListDetail d) = id(sku(d));
idBarcodeSku 'Штрихкод товара' (ListDetail d) = idBarcode(sku(d));
nameSku 'Наименование товара' (ListDetail d) = name(sku(d));
shortNameUOMSku 'Ед. изм. товара' (ListDetail d) = shortNameUOM(sku(d));
canonicalNameSkuGroup 'Группа товара' (ListDetail d) = canonicalName(skuGroup(sku(d)));
nameBrandSku 'Бренд' (ListDetail d) = nameBrand(sku(d));
nameCountrySku 'Страна' (ListDetail d) = nameCountry(sku(d));
nameManufacturerSku 'Производитель' (ListDetail d) = nameManufacturer(sku(d));

listDetail = GROUP MAX ListDetail d BY sku(d), list(d);

//Доступ
dataAccess 'Доступ разрешен' = DATA BOOLEAN (Stock, List);
dataAccess 'Доступ разрешен' = DATA BOOLEAN (StockGroup, List);
limitAccessStock 'Ограничен доступ' (List l) = GROUP SUM 1 IF dataAccess(Stock s, l) OR dataAccess(stockGroup(s), l) MATERIALIZED COMPLEX;

levelParent (List list, StockGroup stockGroup) = GROUP MIN level(stockGroup, StockGroup parent) IF dataAccess(parent, list) MATERIALIZED;
nearestParent (List list, StockGroup stockGroup) = stockGroup(stockGroup, levelParent(list, stockGroup));
nearestIn (List list, StockGroup stockGroup) =
    dataAccess(nearestParent (list, stockGroup), list) MATERIALIZED;

access 'Доступ разрешен' (List list, StockGroup stockGroup) = OVERRIDE
    dataAccess(stockGroup, list),
    nearestIn(list, stockGroup),
    list IS List AND stockGroup IS StockGroup AND NOT limitAccessStock(list); // MATERIALIZED;

access 'Доступ разрешен' (List list, Stock stock) = OVERRIDE
    dataAccess(stock, list),
    nearestIn(list, stockGroup(stock)),
    list IS List AND stock IS Stock AND NOT limitAccessStock(list);

dataAccess 'Доступ разрешен' = DATA BOOLEAN (UserRole, List);
limitAccessRole 'Ограничен доступ' (List l) = GROUP SUM 1 IF dataAccess(UserRole r, l) MATERIALIZED COMPLEX;

access 'Доступ разрешен' (List list, UserRole role) = OVERRIDE
    dataAccess(role, list),
    list IS List AND role IS UserRole AND NOT limitAccessRole(list);

in 'Вкл.' (List l, Sku s) = TRUE IF [GROUP SUM 1 BY list(ListDetail d),  sku(d)](l, s);

changeIn (List l, Sku s) {
    INPUT b = BOOLEAN DO {
        IF NOT b THEN {
            DELETE ListDetail d WHERE list(d) == l AND sku(d) == s;
        }
        IF b THEN NEW d = ListDetail {
            list(d) <- l;
            sku(d) <- s;
        }
    }
}

CONSTRAINT sku(ListDetail d1) == sku(ListDetail d2) AND list(d1) == list(d2) AND d1 != d2 CHECKED BY sku[ListDetail] MESSAGE 'В списке SKU не должен повторятся товар';

FORM list 'Список SKU'
    OBJECTS l = List PANEL
    PROPERTIES (l) name, nameParent, order, id SHOWIF showIDs()
    ORDER name(l)
    
    OBJECTS d = ListDetail
    PROPERTIES(d) idSku, idBarcodeSku, nameSku, shortNameUOMSku, canonicalNameSkuGroup, nameBrandSku, nameCountrySku, nameManufacturerSku, NEW, DELETE 
    FILTERS list(d) == l
    ORDER nameSku(d)
    
    TREE stockTree a = BPSTRING[3], sg = StockGroup  PARENT parent
    PROPERTIES READONLY VALUE(a), sgTreeName = name(sg)
    ORDER sgTreeName
    FILTERS stringEqualsAll(a)
    PROPERTIES(l,sg) access

    OBJECTS s = Stock GRID
    PROPERTIES(s) READONLY id 
    PROPERTIES(s) READONLY stockName = name, nameLegalEntity
    ORDER stockName
    FILTERS isParent(sg, s) OR (s IS Stock AND NOT sg)
    FILTERGROUP inactiveStock FILTER 'Активный' active(s) 'ctrl F10' DEFAULT
    PROPERTIES(l,s) access
    
    OBJECTS r = UserRole
    PROPERTIES access(l, r), name(r) READONLY
    ORDER name(r)
    
    TREE skuTree skg = SkuGroup PARENT parent
    PROPERTIES READONLY order(skg), skuTreeName = name(skg)
    ORDER order(skg), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(skg) 'F5' DEFAULT

    OBJECTS sk = Sku
    PROPERTIES in(l, sk) ON CHANGE changeIn(l, sk)
    PROPERTIES (sk) READONLY id, idBarcode, name, shortNameUOM, canonicalNameSkuGroup, nameBrand, nameCountry, nameManufacturer
    FILTERGROUP in FILTER 'Отмеченные' in(l, sk)
    FILTERS isParent(skg, sk)
    ORDER name(sk)
    
    EDIT List OBJECT l
;

DESIGN list {
    OBJECTS {
        NEW tab AFTER BOX (l) {
            type = TABBED;
            fill = 1;
            MOVE BOX (d) { caption = 'Товары'; }
            NEW access {
                caption = 'Доступ';
                NEW stockSplitContainer{
                    fill = 1;
                    type = SPLITH;
                    MOVE BOX(TREE stockTree) { 
                        caption = 'Группы складов';
                        PROPERTY (access(l,sg)) {valueWidth = 100; }
                    };
                    MOVE BOX(s) {
                        GRID(s) {
                            defaultComponent = TRUE;
                            PROPERTY (access(l,s)) {valueWidth = 100;}
                        }
                    }
                }
                MOVE BOX (r) { caption = 'Роли'; }
            }
            NEW select {
                caption = 'Подбор';
                type = SPLITH;
                fill = 1;
                MOVE BOX (TREE skuTree) { fill = 1; }
                MOVE BOX (sk) { fill = 3; }
            }
        }
    }
}

FORM lists 'Списки SKU'
    
    OBJECTS o = List
    PROPERTIES (o) READONLY canonicalName
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDER canonicalName(o)
    
    OBJECTS d = ListDetail
    PROPERTIES(d) READONLY idSku, idBarcodeSku, nameSku, shortNameUOMSku, canonicalNameSkuGroup, nameBrandSku, nameCountrySku, nameManufacturerSku
    ORDER nameSku(d)
    FILTERS list(d) == o
    
    TREE treeGroups g=List PARENT parent
    PROPERTIES in(g), moveList(g) TOOLBAR
    PROPERTIES (g) READONLYIF isReadonly() order, id SHOWIF showIDs(), name, canonicalName
    PROPERTIES(g) NEWSESSION NEW, EDIT, deleteg=DELETE 
    FILTERS inIFilterName(g) OR NOT filterNameList()
    ORDER order(g), canonicalName(g)
    
    OBJECTS dd = ListDetail
    PROPERTIES(dd) READONLY idSku, idBarcodeSku, nameSku, shortNameUOMSku, canonicalNameSkuGroup, nameBrandSku, nameCountrySku, nameManufacturerSku
    ORDER nameSku(dd)
    FILTERS isParent(list(dd), g)
;

DESIGN lists {
    OBJECTS {
        NEW tab FIRST {
            fill = 1;
            type = TABBED;
            NEW list {
                caption = 'Список';
                MOVE BOX (o);
                MOVE BOX (d);
            }
            NEW tree {
                caption = 'Дерево';
                MOVE BOX (TREE treeGroups);
                MOVE BOX (dd);
            }
        }
    }
}

FORM dialogLists 'Списки SKU'
    TREE treeGroups tg=List PARENT parent
    PROPERTIES READONLY order(tg), id(tg) SHOWIF showIDs(), name(tg)
    ORDER order(tg), name(tg)

    OBJECTS g=List
    PROPERTIES(g) READONLY order, id SHOWIF showIDs(), name, canonicalName 
    ORDER order(g), canonicalName(g)

    FILTERS isParent(g, tg)

    FILTERGROUP filters
        FILTER 'Все листья' isParentLeaf(g, tg) 'F10' DEFAULT
        FILTER 'Всех потомков' isParent(g, tg) 'F9'
        FILTER 'Только непосредственных потомков' parent(g) == tg 'F8'
    
    LIST List OBJECT g
;

DESIGN dialogLists {
    BOX {
        size = (1280, 720);
        NEW rootContainer BEFORE TOOLBARBOX {
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE treeGroups);
            MOVE BOX (g) {
                fill = 2;
                GRID(g) { defaultComponent = TRUE; }
            }
        }
    }
}

NAVIGATOR {
    skuNavigator {
        NEW lists;
    }
}

FORM dialogAddLists 'Списки SKU'
    OBJECTS o = List
    PROPERTIES(o) in, name READONLY 
    ORDER name(o)
    
    OBJECTS d = ListDetail
    PROPERTIES(d) READONLY idSku, idBarcodeSku, nameSku, shortNameUOMSku, canonicalNameSkuGroup, nameBrandSku, nameCountrySku, nameManufacturerSku
    ORDER nameSku(d)
    FILTERS list(d) == o
;

addSkuLists 'Отметить списки SKU' () {
    DIALOG dialogAddLists DO {
        FOR [GROUP SUM 1 IF in(list(ListDetail d)) BY sku(d)](Sku sk) DO {
            select(sk, Stock st) <- TRUE WHERE [FILTER currentBalanceSkuStock.sts](st, sk);
        }
    }
    in(List l) <- NULL;
}

EXTEND FORM currentBalanceSkuStock PROPERTIES addSkuLists();

DESIGN currentBalanceSkuStock {
    filter {
        MOVE PROPERTY (addSkuLists());
    }
}

createSkuListIn 'Создать список SKU' () {
    NEWSESSION NESTED (in[Item]) NEW l = List {
        FOR in(Item i) DO NEW d = ListDetail {
            sku(d) <- i;
            list(d) <- l;
        }
        SHOW list OBJECTS l = l DOCKED;
    }
}

addToSkuListIn 'Добавить в список SKU' () {
    NEWSESSION NESTED (in[Item]) DIALOG dialogLists OBJECTS tg INPUT DO {
        FOR in(Item i) AND NOT [GROUP SUM 1 BY sku(ListDetail d), list(d)](i, tg) DO NEW d = ListDetail {
            sku(d) <- i;
            list(d) <- tg;
        }
        SHOW list OBJECTS l = tg DOCKED;
    }
}

EXTEND FORM items PROPERTIES () createSkuListIn, addToSkuListIn;

DESIGN items {
    actions {
        NEW selected {
            caption = 'Отмеченные';
            MOVE PROPERTY (createSkuListIn());
            MOVE PROPERTY (addToSkuListIn());
        }
    }
}

inCurrentBalance = DATA LOCAL BOOLEAN (Sku);

createSkuListSelect 'Создать список SKU' () {   

    inCurrentBalance(Sku s) <- TRUE IF GROUP SUM 1 IF select(s, Stock st);
    
    NEWSESSION NESTED (inCurrentBalance[Sku]) NEW l = List {
        FOR inCurrentBalance(Sku i) DO NEW d = ListDetail {
            sku(d) <- i;
            list(d) <- l;
        }
        SHOW list OBJECTS l = l DOCKED;
    }
}

addToSkuListSelect 'Добавить в список SKU' () {
    
    inCurrentBalance(Sku s) <- TRUE IF GROUP SUM 1 IF select(s, Stock st);
    
    NEWSESSION NESTED (inCurrentBalance[Sku]) DIALOG dialogLists OBJECTS tg INPUT DO {
        FOR inCurrentBalance(Sku i) AND NOT [GROUP SUM 1 BY sku(ListDetail d), list(d)](i, tg) DO NEW d = ListDetail {
            sku(d) <- i;
            list(d) <- tg;
        }
        SHOW list OBJECTS l = tg DOCKED;
    }
}

EXTEND FORM currentBalanceSkuStock PROPERTIES () addToSkuListSelect, createSkuListSelect;

DESIGN currentBalanceSkuStock {
    actionContainer {
        NEW selected {
            caption = 'Отмеченные';
            MOVE PROPERTY (createSkuListSelect());
            MOVE PROPERTY (addToSkuListSelect());
        }
    }
}

META defineAddSkuLists(object, form, concrete)
    addSkuLists 'Подбор списков SKU' (###object object) {
        DIALOG dialogAddLists DO {
            FOR in(list(ListDetail d)) AND NOT [GROUP SUM 1 BY sku(###object##Detail dd), object(dd)](sku(d), object) DO NEW dd = ###object##Detail {
                object(dd) <- object;
                sku(dd) <- sku(d);
            }
            in(List l) <- NULL;
        }
    }
    EXTEND FORM form PROPERTIES addSkuLists(concrete) DRAW d TOOLBAR;
    DESIGN form { TOOLBAR(d) { MOVE PROPERTY (addSkuLists(concrete)) FIRST; } }
END

META defineNewSkuList (object, form, concrete)
    newSkuList 'Создать список SKU на основе' (###object object) {
        NEWSESSION NEW l = List {
            name(l) <- seriesNumber(object);
            FOR [GROUP SUM 1 BY sku(###object##Detail detail), object(detail)](Sku sku, object) DO NEW detail = ListDetail {
                list(detail) <- l;
                sku(detail) <- sku;
            }
            SHOW list OBJECTS l = l DOCKED;
        }
    } CONFIRM;
    EXTEND FORM form PROPERTIES newSkuList(concrete) DRAW d TOOLBAR;
    DESIGN form { TOOLBAR(d) { MOVE PROPERTY (addSkuLists(concrete)) FIRST; } }
END

overCopy ABSTRACT LIST (List, List);

copy 'Копировать' (List l) {
    NEWSESSION NEW n = List {
        name(n) <- name(l);
        order(n) <- order(l);
        parent(n) <- parent(l);
        dataAccess(Stock st, n) <- dataAccess(st, l);
        dataAccess(StockGroup st, n) <- dataAccess(st, l);
        dataAccess(UserRole r, n) <- dataAccess(r, l);
        
        FOR list(ListDetail d) == l DO NEW nd = ListDetail {
            list(nd) <- n;
            sku(nd) <- sku(d);
        }
        overCopy(l, n);
        SHOW list OBJECTS l = n DOCKED;
    }
}

EXTEND FORM lists PROPERTIES copy(o) DRAW o TOOLBAR;

selectFromList 'Добавить из списка' (List l) {
    DIALOG dialogLists OBJECTS g INPUT DO {
        FOR list(ListDetail d) == g AND NOT listDetail(sku(d), l) DO NEW dd = ListDetail {
            list(dd) <- l;
            sku(dd) <- sku(d);
        }
    }
}

EXTEND FORM list PROPERTIES selectFromList(l) DRAW d TOOLBAR;