MODULE EmployeeSku;

REQUIRE Employee, Sku;

NAMESPACE Sku;

TABLE skuGroupEmployee(SkuGroup, Employee);

//--  Компания
dataAccess 'Доступ разрешен' = DATA BOOLEAN (SkuGroup, Employee);

overCopy(Employee e,Employee employee) + {     
    dataAccess(SkuGroup l,e) <- dataAccess(l,employee) WHERE dataAccess(l,employee);
};

levelParent (Employee employee, SkuGroup skuGroup) = GROUP
                                                               MIN level(skuGroup, SkuGroup parent) IF dataAccess(parent, employee) MATERIALIZED;
nearestParent (Employee employee, SkuGroup skuGroup) = skuGroup(skuGroup, levelParent(employee, skuGroup));
nearestIn (Employee employee, SkuGroup skuGroup) =
    dataAccess(nearestParent (employee, skuGroup), employee);

access 'Доступ разрешен' (Employee employee, SkuGroup skuGroup) = OVERRIDE
    dataAccess(skuGroup, employee),
    nearestIn(employee, skuGroup); // MATERIALIZED;

countAccess 'Кол-во доступных групп' (Employee employee, SkuGroup parent) =
    GROUP
          SUM 1 IF access(employee, SkuGroup child)
                AND isParent(child, parent) MATERIALIZED;

limitAccessSkuGroup 'Ограничение по группам SKU' = DATA BOOLEAN (Employee) COMPLEX;

migrateLimitAccessSkuGroup = DATA BOOLEAN ();
onStarted() +{ 
    IF NOT migrateLimitAccessSkuGroup() THEN {
        limitAccessSkuGroup(Employee e) <- TRUE IF (NOT (GROUP SUM 1 IF dataAccess(SkuGroup gr, e) AND NOT parent(gr))) AND (GROUP SUM 1 IF dataAccess(SkuGroup gr, e)); 
        migrateLimitAccessSkuGroup() <- TRUE;
        APPLY;
    }
}
EXTEND FORM employee
    TREE skuTree skg = SkuGroup PARENT parent(skg)
    PROPERTIES(e) limitAccessSkuGroup
    PROPERTIES READONLY SHOWIF  limitAccessSkuGroup(e) order(skg), skgTreeName = name(skg)
    ORDERS order(skg), skgTreeName
    PROPERTIES(e,skg) SHOWIF limitAccessSkuGroup(e) access
;
DESIGN employee {
    pane {
        NEW skuGroupAccess {
            caption = 'Группы SKU';
            MOVE PROPERTY(limitAccessSkuGroup(e));
            MOVE BOX(TREE skuTree);
        }
    }
}

// ----------------------------------------- Метакод по добавлению фильтров на форму ----------------------- //

META extendFormFilterSkuGroupAccess(form, concrete)
    EXTEND FORM form FILTERS (concrete IS SkuGroup AND NOT limitAccessSkuGroup(currentUser())) OR countAccess(currentUser(), concrete) OR NOT concrete IS SkuGroup;
END
