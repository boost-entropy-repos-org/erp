MODULE SkuLedgerLot;

REQUIRE LotLedger, SkuLedger, Lot;

NAMESPACE Stock;

quantity = ABSTRACT NUMERIC[16,5] (InSkuLedger, Lot);
quantity = ABSTRACT NUMERIC[16,5] (OutFIFOSkuLedger, Lot);

signedQuantity 'Кол-во' (SkuLedger ledger, Lot lot) = 
    MULTI quantity[InSkuLedger, Lot](ledger, lot),
         -quantity[OutFIFOSkuLedger, Lot](ledger, lot) CHARWIDTH 7 MATERIALIZED;

batch (Lot l) = GROUP LAST batch(SkuLedger sl) ORDER DESC dateTime(sl), sl WHERE signedQuantity(sl, l) AND batch(sl) MATERIALIZED INDEXED;
nameBatch 'Партия' (Lot l) = name(batch(l));

skuBalance = GROUP SUM signedQuantity(SkuLedger ledger, Lot lot) IF active(ledger) BY lot, stock(ledger) CHARWIDTH 7 MATERIALIZED;

currentBalance 'Текущий остаток' (Lot l, Stock st) =
    onHand(l, st) (+) skuBalance(l, st) CHARWIDTH 7 MATERIALIZED;
prevCurrentBalance (Lot l, Stock st) = PREV(currentBalance(l, st));

currentBalance 'Текущий остаток' (Lot l) = GROUP SUM currentBalance(l, Stock st) MATERIALIZED;

currentBalanceLot 'Остаток марок' (Sku sk, Stock st) =
    GROUP SUM currentBalance(Lot l, st) IF sku(l) = sk MATERIALIZED;
prevCurrentBalanceLot (Sku sk, Stock st) = PREV(currentBalanceLot(sk, st));

// constraint
@defineOption(checkCurrentBalanceLot, 'Проверять на превышение остатка по маркам', stock);
CONSTRAINT checkCurrentBalanceLot() AND SET(currentBalanceLot(Sku sk, Stock st) > (OVERRIDE currentBalance(sk, st), 0))
    MESSAGE 'Количество КМ на остатках превышает текущий остаток по товару';
    
EXTEND FORM currentBalanceSkuStock
    PROPERTIES (s, st) currentBalanceLot
    
    OBJECTS l = Lot
    PROPERTIES(l) READONLY id, nameBatch
    PROPERTIES(l, st) READONLY currentBalance
    FILTERS currentBalance(l, st), sku(l) = s
;

DESIGN currentBalanceSkuStock {
    ledgerBox {
        MOVE BOX(l);
    }
}

EXTEND FORM lots
    PROPERTIES(l) READONLY nameBatch, currentBalance

    OBJECTS sl = SkuLedger
    PROPERTIES(sl) READONLY date, dateTime, canonicalNameSkuGroup, nameSku, description, nameStock, nameBatch
    PROPERTIES(sl, l) READONLY signedQuantity
    PROPERTIES(sl) EDIT SHOWIF allowedEdit(sl) NEWSESSION, show SHOWIF allowedShow(sl)
    
    ORDERS date(sl) DESC
    FILTERS isPosted(sl),
            signedQuantity(sl, l)

    OBJECTS ll = LotLedger
    PROPERTIES(ll) READONLY dateTime, description, nameStock
    PROPERTIES(ll, l) READONLY quantity
    PROPERTIES(ll) EDIT
    
    ORDERS dateTime(ll) DESC
    FILTERS active(ll),
            quantity(ll, l)
;

DESIGN lots {
    tabbedPane {
        MOVE BOX(sl) { caption = 'Движения'; }
        MOVE BOX(ll) { caption = 'Операции'; }
    }
}