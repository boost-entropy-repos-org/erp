MODULE ImportInit;
REQUIRE PurchaseInvoice, PricingPurchase, PurchaseManufacturingPrice, PricingSum, PriceListLedgerAccount, LegalEntityBy;

NAMESPACE Purchase;

useUNP 'Идентификация поставщика по УНП' = DATA LOCAL NESTED BOOLEAN ();
inputOperation = DATA LOCAL NESTED Purchase.Operation();
nameInputOperation 'Операция' () = name(inputOperation());
newItem 'Создавать новый товар' = DATA LOCAL NESTED BOOLEAN ();
inputStock = DATA LOCAL NESTED Stock();
nameInputStock 'Склад' () = name(inputStock());
useConstraint 'Сохранять, даже если есть ограничения' = DATA LOCAL NESTED BOOLEAN ();

date 'Дата ТТН' = DATA LOCAL NESTED DATE (INTEGER);
num 'Номер ТТН' = DATA LOCAL NESTED VARSTRING[28](INTEGER);
series 'Серия ТТН' = DATA LOCAL NESTED VARSTRING[2](INTEGER);
seriesNumber 'Серия/номер ТТН'(INTEGER i) = CONCAT '', series(i), num(i);

idSupplier 'Код поставщика'= DATA LOCAL NESTED VARSTRING[100](INTEGER);
UNPSupplier 'УНП поставщика'= DATA LOCAL NESTED VARSTRING[100](INTEGER);

supplier (INTEGER i) = OVERRIDE legalEntityUNP(UNPSupplier(i)) IF useUNP(), legalEntity(idSupplier(i));
idSupplierStock 'Код склада поставщика'= DATA LOCAL NESTED VARSTRING[100](INTEGER);
supplierStock (INTEGER i) = stock(idSupplierStock(i));
idCustomerStock 'Код склада покупателя'= DATA LOCAL NESTED VARSTRING[100](INTEGER);
customerStock (INTEGER i) = OVERRIDE inputStock() IF supplier(i), stock(idCustomerStock(i));
idOperation 'Код операции'= DATA LOCAL NESTED VARSTRING[100](INTEGER);
operation (INTEGER i) = OVERRIDE inputOperation() IF supplier(i), Purchase.operation(idOperation(i));

idSku 'Код товара'= DATA LOCAL NESTED VARSTRING[100](INTEGER);
barcode 'Штрихкод товара'= DATA LOCAL NESTED VARSTRING[100](INTEGER);
skuMapping 'Код товара в существующей базе' = DATA LOCAL NESTED VARSTRING[100](INTEGER); // нужен для привязки дробных товаров
nameSku 'Наименование товара' = DATA LOCAL NESTED VARISTRING[255](INTEGER);
nameUOM 'Ед.изм.' = DATA LOCAL NESTED VARSTRING[255](INTEGER);
UOM (INTEGER i) = OVERRIDE  UOMName(nameUOM(i)), UOM(nameUOM(i));
idSkuGroup 'Код группы' = DATA LOCAL NESTED VARSTRING[100](INTEGER);
splitSku 'Дробный (0/1)'  = DATA LOCAL NESTED VARSTRING[100](INTEGER);
sku (INTEGER i) = OVERRIDE sku(skuMapping(i)), sku(idSku(i)), skuBarcode(barcode(i)) IF NOT splitSku(i) == '1', sku(barcode(i));
item (INTEGER i) = OVERRIDE idSku(i), barcode(i);

part 'Код партии'=  DATA LOCAL NESTED VARSTRING[100](INTEGER );
indexBatch(INTEGER i) =PARTITION SUM 1 ORDER idCustomerStock(i), i BY part(i);
quantity 'Количество'= DATA LOCAL NESTED NUMERIC[16,5](INTEGER);
price 'Цена'= DATA LOCAL NESTED NUMERIC[14,4](INTEGER);
manPrice 'Цена изготовитля'= DATA LOCAL NESTED NUMERIC[16,4](INTEGER);
vat 'Ставка НДС поставщика'= DATA LOCAL NESTED NUMERIC[8,2](INTEGER);
vatr 'Ставка розничного НДС'= DATA LOCAL NESTED NUMERIC[8,2](INTEGER);
shipPrice 'Цена учетная розничная ' = DATA LOCAL NESTED NUMERIC[16,4](INTEGER);
shipSum 'Учетная сумма' = DATA LOCAL NESTED NUMERIC[18,4](INTEGER);
expiry 'Годен до (дата)'= DATA LOCAL NESTED DATE (INTEGER);

FORM exportInit 'Экспорт'
    OBJECTS st = Stock PANEL 
    
    OBJECTS bt= Batch
    PROPERTIES  date = date(invoiceDetail(bt)),
                num = number(invoiceDetail(bt)),
                sries= series(invoiceDetail(bt)),
                UNPSupplier = UNP(supplier(invoiceDetail(bt))),   
                idSupplier = id(supplier(invoiceDetail(bt))),
                idSupplierStock = id(supplierStock(invoiceDetail(bt))),
                idCustomerStock = (id(st) IF bt IS Batch),
                operation = (VARSTRING[10]('') IF bt IS Batch),
                idSku = id(sku(bt)),
                barcode = barcode(sku(bt)), 
                nameSku = name(sku(bt)),
                nameUOM = name(UOM(sku(bt))),
                idSkuGroup = id(skuGroup(sku(bt))),
                splitSku = (IF (passScales(sku(bt)) OR split(sku(bt)))  THEN '1' ELSE '0') , 
                part = id(bt),
                quantity = currentBalance(bt,st),
                price = priceA(SystemLedgerPriceListType.supplierPricingPriceListType, sku(bt), st,currentDateTime()),
                manPrice = priceA(SystemLedgerPriceListType.manufacturingPriceStockPriceListType, sku(bt), st,currentDateTime()),
                vat = valueVAT(sku(bt)),
                vatr= valueVAT(sku(bt)),
                shipPrice = priceA(SystemLedgerPriceListType.accountPriceListType, sku(bt), st,currentDateTime()),
                shipSum = sumB(bt,st,currentDateTime()),
                expiry = expiryDate(bt),
                skuMapping = (VARSTRING[10]('') IF bt IS Batch)                       
                
    FILTERS currentBalance(bt,st)
    
;
exportInit 'Экспорт остатков' (Stock st){
    PRINT exportInit OBJECTS st = st XLSX ;
}

resetLoc () {
    date(INTEGER int) <- NULL;
    num (INTEGER int) <- NULL;
    series (INTEGER int) <- NULL;
    idSupplier (INTEGER int) <- NULL;
    idSupplierStock (INTEGER int) <- NULL;
    idCustomerStock (INTEGER int) <- NULL;
    idOperation(INTEGER int) <- NULL;
        
    idSku (INTEGER int) <- NULL;
    barcode (INTEGER int) <- NULL;
    nameSku (INTEGER int) <- NULL;
    nameUOM  (INTEGER int) <- NULL;
    idSkuGroup  (INTEGER int) <- NULL;
    splitSku  (INTEGER int) <- NULL;
    part (INTEGER int) <- NULL;
    quantity (INTEGER int) <- NULL;
    price (INTEGER int) <- NULL;
    manPrice (INTEGER int) <- NULL;
    vat (INTEGER int) <- NULL;
    vatr (INTEGER int) <- NULL;
    shipPrice (INTEGER int) <- NULL;  
    shipSum (INTEGER int) <- NULL;         
    expiry (INTEGER int) <- NULL;    
    UNPSupplier(INTEGER int) <- NULL;    
    skuMapping(INTEGER int) <- NULL;    

}

importInitCustom 'Импорт остатков' ()  {
    idCustomerStock(INTEGER r) <- id(customerStock(r)) WHERE supplier(r); 
    idSupplier(INTEGER r) <- id(supplier(r)) WHERE supplier(r); 

    LOCAL incl =  BOOLEAN (Purchase.UserInvoiceDetail);
                   
    //встречаются очень древние партии по которым потерялся товар
    part(INTEGER r) <- NULL WHERE quantity(r) == 0.0 AND NOT sku(r);
    
    IF newItem() THEN {
        FOR INTEGER  rr = [ GROUP MIN INTEGER r IF supplier(r) AND NOT itemGroup(idSkuGroup(r)) BY idSkuGroup(r)](VARSTRING[100] str)  NEW it = ItemGroup DO {
            id(it) <- str;
            parent(it) <- GROUP MIN ItemGroup gr IF gr IS ItemGroup AND NOT parent(gr);
        }
        FOR INTEGER  rr = [  GROUP MIN INTEGER r IF supplier(r)  AND NOT UOM(r)  BY nameUOM(r)](VARSTRING[100] str)  NEW it = UOM DO {
            id(it) <- str;
            shortName(it) <- str;
            name(it) <- VARISTRING[50](str);
        }
                
        FOR INTEGER  rr = [GROUP MIN INTEGER r IF supplier(r) AND NOT sku(r) BY idSku(r)](VARSTRING[100] str)  NEW it = Item DO {
            id(it) <- str;
            caption(it) <- nameSku(rr);
            itemGroup(it) <- itemGroup(idSkuGroup(rr));
            UOM(it) <- UOM(rr);
            split(it) <- TRUE IF splitSku(rr) == '1';
            passScales(it) <- TRUE IF splitSku(rr) == '1';
            skuType(it) <- SkuType.skuTypeItem;
            
            IF barcode(rr) THEN {
                IF splitSku(rr) == '1' THEN {
                    generateBarcode(it);
                } ELSE {
                    NEW b = Barcode {
                        id(b) <- VARSTRING[15](barcode(rr));
                        sku(b) <- it;
                    }                 
                }               
            }
        }    
    }    
    
    //шапки
    FOR [GROUP MIN INTEGER r IF supplier(r) AND sku(r) BY idCustomerStock(r), idSupplier(r), seriesNumber(r)](VARISTRING[100] idCustomerStock, VARISTRING[100] idSupplier, VARISTRING[100] num) 
        AND NOT Purchase.userInvoice(CONCAT '_', VARSTRING[100](idCustomerStock), VARSTRING[100](idSupplier), VARSTRING[100](num)) 
        NEW i = Purchase.UserInvoice DO {
        
        id(i) <- CONCAT '_', VARSTRING[100](idCustomerStock), VARSTRING[100](idSupplier), VARSTRING[100](num);
    }
    FOR INTEGER r == [GROUP MIN INTEGER ir IF supplier(ir) AND  sku(ir) BY idCustomerStock(ir), idSupplier(ir), seriesNumber(ir)](VARISTRING[100] idCustomerStock, VARISTRING[100] idSupplier, VARISTRING[100] num) 
        AND Purchase.UserInvoice i == Purchase.userInvoice(CONCAT '_', VARSTRING[100](idCustomerStock), VARSTRING[100](idSupplier), VARSTRING[100](num)) DO {
        
        supplier(i) <- legalEntity(idSupplier(r));
        supplierStock(i) <- stock(idSupplierStock(r)) WHERE stock(idSupplierStock(r)) AND legalEntity(idSupplier(r)) == legalEntity(stock(idSupplierStock(r)));
        customer(i) <- legalEntity(stock(idCustomerStock(r)));
        customerStock(i) <- stock(idCustomerStock(r));
        number(i) <- num(r);
        series(i) <- series(r);
        date(i) <- date(r);
        isPosted(i) <- TRUE;
        operation(i) <- operation(r);
        createShipment(i) <- createShipment(operation(r)); 
        createPricing(i) <- createPricing(operation(r)); 
        isClosed(i) <- TRUE;
    }
    
    //строки
    FOR sku(INTEGER r) AND NOT Purchase.userInvoiceDetail(CONCAT '_', idCustomerStock(r), idSupplier(r), seriesNumber(r), item(r), part(r)) NEW d = Purchase.UserInvoiceDetail DO {
        id(d) <- CONCAT '_', idCustomerStock(r), idSupplier(r), seriesNumber(r), item(r), part(r);
    }
        
    FOR Purchase.UserInvoiceDetail d == Purchase.userInvoiceDetail(CONCAT '_', idCustomerStock(INTEGER r), idSupplier(r), seriesNumber(r), item(r), part(r)) INLINE DO {
        userInvoice(d) <- Purchase.userInvoice(CONCAT '_', idCustomerStock(r), idSupplier(r), seriesNumber(r));
        sku(d) <- sku(r);
        quantity(d) <- quantity(r);
        shipmentQuantity(d) <- quantity(r);
        VAT(d) <- valueCurrentVAT(defaultCountry(), vat(r)); 
        valueVAT(d) <- vat(r);
        valueRetailVAT(d) <- vatr(r);
        price(d) <- price(r);
        manufacturingPrice(d) <- manPrice(r);
             
        retailPrice(d) <- shipPrice(r) IF customerStock(r) IS DepartmentStore;
        
        shipmentPrice(d) <-CASE 
                            WHEN shipPrice(r) THEN shipPrice(r) 
                            WHEN customerStock(r) IS DepartmentStore THEN retailPrice(d)
                            ELSE price(r);
        shipmentSum(d) <- shipSum(r);
        idBatch(d) <- part(r) IF indexBatch(r) == 1;
        expiryDate(d) <- expiry(r);
        incl(d) <- TRUE;
    }
    FOR Purchase.UserInvoiceDetail d == Purchase.userInvoiceDetail(CONCAT '_', idCustomerStock(INTEGER r), idSupplier(r), seriesNumber(r), item(r), part(r)) AND indexBatch(r) > 1 DO {
        batch(d) <- batch(part(r));
    }
    FOR [GROUP SUM 1 IF imported(INTEGER in)  BY operation(in),customerStock(in)](Purchase.Operation o, Stock st) DO {
        quantity(Purchase.UserInvoiceDetail d ) <- 0.0 WHERE o==operation(d) AND st == customerStock(d) AND NOT incl(d);
        shipmentQuantity(Purchase.UserInvoiceDetail d ) <- 0.0 WHERE o==operation(d) AND st == customerStock(d) AND NOT incl(d);
        shipmentSum(Purchase.UserInvoiceDetail d ) <- 0.0 WHERE o==operation(d) AND st == customerStock(d) AND NOT incl(d);     
    } 
        
    LOCAL NESTED closeDate = DATE (Stock);
    closeDate (Stock st) <- documentsClosedDate(st) WHERE (GROUP SUM 1 IF sku(INTEGER in) AND customerStock(in) == st);
    documentsClosedDate(Stock st) <- NULL WHERE closeDate(st);
    disableDocumentLog() <- TRUE;
    IF useConstraint() THEN {
        setNoCancelInTransaction();             
    }     
    APPLY;
    
    documentsClosedDate(Stock st) <- closeDate(st) WHERE closeDate(st);
    disableDocumentLog() <- NULL;	    
    APPLY;    
} 

importInit 'Импорт остатков' ()  {
    NEWSESSION {
        resetLoc ();
        INPUT f = EXCELFILE DO {
            
            IMPORT XLS FROM f AS EXCELFILE TO  date,num,series,UNPSupplier, idSupplier,idSupplierStock,idCustomerStock,
                   idOperation,idSku,barcode,nameSku,nameUOM,idSkuGroup,splitSku,part,quantity,price,manPrice,vat,vatr,
                   shipPrice,shipSum,expiry,skuMapping;
                   
            importInitCustom();
        }     
    }   
}
FORM importInitXLSX 'Шаблон импорта остатков'
    OBJECTS i = INTEGER
    PROPERTIES (i) date,num,series,UNPSupplier, idSupplier,idSupplierStock,idCustomerStock,
                  idOperation,idSku,barcode,nameSku,nameUOM,idSkuGroup,splitSku,part,quantity,price,manPrice,vat,vatr,
                  shipPrice,shipSum,expiry,skuMapping
    FILTERS count(i,10)
;

importInitXLSX 'Шаблон импорта' ()  {
    PRINT importInitXLSX XLSX;
}

EXTEND FORM integrationData
    OBJECTS ww = Stock PANEL 
    PROPERTIES (ww) SELECTOR name
    FILTERS isCompany(ww)
    PROPERTIES exportInit(ww)

    PROPERTIES () useUNP, nameInputStock, nameInputOperation, newItem, importInit, importInitXLSX, useConstraint
;
DESIGN integrationData {
    pane {
        NEW integrationBalance {
            caption = 'Начальные остатки';
            NEW balancePrim{
                caption = 'Шаблон';
                type = CONTAINERH;
                MOVE PROPERTY (importInitXLSX());            
            }            
            NEW balanceExport{
                caption = 'Экспорт';
                type = CONTAINERH;
                MOVE PROPERTY (name(ww)){caption = 'Склад';}
                MOVE PROPERTY (exportInit(ww));              
            }
            NEW balanceImport{
                caption = 'Импорт';
                type = CONTAINERH;
                MOVE PROPERTY (useUNP());  
                MOVE PROPERTY (nameInputOperation());  
                MOVE PROPERTY (nameInputStock());  
                MOVE PROPERTY (newItem());
                MOVE PROPERTY (useConstraint());                 
                MOVE PROPERTY (importInit());            
            }            
        }    
    }
}