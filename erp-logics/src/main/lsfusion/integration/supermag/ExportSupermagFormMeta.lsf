MODULE ExportSupermagFormMeta;

REQUIRE ExportSupermag, StockSkuDocument;

//GROUP SMSPEC###typeDoc EXTID 'SMSPEC': ###typeDoc;
doctype = DATA LOCAL STRING[10] (Document);
opcode = DATA LOCAL STRING[100] (Document);
userop = DATA LOCAL STRING[100] (Document);
clientIndex = DATA LOCAL STRING[100] (Document);
location = DATA Stock (Document);
locationFrom = DATA Stock (Document);
locationTo = DATA Stock (Document);
ourSelfClient = DATA LOCAL STRING[100] (Document);

curRate(Document i) = (OVERRIDE rateOn(typeExchange('НБРБ (BYR)'),currencyShortName('USD'),date(i)),(1.0 IF i IS Document));

//различные индексы сделаны для SPECITEM т.к. из одного документа с + и - мы делаем 2 в супермаге
indexPlus (DocumentDetail ad) = PARTITION SUM 1 IF quantity(ad)>0 ORDER ad BY document(ad) CHARWIDTH 4;
indexMinus (DocumentDetail ad) = PARTITION SUM 1 IF quantity(ad)<0 ORDER ad BY document(ad) CHARWIDTH 4;
index (DocumentDetail ad) = PARTITION SUM 1 ORDER ad BY document(ad) CHARWIDTH 4;

priceWithTax = DATA LOCAL NUMERIC[18,4] (DocumentDetail);
priceNoTax = DATA LOCAL NUMERIC[18,4] (DocumentDetail);
valueTax = DATA LOCAL NUMERIC[10,5] (DocumentDetail);
sumTax = DATA LOCAL NUMERIC[18,4] (DocumentDetail);
sumNoTax = DATA LOCAL NUMERIC[18,4] (DocumentDetail);
sumWithTax = DATA LOCAL NUMERIC[18,4] (DocumentDetail);
sumWithTaxCurr = DATA LOCAL NUMERIC[18,4] (DocumentDetail);
docId = DATA LOCAL STRING[100] (Document);
docId (DocumentDetail dd) = docId(document(dd)); 
sumNoTax = DATA LOCAL NUMERIC[18,4] (Document);
sumWithTax = DATA LOCAL NUMERIC[18,4] (Document);
sumWithTaxCurr = DATA LOCAL NUMERIC[18,4] (Document);

META exportFormDoc (typeDoc, formName, doc, docDetail, description, commentary) 
GROUP ###typeDoc;
GROUP SMDOCUMENTS###typeDoc EXTID 'SMDOCUMENTS': ###typeDoc;
GROUP SMSPEC###typeDoc EXTID 'SMSPEC': ###typeDoc;

FORM formName FORMEXTID 'PACKAGE' 
    PROPERTIES ATTR = exportTime(currentDateTime())  EXTID 'name'

    OBJECTS i = ###doc EXTID 'POSTOBJECT'
    PROPERTIES ATTR = 'normal' IF i IS ###doc EXTID 'action'      
    PROPERTIES ATTR = ###description IF i IS ###doc EXTID 'description'  
    
    PROPERTIES = STRING[100](doctype(i)+docId(i)) EXTID 'Id'
    PROPERTIES = STRING[100](docId(i)) EXTID 'ID' IN SMDOCUMENTS###typeDoc
    PROPERTIES = doctype(i) IF i IS ###doc EXTID 'DOCTYPE' IN SMDOCUMENTS###typeDoc
    PROPERTIES = opcode(i) IF i IS ###doc EXTID 'OPCODE' IN SMDOCUMENTS###typeDoc
    PROPERTIES = idSupermag(location(i)) EXTID 'BORNIN' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = exportTime(dateTime(i))  EXTID 'CREATEDAT' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = 0 IF i IS ###doc EXTID 'CURRENCYMULTORDER' IN SMDOCUMENTS###typeDoc      
    PROPERTIES = curRate(i) EXTID 'CURRENCYRATE' IN SMDOCUMENTS###typeDoc 
    PROPERTIES = 1 IF i IS ###doc EXTID 'CURRENCYTYPE' IN SMDOCUMENTS###typeDoc        
    PROPERTIES = IF isPosted(i) THEN 3 ELSE 2 EXTID 'DOCSTATE' IN SMDOCUMENTS###typeDoc           
    PROPERTIES = 1 IF i IS ###doc  EXTID 'ISROUBLES' IN SMDOCUMENTS###typeDoc      
    PROPERTIES = 4 IF i IS ###doc  EXTID 'PRICEROUNDMODE' IN SMDOCUMENTS###typeDoc       
    PROPERTIES = sumWithTax(i) IF i IS ###doc EXTID 'TOTALSUM' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = sumWithTaxCurr(i) IF i IS ###doc EXTID 'TOTALSUMCUR' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = ###commentary IF i IS ###doc EXTID 'COMMENTARY' IN SMDOCUMENTS###typeDoc     
                                                            
    OBJECTS d = ###docDetail EXTID 'SMSPEC' IN ###typeDoc 
    PROPERTIES = docId(d) EXTID 'DOCID'    
    PROPERTIES = doctype(doc(d)) IF d IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = idSku(d) EXTID 'ARTICLE'                          
    PROPERTIES d1= index(d) EXTID 'DISPLAYITEM'
    PROPERTIES = abs(quantity(d)) EXTID 'QUANTITY'
    PROPERTIES = priceWithTax(d) EXTID 'ITEMPRICE'     
    PROPERTIES d2 = NUMERIC[18,4](price(d)/curRate(i)) EXTID 'ITEMPRICECUR'                         
    FILTERS ##doc(d)==i,
            inc(i)  
    ;                                                
END

META exportFormWI (formName, doc, docDetail, description, commentary)   
@exportFormDoc(WI,formName, doc, docDetail, description, commentary);

GROUP SMWAYBILLSIN EXTID 'SMWAYBILLSIN' : WI;
GROUP SMSPECIOWI EXTID 'SMWSPECIO' : WI;

EXTEND FORM formName
    PROPERTIES = userop(i) IF i IS ###doc EXTID 'USEROP' IN SMDOCUMENTSWI        
    PROPERTIES = id(locationTo(i)) EXTID 'LOCATIONTO' IN SMDOCUMENTSWI
    
    PROPERTIES = STRING[100](docId(i)) EXTID 'ID' IN SMWAYBILLSIN      
    PROPERTIES = 'WI' IF i IS ###doc EXTID 'DOCTYPE' IN SMWAYBILLSIN 
    PROPERTIES = 0 IF i IS ###doc EXTID 'GOODSOWNER' IN SMWAYBILLSIN                        
    PROPERTIES = id(legalEntity(locationTo(i))) EXTID 'OURSELFCLIENT' IN SMWAYBILLSIN    
    PROPERTIES = 0 IF i IS ###doc EXTID 'PAYCASH' IN SMWAYBILLSIN      

    PROPERTIES = priceNoTax(d) IF d IS ###docDetail EXTID 'ITEMPRICENOTAX'  
    PROPERTIES = sumWithTax(d) IF d IS ###docDetail EXTID 'TOTALPRICE'  
    PROPERTIES = sumWithTaxCurr(d) IF d IS ###docDetail EXTID 'TOTALPRICECUR'      
    PROPERTIES = sumNoTax(d) IF d IS ###docDetail EXTID 'TOTALPRICENOTAX'            
    
    OBJECTS d2 = ###docDetail EXTID 'SMSPECIO' IN WI 
    PROPERTIES = docId(d2) EXTID 'DOCID'   
    PROPERTIES = 'WI' IF d2 IS ###docDetail EXTID 'DOCTYPE'     
    FILTERS ##doc(d2)==i    
    
    OBJECTS d3 = ###docDetail EXTID 'SMSPECTAX' IN WI
    PROPERTIES = docId(d3) EXTID 'DOCID'    
    PROPERTIES = 'WI' IF d3 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = 0 IF d3 IS ###docDetail  EXTID 'TAXID'        
    PROPERTIES = valueTax(d3) EXTID 'TAXRATE'
    PROPERTIES = sumTax(d3) EXTID 'TAXSUM'            
    FILTERS ##doc(d3)==i   
    ;
END

META exportFormWO (formName, doc, docDetail, description, commentary)   
@exportFormDoc(WO,formName, doc, docDetail, description, commentary);

GROUP SMWAYBILLSOUT EXTID 'SMWAYBILLSOUT' : WO;
GROUP SMSPECIOWO EXTID 'SMWSPECIO' : WO;

EXTEND FORM formName
    PROPERTIES = userop(i) IF i IS ###doc EXTID 'USEROP' IN SMDOCUMENTSWO        
    PROPERTIES = id(locationFrom(i)) EXTID 'LOCATIONFROM' IN SMDOCUMENTSWO
    
    PROPERTIES = STRING[100](docId(i))  EXTID 'ID' IN SMWAYBILLSOUT      
    PROPERTIES = 'WO' IF i IS ###doc EXTID 'DOCTYPE' IN SMWAYBILLSOUT
    PROPERTIES = id(legalEntity(locationFrom(i))) EXTID 'OURSELFCLIENT' IN SMWAYBILLSOUT
    PROPERTIES = STRING[100](docId(i))  EXTID 'INVOICE' IN SMWAYBILLSOUT             
    PROPERTIES = 0 IF i IS ###doc EXTID 'PAYCASH' IN SMWAYBILLSOUT      
    
    PROPERTIES = priceNoTax(d) IF d IS ###docDetail EXTID 'ITEMPRICENOTAX'  
    PROPERTIES = sumWithTax(d) IF d IS ###docDetail EXTID 'TOTALPRICE'  
    PROPERTIES = sumWithTaxCurr(d) IF d IS ###docDetail EXTID 'TOTALPRICECUR'      
    PROPERTIES = sumNoTax(d) IF d IS ###docDetail EXTID 'TOTALPRICENOTAX'            
                
    OBJECTS d2 = ###docDetail EXTID 'SMSPECIO' IN WO 
    PROPERTIES = docId(d2) EXTID 'DOCID'   
    PROPERTIES = doctype(doc(d2)) IF d2 IS ###docDetail EXTID 'DOCTYPE'     
    FILTERS ##doc(d2)==i
        
    OBJECTS d3 = ###docDetail EXTID 'SMSPECTAX' IN WO
    PROPERTIES = docId(d3) EXTID 'DOCID'    
    PROPERTIES = 'WO' IF d3 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = 0 IF d3 IS ###docDetail  EXTID 'TAXID'        
    PROPERTIES = valueTax(d3) EXTID 'TAXRATE'
    PROPERTIES = sumTax(d3) EXTID 'TAXSUM'            
    FILTERS ##doc(d3)==i   
    ;
END

META exportFormLA (formName, doc, docDetail, description, commentary)   
@exportFormDoc(LA,formName, doc, docDetail, description, commentary);

EXTEND FORM formName
    PROPERTIES = id(location(i)) EXTID 'LOCATION' IN SMDOCUMENTSLA
    ;
END

META exportFormFA (formName, doc, docDetail, description, commentary)   
@exportFormDoc(FA,formName, doc, docDetail, description, commentary);

EXTEND FORM formName
    PROPERTIES = id(location(i)) EXTID 'LOCATION' IN SMDOCUMENTSFA
    ;
END 