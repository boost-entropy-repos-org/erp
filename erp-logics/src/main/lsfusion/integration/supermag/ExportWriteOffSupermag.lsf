MODULE ExportWriteOffSupermag;
REQUIRE PricingWriteOff, ExportSupermag;

NAMESPACE WriteOff;

EXTEND FORM operation
    PROPERTIES (o) docTypeSupermag, opcodeSupermag, useropSupermag
;
DESIGN operation{
    propertyContainer {
        MOVE PROPERTY(docTypeSupermag(o));
        MOVE PROPERTY(opcodeSupermag(o));
        MOVE PROPERTY(useropSupermag(o));
    }
}

EXTEND FORM operations
    PROPERTIES (o) READONLY docTypeSupermag, opcodeSupermag, useropSupermag
;

//---------------------- Оптовая торговля (Код операции 1)
GROUP WO;
GROUP SMDOCUMENTS : WO;
GROUP SMWAYBILLSOUT : WO;
GROUP SMDOCPROPS : WO;

inc = DATA LOCAL BOOLEAN (WriteOff);
curRate(WriteOff.WriteOff i) = (OVERRIDE rateOn(typeExchange('НБРБ (BYR)'),currencyShortName('USD'),date(i)),(1.0 IF i IS WriteOff.WriteOff));
curRate(WriteOff.WriteOffDetail d) = curRate(WriteOff.writeOff(d));

dateTimeChange = DATA DATETIME (WriteOff);
dateTimeChange[StockDocumentLedger](WriteOff i) += dateTimeChange(i);

changeDoc(WriteOff d) = CHANGED(seriesNumber(d))OR CHANGED(stock(d))  
    OR CHANGED(dateTime(d)) OR CHANGED(operation(d)) OR CHANGED (sumWriteOffDetail(d)) OR CHANGED(curRate(d)) OR CHANGED(isPosted(d));

changeDoc(WriteOffDetail d) = CHANGED(quantity( d)) OR CHANGED(sku(d)) OR CHANGED(price(d)) OR CHANGED(sum(d)) 
    OR CHANGED(retailPrice(d)) OR CHANGED(supplierPrice(d));
changeDocDetail(WriteOff i) = GROUP SUM 1 IF changeDoc(WriteOffDetail d) AND writeOff(d)==i;

WHEN SET(changeDoc(WriteOff i)) OR SET (changeDocDetail(i)) DO {
    dateTimeChange(i)<- currentDateTime();  
}

FORM exportWriteOffMag FORMEXTID 'PACKAGE'  
    PROPERTIES ATTR = exportTime(currentDateTime())  EXTID 'name'

    OBJECTS i = WriteOff EXTID 'POSTOBJECT'
    PROPERTIES ATTR = 'Расходная накладная' IF i IS WriteOff.WriteOff EXTID 'description'  
    PROPERTIES ATTR = 'normal' IF i IS WriteOff.WriteOff EXTID 'action'      
    
    PROPERTIES = STRING[100]('WO'+seriesNumber(i)) EXTID 'Id'
    PROPERTIES = STRING[100](STRING[100](seriesNumber(i))) EXTID 'ID' IN SMDOCUMENTS
    PROPERTIES = 'WO' IF i IS WriteOff.WriteOff EXTID 'DOCTYPE' IN SMDOCUMENTS    
    PROPERTIES = idSupermag(stock(i)) EXTID 'BORNIN' IN SMDOCUMENTS    
    PROPERTIES = id(legalEntity(stock(i))) EXTID 'CLIENTINDEX' IN SMDOCUMENTS  
    PROPERTIES = note(i) EXTID 'COMMENTARY' IN SMDOCUMENTS  
    PROPERTIES = exportTime(dateTime(i))  EXTID 'CREATEDAT' IN SMDOCUMENTS
    PROPERTIES = 0 IF i IS WriteOff.WriteOff EXTID 'CURRENCYMULTORDER' IN SMDOCUMENTS  
    PROPERTIES = curRate(i) EXTID 'CURRENCYRATE' IN SMDOCUMENTS 
    PROPERTIES = 1 IF i IS WriteOff.WriteOff EXTID 'CURRENCYTYPE' IN SMDOCUMENTS    
    PROPERTIES = IF isPosted(i) THEN 3 ELSE 2 EXTID 'DOCSTATE' IN SMDOCUMENTS       
    PROPERTIES = 1 IF i IS WriteOff.WriteOff  EXTID 'ISROUBLES' IN SMDOCUMENTS  
    PROPERTIES = id(stock(i)) EXTID 'LOCATIONFROM' IN SMDOCUMENTS  
    PROPERTIES = opcodeSupermag(operation(i))  EXTID 'OPCODE' IN SMDOCUMENTS      
    PROPERTIES = 4 IF i IS WriteOff.WriteOff  EXTID 'PRICEROUNDMODE' IN SMDOCUMENTS     
    PROPERTIES = sumWriteOffDetail(i) EXTID 'TOTALSUM' IN SMDOCUMENTS 
    PROPERTIES i1 = NUMERIC[18,4](sumWriteOffDetail(i)/curRate(i)) EXTID 'TOTALSUMCUR' IN SMDOCUMENTS                
       
                           
    OBJECTS d = WriteOff.WriteOffDetail   EXTID 'SMSPEC' IN WO
    PROPERTIES = STRING[100](seriesNumber(d)) EXTID 'DOCID'    
    PROPERTIES = 'WO' IF d IS WriteOff.WriteOffDetail EXTID 'DOCTYPE'      
    PROPERTIES = index(d) EXTID 'SPECITEM'        
    PROPERTIES = idSku(d) EXTID 'ARTICLE'                          
    PROPERTIES d1= index(d) EXTID 'DISPLAYITEM'  
    PROPERTIES = price(d) EXTID 'ITEMPRICE'     
    PROPERTIES d2 = NUMERIC[18,4](price(d)/curRate(d)) EXTID 'ITEMPRICECUR'                
    PROPERTIES d4 = price(d) EXTID 'ITEMPRICENOTAX'   
    PROPERTIES = quantity(d) EXTID 'QUANTITY'  
    PROPERTIES = sum(d) EXTID 'TOTALPRICE'   
    PROPERTIES d3= NUMERIC[18,4](sum(d)/curRate(d)) EXTID 'TOTALPRICECUR'                
    PROPERTIES d5= sum(d) EXTID 'TOTALPRICENOTAX'         
    FILTERS writeOff(d)==i,
            inc(i)                                         
                                                                          
    //-- сама накладная
    PROPERTIES = STRING[100](seriesNumber(i)) EXTID 'ID' IN SMWAYBILLSOUT      
    PROPERTIES = 'WO' IF i IS WriteOff.WriteOff EXTID 'DOCTYPE' IN SMWAYBILLSOUT 
    PROPERTIES = (CONCAT ' ',number(i),series(i)) EXTID 'INVOICE' IN SMWAYBILLSOUT                               
    PROPERTIES = id(legalEntity(stock(i))) EXTID 'OURSELFCLIENT' IN SMWAYBILLSOUT        
    PROPERTIES = 0 IF i IS WriteOff.WriteOff EXTID 'PAYCASH' IN SMWAYBILLSOUT     
 
    OBJECTS dd = WriteOff.WriteOffDetail   EXTID 'SMSPECTAX' IN WO
    PROPERTIES = STRING[100](seriesNumber(dd)) EXTID 'DOCID'    
    PROPERTIES = 'WO' IF dd IS WriteOff.WriteOffDetail EXTID 'DOCTYPE'      
    PROPERTIES = index(dd) EXTID 'SPECITEM'        
    PROPERTIES = 0 IF dd IS WriteOff.WriteOffDetail  EXTID 'TAXID'        
    PROPERTIES = 0 IF dd IS WriteOff.WriteOffDetail EXTID 'TAXRATE'
    PROPERTIES = 0 IF dd IS WriteOff.WriteOffDetail EXTID 'TAXSUM'            
    FILTERS writeOff(dd)==i   
     
    OBJECTS a = WriteOff.WriteOffDetail   EXTID 'SMSPECBY' IN WO
    PROPERTIES = STRING[100](seriesNumber(a)) EXTID 'DOCID'    
    PROPERTIES = 'WO' IF a IS WriteOff.WriteOffDetail EXTID 'DOCTYPE'      
    PROPERTIES = index(a) EXTID 'SPECITEM'        
    PROPERTIES = 0 IF a IS WriteOff.WriteOffDetail EXTID 'EXTRACHARGE'  
    PROPERTIES = (OVERRIDE supplierPrice(a), price(a)) EXTID 'MANUFACTURERSPRICE'     
    PROPERTIES = 0 IF a IS WriteOff.WriteOffDetail EXTID 'STATEREGULATION'                           
    FILTERS writeOff(a)==i    
; 

exportWriteOffMag 'Экспорт в супермаг' (WriteOff i) {
    inc(WriteOff ii) <- NULL;
    inc(i) <- TRUE WHERE opcodeSupermag(operation(i)) AND idSupermag(stock(i));    
    LOCAL NESTED dt = DATETIME();
    dt () <- currentDateTime();
    IF (GROUP SUM 1 IF inc(WriteOff ii)) THEN{    
        TRY{
            EXPORT exportWriteOffMag XML TO System.exportFile;
            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/' + 'WO_' +STRING[30](LONG(i))+'_'+(OVERRIDE seriesNumber(i), '')+'_'+ formulaDateT(dt());  
            fileExists('file://' + exportDirectorySupermag() + '/' + 'WO_' +STRING[30](LONG(i))+'_'+(OVERRIDE seriesNumber(i), '')+'_'+ formulaDateT(dt()) + '.xml'); 
            IF fileExists()  THEN NEWSESSION NESTED LOCAL {
                FOR inc(WriteOff ii)  DO NEW log = SupermagLog {
                    id(log)<- 'WO_' +STRING[30](LONG(ii))+'_'+(OVERRIDE seriesNumber(ii), '')+'_'+ formulaDateT(dt());  
                    dateTime(log) <- currentDateTime();
                    userLogin(log) <- login(currentUser()); 
                    nameContact(log) <- STRING[100](name(currentUser()));
                    stockDocumentLedger(log)<- ii;
                }     
                APPLY NESTED LOCAL;
            }             
                 
        } CATCH {
            logToFile('supermag', CONCAT '\n', 'Расходная накладная (списание)', 'file://' + exportDirectorySupermag() + '/' + 'WO_' +STRING[30](LONG(i))+'_'+(OVERRIDE seriesNumber(i), '')+'_'+ formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
            MESSAGE  'Расходная накладная (списание). Ошибка при экспорте WO_' +STRING[30](LONG(i))+'_'+(OVERRIDE seriesNumber(i), '')+'_'+ formulaDateT(dt()) +'. Обратитесь к администратору' NOWAIT;        
        } 
    }   
    inc(WriteOff ii) <- NULL;
}
exportSupermag(WriteOff s) +{
    exportWriteOffMag(s);
}

exportSupermagAll() +{
    FOR date(WriteOff s AS WriteOff) > sum(currentDate(),-countDaysExportData()) AND dateTimeChange(s) AND NOT (dateTimeChange(s) <lastDateTimeSupermagLog(s)) AND opcodeSupermag(operation(s)) DO {
        exportWriteOffMag(s);
    }
}

EXTEND FORM writeOffs
    PROPERTIES (w) exportWriteOffMag TOOLBAR 
;
DESIGN writeOffs {
    actionContainer{
        NEW mag {
            caption = 'Супермаг';
            MOVE PROPERTY (exportWriteOffMag(w));
        }        
    }
}