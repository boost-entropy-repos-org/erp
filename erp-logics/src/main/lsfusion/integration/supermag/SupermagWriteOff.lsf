MODULE SupermagWriteOff;

REQUIRE SupermagSettings, SupermagFormMeta, Utils, TaxItem, PricingWriteOff;

NAMESPACE WriteOff;

@settingOperationSupermag(WriteOff);

exportWriteOff ABSTRACT LIST (WriteOff);

EXTEND FORM writeOffs
    PROPERTIES (w) exportWriteOff TOOLBAR 
;
DESIGN writeOffs {
    actionContainer{
        NEW mag {
            caption = 'Супермаг';
            MOVE PROPERTY (exportWriteOff(w));
        }        
    }
}

userWriteOffApply 'Сохранить ' (WriteOff doc) {
    formApply();
    IF series(doc) THEN { number(doc) <- seriesNumber(doc); series(doc) <- NULL;} 
    exportWriteOff(doc);
}

EXTEND FORM userWriteOff  PROPERTIES userWriteOffApply(w) TOOLBAR;

DESIGN userWriteOff {
    TOOLBARRIGHT {
        MOVE PROPERTY (userWriteOffApply(w)) BEFORE PROPERTY (formClose()) { imagePath = 'apply.png';} 
        PROPERTY (formOk()) { hide = TRUE; }
        PROPERTY (formApply()) { hide = TRUE; }
    }
}

@settingIntegration(WriteOff, WriteOffDetail);

@exportFilds(writeOff, writeOffDetail); 

@exportFormWO(exportWriteOffWO, writeOff, writeOffDetail, 'документ списания');

EXTEND FORM exportWriteOffWO
    PROPERTIES = indexPlus(docd) EXTID 'SPECITEM'
    PROPERTIES = indexPlus(docd2) EXTID 'SPECITEM'
    PROPERTIES = indexPlus(docd3) EXTID 'SPECITEM'
    PROPERTIES = indexPlus(docd4) EXTID 'SPECITEM'
    PROPERTIES = indexPlus(docd5) EXTID 'SPECITEM'
    FILTERS quantity(docd) > 0
    FILTERS quantity(docd2) > 0
    FILTERS quantity(docd3) > 0
    FILTERS quantity(docd4) > 0
    FILTERS quantity(docd5) > 0
;                

exportWriteOffMag 'Экспорт в супермаг' (WriteOff doc) {
    inc(WriteOff ii) <- NULL;
    IF docTypeSupermag(operation(doc)) AND opcodeSupermag(operation(doc)) THEN {        
        LOCAL NESTED dt = DATETIME();
        dt () <- currentDateTime();
        inc(doc) <- TRUE;
        FOR iterate(INTEGER n, 1, wordCount(docTypeSupermag(operation(doc)),';')) DO {
                docid(doc) <- seriesNumber(doc);                 
                doctype(doc) <- STRING[10] (getWord(docTypeSupermag(operation(doc)),';',n));
                opcode(doc) <- STRING[10] (getWord(opcodeSupermag(operation(doc)),';',n));
                userop(doc) <- STRING[10] (getWord(useropSupermag(operation(doc)),';',n));
                creatdat(doc) <- dateTime(doc);
                bornin(doc) <- OVERRIDE bornin(doc), baseIDSupermag();
                locationFrom(doc) <- stock(doc);
                locationTo(doc) <- stock(doc);
                location(doc)  <- stock(doc);                 
                id(doc) <- CONCAT '', doctype(doc), seriesNumber(doc);
                docstate(doc) <- CASE WHEN isPosted(doc) THEN 3 WHEN isClosed(doc) THEN 0 ELSE 1;
                clientIndex(doc) <- id(contragentSupermag(stockGroup(stock(doc))));
                priceWithTax(WriteOffDetail docd) <- price(docd) WHERE writeOff(docd) = doc;
                priceRoundMode(doc) <- 4 IF doc IS WriteOff;                                                
                skuId(WriteOffDetail docd) <- id(sku(docd));                                                                           
                quantityExp(WriteOffDetail docd) <- quantity(docd);
                valueTax(WriteOffDetail docd) <- valueVAT(sku(docd)) WHERE writeOff(docd) = doc;
                priceNoTax(WriteOffDetail docd) <- NUMERIC[18,4] ( abs(round( priceWithTax(docd) / (1 + valueTax(docd)/100),2))) WHERE writeOff(docd) = doc;
                sumWithTax(WriteOffDetail docd) <- abs(sum(docd)) WHERE writeOff(docd) = doc;
                sumNoTax(WriteOffDetail docd) <- NUMERIC[18,4] ( abs(round( sumWithTax(docd) / (1 + valueTax(docd)/100),2))) WHERE writeOff(docd) = doc;
                sumTax(WriteOffDetail docd) <- sumWithTax(docd) - sumNoTax(docd) WHERE writeOff(docd) = doc;
                priceManufacturer(WriteOffDetail docd) <- supplierPrice(docd);
                sumWithTaxCurr(WriteOffDetail docd) <-round(sumWithTax(docd)/curRate(doc),2) WHERE writeOff(docd) = doc;
                sumWithTax(doc) <- GROUP SUM sumWithTax(WriteOffDetail docd) IF writeOff(docd) = doc;                
                sumWithTaxCurr(doc) <- GROUP SUM sumWithTaxCurr(WriteOffDetail docd) IF writeOff(docd) = doc;                                
                countSpec(doc) <- GROUP SUM 1 IF writeOff(WriteOffDetail docd) = doc;
           TRY {                
                CASE WHEN doctype(doc) = 'WO' THEN EXPORT exportWriteOffWO XML TO System.exportFile;                 
           } CATCH 
                logToFile('supermag', CONCAT '\n', 'Списание ' + id(doc),'Ошибка формирования формы', ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
           
           TRY {       
                IF countSpec(doc) > 0 THEN {   
                    WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/' + doctype(doc) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(doc), '')+'_'+ formulaDateT(dt());
                    WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/COPY/' + doctype(doc) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(doc), '')+'_'+ formulaDateT(dt());
                }
           } CATCH {
                logToFile('supermag', CONCAT '\n', 'Списание ' + id(doc), 'Ошибка записи в файл','file://' + exportDirectorySupermag() + '/' + doctype(doc) + '_' + STRING[30](LONG(doc))+'_'+(OVERRIDE id(doc), '')+'_'+ formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
           }
           
           TRY 
           {      
                fileExists('file://' + exportDirectorySupermag() + '/' + doctype(doc) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(doc), '')+'_'+ formulaDateT(dt()) + '.xml'); 
                IF fileExists()  THEN NEWSESSION NESTED LOCAL {
                    FOR inc(WriteOff ii)  DO NEW log = SupermagLog {
                        id(log)<- doctype(doc) + '_' +STRING[30](LONG(ii))+'_'+(OVERRIDE id(ii), '')+'_'+ formulaDateT(dt());  
                        dateTime(log) <- currentDateTime();
                        userLogin(log) <- login(currentUser()); 
                        nameContact(log) <- STRING[100](name(currentUser()));
                    }     
                    APPLY NESTED LOCAL;
                }             
                     
           } CATCH {
                logToFile('supermag', CONCAT '\n', 'Списание ' + id(doc), 'Ошибка чтения файла' , 'file://' + exportDirectorySupermag() + '/' + doctype(doc) + '_' + STRING[30](LONG(doc))+'_'+(OVERRIDE id(doc), '')+'_'+ formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
           }
        } 
    }       
    inc(WriteOff ii) <- NULL;
    APPLY;
}

exportWriteOff (WriteOff doc) + {
    IF inExport(stock(doc)) AND isPosted(doc) THEN exportWriteOffMag(doc);
}

importWriteOff 'Списания' () {
    filterSql() <- 'd.CreateDat >= TO_DATE(\'' + toDateISO(dateFromImportDocument()) + '\',\'YYYY-MM-DD\') AND ( d.location IN ('+ stocksImport() + ')  OR d.locationfrom IN ('+ stocksImport() + ') OR locationto IN ('+ stocksImport() + ')   ) AND ( ' ;
    LOCAL filterDoc = STRING ();
    LOCAL operDoc = STRING ();
    filterDoc() <-'';
    FOR Operation op IF op IS Operation AND getWord(docTypeSupermag(op),';',1) DO {
       operDoc() <- CONCAT ' AND ', ('d.DOCTYPE = \'' + getWord(docTypeSupermag(op),';',1) + '\''), ('d.OPCODE = ' + getWord(opcodeSupermag(op),';',1)), ('d.USEROP = ' + getWord(useropSupermag(op),';',1)) ; 
       filterDoc() <- CONCAT ' OR ',filterDoc(), '(' + operDoc() + ')';
    }
      
//  чтобы не путаться осталяем только 1 документ
//    FOR Operation op IF op IS Operation AND getWord(docTypeSupermag(op),';',2) DO {
//       operDoc() <- CONCAT ' AND ', ('d.DOCTYPE = \'' + getWord(docTypeSupermag(op),';',2) + '\''), ('d.OPCODE = ' + getWord(opcodeSupermag(op),';',2)), ('d.USEROP = ' + getWord(useropSupermag(op),';',2)) ; 
//       filterDoc() <- CONCAT ' OR ',filterDoc(), '(' + operDoc() + ')';
//    }  
    
    filterDoc() <- substrFrom(filterDoc(),4);
    filterSql() <- CONCAT '', filterSql(), filterDoc(),')'; 
       
    EXTERNAL SQL 'jdbc:oracle:thin:' + SupermagSettings.login() + '/' + SupermagSettings.password() + '@//'+ SupermagSettings.host() + '/' + SupermagSettings.base()  
    EXEC 'Select d.ID, d.doctype, d.opcode, d.userop, UTL_RAW.CAST_TO_VARCHAR2(UTL_ENCODE.BASE64_ENCODE(d.bornin)), d.createdat, d.docstate from Supermag.SMDocuments d where ' + filterSql() TO SupermagSettings.file;   

   // MESSAGE 'Select d.ID, d.doctype, d.opcode, d.userop, d.createdat, d.docstate from Supermag.SMDocuments d where ' + filterSql();     
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, operCode, userOp, bornIn, docDate, docState;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    currentVersion (INTEGER i) <-  (CONCAT '~', STRING(idDoc(i)), STRING(operCode(i)), STRING(userOp(i)), STRING(bornIn(i)), STRING(docDate(i)), STRING(docState(i)) ) IF idDoc(i);                 
    codeOperation (INTEGER i) <- CONCAT '~', STRING(docType(i)), STRING(operCode(i)), STRING(userOp(i)) IF idDoc(i);

    FOR UserWriteOff doc == userWriteOff(idDoc(INTEGER i)) AND NOT lastVersion[WriteOff](doc) = currentVersion(i) DO {
        isPosted(doc) <- TRUE IF docState(i) = 3;
        isClosed(doc) <- TRUE IF docState(i) = 0;      
        lastVersion[WriteOff](doc) <- currentVersion(i);        
    }
    
    APPLY;
}

EXTEND FORM integrationData PROPERTIES importWriteOff();
