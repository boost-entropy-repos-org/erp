MODULE SupermagPricing;

REQUIRE SupermagSettings, Time,  Store, Repricing, Pricing, SupermagFormMeta, PriceList, PurchaseInvoice, SaleInvoice;


//---------------------------------------------------------------------------------------------------------импорт
imported 'Импортируемая' = DATA BOOLEAN (Repricing.Operation);

EXTEND FORM Repricing.operation PROPERTIES imported(o);
DESIGN Repricing.operation {
    paramsContainer {
        MOVE PROPERTY (imported(o));
    }
}

id = DATA ISTRING[100](UserRepricingDetail) INDEXED;
userRepricingDetail = GROUP AGGR UserRepricingDetail d WHERE d IS UserRepricingDetail BY id(d);

@settingIntegration(Repricing, RepricingDetail);

overImportRepricing ABSTRACT LIST();

importRepricing 'Импорт актов переоценки' () {   
                        
    LOCAL reason = INTEGER (INTEGER);
    LOCAL propDocType = ISTRING[50] (INTEGER);
    LOCAL propDocId = ISTRING[50] (INTEGER);
                                                                                                                                                                                  // a.Reason = 0 переоценка по приходу, у нас акт расценки               
    filterSql () <- 'd.DocType = \'AC\' AND d.location IN ('+ stocksImport() + ') AND TRUNC(nvl(a.ExecTime,a.ExecDate)) >= TO_DATE(\'' + toDateISO(dateFromImportDocument()) + '\',\'YYYY-MM-DD\') AND d.DocState IN (0,2,3) AND a.Reason > 0 AND a.Id = d.Id AND a.DOCTYPE = d.DOCTYPE';
    IF length(numberImportDocument()) > 0 THEN filterSql() <- 'd.DocType = \'AC\' AND d.id = \'' + numberImportDocument() + '\' AND a.Id = d.Id AND a.DOCTYPE = d.DOCTYPE';    
        
    EXTERNAL SQL connectionString() 
        EXEC 'SELECT d.ID, d.DOCTYPE, nvl(a.EXECTIME,a.EXECDATE) AS EXECDATE, a.REASON, d.DOCSTATE, d.LOCATION, d.COMMENTARY FROM Supermag.SMActs a, Supermag.SMDocuments d WHERE ' + filterSql() TO SupermagSettings.file;
    
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, docDate, reason, docState, location, comment;

    FOR [GROUP SUM 1 BY reason(INTEGER i)](INTEGER i) AND NOT Repricing.operation(TEXT (reason(i))) DO NEW o = Repricing.Operation {
        id(o) <- ISTRING[100](reason(i));
        name(o) <- ISTRING[100](reason(i));
        allRoles(o) <- TRUE;
    }
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    currentVersion (INTEGER i) <-  (CONCAT '~', STRING(idDoc(i)), STRING(docDate(i)), STRING(docState(i)), STRING(reason(i)), STRING(comment(i)), STRING(location(i)) ) IF idDoc(i);                 
    
    IF length(numberImportDocument()) > 0 THEN {
        MESSAGE 'SELECT d.ID, d.DOCTYPE, nvl(a.EXECTIME,a.EXECDATE) AS EXECDATE, a.REASON, d.DOCSTATE, d.LOCATION, d.COMMENTARY FROM Supermag.SMActs a, Supermag.SMDocuments d WHERE ' + filterSql();
        MESSAGE currentVersion(0);
    }    
    
    FOR idDoc(INTEGER i) AND NOT Repricing.userRepricing(idDoc(i)) DO NEW doc = Repricing.UserRepricing {
        id(doc) <- idDoc(i);
    }
        
    FOR Repricing.UserRepricing rep == Repricing.userRepricing(idDoc(INTEGER i)) AND (NOT lastVersion(rep) = currentVersion(i) OR length(numberImportDocument()) > 0) DO {       
        operation(rep) <- Repricing.operation(TEXT (reason(i)));
        number(rep) <- ISTRING[28] (docNumber(i));
        date(rep) <- DATE (docDate(i));
        time(rep) <- TIME (docDate(i));
        note(rep) <- comment(i);
        departmentStore(rep) <- departmentStore(TEXT (location(i)));
        imported(rep) <- TRUE;
        isPosted(rep) <- docState(i) = 3;
        lastStatusSupermag(rep) <- docState(i);
        lastVersion(rep) <- currentVersion(i);
    }
    
    overImportRepricing();   
    
    LOCAL oldPrice = NUMERIC[16,4](INTEGER);
    LOCAL actReason = INTEGER (INTEGER);
    LOCAL priceType = INTEGER (INTEGER);

    EXTERNAL SQL connectionString() 
        EXEC 'SELECT d.Id, d.DocType, dd.SpecItem, dd.Article, dd.ItemPrice, ad.OldPrice, dd.Quantity, a.Reason, a.PriceType FROM Supermag.SMDocuments d, Supermag.SMActs a, Supermag.SMSpec dd, Supermag.SMSpecActs ad ' + 
             'WHERE ' + filterSql()+' AND dd.DocId = d.Id AND dd.DocType = d.DOCTYPE AND ad.DocId(+) = dd.DOCID AND ad.DocType(+) = dd.DOCTYPE AND ad.SpecItem(+) = dd.SpecItem' TO SupermagSettings.file;
       
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, specItem, idItem, itemPrice, oldPrice, quantity, actReason, priceType;
        
    idDoc(INTEGER i) <- STRING[50](CONCAT '', docType(i), docNumber(i)) WHERE docNumber(i) AND docType(i);
    idDet(INTEGER i) <- STRING[50](CONCAT '~', idDoc(i), specItem(i)) WHERE idDoc(i) AND specItem(i);
    currentVersion (INTEGER i) <- ( CONCAT '~', STRING(idDet(i)), STRING(idItem(i)), STRING(actReason(i)), STRING(priceType(i)), STRING(itemPrice(i)), STRING(oldPrice(i)) ) IF idDet(i);                  

    IF length(numberImportDocument()) > 0 THEN {
        MESSAGE 'SELECT d.Id, d.DocType, dd.SpecItem, dd.Article, dd.ItemPrice, ad.OldPrice, dd.Quantity, a.Reason, a.PriceType FROM Supermag.SMDocuments d, Supermag.SMActs a, Supermag.SMSpec dd, Supermag.SMSpecActs ad ' + 
                             'WHERE ' + filterSql()+' AND dd.DocId = d.Id AND dd.DocType = d.DOCTYPE AND ad.DocId(+) = dd.DOCID AND ad.DocType(+) = dd.DOCTYPE AND ad.SpecItem(+) = dd.SpecItem';
        MESSAGE currentVersion(0);
    }    

    FOR NOT userRepricingDetail(idDet(INTEGER i)) AND userRepricing(idDoc(i)) DO NEW det = UserRepricingDetail {
        id(det) <- idDet(i);
        userRepricing(det) <- userRepricing(idDoc(i));
    }

    FOR UserRepricingDetail det = userRepricingDetail(idDet(INTEGER i)) AND NOT lastVersion(det) = currentVersion(i) DO {
        sku(det) <- item(idItem(i));
        quantity(det) <- quantity(i);
        curRetailPrice(det) <- oldPrice(i);
        retailPrice(det) <- NUMERIC[16,4](itemPrice(i));
        imported(det) <- TRUE;
        dataIndex(det) <- specItem(i);
        lastVersion(det) <- currentVersion(i);
    }
   
    // импорт документов оснований
    LOCAL baseType = ISTRING[50] (INTEGER);
    
    EXTERNAL SQL connectionString() 
    EXEC 'Select d.ID, d.doctype, cb.BASEID, cb.BASEDOCTYPE from Supermag.SMDocuments d, Supermag.SMActs a, Supermag.SMCommonBases cb where a.DocType(+)= D.DocType and a.ID(+)= D.ID AND cb.ID(+) = a.ID AND ' + filterSql() TO SupermagSettings.file;
        
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, baseNumber, baseType;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    idBase(INTEGER i) <- STRING[50](CONCAT '', STRING(baseType(i)), STRING(baseNumber(i)));

    FOR Repricing.UserRepricing doc == Repricing.userRepricing(idDoc(INTEGER i)) AND (baseType(i) = 'AB' OR baseType(i) = 'MA') DO {    
        basisDoc(doc, Document d) <- TRUE IF d = userPriceList(idBase(i)); 
    }    
       
    APPLY;       
}

EXTEND FORM integrationData PROPERTIES () importRepricing;

//////////----------------------------------------------
imported 'Импортируемая' = DATA BOOLEAN (Pricing.Operation);

EXTEND FORM Pricing.operation PROPERTIES imported(o);
DESIGN Pricing.operation {
    paramsContainer {
        MOVE PROPERTY (imported(o));
    }
}

id = DATA ISTRING[100](UserPricingDetail) INDEXED;
userPricingDetail = GROUP AGGR UserPricingDetail d WHERE d IS UserPricingDetail BY id(d);

@settingIntegration(Pricing, PricingDetail);

importPricing 'Импорт актов расценки' () {   
                        
    LOCAL reason = INTEGER (INTEGER);
                                                                                                                                                                                  // a.Reason = 0 переоценка по приходу, у нас акт расценки               
    filterSql () <- 'd.location IN ('+ stocksImport() + ') AND TRUNC(nvl(a.ExecTime,a.ExecDate)) >= TO_DATE(\'' + toDateISO(dateFromImportDocument()) + '\',\'YYYY-MM-DD\') AND d.DocState IN (0,1,2,3) AND a.Reason = 0';
    IF length(numberImportDocument()) > 0 THEN filterSql() <- 'd.id = \'' + numberImportDocument() + '\''; 
    
    EXTERNAL SQL connectionString() 
        EXEC 'SELECT d.ID, d.DOCTYPE, UTL_RAW.CAST_TO_VARCHAR2(UTL_ENCODE.BASE64_ENCODE(d.bornin)), nvl(a.EXECTIME,a.EXECDATE) AS EXECDATE, a.REASON, d.DOCSTATE, d.LOCATION, d.COMMENTARY FROM Supermag.SMActs a, Supermag.SMDocuments d WHERE d.DocType = \'AC\' AND a.Id(+) = d.Id AND a.DOCTYPE(+) = d.DOCTYPE ' + filterSql() TO SupermagSettings.file;
    
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, bornIn, docDate, reason, docState, location, comment;

    FOR [GROUP SUM 1 BY reason(INTEGER i)](INTEGER i) AND NOT Pricing.operation(TEXT (i)) DO NEW o = Pricing.Operation {
        id(o) <- ISTRING[100](i);
        name(o) <- ISTRING[100](i);
        allRoles(o) <- TRUE;
        imported(o) <- TRUE;
    }
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    currentVersion (INTEGER i) <-  (CONCAT '~', STRING(idDoc(i)), STRING(docDate(i)), STRING(bornIn(i)), STRING(docState(i)), STRING(reason(i)), STRING(comment(i)), STRING(location(i)) ) IF idDoc(i);                 
    
    FOR idDoc(INTEGER i) AND NOT Pricing.userPricing(idDoc(i)) AND NOT docState(i) = 1 DO NEW doc = Pricing.UserPricing {
        id(doc) <- idDoc(i);
    }
        
    FOR Pricing.UserPricing doc == Pricing.userPricing(idDoc(INTEGER i)) AND NOT lastVersion(doc) = currentVersion(i) DO {       
        operation(doc) <- Pricing.operation(TEXT (location(i)));
        number(doc) <- ISTRING[28] (docNumber(i));
        bornin(doc) <- bornIn(i);
        date(doc) <- DATE (docDate(i));
        time(doc) <- TIME (docDate(i));
        note(doc) <- comment(i);
        departmentStore(doc) <- departmentStore(TEXT (location(i)));
        imported(doc) <- TRUE;
        isPosted(doc) <- docState(i) = 3;
        lastStatusSupermag(doc) <- docState(i);
        lastVersion(doc) <- currentVersion(i);
    }
    
    LOCAL oldPrice = NUMERIC[16,4](INTEGER);
    LOCAL actReason = INTEGER (INTEGER);
    LOCAL priceType = INTEGER (INTEGER);

    EXTERNAL SQL connectionString() 
        EXEC 'SELECT d.Id, d.DocType, dd.SpecItem, dd.Article, dd.ItemPrice, ad.OldPrice, dd.Quantity, a.Reason, a.PriceType ' + 
             'FROM Supermag.SMDocuments d, Supermag.SMActs a, Supermag.SMSpec dd, Supermag.SMSpecActs ad ' + 
             'WHERE d.DocType = \'AC\' AND a.Id(+) = d.Id AND dd.DocId = d.Id AND dd.DocType = d.DOCTYPE AND ad.DocId(+) = dd.DOCID AND ad.DocType(+) = dd.DOCTYPE AND ad.SpecItem(+) = dd.SpecItem AND ' + filterSql() TO SupermagSettings.file;
       
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, specItem, idItem, itemPrice, oldPrice, quantity, actReason, priceType;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', docType(i), docNumber(i)) WHERE docNumber(i) AND docType(i);
    idDet(INTEGER i) <- STRING[50](CONCAT '~', idDoc(i), specItem(i)) WHERE idDoc(i) AND specItem(i);
    currentVersion (INTEGER i) <- ( CONCAT '~', STRING(idDet(i)), STRING(idItem(i)), STRING(actReason(i)), STRING(priceType(i)), STRING(itemPrice(i)), STRING(oldPrice(i)) ) IF idDet(i);                  

    FOR NOT userPricingDetail(idDet(INTEGER i)) AND userPricing(idDoc(i)) DO NEW det = UserPricingDetail {
        id(det) <- idDet(i);
        userPricing(det) <- userPricing(idDoc(i));
    }

    FOR UserPricingDetail det = userPricingDetail(idDet(INTEGER i)) AND NOT lastVersion(det) = currentVersion(i) DO {
        sku(det) <- item(idItem(i));
        quantity(det) <- quantity(i);
 //       curRetailPrice(det) <- oldPrice(i);
        retailPrice(det) <- NUMERIC[16,4](itemPrice(i));
        imported(det) <- TRUE;
        dataIndex(det) <- specItem(i);
        lastVersion(det) <- currentVersion(i);
    }
   
    // импорт документов оснований
    LOCAL baseType = ISTRING[50] (INTEGER);
    
    EXTERNAL SQL connectionString() 
    EXEC 'Select d.ID, d.doctype, cb.BASEID, cb.BASEDOCTYPE from Supermag.SMDocuments d, Supermag.SMActs a, Supermag.SMCommonBases cb where a.DocType(+)= D.DocType and a.ID(+)= D.ID AND cb.ID(+) = a.ID AND ' + filterSql() TO SupermagSettings.file;
        
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, baseNumber, baseType;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    idBase(INTEGER i) <- STRING[50](CONCAT '', STRING(baseType(i)), STRING(baseNumber(i)));

    FOR Pricing.UserPricing doc == Pricing.userPricing(idDoc(INTEGER i)) AND (baseType(i) = 'AB' OR baseType(i) = 'MA') DO {    
        basisDoc(doc, Document d) <- TRUE IF d = userPriceList(idBase(i)); 
    }
        
    FOR Pricing.UserPricing doc == Pricing.userPricing(idDoc(INTEGER i)) AND (baseType(i) = 'WI') DO {    
        basisDoc(doc, Document d) <- TRUE IF d = Purchase.userInvoice(idBase(i)); 
    }    

    FOR Pricing.UserPricing doc == Pricing.userPricing(idDoc(INTEGER i)) AND (baseType(i) = 'IW') DO {    
        basisDoc(doc, Document d) <- TRUE IF d = Sale.userInvoice(idBase(i)); 
    }    
    
    APPLY;       
}

EXTEND FORM integrationData PROPERTIES () importPricing;