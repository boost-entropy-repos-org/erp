MODULE SupermagFormMeta;

REQUIRE StockSkuDocument;

META overCheckDocStatusSupermag(docMain)
    lastStatusSupermag '' = DATA INTEGER (###docMain);        
    overCheckDocStatusSupermag() + {
        FOR lastStatusSupermag(###docMain doc) != maxDocStatusState(id(doc))  DO {
            IF maxDocStatusState(id(doc)) = 3 THEN { isPosted(doc) <- TRUE; isClosed(doc) <- TRUE ; }        
            IF maxDocStatusState(id(doc)) = 2 THEN { isPosted(doc) <- TRUE; isClosed(doc) <- NULL; }
            IF maxDocStatusState(id(doc)) = 1 THEN { isPosted(doc) <- NULL; isClosed(doc) <- NULL; }
            IF maxDocStatusState(id(doc)) = 0 THEN { isPosted(doc) <- NULL; isClosed(doc) <- TRUE ; }        
            lastStatusSupermag(doc) <- maxDocStatusState(id(doc));        
        }
    }    
END

META exportFilds (docMain, docDetail) 
    doctype = DATA LOCAL STRING[10] (###docMain);
    opcode = DATA LOCAL STRING[10] (###docMain);
    userop = DATA LOCAL STRING[10] (###docMain);
    docstate = DATA LOCAL INTEGER (###docMain);
    opname =  DATA LOCAL STRING[100] (###docMain);
    clientIndex = DATA LOCAL STRING[100] (###docMain);
    location = DATA Stock (###docMain);
    locationFrom = DATA Stock (###docMain);
    locationTo = DATA Stock (###docMain);
    ourSelfClient = DATA LOCAL STRING[100] (###docMain);

    inc = DATA LOCAL BOOLEAN (###docMain);
    priceWithTax = DATA LOCAL NUMERIC[18,4] (###docDetail);
    priceNoTax = DATA LOCAL NUMERIC[18,4] (###docDetail);
    priceManufacturer = DATA LOCAL NUMERIC[18,4] (###docDetail);
    priceRetail = DATA LOCAL NUMERIC[18,4] (###docDetail);
    valueTax = DATA LOCAL NUMERIC[10,5] (###docDetail);
    sumTax = DATA LOCAL NUMERIC[18,4] (###docDetail);
    sumNoTax = DATA LOCAL NUMERIC[18,4] (###docDetail);
    sumWithTax = DATA LOCAL NUMERIC[18,4] (###docDetail);
    sumWithTaxCurr = DATA LOCAL NUMERIC[18,4] (###docDetail);
 //   docId = DATA LOCAL STRING[100] (###docMain);
    docId (###docDetail dd) = id(##docMain(dd)); 
    sumNoTax = DATA LOCAL NUMERIC[18,4] (###docMain);
    sumWithTax = DATA LOCAL NUMERIC[18,4] (###docMain);
    sumWithTaxCurr = DATA LOCAL NUMERIC[18,4] (###docMain);
    
    curRate(###docMain i) = (OVERRIDE rateOn(typeExchange('НБРБ (BYR)'),currencyShortName('USD'),date(i)),(1.0 IF i IS ###docMain));
    
    //различные индексы сделаны для SPECITEM т.к. из одного документа с + и - мы делаем 2 в супермаге
    indexPlus (###docDetail ad) = PARTITION SUM 1 IF quantity(ad)>0 ORDER ad BY ##docMain(ad) CHARWIDTH 4;
    indexMinus (###docDetail ad) = PARTITION SUM 1 IF quantity(ad)<0 ORDER ad BY ##docMain(ad) CHARWIDTH 4;
END

META exportFormDoc (typeDoc, formName, docMain, docDetail, commentary) 

GROUP ###typeDoc;
GROUP SMDOCUMENTS###typeDoc EXTID 'SMDOCUMENTS': ###typeDoc;
GROUP SMCOMMONBASES###typeDoc EXTID 'SMCOMMONBASES': ###typeDoc;
GROUP SMSPEC###typeDoc EXTID 'SMSPEC': ###typeDoc;

FORM formName FORMEXTID 'PACKAGE' 
    PROPERTIES ATTR = exportTime(currentDateTime())  EXTID 'name'

    OBJECTS doc = ###docMain EXTID 'POSTOBJECT'
    PROPERTIES ATTR = 'normal' IF doc IS ###docMain EXTID 'action'      
    PROPERTIES ATTR = opname(doc) IF doc IS ###docMain EXTID 'description'  
    
    PROPERTIES = STRING[100](doctype(doc)+id(doc)) EXTID 'Id'
    PROPERTIES = STRING[100](id(doc)) EXTID 'ID' IN SMDOCUMENTS###typeDoc
    PROPERTIES = doctype(doc) IF doc IS ###docMain EXTID 'DOCTYPE' IN SMDOCUMENTS###typeDoc
    PROPERTIES = opcode(doc) IF doc IS ###docMain EXTID 'OPCODE' IN SMDOCUMENTS###typeDoc
    PROPERTIES = idSupermag(location(doc)) EXTID 'BORNIN' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = exportTime(dateTime(doc))  EXTID 'CREATEDAT' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = 0 IF doc IS ###docMain EXTID 'CURRENCYMULTORDER' IN SMDOCUMENTS###typeDoc      
    PROPERTIES = curRate(doc) EXTID 'CURRENCYRATE' IN SMDOCUMENTS###typeDoc 
    PROPERTIES = 1 IF doc IS ###docMain EXTID 'CURRENCYTYPE' IN SMDOCUMENTS###typeDoc        
    PROPERTIES = docstate(doc) EXTID 'DOCSTATE' IN SMDOCUMENTS###typeDoc           
    PROPERTIES = 1 IF doc IS ###docMain  EXTID 'ISROUBLES' IN SMDOCUMENTS###typeDoc      
    PROPERTIES = 4 IF doc IS ###docMain  EXTID 'PRICEROUNDMODE' IN SMDOCUMENTS###typeDoc       
    PROPERTIES = sumWithTax(doc) IF doc IS ###docMain EXTID 'TOTALSUM' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = sumWithTaxCurr(doc) IF doc IS ###docMain EXTID 'TOTALSUMCUR' IN SMDOCUMENTS###typeDoc    
    PROPERTIES = ###commentary IF doc IS ###docMain EXTID 'COMMENTARY' IN SMDOCUMENTS###typeDoc     
                                                            
    OBJECTS docd = ###docDetail EXTID 'SMSPEC' IN ###typeDoc 
    PROPERTIES = docId(docd) EXTID 'DOCID'    
    PROPERTIES = doctype(##docMain(docd)) IF docd IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = idSku(docd) EXTID 'ARTICLE'                          
    PROPERTIES d1= index(docd) EXTID 'DISPLAYITEM'
    PROPERTIES = abs(quantity(docd)) EXTID 'QUANTITY'
    PROPERTIES = priceWithTax(docd) EXTID 'ITEMPRICE'     
    PROPERTIES d2 = NUMERIC[18,4](price(docd)/curRate(doc)) EXTID 'ITEMPRICECUR'                         
    FILTERS ##docMain(docd)==doc,
            inc(doc)  
    ;                                                
END

META exportFormWI (formName, docMain, docDetail, commentary)   
@exportFormDoc(WI,formName, docMain, docDetail, commentary);

GROUP SMWAYBILLSIN EXTID 'SMWAYBILLSIN' : WI;
GROUP SMSPECIOWI EXTID 'SMWSPECIO' : WI;

EXTEND FORM formName
    PROPERTIES = userop(doc) IF doc IS ###docMain EXTID 'USEROP' IN SMDOCUMENTSWI        
    PROPERTIES = clientIndex(doc) IF doc IS ###docMain EXTID 'CLIENTINDEX' IN SMDOCUMENTSWI        
    PROPERTIES = id(locationTo(doc)) EXTID 'LOCATIONTO' IN SMDOCUMENTSWI
    
    PROPERTIES = STRING[100](id(doc)) EXTID 'ID' IN SMWAYBILLSIN      
    PROPERTIES = 'WI' IF doc IS ###docMain EXTID 'DOCTYPE' IN SMWAYBILLSIN 
    PROPERTIES = 0 IF doc IS ###docMain EXTID 'GOODSOWNER' IN SMWAYBILLSIN                        
    PROPERTIES = id(legalEntity(locationTo(doc))) EXTID 'OURSELFCLIENT' IN SMWAYBILLSIN    
    PROPERTIES = 0 IF doc IS ###docMain EXTID 'PAYCASH' IN SMWAYBILLSIN      

    PROPERTIES = priceNoTax(docd) IF docd IS ###docDetail EXTID 'ITEMPRICENOTAX'  
    PROPERTIES = sumWithTax(docd) IF docd IS ###docDetail EXTID 'TOTALPRICE'  
    PROPERTIES = sumWithTaxCurr(docd) IF docd IS ###docDetail EXTID 'TOTALPRICECUR'      
    PROPERTIES = sumNoTax(docd) IF docd IS ###docDetail EXTID 'TOTALPRICENOTAX'            
       
    OBJECTS docd2 = ###docDetail EXTID 'SMSPECIO' IN WI 
    PROPERTIES = docId(docd2) EXTID 'DOCID'   
    PROPERTIES = 'WI' IF docd2 IS ###docDetail EXTID 'DOCTYPE'     
    FILTERS ##docMain(docd2)==doc    
    
    OBJECTS docd3 = ###docDetail EXTID 'SMSPECTAX' IN WI
    PROPERTIES = docId(docd3) EXTID 'DOCID'    
    PROPERTIES = 'WI' IF docd3 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = 0 IF docd3 IS ###docDetail  EXTID 'TAXID'        
    PROPERTIES = valueTax(docd3) EXTID 'TAXRATE'
    PROPERTIES = sumTax(docd3) EXTID 'TAXSUM'            
    FILTERS ##docMain(docd3)==doc   

    OBJECTS docd4 = ###docDetail EXTID 'SMSPECBY' IN WI
    PROPERTIES = docId(docd4) EXTID 'DOCID'    
    PROPERTIES = 'WI' IF docd4 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = 0 IF docd4 IS ###docDetail EXTID 'EXTRACHARGE'  
    PROPERTIES = priceManufacturer(docd4) EXTID 'MANUFACTURERSPRICE'     
    PROPERTIES = priceRetail(docd4) EXTID 'RETAILPRICE'       
    PROPERTIES = 0 IF docd4 IS ###docDetail EXTID 'STATEREGULATION'                           
    FILTERS ##docMain(docd4)==doc  
    
    OBJECTS docd5 = ###docDetail EXTID 'SLSPECPACKS' IN WI
    PROPERTIES = docId(docd5) EXTID 'DOCID'    
    PROPERTIES = 'WI' IF docd5 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = -1 IF docd5 IS ###docDetail EXTID 'PACKNUM'        
    PROPERTIES = 0 IF docd5 IS ###docDetail EXTID 'NPACKS'       
    PROPERTIES = 0 IF docd5 IS ###docDetail EXTID 'PACKSIZE'     
    FILTERS ##docMain(docd5)==doc     
      
    ;
END

META exportFormWO (formName, docMain, docDetail, commentary)   
@exportFormDoc(WO,formName, docMain, docDetail, commentary);

GROUP SMWAYBILLSOUT EXTID 'SMWAYBILLSOUT' : WO;
GROUP SMSPECIOWO EXTID 'SMWSPECIO' : WO;

EXTEND FORM formName
    PROPERTIES = userop(doc) IF doc IS ###docMain EXTID 'USEROP' IN SMDOCUMENTSWO        
    PROPERTIES = clientIndex(doc) IF doc IS ###docMain EXTID 'CLIENTINDEX' IN SMDOCUMENTSWO        
    PROPERTIES = id(locationFrom(doc)) EXTID 'LOCATIONFROM' IN SMDOCUMENTSWO
    
    PROPERTIES = STRING[100](id(doc))  EXTID 'ID' IN SMWAYBILLSOUT      
    PROPERTIES = 'WO' IF doc IS ###docMain EXTID 'DOCTYPE' IN SMWAYBILLSOUT
    PROPERTIES = id(legalEntity(locationFrom(doc))) EXTID 'OURSELFCLIENT' IN SMWAYBILLSOUT
    PROPERTIES = STRING[100](id(doc))  EXTID 'INVOICE' IN SMWAYBILLSOUT             
    PROPERTIES = 0 IF doc IS ###docMain EXTID 'PAYCASH' IN SMWAYBILLSOUT      
    
    PROPERTIES = priceNoTax(docd) IF docd IS ###docDetail EXTID 'ITEMPRICENOTAX'  
    PROPERTIES = sumWithTax(docd) IF docd IS ###docDetail EXTID 'TOTALPRICE'  
    PROPERTIES = sumWithTaxCurr(docd) IF docd IS ###docDetail EXTID 'TOTALPRICECUR'      
    PROPERTIES = sumNoTax(docd) IF docd IS ###docDetail EXTID 'TOTALPRICENOTAX'            
                
    OBJECTS docd2 = ###docDetail EXTID 'SMSPECIO' IN WO 
    PROPERTIES = docId(docd2) EXTID 'DOCID'   
    PROPERTIES = doctype(##docMain(docd2)) IF docd2 IS ###docDetail EXTID 'DOCTYPE'     
    FILTERS ##docMain(docd2)==doc
        
    OBJECTS docd3 = ###docDetail EXTID 'SMSPECTAX' IN WO
    PROPERTIES = docId(docd3) EXTID 'DOCID'    
    PROPERTIES = 'WO' IF docd3 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = 0 IF docd3 IS ###docDetail  EXTID 'TAXID'        
    PROPERTIES = valueTax(docd3) EXTID 'TAXRATE'
    PROPERTIES = sumTax(docd3) EXTID 'TAXSUM'            
    FILTERS ##docMain(docd3)==doc   

    OBJECTS docd4 = ###docDetail EXTID 'SMSPECBY' IN WO
    PROPERTIES = docId(docd4) EXTID 'DOCID'    
    PROPERTIES = 'WO' IF docd4 IS ###docDetail EXTID 'DOCTYPE'      
    PROPERTIES = 0 IF docd4 IS ###docDetail EXTID 'EXTRACHARGE'  
    PROPERTIES = priceManufacturer(docd4) EXTID 'MANUFACTURERSPRICE'     
    PROPERTIES = priceRetail(docd4) EXTID 'RETAILPRICE'       
    PROPERTIES = 0 IF docd4 IS ###docDetail EXTID 'STATEREGULATION'                           
    FILTERS ##docMain(docd4)==doc    
    ;
END

META exportFormLA (formName, docMain, docDetail, commentary)   
@exportFormDoc(LA,formName, docMain, docDetail, commentary);

EXTEND FORM formName
    PROPERTIES = id(location(doc)) EXTID 'LOCATION' IN SMDOCUMENTSLA
    ;
END

META exportFormFA (formName, docMain, docDetail, commentary)   
@exportFormDoc(FA,formName, docMain, docDetail, commentary);

EXTEND FORM formName
    PROPERTIES = id(location(doc)) EXTID 'LOCATION' IN SMDOCUMENTSFA
    ;
END 