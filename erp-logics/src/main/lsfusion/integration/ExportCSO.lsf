MODULE ExportCSO;

REQUIRE Item, Store, LegalEntityBy, Image;


//----------------------------- Экспорт в КСО -----------------------//

//------------------//PI_SAP_ArticleHierarchyObject.xml
dataExchangeRegisterAddress 'Полный адрес отправки данных КСО' = DATA STRING[255] ();

isCSO 'КСО' = DATA BOOLEAN (CustomGroupType);
EXTEND FORM customGroupType
    PROPERTIES (t) isCSO
;
EXTEND FORM dialogCustomGroupType
    PROPERTIES (t) READONLY isCSO
;
EXTEND FORM customGroupTypes
    PROPERTIES (t) READONLY isCSO
;

GROUP Header EXTID 's:Header';
GROUP To EXTID '=http://schemas.microsoft.com/ws/2005/05/addressing/none:To': Header ;
GROUP Action EXTID '=http://schemas.microsoft.com/ws/2005/05/addressing/none:Action': Header ;
GROUP Body EXTID 's:Body';
GROUP Put EXTID '=http://www.uvs.lt/PI_GP:PutArticleHierarchyObjects' :Body;
GROUP head :Put;
GROUP items:Put;
GROUP Message EXTID 'MessageItemOfArticleHierarchyObjectS7N_Sjo7W' :items;
GROUP itemO EXTID 'Item';

skipCSO = ABSTRACT BOOLEAN (Item);
activeCSO (Item i) = active(i) AND NOT skipCSO(i);

FORM ArticleHierarchyObject FORMEXTID 's=http://schemas.xmlsoap.org/soap/envelope/:Envelope'  
     
    PROPERTIES IN head ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i'             
    PROPERTIES  IN head h1 = 'SAP PI' EXTID 'Receiver'
    PROPERTIES  IN head h2 = 'SAP ERP' EXTID 'Sender'
    PROPERTIES  IN head h3 = TRUE EXTID 'SessionEnd'
    PROPERTIES  IN head h33 = 1 EXTID'SessionMessageNumber'
    PROPERTIES  IN head h4 = [FORMULA STRING[20] 'to_char(($1),\'YYYY-MM-DD\') || \'T\' || to_char(($1),\'HH24:MI:SS\')'](currentDateTime()) EXTID 'SessionTimeStamp'
    PROPERTIES  IN head h5 = 'F' EXTID 'SessionType'                      
                           
    PROPERTIES IN items ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i' 
 
    OBJECTS dd =(i = Item, type = CustomGroupType)   EXTID 'MessageItemOfArticleHierarchyObjectS7N_Sjo7W' IN items
    PROPERTIES  = 'U' IF i IS Item EXTID 'ChangeType'  
         
    PROPERTIES  =idBarcode(i) EXTID 'ArticleId' IN itemO
    PROPERTIES  =id(type) IF i IS Item EXTID 'HierarchyId' IN itemO
    PROPERTIES  =id(customGroup(type, i)) EXTID 'NodeId' IN itemO
                    
    FILTERS customGroup(type, i), 
            activeCSO(i),
            isCSO(type) 
;
httpHeadersAHO(TEXT name) = CASE
    WHEN name == 'SOAPAction' THEN 'http://www.uvs.lt/PI_GP/I_PI_GP/PutArticleHierarchyObjects';
    
articleHierarchyObject 'Привязка товаров КСО (ArticleHierarchyObject)' (){
    EXPORT ArticleHierarchyObject XML TO System.exportFile;     
    LOCAL result = FILE();
    LOCAL message = TEXT();
    TRY {
        logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleHierarchyObject', 'Sending')));
        EXTERNAL HTTP dataExchangeRegisterAddress() HEADERS httpHeadersAHO PARAMS System.exportFile() TO result;  
        message() <- 'ArticleHierarchyObject - отправлен';
    } CATCH {
        logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleHierarchyObject', messageCaughtException()), lsfStackTraceCaughtException(), javaStackTraceCaughtException()));
        fileToString(result(), 'UTF-8');
        logToFile('CSO', (CONCAT '\n', 'ArticleHierarchyObject',resultString()));    
        message() <- 'ArticleHierarchyObject - есть ошибки, обратитесь к администратору';        
    }
    IF message()  THEN {
        MESSAGE message() NOWAIT;
    }                
}

//----------- //PI_SAP_ArticleMessage.xml
GROUP PutL EXTID '=http://www.uvs.lt/PI_GP:PutArticleMessage' :Body;
GROUP headL EXTID 'head' :PutL;
GROUP itemsL EXTID 'items' :PutL;
GROUP itemL EXTID 'Item';
GROUP ArticleText : itemL;

Description = ABSTRACT CASE ISTRING[255](Item, INTEGER);
Description (Item i, INTEGER  int) +=  WHEN  int == 1 AND i IS Item THEN ISTRING[255](nameAttribute(i));
countDescription (Item i)= GROUP MAX INTEGER  int IF Description(i, int);

LanguageId = ABSTRACT CASE STRING[3](Item, INTEGER);
LanguageId (Item i, INTEGER  int) +=  WHEN  int == 1 AND i IS Item THEN 'RU';

FORM ArticleMessage FORMEXTID 's=http://schemas.xmlsoap.org/soap/envelope/:Envelope'       
    PROPERTIES IN headL ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i'             
    PROPERTIES  IN headL h1 = 'SAP PI' EXTID 'Receiver'
    PROPERTIES  IN headL h2 = 'SAP ERP' EXTID 'Sender'
    PROPERTIES  IN headL h3 = TRUE EXTID 'SessionEnd'
    PROPERTIES  IN headL h33 = 1 EXTID'SessionMessageNumber'
    PROPERTIES  IN headL h4 = [FORMULA STRING[20] 'to_char(($1),\'YYYY-MM-DD\') || \'T\' || to_char(($1),\'HH24:MI:SS\')'](currentDateTime()) EXTID 'SessionTimeStamp'
    PROPERTIES  IN headL h5 = 'F' EXTID 'SessionType'                      
                           
    PROPERTIES IN itemsL ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i'
    
    OBJECTS dd = (i = Item, type = CustomGroupType) EXTID 'MessageItemOfArticleMessageS7N_Sjo7W' IN itemsL
    PROPERTIES   = 'U' IF i IS Item EXTID 'ChangeType'         
    PROPERTIES  i1=idBarcode(i) EXTID 'ArticleId' IN itemL
    
    OBJECTS gg = (ii = Item, int=INTEGER) EXTID 'ArticleHierarchyDescriptionData' IN ArticleText
    PROPERTIES (ii,int) Description, LanguageId
    FILTERS iterate(int,1,countDescription(ii)), ii==i                    
    FILTERS customGroup(type, i), 
            activeCSO(i),
            isCSO(type)
; 
httpHeadersAM(TEXT name) = CASE
    WHEN name == 'SOAPAction' THEN 'http://www.uvs.lt/PI_GP/I_PI_GP/PutArticleMessage';
    
articleMessage 'Наименование товаров КСО (ArticleMessage)' (){
    EXPORT ArticleMessage XML TO System.exportFile;
    LOCAL result = FILE();
    LOCAL message = TEXT();    
    TRY {
        logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleMessage', 'Sending')));
        EXTERNAL HTTP dataExchangeRegisterAddress() HEADERS httpHeadersAM PARAMS System.exportFile() TO result; 
        message() <- 'ArticleMessage - отправлен';
    } CATCH { 
        logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleMessage', messageCaughtException()), lsfStackTraceCaughtException(), javaStackTraceCaughtException()));
        fileToString(result(), 'UTF-8');
        logToFile('CSO', (CONCAT '\n', 'ArticleMessage',resultString()));    
        message() <- 'ArticleMessage - есть ошибки, обратитесь к администратору';      
    }
    IF message()  THEN {
        MESSAGE message() NOWAIT;
    }                        
}

//------------ //PI_SAP_ArticleDocumentMessage.xml
GROUP PutI EXTID '=http://www.uvs.lt/PI_GP:PutArticleDocumentMessage' :Body;
GROUP headI EXTID 'head' :PutI;
GROUP itemsI EXTID 'items' :PutI;
GROUP itemI EXTID 'Item';
GROUP DocumentData : itemI;
GROUP ArticleDocumentData : DocumentData;

timeStamp = DATA LOCAL STRING ();

localImage = DATA LOCAL IMAGEFILE (Item);

countImagesCSO 'Кол-во картинок в одном документе' = DATA INTEGER ();

number = DATA LOCAL INTEGER ();
isEnd = DATA LOCAL BOOLEAN ();

index(Item i, CustomGroupType t) = PARTITION SUM 1 IF customGroup(t, i) AND activeCSO(i) AND isCSO(t) ORDER i,t;
countIn() = GROUP SUM 1 IF customGroup(CustomGroupType t, Item i) AND activeCSO(i) AND isCSO(t);

FORM ArticleDocumentMessage FORMEXTID 's=http://schemas.xmlsoap.org/soap/envelope/:Envelope'   
          
    PROPERTIES IN headI ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i'             
    PROPERTIES  IN headI         h1 = 'SAP PI' EXTID 'Receiver'
    PROPERTIES  IN headI         h2 = 'SAP ERP' EXTID 'Sender'
    PROPERTIES  IN headI         h3 = (IF isEnd() THEN 'true' ELSE 'false') EXTID 'SessionEnd'
    PROPERTIES  IN headI         h33 = number() EXTID'SessionMessageNumber'
    PROPERTIES  IN headI         h4 = timeStamp() EXTID 'SessionTimeStamp'
    PROPERTIES  IN headI         h5 = 'F' EXTID 'SessionType'                      
                           
    PROPERTIES IN itemsI ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i' 
        
    OBJECTS dd = (i = Item, type = CustomGroupType) EXTID 'MessageItemOfArticleDocumentMessageS7N_Sjo7W' IN itemsI
    PROPERTIES  = 'U' IF i IS Item EXTID 'ChangeType' 
    
    PROPERTIES  i1=idBarcode(i) EXTID 'ArticleId' IN itemI
    PROPERTIES  IN ArticleDocumentData    =idBarcode(i) EXTID 'DokNr' 
    PROPERTIES  IN ArticleDocumentData    =localImage(i) EXTID 'Object'    
    FILTERS index(i,type),
            (index(i,type) > ((number() - 1) * countImagesCSO())) OR NOT countImagesCSO(),
            (index(i,type) <= (number() * countImagesCSO())) OR NOT countImagesCSO()
; 

httpHeadersADM(TEXT name) = CASE
    WHEN name == 'SOAPAction' THEN 'http://www.uvs.lt/PI_GP/I_PI_GP/PutArticleDocumentMessage';

articleDocumentMessage 'Изображения товаров КСО (ArticleDocumentMessage)' (){
    localImage(Item i) <- NULL;
    FOR customGroup(CustomGroupType type, Item i) AND activeCSO(i) AND isCSO(type) AND image(i) DO {
        localImage(i) <- image(i);
        getFileSize(FILE(localImage(i)));
        IF fileSize() > 15000 THEN {
            resizeImage(localImage(i), sqrt(DOUBLE(fileSize())/10000));
            localImage(i) <- resizedImage();
        }
        convertImage(localImage(i), 'png');
        localImage(i) <- convertedImage();
        
    }
    timeStamp() <- [FORMULA STRING[20] 'to_char(($1),\'YYYY-MM-DD\') || \'T\' || to_char(($1),\'HH24:MI:SS\')'](currentDateTime());
    LOCAL countN = INTEGER ();
    countN() <- OVERRIDE divideInteger(countIn(),countImagesCSO()) (+) (IF mod(countIn(),countImagesCSO()) > 0 THEN 1), 1;
    FOR iterate(INTEGER n, 1, countN()) DO {
        number() <- n;
        isEnd() <- n == countN();
        EXPORT ArticleDocumentMessage XML TO System.exportFile;
        LOCAL result = FILE();
        LOCAL message = TEXT();
        TRY {
            logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleDocumentMessage', 'Sending')));
            EXTERNAL HTTP dataExchangeRegisterAddress() HEADERS httpHeadersADM PARAMS System.exportFile() TO result;
            message() <- 'ArticleDocumentMessage - отправлен';
        } CATCH {
            logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleDocumentMessage', messageCaughtException()), lsfStackTraceCaughtException(), javaStackTraceCaughtException()));
            fileToString(result(), 'UTF-8');
            logToFile('CSO', (CONCAT '\n', 'ArticleDocumentMessage',resultString()));
            message() <- 'ArticleDocumentMessage - есть ошибки, обратитесь к администратору';
        }
        IF message()  THEN {
            MESSAGE message() NOWAIT;
        }
    }
}

//------------ //PI_SAP_ArticleHierarchy.xml
GROUP PutC EXTID '=http://www.uvs.lt/PI_GP:PutArticleHierarchies' :Body;
GROUP headC EXTID 'head' :PutC;
GROUP itemsC EXTID 'items' :PutC;
GROUP itemC EXTID 'Item';
GROUP HeaderText: itemC;
GROUP ArticleHierarchyDescriptionData:HeaderText; 
GROUP Node: itemC;
GROUP NodeText;
GROUP ArticleHierarchyDescriptionDataC EXTID 'ArticleHierarchyDescriptionData' :NodeText;

FORM ArticleHierarchy FORMEXTID 's=http://schemas.xmlsoap.org/soap/envelope/:Envelope'     
      
    PROPERTIES IN headC ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i'             
    PROPERTIES  IN headC  = 'SAP PI' EXTID 'Receiver'
    PROPERTIES  IN headC  = 'SAP ERP' EXTID 'Sender'
    PROPERTIES  IN headC  = TRUE EXTID 'SessionEnd'
    PROPERTIES  IN headC  = 1 EXTID'SessionMessageNumber'
    PROPERTIES  IN headC  = [FORMULA STRING[20] 'to_char(($1),\'YYYY-MM-DD\') || \'T\' || to_char(($1),\'HH24:MI:SS\')'](currentDateTime()) EXTID 'SessionTimeStamp'
    PROPERTIES  IN headC  = 'F' EXTID 'SessionType'    

    PROPERTIES IN itemsC ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:i' 
    OBJECTS type = CustomGroupType EXTID 'MessageItemOfArticleHierarchyS7N_Sjo7W' IN itemsC    
    PROPERTIES                                         = 'U' IF type IS CustomGroupType EXTID 'ChangeType'  
    PROPERTIES IN itemC                                = 'P1' IF type IS CustomGroupType EXTID 'DistributionChannel'    
    PROPERTIES IN ArticleHierarchyDescriptionData      = name(type) EXTID  'Description'
    PROPERTIES IN ArticleHierarchyDescriptionData      = 'RU' IF type IS CustomGroupType EXTID  'LanguageId' 
    PROPERTIES IN itemC                                =id(type) EXTID 'HierarchyId'                
    FILTERS isCSO(type)
    
    OBJECTS    i = CustomGroup EXTID 'ArticleHierarchyNode' IN Node          

    PROPERTIES                                        = id(i) EXTID 'NodeId'
    PROPERTIES IN ArticleHierarchyDescriptionDataC    = name(i) EXTID 'Description'
    PROPERTIES IN ArticleHierarchyDescriptionDataC    = 'RU' IF i IS CustomGroup EXTID 'LanguageId'     
    PROPERTIES                                        = id(parent(i)) EXTID 'ParentId'
    
    OBJECTS in = CustomGroupType IN itemC EXTID 'SalesOrganization'
    FILTERS in == type     
    PROPERTIES                                        = '1' IF in IS CustomGroupType  EXTID 'value'       
                                         
    OBJECTS in1 = CustomGroupType IN itemC EXTID 'Type'
    FILTERS in1 == type     
    PROPERTIES                                        = '0' IF in1 IS CustomGroupType  EXTID  'value'  
                
    FILTERS customGroupType(i)==type
;

httpHeadersAH(TEXT name) = CASE
    WHEN name == 'SOAPAction' THEN 'http://www.uvs.lt/PI_GP/I_PI_GP/PutArticleHierarchies';
    
articleHierarchy 'Иерархия групп КСО (ArticleHierarchy)' (){
    EXPORT ArticleHierarchy XML TO System.exportFile;     
    LOCAL result = FILE();
    LOCAL message = TEXT();    
    TRY {
        logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleHierarchy', 'Sending')));
        EXTERNAL HTTP dataExchangeRegisterAddress() HEADERS httpHeadersAH PARAMS System.exportFile() TO result;  
        message() <- 'ArticleHierarchy - отправлен'; 
    } CATCH { 
        logToFile('CSO',(CONCAT '\n\n', (CONCAT ' - ', 'ArticleHierarchy', messageCaughtException()), lsfStackTraceCaughtException(), javaStackTraceCaughtException()));
        fileToString(result(), 'UTF-8');
        logToFile('CSO', (CONCAT '\n', 'ArticleHierarchy',resultString()));   
        message() <- 'ArticleHierarchy - есть ошибки, обратитесь к администратору';               
    }
    IF message()  THEN {
        MESSAGE message() NOWAIT;
    }            
}

EXTEND FORM integrationData 
    PROPERTIES() dataExchangeRegisterAddress, countImagesCSO
    PROPERTIES() articleHierarchyObject, articleMessage, articleDocumentMessage, articleHierarchy
;

DESIGN integrationData {
    pane {
        NEW kso {
            caption = 'КСО';
            MOVE PROPERTY (dataExchangeRegisterAddress());
            MOVE PROPERTY (countImagesCSO());
            MOVE PROPERTY (articleHierarchyObject());
            MOVE PROPERTY (articleMessage());
            MOVE PROPERTY (articleDocumentMessage());
            MOVE PROPERTY (articleHierarchy());            
        }
    }
}

