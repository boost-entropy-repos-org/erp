MODULE TerminalHandler;

REQUIRE Terminal, MachineryPriceTransaction, Item;

skipPrefix 'Не выгружать префикс' = DATA BOOLEAN(); 

process 'Обработка документа ТСД' (terminalDocument) = ACTION ABSTRACT LIST (TerminalDocument);

priceBefore 'Старая цена' = ABSTRACT NUMERIC[16,4](Sku, Stock);
priceBeforeIdBarcodeId(VARSTRING[15] sku, VARSTRING[100] stock) = priceBefore(skuBarcode(sku), stock(stock));

checkIdTerminal 'Ограничение доступа по коду терминала' = DATA BOOLEAN ();

@defineExternalizable(terminal, VARSTRING[100]);
blocked 'Заблокирован' = DATA BOOLEAN (Terminal);
lastConnectionTime 'Последнее подключение' = DATA DATETIME (Terminal);
lastUser 'Пользователь' = DATA CustomUser (Terminal);
shortNameLastUser 'Пользователь' (Terminal terminal) = shortName(lastUser(terminal));

manufacturer = ABSTRACT LegalEntity (Item);
nameManufacturer (Barcode barcode) = name(manufacturer(skuBarcode(id(barcode))));

EXTEND FORM groupTerminal PROPERTIES(t) AFTER npp(t) id, blocked, lastConnectionTime, shortNameLastUser;
EXTEND FORM groupsTerminal
    PROPERTIES () checkIdTerminal
    PROPERTIES(t) AFTER npp(t) READONLY id, blocked, lastConnectionTime, shortNameLastUser;

currentRetailPricingPrice 'Розничная цена' (Barcode barcode, Stock stock) =
    price[PriceListLedger,LedgerPriceListType](currentRetailPricingPriceListLedger(sku(barcode), stock), SystemLedgerPriceListType.retailPricingPriceListType);     
    
hostTerminalServer 'Хост' = DATA VARSTRING[30] ();
portTerminalServer 'Порт' = DATA INTEGER ();    
useCurrentPriceInTerminal 'Загружать в терминал текущие цены' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() hostTerminalServer, portTerminalServer, useCurrentPriceInTerminal, skipPrefix
;
DESIGN options {
    machinery {
        NEW terminalServer {
            caption = 'Настройки ТСД';
            MOVE PROPERTY(hostTerminalServer());
            MOVE PROPERTY(portTerminalServer());
            MOVE PROPERTY(useCurrentPriceInTerminal());
            MOVE PROPERTY(skipPrefix());
        }
    }
}    