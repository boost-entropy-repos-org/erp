MODULE TerminalHandler;

REQUIRE Terminal, MachineryPriceTransaction, Item, PurchaseManufacturingPrice;

process 'Обработка документа ТСД' (terminalDocument) = ACTION ABSTRACT LIST (TerminalDocument);

priceBefore 'Старая цена' = ABSTRACT NUMERIC[16,4](Sku, Stock);
priceBeforeIdBarcodeId(VARSTRING[15] sku, VARSTRING[100] stock) = priceBefore(skuBarcode(sku), stock(stock));

checkIdTerminal 'Ограничение доступа по коду терминала' = DATA BOOLEAN ();

@defineExternalizable(terminal, VARSTRING[100]);
blocked 'Заблокирован' = DATA BOOLEAN (Terminal);
lastConnectionTime 'Последнее подключение' = DATA DATETIME (Terminal);
lastUser 'Пользователь' = DATA CustomUser (Terminal);
shortNameLastUser 'Пользователь' (Terminal terminal) = shortName(lastUser(terminal));

manufacturer = ABSTRACT LegalEntity (Item);
nameManufacturer (Barcode barcode) = name(manufacturer(skuBarcode(id(barcode))));
nameManufacturerSku (Purchase.OrderDetail d) = name(manufacturer(sku(d)));
passScalesSku(Purchase.OrderDetail d) = passScales(sku(d));

EXTEND FORM groupTerminal PROPERTIES(t) AFTER npp(t) id, blocked, lastConnectionTime, shortNameLastUser;
EXTEND FORM groupsTerminal
    PROPERTIES () checkIdTerminal
    PROPERTIES(t) AFTER npp(t) READONLY id, blocked, lastConnectionTime, shortNameLastUser;

currentRetailPricingPrice 'Розничная цена' (Barcode barcode, Stock stock) =
    price[PriceListLedger,LedgerPriceListType](currentRetailPricingPriceListLedger(sku(barcode), stock), SystemLedgerPriceListType.retailPricingPriceListType);     
currentBalance (Barcode barcode, Stock stock) = currentBalance(sku(barcode), stock);
    
hostTerminalServer 'Хост' = DATA VARSTRING[30] ();
portTerminalServer 'Порт' = DATA INTEGER ();    
useCurrentPriceInTerminal 'Загружать в терминал текущие цены' = DATA BOOLEAN ();
useCurrentQuantityInTerminal 'Загружать в терминал текущие остатки' = DATA BOOLEAN ();
dataPriceListTypeTerminal 'Вид цен' = DATA LedgerPriceListType();
nameDataPriceListTypeTerminal 'Вид цен' = name(dataPriceListTypeTerminal());
priceListTypeTerminal 'Вид цен' = OVERRIDE SystemLedgerPriceListType.manufacturingPriceStockPriceListType, dataPriceListTypeTerminal();

EXTEND FORM options
    PROPERTIES() hostTerminalServer, portTerminalServer, useCurrentPriceInTerminal, useCurrentQuantityInTerminal, nameDataPriceListTypeTerminal
;
DESIGN options {
    machinery {
        NEW terminalServer {
            caption = 'Настройки ТСД';
            MOVE PROPERTY(hostTerminalServer());
            MOVE PROPERTY(portTerminalServer());
            MOVE PROPERTY(useCurrentPriceInTerminal());
            MOVE PROPERTY(useCurrentQuantityInTerminal());
            MOVE PROPERTY(nameDataPriceListTypeTerminal());
        }
    }
}    